Const Int BadzWarpSpd = 115;

actor OrbitalMissileBoss : CSCCWepBase
{
Inventory.Pickupmessage "Satellite Sign \"Orbital Missile\""
dropitem "OrbitalMissileWepCS"
Obituary "$OB_ORBITALMISSILE"
tag "$TAG_ORBITALMISSILE"
weapon.ammotype "BadzStuffAmmo"
weapon.ammotype2 "BadzWarpAmmo"
States
{
Spawn:
TNT1 A 0
stop
Ready:
MCAR H 1 ACS_ExecuteAlways(998,0,DYE_BADZCOLOR)
MCAR H 0 A_GunFlash
MCAR H 0 A_GiveInventory("BadzAmmoCheck",1)
MCAR H 0 A_Jumpif(CallACS("CSACS_ButtonCheck",BT_User1) == True,"User1")
MCAR H 1 A_WeaponReady(WRF_ALLOWRELOAD)
Goto Ready+2

Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
MCAR H 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
MCAR H 1 A_Raise
Loop

Fire:
MCAR H 0 A_PlaySoundEx("weapon/napalm","Weapon")
MCAR H 0 A_GiveInventory("BadzMainTeamcolor")
MCAR IIIIJJJJHHHHHHHHHHHHHHHHHHHH 1 A_GiveInventory("BadzAmmoCheck",1)
goto Ready+2

AltFire:
MCAR H 0 A_JumpIfInventory("BadzStackCount",1,"SummonMissile")
MCAR H 0 A_JumpIfInventory("BadzStuffAmmo",140,1)
Goto NoAmmo
InitiateMissile:
MCAR H 0 A_SpawnItemEx("BadzAmmoCheckerThing")
SummonMissile:
MCAR H 1 A_GiveInventory("ShieldCheck",1)
MCAR H 1 A_JumpIfInventory("BadzStackCount",5,"NoAmmo")
MCAR H 0 A_GiveInventory("BadzStackCount",1)
MCAR H 0 A_GiveInventory("BadzAltTeamcolor")
MCAR H 0 A_PlaySoundEx("weapon/magnetmissile","Weapon")
MCAR HIJ 2
Goto Ready+2

Reload: //ReleaseMissiles:
TNT1 A 0 A_PlaySoundEx("misc/gravitywarn","Weapon")
TNT1 A 0 A_TakeInventory("ShieldCheck",999)
TNT1 A 0 A_TakeInventory("BadzStackCount",5)
Goto Reload.Hold
Reload.Hold:
MCAR HHHH 1 A_GiveInventory("BadzAmmoCheck",1)
MCAR H 0 A_Jumpif(CallACS("CSACS_ButtonCheck",BT_Reload) == True,"Reload.Hold")
goto Ready+2

User1:
MCAR H 0 A_JumpIfInventory("BadzWarpAmmo",52,1)
Goto User1.Hold
MCAR HHIIJJ 1 A_GiveInventory("BadzAmmoCheck",1)
MCAR K 0 A_PlaySoundEx("misc/gravitywarn","Weapon")
MCAR J 0 SetPlayerProperty(0,1,4)
WEA3 N 0 A_TakeInventory("BadzWarpAmmo",52)
WEA3 J 0 A_SpawnItemEx("BadzWarpFX",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
MCAR J 0 A_GiveInventory("BadzAmmoCheck",1)
MCAR J 1 A_ChangeVelocity(cos(pitch)*BadzWarpSpd,0,sin(pitch)*-BadzWarpSpd,CVF_RELATIVE|CVF_REPLACE)
WEA3 J 0 A_SpawnItemEx("BadzWarpFX",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
MCAR J 0 A_GiveInventory("BadzAmmoCheck",1)
MCAR J 1 A_ChangeVelocity(cos(pitch)*BadzWarpSpd,0,sin(pitch)*-BadzWarpSpd,CVF_RELATIVE|CVF_REPLACE)
WEA3 J 0 A_SpawnItemEx("BadzWarpFX",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
MCAR J 0 A_GiveInventory("BadzAmmoCheck",1)
MCAR J 1 A_ChangeVelocity(cos(pitch)*BadzWarpSpd,0,sin(pitch)*-BadzWarpSpd,CVF_RELATIVE|CVF_REPLACE)
WEA3 J 0 A_SpawnItemEx("BadzWarpFX",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
MCAR J 0 A_GiveInventory("BadzAmmoCheck",1)
MCAR J 1 A_ChangeVelocity(cos(pitch)*BadzWarpSpd,0,sin(pitch)*-BadzWarpSpd,CVF_RELATIVE|CVF_REPLACE)
MCAR H 0 A_Stop
MCAR J 0 SetPlayerProperty(0,0,4)
MCAR JJII 1 A_GiveInventory("BadzAmmoCheck",1)
Goto User1.Hold
User1.Hold:
MCAR HHHH 1 A_GiveInventory("BadzAmmoCheck",1)
MCAR H 0 A_Jumpif(CallACS("CSACS_ButtonCheck",BT_User1) == True,"User1")
Goto Ready+2

Flash:
TNT1 A 0 A_SpawnItemEx("BadzHolographThing",0,0,54,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 1 A_SetTranslucent(1.0)
TNT1 A 0 A_SpawnItemEx("BadzHolographThing",0,0,54,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 1 A_SetTranslucent(0.9)
TNT1 A 0 A_GiveInventory("BadzWarpAmmo",1)
TNT1 A 0 A_JumpIfInventory("IsDead",1,"UnFlash")
Goto Flash
UnFlash:
TNT1 A 0 A_TakeInventory("ShieldCheck",1)
TNT1 A 7 A_SetTranslucent(0.8)
TNT1 A 7 A_SetTranslucent(0.6)
TNT1 A 7 A_SetTranslucent(0.4)
stop

NoAmmo:
MCAR H 1 
Goto Ready+2
}
}


actor BadzAmmoCheck : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("ShieldCheck",1,"Nope")
TNT1 A 0 A_GiveInventory("BadzStuffAmmo",1)
goto Nope
Nope:
TNT1 A 0 
stop
}
}

actor BadzMainTeamcolor : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("LightTeamFlag",1,"LightFire")
TNT1 A 0 A_JumpIfInventory("WilyTeamFlag",1,"WilyFire")
TNT1 A 0 A_JumpIfInventory("CossackTeamFlag",1,"CossackFire")
TNT1 A 0 A_JumpIfInventory("KingTeamFlag",1,"KingFire")
TNT1 A 0 A_Jump(256,"Default")
goto Default
Default:
TNT1 A 0 A_FireCustomMissile("BadzShot",0,0,8,0)
stop
LightFire:
TNT1 A 0 A_FireCustomMissile("BadzShotL",0,0,8,0)
stop
WilyFire:
TNT1 A 0 A_FireCustomMissile("BadzShotW",0,0,8,0)
stop
CossackFire:
TNT1 A 0 A_FireCustomMissile("BadzShotC",0,0,8,0)
stop
KingFire:
TNT1 A 0 A_FireCustomMissile("BadzShotK",0,0,8,0)
stop
}
}

actor BadzAltTeamcolor : BadzMainTeamcolor{
States
{
Default:
TNT1 A 0 A_SpawnItemEx("OrbitalMissile",0,0,0)
stop
LightFire:
TNT1 A 0 A_SpawnItemEx("OrbitalMissileL",0,0,0)
stop
WilyFire:
TNT1 A 0 A_SpawnItemEx("OrbitalMissileW",0,0,0)
stop
CossackFire:
TNT1 A 0 A_SpawnItemEx("OrbitalMissileC",0,0,0)
stop
KingFire:
TNT1 A 0 A_SpawnItemEx("OrbitalMissileK",0,0,0)
stop
}}

actor BadzWarpFX : GeminiTrail{
States{
Spawn:
TNT1 A 0
JUNM AAAAA 1 A_FadeOut(0.2)
stop
}
}

actor BadzStuffAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 280
}

actor BadzWarpAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 210
}

actor BadzStackCount : Inventory
{
inventory.amount 1
inventory.maxamount 5
}

actor BadzHolographThing : GeminiTrail
{
Renderstyle "translucent"
alpha 0.3
scale 0.8
+NOTIMEFREEZE
states
{
Spawn:
TNT1 A 0
ORBD W 1
stop
}}

actor BadzShot : BasicProjectile
{
PROJECTILE
+LOOKALLAROUND
+SKYEXPLODE
+MTHRUSPECIES

+SLIDESONWALLS
+NOEXPLODEFLOOR
damagetype "BadzBuster"
Obituary "$OB_BADZBUSTER"
Speed 40
Damage (14)
radius 12
height 16
scale 2.5
States
{
Spawn:
DARS D 2 A_SpawnItemEx("BadzBusterTrail",0,0,0,momx*0.8,momy*0.8,momz*0.8,0,SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_ChangeFlag("SLIDESONWALLS",0)
TNT1 A 0 A_ChangeFlag("NOEXPLODEFLOOR",0)
Spawn2:
DARS D 2 A_SpawnItemEx("BadzBusterTrail",0,0,0,momx*0.8,momy*0.8,momz*0.8,0,SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
loop
Death:
TNT1 A 1
stop
}
}

actor BadzBusterTrail : BadzShot
{
Damage(3)
Scale 1.0
States
{
Spawn:
TNT1 A 0
ASEX ABCDEF 1
stop
Death:
ASEX BCDEF 1
stop
}}

actor BadzShotL : BadzShot{Translation "216:216=75:75", "210:210=202:202"}
actor BadzShotW : BadzShot{Translation "216:216=41:41", "210:210=171:171"}
actor BadzShotC : BadzShot{Translation "216:216=128:128", "210:210=101:101"}
actor BadzShotK : BadzShot{Translation "216:216=232:232", "210:210=215:215"}

actor BadzAmmoCheckerThing : BasicHelper
{
States
{
Spawn:
TNT1 A 1
TNT1 A 1 A_JumpIfInTargetInventory("BadzStuffAmmo",5,1)
goto Remove
TNT1 A 1
TNT1 A 1 A_JumpIfInTargetInventory("ShieldCheck",1,"Spawn")
stop
Remove:
TNT1 A 0 A_TakeFromTarget("ShieldCheck",989)
TNT1 A 0 A_TakeFromTarget("BadzStackCount",5)
stop
}
}

actor OrbitalMissile
{
var int user_stack;
var int user_dist;
+MISSILE
+NOCLIP
+RIPPER
Damagetype "OrbitalMissile"
Obituary "$OB_ORBITALMISSILE"
+NOINTERACTION
+DONTSPLASH
+NOBLOCKMONST 
+NOTELEFRAG
+MTHRUSPECIES
+THRUGHOST
Radius 8
Height 12
Scale 2.5
Translation "210:210=254:254", "220:220=3:3", "197:197=254:254", "196:196=251:251"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetUserVar(user_dist,0)
TNT1 A 0 A_JumpIfIntargetInventory("BadzStackCount",5,"StackFive")
TNT1 A 0 A_JumpIfIntargetInventory("BadzStackCount",4,"StackFour")
TNT1 A 0 A_JumpIfIntargetInventory("BadzStackCount",3,"StackThree")
TNT1 A 0 A_JumpIfIntargetInventory("BadzStackCount",2,"StackTwo")
StackOne:
TNT1 A 1 A_SetUserVar("user_stack",72)
TNT1 A 0 A_Jump(256,"Orbit")
Orbit:
SPRE D 1 A_Warp(AAPTR_TARGET,0,user_dist,32,angle,WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION)
TNT1 A 0 A_SetAngle(angle+12)
TNT1 A 0 A_SetUserVar("user_dist",user_dist+5)
TNT1 A 0 A_JumpIf(user_dist >= user_stack, "RangeSet")
SPRE E 1 A_Warp(AAPTR_TARGET,0,user_dist,32,angle,WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION)
TNT1 A 0 A_SetAngle(angle+12)
TNT1 A 0 A_Explode(7,20,0,0,20)
TNT1 A 0 A_SpawnItemEx("OrbitalMissileTrail",-5,0,0,0,2,0,0,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SetUserVar("user_dist",user_dist+5)
TNT1 A 0 A_JumpIf(user_dist >= user_stack, "RangeSet")
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
SPRE E 1 A_JumpIfInTargetInventory("ShieldCheck",1,"Orbit")
goto Death
RangeSet:
TNT1 A 0 A_SetUserVar(user_dist,user_stack)
OkPerfect:
SPRE D 1 A_Warp(AAPTR_TARGET,0,user_dist,32,angle,WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION)
TNT1 A 0 A_SetAngle(angle+12)
SPRE E 1 A_Warp(AAPTR_TARGET,0,user_dist,32,angle,WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION)
TNT1 A 0 A_SetAngle(angle+12)
TNT1 A 0 A_Explode(7,20,0,0,20)
TNT1 A 0 A_SpawnItemEx("OrbitalMissileTrail",-5,0,0,0,2,0,0,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
SPRE D 1 A_Warp(AAPTR_TARGET,0,user_dist,32,angle,WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION)
TNT1 A 0 A_SetAngle(angle+12)
SPRE E 1 A_Warp(AAPTR_TARGET,0,user_dist,32,angle,WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION)
TNT1 A 0 A_SetAngle(angle+12)
TNT1 A 0 A_Explode(7,20,0,0,20)
TNT1 A 0 A_SpawnItemEx("OrbitalMissileTrail",-5,0,0,0,2,0,0,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_TakeFromTarget("BadzStuffAmmo",1)
SPRE E 1 A_JumpIfInTargetInventory("ShieldCheck",1,"OkPerfect")
goto Death
StackFive:
TNT1 A 1 A_SetUserVar("user_stack",140)
TNT1 A 0 A_Jump(256,"Orbit")
Goto Orbit
StackFour:
TNT1 A 1 A_SetUserVar("user_stack",123)
TNT1 A 0 A_Jump(256,"Orbit")
Goto Orbit
StackThree:
TNT1 A 1 A_SetUserVar("user_stack",106)
TNT1 A 0 A_Jump(256,"Orbit")
Goto Orbit
StackTwo:
TNT1 A 1 A_SetUserVar("user_stack",89)
TNT1 A 0 A_Jump(256,"Orbit")
Goto Orbit
Death:
TNT1 A 0 A_TakeFromTarget("BadzStuffAmmo",30)
TNT1 A 2 A_SpawnItemEx("OrbitalMissileRelease",0,0,0,0,0,(0.20944*user_dist),0,SXF_TRANSFERTRANSLATION) //If you're wondering, that's Ï€/15
stop
}
}

actor OrbitalMissileProtect : PowerProtection
{
Powerup.Duration 5
damagefactor "OrbitalMissile", 0.0
}

actor OrbitalMissileTrail : BasicClientside
{
PROJECTILE
Radius 8
Height 12
Scale 2.5
Damage (0)
renderstyle "translucent"
alpha 0.7
States
{
Spawn:
ORBD DDD 3 A_FadeOut(0.2)
stop
}}

actor OrbitalMissileRelease
{
var int user_thatfast;
PROJECTILE
+MTHRUSPECIES
DamageType "OrbitalMissile"
Obituary "$OB_ORBITALMISSILE"
Radius 8
Height 12
Scale 2.5
Damage (0)
renderstyle "translucent"
Alpha 1.0
+RIPPER
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetUserVar(user_thatfast,momz) //What a terribly hacky way to achieve something
TNT1 A 1 A_ScaleVelocity(-1.0)
TNT1 A 0 A_SpawnItemEx("PunyExplosion")
TNT1 A 0 A_JumpIf(user_thatfast<5,"Death")
TNT1 A 0 ThrustThingZ(0,0,0,1)
SpinSpinSpin:
SPRE D 1 A_ChangeVelocity(user_thatfast,0,0,CVF_RELATIVE|CVF_REPLACE)
SPRE D 0 A_SetAngle(angle+12)
SPRE D 0 A_GiveInventory("CutterFlag",1)
SPRE E 1 A_ChangeVelocity(user_thatfast,0,0,CVF_RELATIVE|CVF_REPLACE)
SPRE D 0 A_SetAngle(angle+12)
SPRE D 0 A_Explode(7,20,0,0,20)
SPRE D 0 A_SpawnItemEx("OrbitalMissileTrail",0,0,0,0,2,0,0,SXF_TRANSFERTRANSLATION)
SPRE D 0 A_JumpIfInventory("CutterFlag",52,"OkBye")
loop
OkBye:
TNT1 A 0 A_SpawnItemEx("PunyExplosion")
Whee:
SPRE DE 1 A_SpawnItemEx("OrbitalMissileTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
loop
Death:
TNT1 A 1 A_SpawnItemEx("PunyExplosion")
stop
}}

actor OrbitalMissileL : OrbitalMissile {Translation "210:210=198:198", "220:220=199:199", "197:197=198:198", "196:196=202:202"}
actor OrbitalMissileW : OrbitalMissile {Translation "220:220=45:45", "210:210=174:174", "197:197=45:45", "196:196=171:171"}
actor OrbitalMissileC : OrbitalMissile {Translation "220:220=132:132", "210:210=109:109", "197:197=132:132", "196:196=101:101"}
actor OrbitalMissileK : OrbitalMissile {Translation "220:220=232:232", "210:210=229:229", "197:197=232:232", "196:196=215:215"}

actor PunyExplosion : GigaAstroExplode
{
Scale 0.8
States
{
Spawn:
TNT1 A 0
ASEX ABCDEF 1
stop
}}