actor MessatsuWep : CSCCWepBase
{
Weapon.AmmoUse 1
Weapon.AmmoGive 0
Weapon.SlotNumber 1
Obituary "%o didn't block %k's Gou Hadouken."
Inventory.Pickupmessage "You got Messatsu's memory chip! ...Useless!"
Tag "Messatsu"
weapon.ammotype "MessatsuAmmo"
inventory.icon "NULLICON"
dropitem "GouhadouWep"
+WEAPON.WIMPY_WEAPON
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{
Spawn:
WEAP X 1
stop
Ready:
GHAD A 0 ACS_ExecuteAlways(998,0,WCCS_MESSATSU, 1)
GHAD A 0 A_JumpIfInventory("RagingDemonFlag", 1, "RagingDemonReady")
GHAD A 0 A_JumpIfInventory("TemaFlag", 1, "TemaReady")
GHAD A 0 A_JumpIfInventory("MisogiFlag", 1, "MisogiReady")
GHAD A 0 A_JumpIfInventory("MessParryFlag", 1, "ParryAttempt")
GHAD A 1 A_WeaponReady
Goto Ready+1
ParryAttempt:
GHAD A 0 A_GiveInventory("MessParry", 1)
GHAD A 0 A_ChangeVelocity(0.25*momx, 0.25*momy, 0.5*momz, CVF_REPLACE)
GHAD A 0 SetPlayerProperty(0, 1, 4)
GHAD A 0 A_GiveInventory("MessParryAttempt", 1)
GHPA AC  1
GHAD A 0 A_SpawnItemEx("GouShoryuFX", 48, -12, 32)
Goto ParryWait
ParryWait:
GHPA D 1 
GHPA D 1 A_JumpIfInventory("MessParry", 1, "ParryWait")
GHPA D 1
GHPA D 1 A_JumpIfInventory("ParryStrength", 1, 2)
GHPA D 7
GHPA CBA 1
GHAD A 0 SetPlayerProperty(0, 0, 4)
GHAD A 0 A_TakeInventory("MessParryFlag", 1)
GHAD A 0 A_ClearRefire
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
GHAD A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
GHAD A 1 A_Raise
Loop
RagingDemonReady:
GHAD G 0 A_TakeInventory("MessSuperFlag", 56)
GHAD G 0 A_TakeInventory("RagingDemonReady", 1)
GHAD G 0 A_TakeInventory("TemaReady", 1)
GHAD G 0 A_TakeInventory("MisogiReady", 1)
GHAD G 0 A_GiveInventory("RagingDemonReady", 1)
GHAD G 0 A_PlaySoundEx("weapon/supermove","Auto")
GHAD G 0 A_ScaleVelocity(0.50)
/*GHAD G 0 A_SpawnItemEx("MessShadow", 0, 0, 0, 0, -4)
GHAD G 0 A_SpawnItemEx("MessShadow", 0, 0, 0, 0, 4)
GHAD G 5
GHAD G 0 A_SpawnItemEx("MessShadow", 0, 0, 0, 0, -4)
GHAD G 0 A_SpawnItemEx("MessShadow", 0, 0, 0, 0, 4)

//GHAD G 20 A_PlaySoundEx("weapon/superboost","Weapon")

GHAD G 0 A_GiveInventory("MessSuperFlag", 56)
*/
RagingDemonReadyLoop:
GHAD G 0 A_SpawnItemEx("MessShadow", -8, 0, 0, 0, -4)
GHAD G 1 A_SpawnItemEx("MessShadow", -8, 0, 0, 0, 4)
GHAD B 0 A_GiveInventory("MessSuperFlag", 3)
GHAD B 0 A_JumpIfInventory("MessSuperFlag", 56, "RagingDemonReadyFinish")
loop
RagingDemonReadyFinish:
GHAD B 3 A_TakeInventory("MessatsuFlag", 999)
Goto TakeFlags
TemaReady:
TNT1 A 0 A_TakeInventory("MessSuperFlag", 56)
TNT1 A 0 A_TakeInventory("RagingDemonReady", 1)
TNT1 A 0 A_TakeInventory("TemaReady", 1)
TNT1 A 0 A_TakeInventory("MisogiReady", 1)
TNT1 A 0 A_GiveInventory("TemaReady", 1)
GHAD G 0 A_ScaleVelocity(0.50)
GHAD B 0 A_PlaySoundEx("weapon/supermove","Auto")
TemaReadyLoop:
GHAD B 1 A_SpawnItemEx("MessShadow", -8, 0, 0, -3, Random(-3, 3))
GHAD B 0 A_GiveInventory("MessSuperFlag", 1)
GHAD B 0 A_JumpIfInventory("MessSuperFlag", 18, "TemaReadyFinish")
loop
TemaReadyFinish:
GHAD B 3 A_TakeInventory("MessatsuFlag", 999)
//GHAD B 0 A_GiveInventory("MessSuperflag", 18)
Goto TakeFlags
MisogiReady:
TNT1 A 0 A_TakeInventory("MessSuperFlag", 56)
TNT1 A 0 A_TakeInventory("RagingDemonReady", 1)
TNT1 A 0 A_TakeInventory("TemaReady", 1)
TNT1 A 0 A_TakeInventory("MisogiReady", 1)
TNT1 A 0 A_GiveInventory("MisogiReady", 1)
GHAD G 0 A_ScaleVelocity(0.50)
GHAD A 0 A_PlaySoundEx("weapon/supermove","Auto")
GHAD KJI 1 A_SpawnItemEx("MessShadow", -8)
MisogiReadyLoop:
TNT1 A 1 A_SpawnItemEx("MessShadow", -8, 0, 8, 0, 0, 8)
TNT1 A 0 A_GiveInventory("MessSuperFlag", 2)
TNT1 A 0 A_JumpIfInventory("MessSuperFlag", 36, "MisogiReadyFinish")
loop
MisogiReadyFinish:
TNT1 A 3 A_PlaySoundEx("weapon/mirrorabsorb","Auto")
//TNT1 AAAAAAAAAAAAA 1 A_SpawnItemEx("MessShadow", -8, 0, 8, 0, 0, 8)
GHAD IJK 1
//TNT1 A 0 A_GiveInventory("MessSuperFlag", 36)
Goto TakeFlags
TakeFlags:
TNT1 A 0 A_TakeInventory("RagingDemonFlag", 1)
TNT1 A 0 A_TakeInventory("TemaFlag", 1)
TNT1 A 0 A_TakeInventory("MisogiFlag", 1)
TNT1 A 0 A_SpawnItemEx("MessShadowWatcher", 1)
TNT1 A 0 A_ClearRefire
Goto Ready+1
Fire:
GHAD D 0 
GHAD D 0 A_JumpIfInventory("RagingDemonReady", 1, "RagingDemon")
GHAD D 0 A_JumpIfInventory("TemaReady", 1, "Tema")
GHAD D 0 A_JumpIfInventory("MisogiReady", 1, "GroundSlam")
GHAD D 0 A_JumpIfInventory("MessatsuCharge", 1, "RealFire")
GHAD A 0 A_JumpIfInventory("IsBot", 1, "BotCheck")
Goto RealFire
RealFire:
GHAD A 0 ACS_ExecuteAlways(991,0,WCCS_MESSATSU)
GHAD A 0 A_TakeInventory("MessatsuCharge",999)
GHAD A 0 A_JumpIf(ACS_NamedExecuteWithResult("EnemyProximity", 896) == 0, 2)
GHAD A 0 A_GiveInventory("MessatsuAmmo", 1)
GHAD A 0 A_PlaySoundEx("weapon/mbuster","Weapon")
GHAD A 0 A_FireCustomMissile("Gouhadou",0,0,8,0)
GHAD BC 1
GHAD D 8 
GHAD C 4 
GHAD B 0 A_Refire
goto Ready+1

ReleaseCharge:
GHAD A 0 ACS_ExecuteAlways(991,0,WCCS_MESSATSU)
GHAD A 0 A_TakeInventory("MessatsuFlag", 50)
GHAD A 0 A_StopSound(CHAN_WEAPON)
GHAD A 0 A_JumpIfInventory("MessatsuCharge", 35, "Fire4")
GHAD A 0 A_JumpIfInventory("MessatsuCharge", 25, "Fire3")
GHAD A 0 A_JumpIfInventory("MessatsuCharge", 15, "Fire2")
Goto Fire

Fire2:
GHAD A 0 A_TakeInventory("MessatsuCharge",35)
GHAD A 0 A_GiveInventory("MessatsuFlag", 2)
GHAD A 0 A_FireCustomMissile("Shakunetsu",0,0,8,0)
GHAD A 0 A_PlaySoundEx("weapon/pharaoh2","Weapon")
GHAD A 0 A_JumpIf(ACS_NamedExecuteWithResult("EnemyProximity", 896) == 0, 2)
GHAD A 0 A_GiveInventory("MessatsuAmmo", 2)

GHAD BCD 1
goto Cooldown

Fire3:
GHAD A 0 A_TakeInventory("MessatsuCharge",35)
GHAD A 0 A_GiveInventory("MessatsuFlag", 4)
GHAD A 0 A_FireCustomMissile("Shakunetsu2",0,0,8,0)
GHAD A 0 A_FireCustomMissile("Shakunetsu",0,0,8,0)
GHAD A 0 A_PlaySoundEx("weapon/pharaoh2","Weapon")
GHAD A 0 A_JumpIf(ACS_NamedExecuteWithResult("EnemyProximity", 896) == 0, 2)
GHAD A 0 A_GiveInventory("MessatsuAmmo", 2)
GHAD BCD 1
goto Cooldown

Fire4:
GHAD A 0 A_TakeInventory("MessatsuCharge",35)
GHAD A 0 A_GiveInventory("MessatsuFlag", 6)
GHAD A 0 A_FireCustomMissile("Shakunetsu3",0,0,8,0)
GHAD A 0 A_FireCustomMissile("Shakunetsu2",0,0,8,0)
GHAD A 0 A_FireCustomMissile("Shakunetsu",0,0,8,0)
GHAD A 0 A_PlaySoundEx("weapon/pharaoh2","Weapon")
GHAD A 0 A_JumpIf(ACS_NamedExecuteWithResult("EnemyProximity", 896) == 0, 2)
GHAD A 0 A_GiveInventory("MessatsuAmmo", 2)
GHAD BCD 1
goto Cooldown


CoolDown:
//GHAD A 0 A_TakeInventory("MessatsuCharge",35)
GHAD D 3 A_TakeInventory("MessatsuFlag", 1)
GHAD A 0 A_JumpIfInventory("MessatsuFlag", 1, "CoolDown")
GHAD C 5 
GHAD A 0 A_Refire
goto Ready+1

Hold:
GHAD A 0 A_JumpIfInventory("MessatsuCharge",1,"RealHold")
GHAD A 0 A_JumpIfInventory("IsBot", 1, "BotCheck")
Goto RealHold
RealHold:
GHAD A 0 A_JumpIfInventory("MessatsuCharge",18,"Charge")
GHAD A 1 A_GiveInventory("MessatsuCharge",1)
GHAD A 0 A_Refire
Goto Fire
Charge:
GHAD A 0 A_JumpIfInventory("MessatsuCharge",19,"Charge1")
GHAD A 0 A_PlaySoundEx("weapon/coilstart","Weapon")
Goto Charge1
Charge1:
GHAD A 0 A_JumpIfInventory("MessatsuCharge",35,"Charge2Start")
GHAD F 1 ACS_ExecuteAlways(991,0,WCCS_MESSCHARGE)
GHAD A 0 A_Refire("Charge1.a")
Goto ReleaseCharge
Charge1.a:
GHAD A 1 ACS_ExecuteAlways(991,0,WCCS_MESSATSU)
GHAD A 0 A_GiveInventory("MessatsuCharge",1)
GHAD A 0 A_Refire("Charge")
Goto ReleaseCharge
BotFire:
GHAD A 0 A_Jump(16, "ReleaseCharge")
GHAD A 0 A_Refire("Charge2")
Goto ReleaseCharge
Charge2Start:
GHAD A 0 A_GunFlash("Sound.MessCharge", GFF_NOEXTCHANGE)
Charge2:
GHAD S 1 ACS_ExecuteAlways(991,0,WCCS_MESSCHARGE2)
GHAD A 0 A_Refire("Charge2.a")
Goto ReleaseCharge
Charge2.a:
GHAD A 1 ACS_ExecuteAlways(991,0,WCCS_MESSATSU)
GHAD A 0 A_JumpIfInventory("IsBot", 1, "BotFire")
GHAD A 0 A_Refire("Charge2")
GHAD A 0 A_JumpIfInventory("MessatsuCharge", 35, 2)
GHAD A 0 A_GiveInventory("MessatsuCharge",1)
GHAD A 0 A_Refire
Goto ReleaseCharge

AltFire:
GHAD A 0 A_JumpIf(ACS_NamedExecuteWithResult("EnemyProximity", 896) == 0, 2)
GSHR A 0 A_GiveInventory("MessatsuAmmo", 2)
GSHR A 0 A_CheckFloor("Shoryu")
GSHR A 0 A_JumpIf(momz == 0 && z-floorz <= 56, "Shoryu")
//Goto Tatsumaki
Goto DemonFlip
Shoryu:
GHAD A 0 A_PlaySoundEx("weapon/scorchwheel","Weapon")
GSHR A 0 ACS_NamedExecuteAlways("core_flyplayer",0,0,2)
GSHR A 3 A_SpawnItemEx("GouShoryu1", 32,8+Random(-8, 8),16, 0,0,0,0, SXF_ABSOLUTEMOMENTUM)
GSHR B 3 A_SpawnItemEx("GouShoryu1", 48,8+Random(-8, 8),24, 0,0,0,0, SXF_ABSOLUTEMOMENTUM)
GSHR C 0 A_SpawnItemEx("GouShoryu1", 48,8+Random(-8, 8),32, 0,0,0,0, SXF_ABSOLUTEMOMENTUM)
//GSHR C 0 SetPlayerProperty(0,1,0)
GSHR C 3 A_ScaleVelocity(0.50)
GSHR C 0 A_SetArg(4, 15)
GSHR C 0 A_ChangeVelocity(10, 0, 30, CVF_RELATIVE)
Goto AltHold
AltHold:
GSHR D 1 A_SpawnItemEx("GouShoryu2", 48,8+Random(-8, 8),32, momx,momy,0,0, SXF_ABSOLUTEMOMENTUM)
GSHR D 0 A_SetArg(4, Args[4] - 1)
GSHR D 0 A_JumpIf(ceilingz - z < 64 || Args[4] <= 0, "ShoryuCeiling")
GSHR D 0 A_JumpIf(momz <= 0, "EndShoryu")
GSHR D 0 A_JumpIfInventory("MessShoryuFlag", 10, "EndShoryu2")
GSHR D 0 A_Refire
Goto EndShoryu
ShoryuCeiling:
GSHR D 0 A_JumpIfInventory("MessShoryuFlag", 2, "EndShoryu2")
Goto EndShoryu
EndShoryu:
GSHR D 0 A_JumpIfInventory("MessShoryuFlag", 10, "EndShoryu2")
GSHR D 0 A_ClearRefire
GSHR D 2 A_ScaleVelocity(0.5)
GSHR D 2 ACS_NamedExecuteAlways("core_flyplayer",0,0,0)
GSHR C 0 A_SpawnItemEx("GouShoryuFX", 48,8+Random(-8, 8),32, momx,momy,0,0, SXF_ABSOLUTEMOMENTUM)
GSHR CC 1 //A_SetAngle(angle+30)
GSHR B 0 A_SpawnItemEx("GouShoryuFX", 48,8+Random(-8, 8),32, momx,momy,0,0, SXF_ABSOLUTEMOMENTUM)
GSHR BB 1 //A_SetAngle(angle+30)
GSHR A 0 A_SpawnItemEx("GouShoryuFX", 48,8+Random(-8, 8),32, momx,momy,0,0, SXF_ABSOLUTEMOMENTUM)
GSHR AA 1 //A_SetAngle(angle+30)
InAir:
GSHR A 0 ThrustThingZ(0, 7, 1, 1)
//GSHR A 0 A_ChangeVelocity(momx, momy, -35, CVF_REPLACE|CVF_RELATIVE)
GSHR A 0 A_GiveInventory("MessatsuFlag", 1)
GSHR A 1 A_SpawnItemEx("GouShoryuFX", 48,8+Random(-8, 8),32, momx,momy,0,0, SXF_ABSOLUTEMOMENTUM)
GSHR A 0 A_JumpIf(z-floorz<=56 && momz == 0, "Landing")
GSHR A 0 A_JumpIfInventory("MessatsuFlag", 35, "Landing")
Goto InAir
Landing:
GHAD B 0 A_TakeInventory("MessShoryuFlag", 10)
GHAD B 0 A_TakeInventory("MessatsuFlag", 999)
GHAD B 6 A_ScaleVelocity(0.75)
//GHAD BBBBBB 1 A_SetAngle(angle+30)
GHAD B 0 SetPlayerProperty(0,0,1)
Goto Ready+1
EndShoryu2:
GSHR D 0 A_ClearRefire
GSHR D 0 A_ScaleVelocity(0.5)
GSHR D 2 Offset(32, 0)
GSHR D 2 Offset(64, 32)
GSHR D 0 A_WeaponReady(WRF_NOFIRE| WRF_NOSWITCH)
GSHR D 0 ACS_NamedExecuteAlways("core_flyplayer",0,0,0)
GSHR D 0 A_SpawnItemEx("GouShoryuSpike", 16, 0, 32) 
GSHR J 0 Offset(0, 64)
Goto InAir2
InAir2:
GHAD J 0 ThrustThingZ(0, 7, 1, 1)
GHAD J 0 A_GiveInventory("MessatsuFlag", 1)
GHAD J 1 A_SpawnItemEx("GouShoryuFX", 48,8+Random(-8, 8),32, momx,momy,0,0, SXF_ABSOLUTEMOMENTUM)
GHAD J 0 A_JumpIf(z-floorz<=56 && momz == 0, "Landing2")
GHAD J 0 A_JumpIfInventory("MessatsuFlag", 35, "Landing2")
Goto InAir2
Landing2:
GHAD B 0 A_TakeInventory("MessShoryuFlag", 10)
GHAD B 0 A_WeaponReady(WRF_NOFIRE| WRF_NOSWITCH)
GHAD B 0 SetPlayerProperty(0,1,0)
GHAD B 0 A_ScaleVelocity(0.5)
GHAD K 0 A_SpawnItemEx("GouShoryuSlam", 32) 
GHAD KLMN 1
GHAD B 0 SetPlayerProperty(0,0,0)
TNT1 A 5 A_TakeInventory("MessatsuFlag", 999)
GHAD NML 2

Goto Ready+1
DemonFlip:
GHAD O 0 A_JumpIf(ACS_NamedExecuteWithResult("EnemyProximity", 896) == 0, 2)
GHAD O 0 A_GiveInventory("MessatsuAmmo", 2)
GHAD O 0 A_PlaySoundEx("weapon/tengudash", "Weapon")
GHAD OOOOOOOO 0 A_SpawnItemEx("GouShoryuFX", Random(16, 32), 0 ,Random(0, -32), 0,0,0, Random(0, 360))
GHAD O 0 A_TakeInventory("JumpCancler", 1)
GHAD O 0 A_GiveInventory("DemonFlipActive", 1)
GHAD O 0 ACS_NamedExecuteAlways("DemonFlipCheck", 0)
GHAD O 7 Offset(32, 32) A_ChangeVelocity(22, 0, 14, CVF_REPLACE|CVF_RELATIVE)
Goto DemonFlipLoop
DemonFlipLoop:
GHAD O 0 A_JumpIfInventory("MessPalm", 1, "DemonFlipEnd")
GHAD O 0 A_JumpIfInventory("MessKick", 1, "DemonFlipEnd")
GHAD O 1 A_JumpIf(momz < 0, "DemonFlipLoop2")
GHAD O 0 A_CheckFloor("Gozan")
loop
DemonFlipLoop2:
GHAD O 0 A_JumpIfInventory("MessPalm", 1, "DemonFlipEnd")
GHAD O 0 A_JumpIfInventory("MessKick", 1, "DemonFlipEnd")
GHAD O 1 
GHAD O 0 A_CheckFloor("Gozan")
GHAD O 0 A_JumpIf(momz == 0, "DemonFlipEnd")
GHAD O 0 A_JumpIfInventory("IsBot", 1, "DemonFlipOpt")
loop
DemonFlipOpt:
GHAD O 0 A_Jump(32, "Gosho", "HyakkiGojin")
Goto DemonFlipLoop2
DemonFlipEnd:
GHAD A 0 A_TakeInventory("DemonFlipActive", 1)
GHAD A 0 A_ClearRefire
GHAD A 0 A_JumpIfInventory("MessPalm", 1, "Gosho")
GHAD A 0 A_JumpIfInventory("MessKick", 1, "HyakkiGojin")
Goto Ready+1
Gozan:
GHAD O 0 A_TakeInventory("DemonFlipActive", 1)
GHAD O 0 A_PlaySoundEx("weapon/scorchwheel","Weapon")
GHAD O 0 A_ChangeVelocity(25, 0, 0, CVF_REPLACE|CVF_RELATIVE)
GHAD PQR 1
GozanLoop:
GHAD R 0 SetPlayerProperty(0,1,0)
//GHAD RRRRRRRR 1 A_SpawnItemEx("Gozan",32,0,24,Random(-3, 3),Random(-3, 3),0,0,SXF_NOCHECKPOSITION)
GHAD R 1 A_SpawnItemEx("Gozan",32,0,24,Random(-3, 3),Random(-3, 3),0,0,SXF_NOCHECKPOSITION)
GHAD R 0 A_ScaleVelocity(0.95)
GHAD R 1 A_SpawnItemEx("Gozan",32,0,24,Random(-3, 3),Random(-3, 3),0,0,SXF_NOCHECKPOSITION)
GHAD R 0 A_ScaleVelocity(0.95)
GHAD R 1 A_SpawnItemEx("Gozan",32,0,24,Random(-3, 3),Random(-3, 3),0,0,SXF_NOCHECKPOSITION)
GHAD R 0 A_ScaleVelocity(0.95)
GHAD R 1 A_SpawnItemEx("Gozan",32,0,24,Random(-3, 3),Random(-3, 3),0,0,SXF_NOCHECKPOSITION)
GHAD R 0 A_ScaleVelocity(0.95)
GHAD R 1 A_SpawnItemEx("Gozan",32,0,24,Random(-3, 3),Random(-3, 3),0,0,SXF_NOCHECKPOSITION)
GHAD R 0 A_ScaleVelocity(0.95)
GHAD R 1 A_SpawnItemEx("Gozan",32,0,24,Random(-3, 3),Random(-3, 3),0,0,SXF_NOCHECKPOSITION)
GHAD R 0 A_ScaleVelocity(0.95)
GHAD R 1 A_SpawnItemEx("Gozan",32,0,24,Random(-3, 3),Random(-3, 3),0,0,SXF_NOCHECKPOSITION)
GHAD R 0 A_ScaleVelocity(0.95)
GHAD R 1 A_SpawnItemEx("Gozan",32,0,24,Random(-3, 3),Random(-3, 3),0,0,SXF_NOCHECKPOSITION)
GHAD R 0 A_ScaleVelocity(0.95)
GHAD R 1 A_SpawnItemEx("Gozan",32,0,24,Random(-3, 3),Random(-3, 3),0,0,SXF_NOCHECKPOSITION)
GHAD R 2 A_ScaleVelocity(0.50)
GHAR R 0 SetPlayerProperty(0,0,0)
Goto HyakkiGojinEnd
HyakkiGojin:
GHAD O 1 A_Stop
GHAD O 0 A_PlaySoundEx("weapon/scorchwheel","Weapon")
GHAD O 0 A_ChangeVelocity(17, 0, -20, CVF_REPLACE|CVF_RELATIVE)
GHAD P 1 A_SpawnItemEx("Gojin",32,0,8,Random(-3, 3),Random(-3, 3),0,0,SXF_NOCHECKPOSITION)
GHAD P 0 A_JumpIf(momz==0, "HyakkiGojinEnd")
GHAD Q 1 A_SpawnItemEx("Gojin",32,0,8,Random(-3, 3),Random(-3, 3),0,0,SXF_NOCHECKPOSITION)
GHAD Q 0 A_JumpIf(momz==0, "HyakkiGojinEnd")
GHAD R 1 A_SpawnItemEx("Gojin",32,0,8,Random(-3, 3),Random(-3, 3),0,0,SXF_NOCHECKPOSITION)
GHAD R 0 A_JumpIf(momz==0, "HyakkiGojinEnd")
HyakkiGojin2:
GHAD R 1 A_ChangeVelocity(17, 0, -20, CVF_REPLACE|CVF_RELATIVE)
GHAD R 0 A_SpawnItemEx("Gojin",32,0,8,Random(-3, 3),Random(-3, 3),0,0,SXF_NOCHECKPOSITION)
GHAD R 0 A_JumpIf(momz==0, "HyakkiGojinEnd")
loop
HyakkiGojinEnd:
GHAD RQPO 1
Goto Ready+1

Gosho:
GHAD B 1 A_ChangeVelocity(momx*0.25, momy*0.25, 0, CVF_REPLACE)//A_ScaleVelocity(0.25)
GHAD C 1
GHAD C 0 A_PlaySoundEx("weapon/cfire","Weapon")
GHAD D 5 A_FireCustomMissile("GoshoAttack", 0, 0, -8)
Goto GoshoEnd
GoshoEnd:
GHAD DC 4
GHAD B 2 A_ChangeVelocity(5, 0, -5, CVF_RELATIVE)
GHAD B 1 A_JumpIf(momz==0, "GoshoEnd2")
wait
GoshoEnd2:
GHAD A 1
Goto Ready+1
RagingDemon:
GHAD A 0 A_TakeInventory("RagingDemonFlag", 1)
GHAD A 0 A_TakeInventory("RagingDemonReady", 1)
GHAD A 0 A_TakeInventory("MessatsuCharge", 35)
GHAD A 0 A_TakeInventory("MessSuperFlag", 56)
GHAD A 0 A_TakeInventory("MessatsuAmmo", 56)
GHAD A 0 A_TakeInventory("MessatsuFlag", 999)
GHAD A 0 A_PlaySoundEx("weapon/supermove","Auto")
GHAD A 0 A_Stop
GHAD A 0 ACS_NamedExecuteAlways("core_flyplayer",0,0,2)
GHAD A 0 SetPlayerProperty(0,1,0)
GHAD A 0 SetPlayerProperty(0,1,4)
GHAD C 0 A_GiveInventory("MessatsuFlag", 18)
GHAD B 0 A_SpawnItemEx("MessShadow", 0, 0, 0, 0, -4)
GHAD B 0 A_SpawnItemEx("MessShadow", 0, 0, 0, 0, 4)
GHAD B 1 A_PlaySoundEx("weapon/superboost","Weapon")
Goto RagingDemonBegin
RagingDemonBegin:
GHAD G 0 A_ChangeVelocity(40, 0, 0, CVF_RELATIVE|CVF_REPLACE)
GHAD G 0 A_PlaySoundEx("weapon/superboost","Weapon")
GHAD G 1 A_SpawnItemEx("MessShadow", -8)
GHAD G 0 A_JumpIfInventory("NoSGS", 1, "DemonEnd")
GHAD G 0 A_TakeInventory("MessatsuFlag", 1)
GHAD G 0 A_SpawnItemEx("ShunGokuSatsu", 32,0,16, momx,momy,momz,0, SXF_ABSOLUTEMOMENTUM)
GHAD G 0 A_PlaySoundEx("weapon/superboost","Weapon")
GHAD G 1 A_SpawnItemEx("MessShadow", -8)
GHAD G 0 A_JumpIfInventory("NoSGS", 1, "DemonEnd")
GHAD G 0 A_TakeInventory("MessatsuFlag", 1)
GHAD G 0 A_JumpIfInventory("MessatsuFlag", 1, "RagingDemonBegin")
DemonEnd:
GHAD G 0 A_Stop
GHAD G 0 SetPlayerProperty(0,0,4)
GHAD G 0 A_TakeInventory("MessatsuFlag", 18)
//GHAD G 0 A_ScaleVelocity(0.3)
GHAD G 5 ACS_NamedExecuteAlways("core_flyplayer",0,0,0)
GHAD G 0 A_StopSound(CHAN_WEAPON)
DemonWait:
GHAD G 1
GHAD G 0 A_JumpIfInventory("NoSGS", 1, "DemonWait")
GHAD A 0 SetPlayerProperty(0,0,0)
//GHAD G 0 A_SpawnItemEx("ReSolidifier")
Goto Ready+1

Tema:
GHAD A 0 SetPlayerProperty(0,1,4)
GHAD A 0 A_Stop
GHAD A 0 A_TakeInventory("TemaReady", 1)
GHAD A 0 A_TakeInventory("MessatsuAmmo", 18)
TNT1 A 0 A_TakeInventory("MessSuperFlag", 56)
GHAD G 0 A_PlaySoundEx("weapon/supermove","Auto")
GHAD G 0 A_JumpIf(z-floorz==0,"Messatsu")
GHAD G 0 A_GiveInventory("MessatsuFlag", 56)
GHAD G 0 ACS_NamedExecuteAlways("core_flyplayer",0,0,2)
GHAD H 0 A_ChangeVelocity(-Cos(pitch)*3, 0, sin(pitch)*2,CVF_RELATIVE)
Tema2:
GHAD H 0 A_PlaySoundEx("weapon/mbuster","Weapon")
GHAD H 1 A_FireCustomMissile("GouZankuu",random(-7, 7),0,Random(-8, -32),Random(-32, 32))
GHAD H 1 A_FireCustomMissile("GouZankuu",random(-7, 7),0,Random(8, 32),Random(-8, 8))
GHAD H 0 A_TakeInventory("MessatsuFlag", 4)
GHAD H 0 A_PlaySoundEx("weapon/mbuster","Weapon")
GHAD H 1 A_FireCustomMissile("GouZankuu",random(-7, 7),0,Random(8, 32),Random(-32, 32))
GHAD H 1 A_FireCustomMissile("GouZankuu",random(-7, 7),0,Random(-8, -32),Random(-8, 8))
GHAD H 0 A_TakeInventory("MessatsuFlag", 4)
GHAD H 0 A_SpawnItemEx("MessShadow", -8, 0, 0, -2, Random(-1, 1))
GHAD H 0 A_JumpIfInventory("MessatsuFlag", 1, "Tema2")
GHAD H 1 A_Stop
GHAD H 1 ACS_NamedExecuteAlways("core_flyplayer",0,0,0)
GHAD H 0 SetPlayerProperty(0,0,4)
Goto Ready+1
Messatsu:
GHAD H 0 A_PlaySoundEx("weapon/cfire","Weapon")
GHAD H 0 A_ChangeVelocity(-Cos(pitch)*4, 0, sin(pitch)*7,CVF_RELATIVE)
GHAD H 0 A_FireCustomMissile("MessatsuGouHadou",0,0,0,0)
GHAD H 0 A_FireCustomMissile("MessatsuGouHadou1",0,0,0,0)
GHAD H 0 A_FireCustomMissile("MessatsuGouHadou2",0,0,0,0)
GHAD H 0 A_FireCustomMissile("MessatsuGouHadou3",0,0,0,0)
GHAD H 0 A_FireCustomMissile("MessatsuGouHadou4",0,0,0,0)
GHAD H 0 A_FireCustomMissile("MessatsuGouHadou5",0,0,0,0)
GHAD H 0 //A_Stop
GHAD HHHHHHHHHH 2 A_SpawnItemEx("MessShadow", -8, 0, 0, -2, Random(-1, 1))
GHAD A 0 SetPlayerProperty(0,0,4)
Goto Ready+1

GroundSlam:
GroundAttack:
GHAD A 0 A_TakeInventory("MessatsuAmmo", 36)
TNT1 A 0 A_TakeInventory("MessSuperFlag", 56)
GHAD A 0 ACS_NamedExecuteAlways("core_flyplayer",0,0,2)
GHAD A 0 SetPlayerProperty(0,1,4)
GHAD A 0 A_Stop
GHAD A 0 A_PlaySoundEx("weapon/supermove","Auto")
GHAD NMLKJI 1 A_SpawnItemEx("MessShadow", -8)
GHAD I 0 A_SpawnItemEx("MisogiFX")
TNT1 A 3 A_PlaySoundEx("weapon/lightningbolt","Auto")//A_PlaySoundEx("weapon/mirrorabsorb","Auto")
TNT1 AAAAAAAA 1 A_SpawnItemEx("MessShadow", -8, 0, 8, 0, 0, 8)
TNT1 A 0 A_CheckFloor("GroundAttack2")
TNT1 A 0 A_ChangeVelocity(0, 0, -35)
FindFloor:
TNT1 A 0 A_ChangeVelocity(0, 0, -3)
TNT1 A 1 A_SpawnItemEx("MessShadow", -8, 0, 8, 0, 0, 8)

TNT1 A 0 A_JumpIf(momz > -30, "GroundAttack2")
Loop
GroundAttack2:
TNT1 A 2
TNT1 A 0 A_PlaySound("misc/hugequake2")
TNT1 A 0 Radius_Quake(1,15,0,64,0)
TNT1 A 0 A_SpawnItemEx("Misogi")
TNT1 A 0 A_TakeInventory("MessatsuFlag", 999)
GHAD IJKLMN 1
TNT1 A 4
GHAD NML 4 
GHAD N 0 SetPlayerProperty(0,0,4)
GHAD A 0 ACS_NamedExecuteAlways("core_flyplayer",0,0,0)
GHAD A 0 A_TakeInventory("MisogiReady", 1)
Goto Ready+1

NoAmmo:
GHAD A 1
Goto Ready+1
Flash:
TNT1 A 1
stop
NoFlash:
TNT1 A 1
stop

BotCheck:
GSHR A 0 ACS_NamedExecuteAlways("core_flyplayer",0,0,0)
GHAD A 0 A_JumpIfInventory("MessatsuCharge",35,"Fire4")
GHAD A 0 A_JumpIfInventory("MessatsuCharge",19,"Charge1")
GHAD A 0 A_Jump(32, "BotAbility")
GHAD A 0 A_JumpIfCloser(96, "AltFire")
GHAD A 0 A_JumpIfCloser(384, "BotDemonFlip")
GHAD A 0 A_Jump(128, "BotHold")
Goto RealFire
BotHold:
GHAD A 0 A_GiveInventory("MessatsuCharge", 1)
GHAD A 0 A_Refire
Goto RealFire
BotAbility:

GHAD A 0 A_JumpIfInventory("MessatsuAmmo", 56, "RagingDemonReady")
GHAD A 0 A_JumpIfInventory("MessatsuAmmo", 36, "MisogiReady")
GHAD A 0 A_JumpIfInventory("MessatsuAmmo", 18, "TemaReady")
Goto Fire

BotDemonFlip:
GHAD A 2 ThrustThingZ(0,30,0,0)
GHAD A 0 A_Jump(64, "RealFire") 
GHAD A 0 A_FaceTarget
//GHAD A 1 ThrustThingZ(0,30,0,0)
GHAD A 6 A_Recoil(-10)
GHAD A 0 A_ChangeVelocity(momx, momy, momz/4, CVF_REPLACE)
Goto DemonFlip//Tatsumaki

Sound.MessCharge:
TNT1 A 9 A_PlaySoundEx("weapon/adapterchargeloop","Weapon",0)
TNT1 A 0 A_JumpIfInventory("IsDead", 1, "NoFlash")
TNT1 A 1 A_JumpIfInventory("MessatsuCharge",1,"Sound.MessCharge")
stop
}
}

Actor RagingDemonFlag : Inventory{}
Actor TemaFlag : Inventory{}
Actor MisogiFlag : Inventory{}

Actor RagingDemonReady : Powerup
{
Powerup.Duration 175
}

Actor TemaReady : RagingDemonReady {}
Actor MisogiReady: RagingDemonReady {}

actor MessatsuFlag : Inventory
{
inventory.amount 1
inventory.maxamount 100
}

actor MessatsuCharge : Inventory
{
inventory.amount 1
inventory.maxamount 35
}

actor MessSuperFlag : Inventory
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 56
}
actor MessatsuAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 56
}

actor MisogiProtect : PowerProtection
{
Powerup.Duration 20
DamageFactor "Misogi", 0.10
}


actor TatsumakiProtect : PowerProtection
{
Powerup.Duration 35
DamageFactor "Tatsumaki", 0.23
}

actor GojinProtect : PowerProtection
{
Powerup.Duration 35
DamageFactor "Gojin", 0.15
DamageFactor "Gozan", 0.15
}


actor TatsumakiFullProtect : PowerProtection
{
Powerup.Duration 20
DamageFactor "Tatsumaki", 0.00
}


actor GouShoryuFullProtect : PowerProtection
{
Powerup.Duration 20
DamageFactor "GouShoryuPush", 0.00
}


actor MessHitFlag : Inventory
{
inventory.amount 1
inventory.maxamount 100
}

actor Gouhadou : CSCCWepBase
{
PROJECTILE
+LOOKALLAROUND
+SKYEXPLODE
+MTHRUSPECIES
Translation "192:192=59:59", "198:198=61:61"
Obituary "%o didn't block %k's Gou Hadouken."
damagetype "GouHadou"
Damage (18)
RenderStyle "translucent"
Alpha 0.90
Speed 40
radius 12
height 8
scale 4.0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("LightTeamFlag",1,"BFire")
TNT1 A 0 A_JumpIfInTargetInventory("WilyTeamFlag",1,"RFire")
TNT1 A 0 A_JumpIfInTargetInventory("CossackTeamFlag",1,"GFire")
TNT1 A 0 A_JumpIfInTargetInventory("KingTeamFlag",1,"YFire")
Goto LoopJump
BFire:
TNT1 A 0 Thing_SetTranslation(0, 230)
Goto LoopJump
RFire:
TNT1 A 0 Thing_SetTranslation(0, 231)
Goto LoopJump
GFire:
TNT1 A 0 Thing_SetTranslation(0, 232)
Goto LoopJump
YFire:
TNT1 A 0 Thing_SetTranslation(0, 233)
Goto LoopJump
LoopJump:
TNT1 A 0 A_Jump(256, "SpawnLoop")
Goto SpawnLoop
SpawnLoop:
TNT1 A 2
GHAD TU 3
Goto SpawnLoop+1
Death:
GHAD TU 3 A_FadeOut(0.45)
loop
}
}



actor GouZankuu : Gouhadou
{
damagetype "GouZankuu"
Obituary "%o couldn't slip through %k's deluge of Zanku Hadoukens."
Damage (12)
scale 1.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 ThrustThingZ(0, random(-10,10), random(0,1), 1)
Goto Super::Spawn+1
SpawnLoop:
TNT1 A 2
GHAD T 0 A_ScaleVelocity(1.05)
//GHAD TU 3 bright
MGHA ABDBCDBAECAEABF 2 bright
Goto SpawnLoop+1
}
}



actor Shakunetsu : Gouhadou
{
+DONTBLAST
//+RIPPER
Translation "192:192=43:43", "198:198=189:189", "168:168=174:174"
Obituary "%o didn't block %k's Shakunetsu Hadouken."
damagetype "Shakunetsu"
//RenderStyle "add"
Alpha 0.75
Damage(22)
Speed 45
Radius 16
States
{
SpawnLoop:
TNT1 A 2
GHAD T 3 A_SpawnItemEx("ShakunetsuFX", 0, Random(-32, 32), 0, Random(-8, -12), Random(-2, 2), 0, 0, SXF_TRANSFERTRANSLATION)
GHAD U 3
Goto SpawnLoop+1
Death:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
TNT1 AAAAA 0 A_SpawnItemEx("ShakunetsuFX", 0, Random(-32, 32), Random(-8, 8), Random(-1, -2), Random(-2, 2), Random(1, 3), 0, SXF_TRANSFERTRANSLATION)
Goto "Gouhadou::Death"
}
}

actor Shakunetsu2 : Shakunetsu
{
Radius 15
Damage(15)
States
{
SpawnLoop:
TNT1 A 2
GHAD T 0 A_SpawnItemEx("ShakunetsuFX", 0, Random(-32, 32), 0, Random(-8, -12), Random(-2, 2), 0, 0, SXF_TRANSFERTRANSLATION)
GHAD T 3 A_SpawnItemEx("Shakunetsu3FX", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
GHAD U 0 A_SpawnItemEx("ShakunetsuFX", 0, Random(-32, 32), 0, Random(-8, -12), Random(-2, 2), 0, 0, SXF_TRANSFERTRANSLATION)
GHAD U 3 A_SpawnItemEx("Shakunetsu3FX", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
Goto SpawnLoop+1
}
}

actor Shakunetsu3 : Shakunetsu
{
Damage(13)
Radius 14
States
{
SpawnLoop:
TNT1 A 2
GHAD T 1 A_SpawnItemEx("ShakunetsuFX", 0, Random(-32, 32), 0, Random(-8, -12), Random(-2, 2), 0, 0, SXF_TRANSFERTRANSLATION)
GHAD TT 1 A_SpawnItemEx("Shakunetsu3FX", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
GHAD U 1 A_SpawnItemEx("ShakunetsuFX", 0, Random(-32, 32), 0, Random(-8, -12), Random(-2, 2), 0, 0, SXF_TRANSFERTRANSLATION)
GHAD UU 1 A_SpawnItemEx("Shakunetsu3FX", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
Goto SpawnLoop+1
}

}

actor ShakunetsuFX
{
Gravity 0.2
Renderstyle "Add"
//Translation "248:248=174:174", "41:41=189:189"
States
{
Spawn:
GHFX D 1 A_Fadeout(0.02)
GHFX D 0 A_CheckFloor("Death")
loop
Death:
TNT1 A 0
stop
}
}


actor Shakunetsu3FX : GeminiTrail
{
//Translation "192:192=43:43", "198:198=189:189", "168:168=174:174"
RenderStyle "add"
Alpha 0.8
scale 4.0
States
{
Spawn:
TNT1 A 2
GHAD TTUU 1 A_FadeOut(0.2)
wait
}
}

actor NoSGS : Powerup 
{
powerup.duration 20
}

actor ShunGokuSatsu
{
Obituary "%o was instantly defeated by %k's Lagless Hell Frag."
PROJECTILE
+LOOKALLAROUND
+SKYEXPLODE
+DONTBLAST
+HITTRACER
damagetype "ShunGokuSatsu"
Translation "248:248=59:59", "41:41=61:61"
Damage (10)
radius 24
height 16
scale 2.5
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_SpawnItemEx("ShunGokuSatsuFire", 0, Random(-32, 32), 0, Random(-8, -12), Random(-2, 2))
TNT1 A 0 A_ChangeFlag("THRUACTORS", 1)
TNT1 AAA 1 A_SpawnItemEx("ShunGokuSatsuFire", 0, Random(-32, 32), 0, Random(-8, -12), Random(-2, 2))
stop
XDeath:
Crash:
TNT1 A 0 A_JumpIfInTargetInventory("NoSGS", 1, "Death")
TNT1 A 0 A_Warp(AAPTR_TRACER, 0, 0, 0, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
TNT1 A 0 A_PlaySoundEx("weapon/mirrorabsorb","Auto")
TNT1 A 0 A_GiveInventory("NoSGS", 1, AAPTR_TARGET)
TNT1 A 0 A_GiveInventory("MessatsuFlag", 15)
TNT1 A 0 A_JumpIfInTargetinventory("MessatsuFlag", 18, 2)
TNT1 A 0 A_GiveToTarget("MessatsuFlag", 6)
MDK:
TNT1 A 1 A_Explode(8,128,0,0,128)
TNT1 AA 0 A_SpawnItemEx("ShunGokuSatsuFire",Random(-64, 64),Random(-64, 64),Random(0, 96),random(-5,5),random(-5,5),0,random(0,360))
TNT1 A 0 A_TakeInventory("MessatsuFlag", 1)
TNT1 A 0 A_SpawnItemEx("ShunGokuSatsuHitFX",Random(-64, 64),Random(-64, 64),Random(0, 80))
TNT1 A 0 A_JumpIfInventory("MessatsuFlag", 1, "MDK")
Goto Death
Death:
TNT1 AA 5
stop
}
}

actor Gojin : Gouhadou
{
PROJECTILE
+RIPPER
+DONTBLAST
Damage (0)
Obituary "%o was kicked out by %k's Hyakki Gojin."
scale 2.5
damagetype "Gojin"
Translation "192:192=59:59", "198:198=61:61"
Renderstyle "Add"
Speed 0
height 0
radius 0
States
{
Spawn:
TNT1 A 0
Goto Super::Spawn+1
SpawnLoop:
TNT1 A 0 A_Explode(30,64, 0, 0, 64)
TNT1 A 0 A_Stop
GHFX DEFG 2 A_Fadeout
Goto SpawnLoop+2
Death:
TNT1 A 0
stop
}
}
Actor Gozan : Gouhadou
{
PROJECTILE
+RIPPER
+DONTBLAST
Damage (0)
Obituary "%o was tripped up by %k's Hyakki Gozan."
scale 2.5
damagetype "Gozan"
Translation "192:192=59:59", "198:198=61:61"
Renderstyle "Add"
Speed 0
height 0
radius 0
States
{
Spawn:
TNT1 A 0
Goto Super::Spawn+1
SpawnLoop:
TNT1 A 0 A_Explode(25,64, 0, 0, 64)
TNT1 A 0 A_Stop
GHFX DEFG 2 A_Fadeout
Goto SpawnLoop+2
Death:
TNT1 A 0
stop
}
}


actor ShunGokuSatsuFire : Gouhadou
{
+NOINTERACTION
Gravity 0.1
Renderstyle "Add"
Translation "192:192=59:59", "198:198=61:61"
Scale 2.0
States
{
Spawn:
GHFX D 0
Goto Super::Spawn+1
SpawnLoop:
GHFX DEFG 2
GHFX D 0 A_Stop
GHFX DEFG 2
stop
Death:
TNT1 A 0
stop
}
}



actor ShunGokuSatsuHitFX : ShunGokuSatsuFire
{
Scale 1.5
Reactiontime 5
States
{
Spawn:
MMFX A 2
TNT1 A 1 A_CountDown
loop
}
}

actor ShunGokuSatsuLogo
{
Scale 2.0
Renderstyle "Translucent"
Alpha 0.1
+NOGRAVITY
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Warp(AAPTR_TARGET, 0, 0, 0, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
TNT1 A 0 Radius_Quake(1,15,0,64,0)
TNT1 A 5 A_TransferPointer(AAPTR_TARGET, AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_TARGET)
TNT1 A 0 A_TakeInventory("NoSGS", 1, 0, AAPTR_TARGET)
SGSD AAAAA 1 A_FadeIn(0.2)
SGSD A 100
SGSD A 2 A_Fadeout(0.1)
wait
}
}
actor GouShoryu1 : Gouhadou
{
Obituary "%o was punched skyward by %k's GouShoryuKen."
PROJECTILE
Translation "192:192=59:59", "198:198=61:61"
Renderstyle "Add"
+LOOKALLAROUND
+SKYEXPLODE
+DONTBLAST
+RIPPER
damagetype "GouShoryuStun"
Damage (0)
radius 5
height 5
scale 2.5
States
{
Spawn:
TNT1 A 0
Goto Super::Spawn+1
SpawnLoop:
TNT1 A 1 A_Explode(6,48,0, 0, 48)
GHFX ABC 1 
Goto Death
Death:
GHFX ABC 1 A_Fadeout(0.1)
wait
}
}

actor GouShoryu2 : GouShoryu1
{
damagetype "GouShoryuPush"
}

actor GouShoryuFX : GouShoryu2
{
+NOINTERACTION
States
{
Spawn:
TNT1 A 1 
Goto Gouhadou::Spawn+1 //Mental note: Watch this one
SpawnLoop:
GHFX DEFG 2
Goto Death
Death:
GHFX DEFG 2 A_Fadeout(0.2)
wait
}
}

actor MessatsuGouhadou : Gouhadou
{
damagetype "GouZankuu"
Obituary "%o couldn't parry %k's Messatsu Gouhadou."
Damage (25)
Alpha 0.4
scale 4.0
Radius 32
States
{
Spawn:
TNT1 A 1 
Goto Super::Spawn+1
SpawnLoop:
TNT1 A 2
MGHA ABDBCDBAECAEABF 2 A_SpawnItemEx("MessatsuGouhadouFX", 0, 0, 0, momx/4, momy/4, momz/4, 0, SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
Goto SpawnLoop+1
Death:
MGHA ABAB 2 A_Fadeout(0.2)
wait
}
}

actor MessatsuGouhadou1 : MessatsuGouhadou
{
Radius 30
Damage (15)
}

actor MessatsuGouhadou2 : MessatsuGouhadou
{
Radius 28
Damage (9)
}

actor MessatsuGouhadou3 : MessatsuGouhadou
{
Radius 26
Damage (8)
}

actor MessatsuGouhadou4 : MessatsuGouhadou
{
//RenderStyle "Add"
Radius 24
Damage (7)
}

actor MessatsuGouhadou5 : MessatsuGouhadou
{
RenderStyle "Add"
Radius 22
Damage (6)
}

actor MessatsuGouhadouFX : Shakunetsu3FX
{

Alpha 0.2
scale 3.5
States
{
Spawn:
TNT1 A 2
MGHA AB 2 A_Fadeout(0.05)
wait
}
}


actor Misogi
{
PROJECTILE
+DONTBLAST
+RIPPER
+NOCLIP
DAMAGE (0)
damagetype "MisogiImpact"
Scale 2.0
Radius 1
Height 1
Obituary "%o was shattered in pieces by %k's Misogi."
var int user_MisogiAngle;
Translation "192:192=43:43"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetUserVar("user_MisogiAngle", 0)
TNT1 A 0 A_ChangeFlag("NOCLIP", 0)
TNT1 A 1 A_Explode(65, 160, 0, 0, 64)
TNT1 A 0 A_JumpIfInTargetInventory("LightTeamFlag",1,"BFire")
TNT1 A 0 A_JumpIfInTargetInventory("WilyTeamFlag",1,"RFire")
TNT1 A 0 A_JumpIfInTargetInventory("CossackTeamFlag",1,"GFire")
TNT1 A 0 A_JumpIfInTargetInventory("KingTeamFlag",1,"YFire")
Goto SpawnLoop
BFire:
TNT1 A 0 Thing_SetTranslation(0, 230)
Goto SpawnLoop
RFire:
TNT1 A 0 Thing_SetTranslation(0, 231)
Goto SpawnLoop
GFire:
TNT1 A 0 Thing_SetTranslation(0, 232)
Goto SpawnLoop
YFire:
TNT1 A 0 Thing_SetTranslation(0, 233)
Goto SpawnLoop

SpawnLoop:
TNT1 A 0 A_SpawnItemEx("MisogiPart",8,0,0,25,0,0,user_MisogiAngle, SXF_TRANSFERTRANSLATION)
TNT1 A 1 A_SetUserVar("user_MisogiAngle", user_MisogiAngle + 15)
Goto SpawnLoop2
SpawnLoop2:
TNT1 A 0 A_SpawnItemEx("MisogiPart",8,0,0,25,0,0,user_MisogiAngle, SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("MisogiPart",8,0,0,25,0,0,360-user_MisogiAngle, SXF_TRANSFERTRANSLATION)
TNT1 A 1 A_SetUserVar("user_MisogiAngle", user_MisogiAngle + 15)
TNT1 A 0 A_JumpIf(user_MisogiAngle >= 165, "Death")
loop
Death:
TNT1 A 5 A_SpawnItemEx("MisogiPart",8,0,0,25,0,0, 180, SXF_TRANSFERTRANSLATION)
stop
}
}

actor MisogiPart : Gouhadou
{
PROJECTILE
Obituary "%o just couldn't defend against %k's Misogi."
Scale 2.0
//Translation "192:192=43:43"
+NOEXPLODEFLOOR
+RIPPER
+STEPMISSILE
MaxStepHeight 48
Speed 25
Damage (20) 
damagetype "Misogi"
Radius 16
Height 16
States
{
Spawn:
TNT1 A 0 
Goto "Super::Spawn+1"
SpawnLoop:
DUOR T 1 
DUOR T 0 A_SpawnItemEx("MisogiWave", 0, 0, 0, 0, 0, 12, 0, SXF_TRANSFERTRANSLATION)
DUOR T 1 A_FadeOut(0.04)
DUOR T 0 A_SpawnItemEx("MisogiWave", 0, 0, 0, 0, 0, 12, 0, SXF_TRANSFERTRANSLATION)
DUOR T 1 A_FadeOut(0.04)
DUOR T 0 A_SpawnItemEx("MisogiWave", 0, 0, 0, 0, 0, 12, 0, SXF_TRANSFERTRANSLATION)
DUOR T 1 A_FadeOut(0.04)
DUOR T 0 A_SpawnItemEx("MisogiWave", 0, 0, 0, 0, 0, 12, 0, SXF_TRANSFERTRANSLATION)
DUOR T 1 A_FadeOut(0.04)
DUOR T 0 A_SpawnItemEx("MisogiWave", 0, 0, 0, 0, 0, 12, 0, SXF_TRANSFERTRANSLATION)
DUOR T 1 A_FadeOut(0.04)
DUOR T 0 A_SpawnItemEx("MisogiWave", 0, 0, 0, 0, 0, 12, 0, SXF_TRANSFERTRANSLATION)
DUOR T 1 A_FadeOut(0.04)
DUOR T 0 A_SpawnItemEx("MisogiWave", 0, 0, 0, 0, 0, 12, 0, SXF_TRANSFERTRANSLATION)
DUOR T 1 A_FadeOut(0.04)
DUOR T 0 A_SpawnItemEx("MisogiWave", 0, 0, 0, 0, 0, 12, 0, SXF_TRANSFERTRANSLATION)
DUOR T 1 A_FadeOut(0.04)
DUOR T 0 A_SpawnItemEx("MisogiWave", 0, 0, 0, 0, 0, 12, 0, SXF_TRANSFERTRANSLATION)
DUOR T 1 A_FadeOut(0.04)
DUOR T 0 A_SpawnItemEx("MisogiWave", 0, 0, 0, 0, 0, 12, 0, SXF_TRANSFERTRANSLATION)
Goto Death
Death:
DUOR T 1 A_FadeOut 
wait
}
}

actor MisogiWave : MisogiPart
{
PROJECTILE
//+NOINTERACTION
//+CLIENTSIDEONLY
-NOGRAVITY
//Gravity 2.0
RenderStyle "add"
//Damage (0)
//Radius 16
//Height 16
States
{
Spawn:
TNT1 A 1
TNT1 A 0 A_JumpIfCloser(64, "Death")
DUOR T 1 A_FadeOut(0.03)
Wait
Death:
TNT1 A 0 
stop
}
}

Actor MisogiFX : LightningExtend
{
-FLOORHUGGER
RenderStyle "Add"
Translation "210:210=59:59", "229:229=61:61"
Alpha 0.95
States
{
Spawn:
LIBO L 0 
LIBO L 0 A_JumpIf(ceilingz-z <= 32, "Death")
LIBO L 0 A_SpawnItemEx("MisogiFX", 0, 0, 160)
Spawn2:
LIBO LLMN 1 A_Fadeout(0.02)
wait
}
}

actor MessShadow : GeminiTrail
{
Translation "192:192=59:59", "198:198=61:61"
Renderstyle "Add"
+FORCEYBILLBOARD
Alpha 0.95
States
{
Spawn:
MTSU F 0
MTSU F 5 
MTSU G 1 A_Fadeout(0.1)
Goto Spawn+2
}
}

actor MessShadow2 : MessShadow
{
Alpha 0.1
States
{
Spawn:
TNT1 A 5
MTSU FFFFF 1 A_FadeIn(0.08)
MTSU F 1 A_Fadeout(0.025)
wait
}
}



actor MessShadowSpawn : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_SpawnItemEx("MessShadow2", -8, 0, 0)
stop
}
}

actor MessShadowWatcher
{
PROJECTILE
+NOINTERACTION

renderstyle none
States
{
Spawn:
TNT1 A 1
TNT1 A 0 A_JumpIfInTargetInventory("TemaReady", 1, "Check")
TNT1 A 0 A_JumpIfInTargetInventory("MisogiReady", 1, "Check")
TNT1 A 0 A_JumpIfInTargetInventory("RagingDemonReady", 1, "Check")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("MessSuperFlag", 56)//A_TakeInventory("MessSuperFlag", 56, AAPTR_TARGET)
stop
Check:
TNT1 A 1 A_JumpIfInTargetInventory("MessSuperFlag", 1, "Give")
stop
Give:
TNT1 A 3 A_GiveInventory("MessShadowSpawn", 1, AAPTR_TARGET)
Goto Spawn
}
}



actor MessatsuHit
{
Height 0
Radius 0
+NOINTERACTION
-SOLID
+NOGRAVITY
+MISSILE
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveToTarget("MessatsuHitPickup",1)
stop
}
}

actor MessatsuHitPickup : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_GiveToTarget("MessatsuAmmo",3)
stop
}
}

actor GouShoryuSpike
{
PROJECTILE
+NOINTERACTION
damagetype "GouShoryuSpike"
Obituary "%o was spiked by %k's GouShoryuKen."
Radius 1
Height 1
scale 2.5
speed 0
Damage (0)
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Explode(5, 128, 0, 0, 128)
stop
}
}

actor GouShoryuSlam : GouShoryuSpike
{
damagetype "GouShoryuSlam"
renderstyle "add"
Alpha 0.8
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
TNT1 A 0 A_Explode(20, 128, 0, 0, 32)
TNT1 AAAAAAAAAA 0 A_SpawnItemEx("SlamFX", Random(-16, 16), Random(-16, 16), 0, Random(0, 15), 0,Random(0, 15), Random(0,359))
TNT1 AAAAAAAAAA 0 A_SpawnItemEx("SlamFX", Random(-16, 16), Random(-16, 16), 0, Random(0, 15), 0,Random(0, 15), Random(0,359))
stop
}
}

Actor GoshoAttack : Gouhadou
{
Damagetype "GoSho"
Obituary "%o was face palmed by %k's Hyakki Gosho."
Damage (0)
+THRUACTORS
States
{
Spawn:
TNT1 A 0 
Goto "Super::Spawn+1"
SpawnLoop:
TNT1 A 0 A_Explode(50, 80, 0, 0, 64)
TNT1 AAAAAAAAAA 0 A_SpawnItemEx("SlamFX", Random(-16, 16), Random(-16, 16), 0, Random(0, 15), 0,Random(0, 15), Random(0,359))
Goto Death
Death:
TNT1 A 0
stop
}

}
actor SlamFX : Gouhadou
{
+THRUACTORS
-NOGRAVITY
Damage(0)
Renderstyle "Add"
Translation "192:192=59:59", "198:198=61:61"
States
{
Spawn:
GHFX D 0
Goto Super::Spawn+1
SpawnLoop:
GHFX DEFGDEFG 2
Goto FadeLoop
FadeLoop:
GHFX DEFG 2 A_Fadeout
Goto Death
Death:
TNT1 A 0
stop
}
}

actor MessMeterOkay : Powerup
{
Powerup.Duration -2
}
actor MessParryFlag : Powerup
{
Powerup.duration 5
}

actor MessParryAttempt : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 //A_GiveInventory("MessParry", 1)
TNT1 A 0 ACS_NamedExecuteAlways("MessParryHealth", 0, health)
TNT1 A 0 A_ChangeFlag("NOPAIN", 1)
TNT1 A 0 SetPlayerProperty(0, 1, PROP_BUDDHA)
TNT1 A 0 A_ChangeFlag("DONTRIP", 1)
TNT1 A 0 A_ChangeFlag("REFLECTIVE", 1)
TNT1 A 0 A_SpawnItemEx("ParryWatcher", 0, 0, 8)
stop
}
}

actor MessParryRemove: CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
TNT1 A 0 SetPlayerProperty(0, 0, PROP_BUDDHA)
TNT1 A 0 A_ChangeFlag("DONTRIP", 0)
TNT1 A 0 A_ChangeFlag("REFLECTIVE", 0)
stop
}
}

actor MessShoryuFlag : Inventory
{
inventory.amount 1
inventory.maxamount 10
}

actor MessParry : Powerup
{
Powerup.duration 10
}

actor NoParry : Powerup //Can't parry rippers after first hit
{
Powerup.duration 5
}

actor ParryWatcher
{
PROJECTILE
+NOINTERACTION
renderstyle none
States
{
Spawn:
TNT1 A 1
TNT1 A 0 A_JumpIfInTargetInventory("MessParry", 1, "Spawn")
TNT1 A 0 
Goto Remove

Remove:
TNT1 A 0 A_GiveToTarget("MessParryRemove", 1)
stop
}
}

actor ParryHUD
{
PROJECTILE
+NOINTERACTION
renderstyle none
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveToTarget("ParryHUDColor", 1)
Goto Spawn2
Spawn2:
TNT1 A 1
TNT1 A 0 A_JumpIfInTargetInventory("ParryStrength", 1, "Spawn2")
TNT1 A 0 
Goto Remove

Remove:
TNT1 A 0 A_TakeFromTarget("ParryHUDColor", 1)
stop
}
}



actor ParryHUDColor : Powerup
{
Powerup.Color "FF FF FF", 0.2
Powerup.Duration -10
}

actor ParryStrength : PowerDamage
{
damagefactor "normal", 1.5
Powerup.duration 105

}

actor ParryProtect : PowerProtection
{
DamageFactor "Normal", 0.5
Powerup.duration 105
}

actor ParrySpeed : PowerSpeed
{
Powerup.duration 105
Speed 1.30
}

actor ParryBlock : PowerInvulnerable
{
Powerup.duration -1
}

actor MessParrySuccess: CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_GiveInventory("ParryStrength", 1)
TNT1 A 0 A_GiveInventory("ParryProtect", 1)
TNT1 A 0 A_GiveInventory("ParrySpeed", 1)
TNT1 A 0 A_GiveInventory("ParryBlock", 1)
TNT1 A 0 A_SpawnItemEx("ParryHUD", 0 ,0, 16)
TNT1 A 0 A_GiveInventory("MessatsuAmmo", 4)
TNT1 A 0 A_PlaySoundEx("weapon/parry","auto")
TNT1 A 0 ACS_ExecuteAlways(976, 0)
TNT1 A 0 A_TakeInventory("MessParry", 1)
TNT1 AAAAA 0 A_SpawnItemEx("SlamFX", 48, -12, 32, Random(0, 15), 0,Random(0, 15), Random(-60, 60))
TNT1 A 0 A_CheckFloor("End")
TNT1 A 0 ThrustThingZ(0, 25, 0, 0)
stop
End:
TNT1 A 0
stop
}
}

Actor MessPalm : Powerup
{
Powerup.Duration 15
}

Actor MessKick : MessPalm {}

Actor DemonFlipActive : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor MessatsuClassItems : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0
TNT1 A 0 A_GiveInventory("TemaInv", 1)
TNT1 A 0 A_GiveInventory("RagingDemonInv", 1)
TNT1 A 0 A_GiveInventory("MisogiInv", 1)
TNT1 A 0 A_GiveInventory("IsMessatsu", 1)
TNT1 A 0 ACS_NamedExecuteAlways("MessParryCheck", 0)
TNT1 A 0 ACS_NamedExecuteAlways("MessDamageAmmo", 0)
stop
}
}

actor IsMessatsu : Once {}
