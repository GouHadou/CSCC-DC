actor JaxPsycho : CSCCWepBase
{
Weapon.AmmoUse 7
Inventory.Pickupmessage "You got Jax's Telekentic Powers... It's No Use!"
Obituary "$OB_JAXPSYCHO"
weapon.ammotype "JaxPsychoAmmo"
DropItem "TimeSlowWep"
States
{
Spawn:
WEA3 F 1
loop
Ready:
FSTO A 0 ACS_ExecuteAlways(998,0,DYE_JAX)
//JAX7 A 0 A_OverLay(1000,"FlashFire")
JAX7 A 0 A_GunFlash("FlashReady")
Ready2:
JAX7 A 1 A_WeaponReady
FSTO A 0 A_GiveInventory("JaxPsychoAmmoGain",1)
loop
Deselect:
TNT1 A 0 A_JumpIfInventory("JaxHolding",1,"DeselectFire")
Deselect2:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
FSTO A 1 A_Lower
Loop
DeselectFire:
TNT1 A 0 A_GiveInventory("DeselectFireFlag",1)
goto Fire2
DeselectFireEnd:
TNT1 A 0 A_TakeInventory("DeselectFireFlag",1)
goto Deselect2
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
FSTO A 1 A_Raise
Loop

Fire:
TNT1 A 0 A_JumpIfInventory("JaxHolding",1,"Fire2")
FSTO A 0 A_JumpIfNoAmmo("NoAmmo")
FSTO A 0 A_PlaySoundEx("weapon/pchargelong","Weapon")
ChargeUp:
FSTO A 0 A_GiveInventory("JaxSelfSlow",1)
FSTO A 0 A_FireCustomMissile("JaxHoldChargeUp",0,0,8,0)
JAX7 B 1 A_GiveInventory("JaxHoldCharge",5)
JAX7 B 0 A_JumpIfInventory("JaxHoldCharge",150,"Fire1")
loop
Fire1:
TNT1 A 0 A_GiveInventory("JaxHoldStart",1)
TNT1 A 0 A_GiveInventory("JaxHolding",1)
FSTO A 0 A_GiveInventory("JaxSelfSlow",1)
//FSTO A 0 A_FireCustomMissile("JaxHold",0,1,8,0)
FSTO A 0 A_FireCustomMissile("JaxHold",0,0,8,0)
HEAT G 0 A_TakeInventory("JaxPsychoAmmo",6,TIF_NOTAKEINFINITE)
JAX7 B 2
FSTO A 0 A_PlaySoundEx("weapon/gravityhold","Weapon")
JAX7 CD 2
JAX7 E 10
FireWait:
FSTO A 0 A_GiveInventory("JaxPsychoAmmoDeplete",1)
JAX7 E 1 A_WeaponReady
FireWait2:
TNT1 A 0 A_JumpIfInventory("JaxHolding",1,"FireWaitAmmoCheck")
goto Fire2
FireWaitAmmoCheck:
TNT1 A 0 A_JumpIfInventory("JaxPsychoAmmo",1,"FireWait")
Fire2:
TNT1 A 0 A_TakeInventory("JaxHolding",1)
TNT1 A 0 A_GiveInventory("JaxFlinging",1)
CENF A 0 A_PlaySoundEx("weapon/centaurflash","Weapon")
CENF AAAAAAAAAA 0 A_SpawnItemEx("JaxFlashFX", 0, 0, 28, 256/12, 0, random2(256/12), random(0, 359))
CENF AAAAAAAAAA 0 A_SpawnItemEx("JaxFlashFX", 0, 0, 28, 256/12, 0, random2(256/12), random(0, 359))
FSTO A 0 A_GiveInventory("JaxSelfSlow",1)
CENF A 0 A_FireCustomMissile("JaxFlash",0,0,8,0)
//CENF A 0 ACS_ExecuteAlways(976,0)
JAX7 FFGGHHIIJJKK 1 A_GiveInventory("JaxPsychoAmmoGain",1)
TNT1 AAAAAAAAAAAAAA 1 A_GiveInventory("JaxPsychoAmmoGain",1)
JAX7 LLMM 1 A_GiveInventory("JaxPsychoAmmoGain",1)
//JAX7 FGHIJK 2
//TNT1 A 14
//JAX7 LM 2
TNT1 A 0 A_JumpIfInventory("DeselectFireFlag",1,"DeselectFireEnd")
//JAX7 A 1 A_WeaponReady(WRF_NOPRIMARY)
Goto Ready2

AltFire:
JAX7 A 0 A_GunFlash("FlashFire")
JAX7 A 0 A_JumpIfInventory("JaxHolding",1,"AltWait2")
AltWait:
JAX7 A 1 A_GiveInventory("JaxPsychoAmmoGain",1)
JAX7 A 0 A_JumpIfInventory("FlashFireFlag",1,"AltWait")
//JAX7 A 1 A_WeaponReady(WRF_NOSECONDARY)
goto Ready2
AltWait2:
JAX7 E 1 A_GiveInventory("JaxPsychoAmmoDeplete",1)
JAX7 A 0 A_JumpIfInventory("FlashFireFlag",1,"AltWait2")
//JAX7 E 1 A_WeaponReady(WRF_NOSECONDARY)
goto FireWait2

NoAmmo:
FSTO A 0 A_GiveInventory("JaxPsychoAmmoGain",1)
JAX7 A 1 //ACS_Execute(979,0)
Goto Ready2

FlashReady:
JAX8 A 1
loop
FlashFire:
FSTO A 0 A_GiveInventory("FlashFireFlag",1)
FSTO A 0 A_GiveInventory("JaxSelfSlow",1)
FSTO A 0 A_FireCustomMissile("PsychoMine",0,0,8,0)
FSTO A 0 A_PlaySoundEx("misc/wilybeam","Weapon")
JAX8 BC 2
JAX8 D 38
JAX8 C 3
FSTO A 0 A_TakeInventory("FlashFireFlag",1)
goto FlashReady
}
}

actor FlashFireFlag : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor DeselectFireFlag : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor JaxPsycho1 : JaxPsycho
{
Weapon.SlotNumber 1
Tag "$TAG_JAXPSYCHO1"
inventory.icon "GRAVSI"
}

actor JaxPsycho2 : JaxPsycho
{
Weapon.SlotNumber 2
Tag "$TAG_JAXPSYCHO2"
inventory.icon "CENTSI"
states
{
Fire:
FSTO A 0
goto Fire2
}
}

actor JaxPsychoAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 13
+INVENTORY.IGNORESKILL
}

actor JaxPsychoSubAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 34
+INVENTORY.IGNORESKILL
}

actor JaxHoldCharge : Ammo
{
inventory.amount 1
//inventory.maxamount 100
inventory.maxamount 150
+INVENTORY.IGNORESKILL
}

actor JaxPsychoAmmoGain : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_TakeInventory("JaxHoldCharge",999)
TNT1 A 0 A_JumpIfInventory("JaxPsychoAmmo",13,"Pickup3")
TNT1 A 0 A_JumpIfInventory("JaxPsychoSubAmmo",34,"Pickup2")
TNT1 A 0 A_GiveInventory("JaxPsychoSubAmmo",1)
stop
Pickup2:
TNT1 A 0 A_TakeInventory("JaxPsychoSubAmmo",34)
TNT1 A 0 A_GiveInventory("JaxPsychoAmmo",1)
stop
Pickup3:
TNT1 A 0
stop
}
}

actor JaxPsychoAmmoDeplete : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_GiveInventory("JaxPsychoAmmoDeplete2",1)
TNT1 A 0 A_GiveInventory("JaxPsychoAmmoDeplete2",1)
stop
}
}

actor JaxPsychoAmmoDeplete2 : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("JaxHoldCharge",1,"Pickup3")
TNT1 A 0 A_JumpIfInventory("JaxPsychoSubAmmo",1,"Pickup2")
TNT1 A 0 A_TakeInventory("JaxPsychoAmmo",1,TIF_NOTAKEINFINITE)
TNT1 A 0 A_GiveInventory("JaxPsychoSubAmmo",34)
stop
Pickup2:
TNT1 A 0 A_TakeInventory("JaxPsychoSubAmmo",1)
stop
Pickup3:
TNT1 A 0 A_TakeInventory("JaxHoldCharge",1)
stop
}
}


actor JaxPsychoBase
{
PROJECTILE
Damage (0)
scale 2.5
States
{
Death:
Death2:
//BALA A 0 A_Stop
//BALA A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
//BALA A 0 A_Explode(35, 100, 0, 0, 50)
//NAPA EFGHIJKLMNOPQRS 1
ASEX A 0 A_GiveInventory("ProjectileDCode",1)
ASEX A 0 A_PlaySoundEx("weapon/lightningbolt","Weapon")
ASEX A 0 A_Explode(28,125,0,0,75)
ASEX ABCDEF 2
stop
}
}

actor PsychoMine : JaxPsychoBase
{
damagetype "PsychoMine"
Obituary "$OB_JAXMINE"
Speed 50
//Radius 25
//Height 16
Height 32
Radius 32
reactiontime 1

+BOUNCEONWALLS
+BOUNCEONFLOORS
+BOUNCEAUTOOFF
+CANBOUNCEWATER
WALLBOUNCEFACTOR 1.0
BOUNCEFACTOR 1.0

mass 9999
//+SHOOTABLE
-NOBLOCKMAP
+NOBLOOD
//Health 1000
Health 400
+NOTARGETSWITCH
PainChance 256

+GHOST
+THRUSPECIES
+MTHRUSPECIES

//+NORADIUSDMG
DamageFactor "JaxHold", 0.0
DamageFactor "JaxFlash", 0.0
DamageFactor "PsychoMine", 0.0
DamageFactor "PsychoConduit", 0.0

+DONTRIP
States
{
Spawn:
WCPJ M 0
Spawn1:
WCPJ M 0 A_GiveInventory("ProjectileCode",1)
//WCPJ M 0 ACS_NamedExecuteAlways("PlayerSpeciesSynch",0)
WCPJ M 0 ACS_NamedExecuteWithResult("PlayerSpeciesSynch")
WCPJ M 0 A_ChangeFlag("SHOOTABLE",1)
Spawn2:
//TBOL NNMM 1 A_GiveInventory("PsychoMineFunc")
WCPJ MMNNOONN 1 A_GiveInventory("PsychoMineFunc")
loop
Pain:
TBOL N 0 A_PlaySound("item/protoreflect")
goto Spawn2
}
}

actor PsychoMineFunc : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_GiveInventory("CutterFlag",1)
TNT1 A 0 A_JumpIfInventory("CutterFlag",210,"TimeOut")
TNT1 A 0 A_JumpIf(CheckClass("PsychoMineLaunch", AAPTR_DEFAULT, true),"PickupNo")
TNT1 A 0 A_JumpIfInventory("MineFlung",1,"PickupNo")
TNT1 A 0 A_GiveInventory("PsychoMineFlingCheck",1)
//TNT1 A 0 A_JumpIfInTargetInventory("JaxFlinging",1,"Pickup2")
TNT1 A 0 A_ScaleVelocity(0.75)
//TNT1 A 0 A_CountDown
stop
Pickup2:
TNT1 A 0 A_ChangeFlag("DOOMBOUNCE",0)
TNT1 A 0 ACS_ExecuteWithResult(259,1)
REMI B 0 A_ChangeVelocity(cos(pitch)*30, 0, -sin(pitch)*30,CVF_RELATIVE)
TNT1 A 0 A_GiveInventory("MineFlung",1)
stop
PickupNo:
TNT1 A 0
stop
TimeOut:
TNT1 A 0 A_CountDown
stop
}
}

actor PsychoMineFlingCheck : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("JaxFlinging",1,"PickupNo")
TNT1 A 0 A_JumpIfInTargetInventory("JaxFlinging",1,"Pickup2")
stop
Pickup2:
TNT1 A 0 ACS_ExecuteWithResult(259,1)
TNT1 A 0 A_GiveInventory("CutterFlag",70)
TNT1 A 0 A_CustomMissile("PsychoMineLaunch",0,0,0,CMF_AIMDIRECTION,-pitch)
TNT1 A 0 A_GiveInventory("JaxFlinging",1)
stop
PickupNo:
TNT1 A 0
stop
}
}

actor PsychoMineLaunch : PsychoMine
{
Health 200
-DOOMBOUNCE
Speed 30
}

actor MineFlung : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor JaxFlash : CentaurFlash
{
damagetype "JaxFlash"
Obituary "$OB_JAXPSYCHO"
States
{
Spawn:
TNT1 A 0
Spawn2:
TNT1 A 1 A_Explode(7,512,0)
stop
}
}

actor JaxFlashFX : CentaurFlashFX
{
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
goto "Super::Spawn"
}
}

actor PsychoConduit : JaxPsychoBase
{
+NOINTERACTION
+DONTBLAST
radius 1
height 1
damagetype "PsychoConduit"
Obituary "$OB_JAXCONDUIT"
reactiontime 35
RenderStyle "Translucent"
Alpha 0.5
States
{
Spawn:
WCPJ M 0
WCPJ M 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
WCPJ MMNNOONN 1 A_GiveInventory("PsychoConduitFunc")
loop
Death:
WCPJ M 0 A_GiveInventory("JaxConduitResist",1,AAPTR_TRACER)
goto Death2
}
}

actor PsychoConduitFunc : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_Warp(AAPTR_TRACER, 0, 0, 0, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
TNT1 A 0 A_CountDown
stop
}
}

actor JaxHold : JaxFlash
{
damagetype "JaxHold"
Obituary "$OB_JAXPSYCHO"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("JaxHoldFX")
goto Spawn2
}
}

actor JaxHoldFX : GravityHoldFX
{
+MISSILE
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileFXCode",1)
TNT1 A 0 A_SetUserVar("user_angle", random(0, 6))
SpawnLoop:
//TNT1 A 0 A_SpawnItemEx("JaxHoldParticle", 512, 0, 512, 0, 0, -60, user_angle, SXF_ABSOLUTEANGLE|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("JaxHoldParticle", 512, 0, 0, 0, 0, 60, user_angle, SXF_ABSOLUTEANGLE|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SetUserVar("user_angle", user_angle + 6)
TNT1 A 0 A_Countdown
loop
}
}

actor JaxHoldParticle : GravityHoldParticle {}

actor JaxHoldChargeUp : JaxHold
{
+CLIENTSIDEONLY
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_SpawnItemEx("JaxHoldChargeUpFX")
stop
}
}

actor JaxHoldChargeUpFX : JaxHoldFX
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileFXCode",1)
TNT1 A 0 A_SetUserVar("user_angle", random(0, 6))
SpawnLoop:
TNT1 A 0 A_SpawnItemEx("JaxHoldChargeUpParticle", 512, 0, 0, 0, 0, 60, user_angle, SXF_ABSOLUTEANGLE|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SetUserVar("user_angle", user_angle + 6)
TNT1 A 0 A_Countdown
loop
}
}

actor JaxHoldChargeUpParticle : JaxHoldParticle
{
States
{
Spawn:
GHOL A 3
stop
}
}

actor JaxHeldStart : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_GiveInventory("JaxHoldResist",1)
TNT1 A 0 ThrustThingZ(0,30,0,0)
TNT1 A 0 A_SpawnItemEx("JaxHolder")
stop
}
}

actor JaxHolder
{
PROJECTILE
Damage (0)
scale 2.5
+NOINTERACTION
+DONTBLAST
radius 1
height 1
+SERVERSIDEONLY
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TRACER) //target's target to self's tracer
TNT1 A 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_DEFAULT,AAPTR_TARGET) //target <-> tracer
TNT1 A 1 A_JumpIfInTargetInventory("JaxHoldStart",1,"Spawn2S")
stop
Spawn2S:
TNT1 A 1
Spawn2:
TNT1 A 0 A_Warp(AAPTR_TRACER, 0, 0, 0, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
TNT1 A 0 A_JumpIfCloser(600,"Spawn3")
TNT1 A 0 A_JumpIfCloser(1200,"PullIn1")
TNT1 A 0 A_JumpIfCloser(1800,"PullIn2")
TNT1 A 0 A_JumpIfCloser(2400,"PullIn3")
goto Spawn3
PullIn1:
TNT1 A 0 A_FaceTarget
TNT1 A 0 A_GiveInventory("TempPitProtection",1,AAPTR_TRACER)
TNT1 A 0 ThrustThing(angle*256/360,1,0,CallACS("core_gettracer",0))
goto Spawn3
PullIn2:
TNT1 A 0 A_FaceTarget
TNT1 A 0 A_GiveInventory("TempPitProtection",1,AAPTR_TRACER)
TNT1 A 0 ThrustThing(angle*256/360,2,0,CallACS("core_gettracer",0))
goto Spawn3
PullIn3:
TNT1 A 0 A_FaceTarget
TNT1 A 0 A_GiveInventory("TempPitProtection",1,AAPTR_TRACER)
TNT1 A 0 ThrustThing(angle*256/360,3,0,CallACS("core_gettracer",0))
Spawn3:
TNT1 A 1 A_GiveInventory("JaxHeld",1,AAPTR_TRACER)
TNT1 A 0 A_JumpIfInventory("JaxHoldResist",1,"NotDead",AAPTR_TRACER)
stop
NotDead:
TNT1 A 0 A_JumpIfInTargetInventory("JaxHolding",1,"Spawn2")
TNT1 A 1 A_JumpIfInTargetInventory("JaxFlinging",1,"Spawn4")
//TNT1 AA 1 A_JumpIfInTargetInventory("JaxFlinging",1,"Spawn4")
stop
Spawn4:
TNT1 A 0 ACS_ExecuteWithResult(259,2)
TNT1 A 0 A_GiveInventory("TempPitProtection",1,AAPTR_TRACER)
TNT1 A 0 ThrustThing(angle*256/360,30,0,CallACS("core_gettracer",0))
TNT1 A 1 A_SpawnItemEx("PsychoConduit",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
stop
}
}

actor JaxHeld : CustomInventory
{
States
{
Pickup:
//TNT1 A 0 A_GiveInventory("HealthDisplay", 1)
TNT1 A 0 A_GiveInventory("JaxHoldResist",1)
TNT1 A 0 A_CheckFloor("Rise")
TNT1 A 0 A_GiveInventory("JaxHeldSpeed",1)
TNT1 A 0 A_JumpIf(momz<=0,"StopFall")
stop
StopFall:
TNT1 A 0 ThrustThingZ(0,0,0,0)
stop
Rise:
TNT1 A 0 ThrustThingZ(0,5,0,0)
stop
}
}

actor JaxHoldStart : PowerUp { Powerup.Duration 3 }
//actor JaxHolding : PowerUp { Powerup.Duration 700 }
actor JaxFlinging : PowerUp { Powerup.Duration 3 }

actor JaxHoldResist : PowerProtection
{
Powerup.Duration 25
DamageFactor "Normal", 1.0
DamageFactor "JaxHold", 0.0
DamageFactor "JaxFlash", 0.0
}

actor JaxConduitResist : PowerProtection
{
Powerup.Duration 3
DamageFactor "Normal", 1.0
DamageFactor "PsychoConduit", 0.2
}

actor JaxSelfSlow : PowerSpeed
{
Powerup.Duration 15
+POWERSPEED.NOTRAIL
Speed 0.67
}

actor JaxHeldSpeed : PowerSpeed
{
Powerup.Duration 3
//+POWERSPEED.NOTRAIL
Speed 1.9
}

actor JaxHolding : PowerSpeed
{
Powerup.Duration 700
+POWERSPEED.NOTRAIL
Speed 0.85
}