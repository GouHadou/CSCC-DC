actor BubbleArrowBoss : CSCCWepBase
{
Weapon.AmmoUse 0
Weapon.AmmoGive 0
weapon.ammotype "KnuxAmmo"
Weapon.AmmoUse2 0
Weapon.AmmoGive2 0
weapon.ammotype2 "KnuxAmmo2"
Weapon.SlotNumber 1
Inventory.Pickupmessage "$PU_BUBBLEARROW"
Obituary "$OB_BUBBLEARROW"
Tag "$TAG_BUBBLEARROW"
inventory.icon "NULLICON"
dropitem ""
States
{
Spawn:
TNT1 A 1
loop
Deselect:
TNT1 A 0 //A_SelectWeapon("ReselectBubbleArrowBoss")
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
TNT1 A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
TNT1 A 1 A_Raise
Loop

Ready:
TNT1 A 0 A_JumpIfInventory("Once",1,"Ready2")
TNT1 A 0 A_GiveInventory("Once",1)
TNT1 A 0 ACS_ExecuteAlways(991,0,DYE_KNUX)
Ready2:
BBOW A 0 A_JumpIfInventory("KnuxAmmo",240,"Ready3")
BBOW A 1 A_WeaponReady(WRF_NOSECONDARY)
BBOW A 0 //A_GiveInventory("KnuxAmmoPickup",1)
loop
Ready3:
BBOW A 1 A_WeaponReady
BBOW A 0 //A_GiveInventory("KnuxAmmoPickup",1)
loop

Fire:
BBOW BCD 3
Goto Hold
	Fire.Quick:
	BBOW CD 3
	Goto Hold

Hold:
TNT1 A 0 //A_GiveInventory("KnuxBowSpeed",1)
TNT1 A 0 A_JumpIfInventory("IsBot", 1, "BotCheck1")
TNT1 A 0 A_JumpIfInventory("KnuxCharge",45,"Hold.Sticky") //60
TNT1 A 0 A_GiveInventory("KnuxCharge",1)
TNT1 A 0 A_JumpIfInventory("KnuxCharge",17,"Hold.Bouncer")  //30
BBOW E 1 
BBOW E 0 A_Refire("Hold")
Goto Fire.Neutral
Hold.Bouncer:
TNT1 A 0 A_JumpIfInventory("KnuxCharge",18,2)  //30
BBOW H 0 A_PlaySoundEx("weapon/mirroractivate","Weapon")
BBOW F 1 
BBOW F 0 A_Refire("Hold")
Goto Fire.Bouncer
Hold.Sticky:
TNT1 A 0 A_JumpIfInventory("KnuxCharge",46,3)  //30
BBOW H 0 A_PlaySoundEx("weapon/mirroractivate","Weapon")
TNT1 A 0 A_GiveInventory("KnuxCharge",1)
BBOW G 1 
BBOW G 0 A_Refire("Hold")
goto Fire.Sticky


Fire.Neutral:
BBOW H 0 A_PlaySoundEx("weapon/pshot","Weapon")
BBOW H 0 A_GiveInventory("BubbleArrowShooter")
goto Fire.After

Fire.Bouncer:
BBOW H 0 A_PlaySoundEx("weapon/pshot","Weapon")
BBOW H 0 A_GiveInventory("BubbleArrow2Shooter")
goto Fire.After

Fire.Sticky:
BBOW H 0 A_PlaySoundEx("weapon/pshot","Weapon")
BBOW H 0 A_GiveInventory("BubbleArrow3Shooter")
Goto Fire.After


Fire.After:
BBOW H 10
BBOW H 0 A_TakeInventory("KnuxCharge",999)
BBOW I 5
BBOW IIIIII 1 A_Refire("Fire.Quick")
goto Ready2

BotCheck1:
BBOW H 0 A_Jump(16, 1)
Goto Hold+1
TNT1 A 0 A_JumpIfInventory("KnuxCharge",60,"BotCheck3")
TNT1 A 0 A_JumpIfInventory("KnuxCharge",30,"BotCheck2")
TNT1 A 0 A_Jump(64, "AltFire")
Goto Fire.Neutral
BotCheck2:
BBOW H 0 A_Jump(16, "Fire.Bouncer")
Goto Hold.Bouncer
BotCheck3:
BBOW H 0 A_Jump(192, "Fire.Sticky")
Goto Hold.Sticky

AltFire:
BBOW I 0 A_TakeInventory("KnuxAmmo",240)
BBOW I 2 
BBOW J 2 A_GunFlash
BBOW K 15 
BBOW KJI 3
goto Ready2
Flash:
BBO2 AB 2
BBO2 B 0 A_PlaySoundEx("weapon/oilshot","Weapon")
BBO2 C 10 A_SpawnItemEx("KnuxBouncer", cos(-pitch)*25,8,20+(sin(pitch)*25), cos(-pitch)*20, 0, sin(-pitch)*20, 0, SXF_TRANSFERTRANSLATION) 
BBO2 BA 2
stop
NoAmmo:
TNT1 A 0
goto Ready2
}
}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor KnuxAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 480
}
actor KnuxAmmo2 : KnuxAmmo
{
inventory.maxamount 640
}

actor KnuxBowSpeed : PowerSpeed
{
inventory.amount 1
inventory.maxamount 1
Powerup.Duration 17
Speed 0.4
}

actor KnuxFoamSlow : PowerSpeed
{
inventory.amount 1
inventory.maxamount 1
Powerup.Duration 35
Speed 0.65
}

actor KnuxAmmoBowPrimePickup : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_GiveInventory("KnuxAmmo",1)

TNT1 A 0 A_JumpIfInventory("KnuxMineItem",6,"Full")
TNT1 A 0 A_JumpIfInventory("KnuxAmmo2",640,"GiveMine")
TNT1 A 0 A_GiveInventory("KnuxAmmo2",1)
stop
GiveMine:
TNT1 A 0 A_TakeInventory("KnuxAmmo2",640)
TNT1 A 0 A_GiveInventory("KnuxMineItem",1)
stop
Full:
TNT1 A 0
stop
}
}

actor KnuxAmmoPickup : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_GiveInventory("KnuxAmmo",2)

TNT1 A 0 A_JumpIfInventory("KnuxMineItem",6,"Full")
TNT1 A 0 A_JumpIfInventory("KnuxAmmo2",639,"GiveMine")
TNT1 A 0 A_GiveInventory("KnuxAmmo2",2)
stop
GiveMine:
TNT1 A 0 A_TakeInventory("KnuxAmmo2",639)
TNT1 A 0 A_GiveInventory("KnuxMineItem",1)
stop
Full:
TNT1 A 0
stop
}
}

actor KnuxMineItem : CustomInventory
{
inventory.amount 1
inventory.maxamount 6
inventory.icon "KnuxIcon"
+INVBAR
+INVENTORY.KEEPDEPLETED
states
{
Spawn:
TNT1 A 0
stop
Use:
TNT1 A 0 A_jumpIfInventory("Knuxcharge",1,"Nope")
TNT1 A 0 A_PlaySoundEx("weapon/CrackerThrow","Weapon")
TNT1 A 0 A_GiveInventory("KnuxMineShooter",1)
stop

Nope:
TNT1 A 0
Fail
}
}


actor KnuxMineThrust : CustomInventory
{
states
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 ThrustThingZ(0,180,0,0)
stop

Nope:
TNT1 A 0
Fail
}
}

actor KnuxCharge : Inventory
{
inventory.amount 1
inventory.maxamount 60
}

actor Sticky : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor Detonated : Sticky {}
actor KnuxNoBubble : Once {}
actor KnuxBubbleFlag : Once {}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor BubbleArrow : BasicProjectile
{
Obituary "$OB_BUBBLEARROW"
+FORCEXYBILLBOARD
+HITTRACER
damagetype "BubbleArrow"
Speed 50//40
Damage (10)
radius 12
height 8
scale 2.5
States
{
Spawn:
BARW A 5
BARW A 0 A_SpawnItemEx("BubbleArrowTrailS", 0, 0, 0, Random(-1, 1), Random(-1, 1), Random(1, 3),0,1)
Loop
XDeath:
TNT1 A 0 A_SetScale(scalex*0.75, scaley*0.75)
TNT1 A 0 A_JumpifInventory("KnuxBubbleFlag",1,"Terminate",AAPTR_TRACER)
TNT1 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
TNT1 A 0 A_Explode(10, 96, 0,0,64)
Goto Death+3
Death:
TNT1 A 0 A_SetScale(scalex*0.75, scaley*0.75)
TNT1 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
TNT1 A 0 A_Explode(10, 96, 1,0,64)
TNT1 A 0 A_Stop
NAPA EFGHIJKLMNOPQRS 1
stop

Terminate:
TNT1 A 0 
stop
}
}

	actor BubbleArrow_BLUE : BubbleArrow{translation "222:222=74:74","103:103=205:205", "97:97=196:196",   "209:221=205:205"}
	actor BubbleArrow_RED : BubbleArrow{translation "222:222=41:41","103:103=171:171", "97:97=170:170",   "209:221=171:171"}
	actor BubbleArrow_GREEN : BubbleArrow{translation "222:222=128:128","103:103=104:104", "97:97=138:138",   "209:221=104:104"}
	actor BubbleArrow_YELLOW : BubbleArrow{translation "222:222=232:232","103:103=229:229", "97:97=214:214",  "209:221=229:229"}

actor BubbleArrow2 : BubbleArrow
{
damagetype "BubbleArrow2"
+HEXENBOUNCE
-ALLOWBOUNCEONACTORS
+DONTBLAST
//-BOUNCEONFLOORS
BounceFactor 0.91
WallBounceFactor 0.91
Bouncecount 2
//Speed 50
Damage (10)
states
{
Spawn:
BARW B 4 
BARW B 0  A_SpawnItemEx("BubbleArrowTrailS", 0, 0, 0, Random(-1, 1), Random(-1, 1), Random(1, 3),0,1)
Loop
XDeath:
TNT1 A 0 A_JumpifInventory("KnuxBubbleFlag",1,"Terminate",AAPTR_TRACER)
TNT1 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
TNT1 A 0 A_Explode(15, 128, 0,0,96)
Goto Death+2
Death:
TNT1 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
TNT1 A 0 A_Explode(15, 128, 1,0,96)
TNT1 A 0 A_Stop
NAPA EFGHIJKLMNOPQRS 1
stop
}
}

	actor BubbleArrow2_BLUE : BubbleArrow2{translation "222:222=74:74","103:103=205:205", "97:97=196:196",   "209:221=205:205"}
	actor BubbleArrow2_RED : BubbleArrow2{translation "222:222=41:41","103:103=170:170", "97:97=170:170",   "209:221=171:171"}
	actor BubbleArrow2_GREEN : BubbleArrow2{translation "222:222=128:128","103:103=104:104", "97:97=138:138",   "209:221=104:104"}
	actor BubbleArrow2_YELLOW : BubbleArrow2{translation "222:222=232:232","103:103=229:229", "97:97=214:214",  "209:221=229:229"}

actor BubbleArrow3 : BubbleArrow
{
damagetype "BubbleArrow3"
Speed 65
Args 0
Damage (20)
states
{
Spawn:
BARW C 2
BARW C 0 A_SpawnItemEx("BubbleArrowTrail", 0, 0, 0, Random(-1, 1), Random(-1, 1), Random(1, 3),0,1)
Loop
Death:
BARW C 0 A_PlaySound("weapon/crashland")
BARW C 0 A_CheckFloor("Death.Floor")
BARW C 0 A_CheckCeiling("Death.Ceiling")
Goto Death.Wall

Death.Wall:
BARW C 1
BARW C 0 A_SetArg(0,Args[0]+2)
BARW C 1 A_JumpIf(Args[0] < 35,"Death.Wall")
BARW C 1 A_Jump(256,"Detonate")
Goto Detonate
/*
BARW CCCCCCCCCC 1
BARW CCCCCCCCCC 1
BARW CCCCCCCCCC 1
*/

Death.Floor:
BARW L 1
BARW L 0 A_SetArg(0,Args[0]+2)
BARW L 1 A_JumpIf(Args[0] < 35,"Death.Floor")
BARW L 1 A_Jump(256,"Detonate")
Goto Detonate

Death.Ceiling:
BARW M 1
BARW M 0 A_SetArg(0,Args[0]+2)
BARW M 1 A_JumpIf(Args[0] < 35,"Death.Ceiling")
BARW M 1 A_Jump(256,"Detonate")
Goto Detonate

XDeath:
TNT1 A 0 A_JumpifInventory("KnuxBubbleFlag",1,"Terminate",AAPTR_TRACER)
TNT1 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
TNT1 A 0 A_Explode(25, 128, 0,0,96)
Goto Detonate+2
Detonate:
TNT1 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
TNT1 A 0 A_Explode(45, 128, 1,0,96)
TNT1 A 0 A_Stop
NAPA EFGHIJKLMNOPQRS 1
stop
}
}

	actor BubbleArrow3_BLUE : BubbleArrow3{translation "222:222=74:74","103:103=205:205", "97:97=196:196",   "209:221=205:205"}
	actor BubbleArrow3_RED : BubbleArrow3{translation "222:222=41:41","103:103=170:170", "97:97=170:170" ,   "209:221=171:171"}
	actor BubbleArrow3_GREEN : BubbleArrow3{translation "222:222=128:128","103:103=104:104", "97:97=138:138",   "209:221=104:104"}
	actor BubbleArrow3_YELLOW : BubbleArrow3{translation "222:222=232:232","103:103=229:229", "97:97=214:214",  "209:221=229:229"}



actor BubbleArrow4 : BubbleArrow
{
damagetype "BubbleArrowSplit"
states
{
Spawn:
BARW A 1
TNT1 A 0 A_PlaySoundEx("weapon/BurstBubAppear", "Weapon")
BARW A 0 A_SpawnItemEx("BubbleDangerTrail", 0, 0, 0, Random(-1, 1), Random(-1, 1), Random(-1, 1),0,1)
Loop

XDeath:
TNT1 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
TNT1 A 0 A_Explode(20, 128, 0,0,96)
Goto Detonate+2
Detonate:
TNT1 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
TNT1 A 0 A_Explode(30, 128, 1,0,96)
TNT1 A 0 A_Stop
NAPA EFGHIJKLMNOPQRS 1
stop
}
} 

	actor BubbleArrow4_BLUE : BubbleArrow4{translation "222:222=74:74","103:103=205:205", "97:97=196:196",   "209:221=205:205"}
	actor BubbleArrow4_RED : BubbleArrow4{translation "222:222=41:41","103:103=170:170", "97:97=170:170" ,   "209:221=171:171"}
	actor BubbleArrow4_GREEN : BubbleArrow4{translation "222:222=128:128","103:103=104:104", "97:97=138:138",   "209:221=104:104"}
	actor BubbleArrow4_YELLOW : BubbleArrow4{translation "222:222=232:232","103:103=229:229", "97:97=214:214",  "209:221=229:229"}
	

actor BubbleArrow5 : BubbleArrow2
{
damagetype "BubbleArrowSplit"
BounceCount 5
Speed 55
Damage (25) //45
+SEEKERMISSILE
+SCREENSEEKER
states
{
Spawn:
BARW PP 2 A_SeekerMissile(0, 1, SMF_LOOK|SMF_PRECISE, 256, 7) 
BARW P 0  A_SpawnItemEx("BubbleArrowTrailS", 0, 0, 0, Random(-1, 1), Random(-1, 1), Random(1, 3),0,1)
Loop

XDeath:
TNT1 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
TNT1 A 0 A_Explode(15, 128, 0,0,96)
Goto Death+2
Death:
TNT1 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
TNT1 A 0 A_Explode(15, 128, 1,0,96)
TNT1 A 0 A_Stop
NAPA EFGHIJKLMNOPQRS 1
stop
}
}

	actor BubbleArrow5_BLUE : BubbleArrow5{translation "222:222=74:74","103:103=205:205", "97:97=196:196",   "209:221=205:205"}
	actor BubbleArrow5_RED : BubbleArrow5{translation "222:222=41:41","103:103=170:170", "97:97=170:170" ,   "209:221=171:171"}
	actor BubbleArrow5_GREEN : BubbleArrow5{translation "222:222=128:128","103:103=104:104", "97:97=138:138",   "209:221=104:104"}
	actor BubbleArrow5_YELLOW : BubbleArrow5{translation "222:222=232:232","103:103=229:229", "97:97=214:214",  "209:221=229:229"}
	

actor BubbleDangerTrail : BasicProjectile
{
scale 2.5
-SOLID
height 16
radius 12
Reactiontime 21
Damage (0)
States
{
Spawn:
TNT1 A 2
Spawn2:
TNT1 A 0 A_ScaleVelocity(0.8)
BARW N 5 A_CountDown 
TNT1 A 0 A_ScaleVelocity(0.8)
BARW O 5 A_CountDown 
loop

Crash:
TNT1 A 0 A_Explode(5, 68, 0,0,68)
TNT1 A 0 A_PlaySoundEx("weapon/BurstExplode", "Weapon")
HARD EDCB 2
stop

Death:
TNT1 A 0 A_Explode(5, 68, 0,0,68)
TNT1 A 0 A_PlaySoundEx("weapon/Burstpop", "Weapon")
BARW E 3
stop
}
}	

actor KnuxBouncer
{
//PROJECTILE
+MTHRUSPECIES
+MISSILE
+NOGRAVITY
//+NOTARGETSWITCH
+QUICKTORETALIATE
radius 35
height 70
damagetype "KnuxRebound"
damage (0)
+GHOST
+DOOMBOUNCE
+SHOOTABLE
+INVULNERABLE
Health 100
+REFLECTIVE
+SHIELDREFLECT
+RIPPER
+NORADIUSDMG
+NODAMAGETHRUST
+FORCEYBILLBOARD
+DONTRIP
+DONTBLAST
scale 3.5
Mass 9999999
speed 20
PainChance "BubbleArrow", 255
PainChance "BubbleArrow2", 255
PainChance "BubbleArrow3", 255
PainChance 255

reactiontime 600
obituary "$OB_BUBBLEBOUNCER"
States
{
Spawn:
BARW F 0
BARW F 0 A_SetArg(1,ACS_ExecuteWithResult(257))
BARW F 0 A_SetArg(0, 0)
BAWW F 0 A_GiveToTarget("KnuxNoBubble", 1)
BAWW F 0 A_GiveInventory("KnuxBubbleFlag", 1)
//BARW F 0 A_TransferPointer(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_TRACER)
BARW F 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TARGET)
BARW FFFFFFFFFF 1 ACS_ExecuteAlways(247,0,-10,48, 1)
BARW FF 1 A_Stop
BARW F 0 A_ChangeFlag("FLOATBOB", 1)
BARW F 0 Thing_ChangeTID(0, Args[1] - 100)
BARW F 0 A_ChangeFlag("INVULNERABLE", 0)
BAWW F 0 A_TakeFromTarget("KnuxNoBubble", 1)
Goto Spawn2
Spawn2:
//BARW F 0 A_TransferPointer(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TRACER, AAPTR_TARGET)
BARW F 0 A_RearrangePointers(AAPTR_TRACER, AAPTR_DEFAULT, AAPTR_DEFAULT)
BARW F 1 A_SetArg(0, Args[0]+1)
BARW F 0 ACS_ExecuteAlways(259, 0, 1)
BARW F 0 ACS_ExecuteAlways(247,0,-10,48, 1)
BARW F 0 A_JumpIf(Args[0] >= 300, "Death")//A_Countdown
BARW F 0 A_JumpIfInTargetInventory("KnuxNoBubble", 1, "Death")
BARW F 0 A_JumpIfInTargetInventory("IsDead", 1, "Death")
loop
Pain.BubbleArrow:
BARW F 1
BARW F 0 A_JumpIf(Args[1] != ACS_ExecuteWithResult(257), "Spawn2")
BARW F 2 ACS_ExecuteAlways(259, 0, 1)
BARW F 0 A_ChangeFlag("THRUACTORS", 1)
BARW H 0 A_Changeflag("SHOOTABLE",0)
BARW F 0 A_Fadeout(0.5)
BARW F 1 A_GiveInventory("BubbleArrow4Shooter")
Goto Death
Pain.BubbleArrow2:
BARW F 1
BARW F 0 A_JumpIf(Args[1] != ACS_ExecuteWithResult(257), "Spawn2")
BARW F 2 ACS_ExecuteAlways(259, 0, 1)
BARW F 0 A_ChangeFlag("THRUACTORS", 1)
BARW H 0 A_Changeflag("SHOOTABLE",0)
BARW F 0 A_Fadeout(0.5)
BARW F 1 A_GiveInventory("BubbleArrow5Shooter")
Goto Death
Pain.BubbleArrow3:
BARW F 1
BARW F 0 A_JumpIf(Args[1] != ACS_ExecuteWithResult(257), "Spawn2")
BARW F 2 ACS_ExecuteAlways(259, 0, 1)
BARW F 0 A_ChangeFlag("THRUACTORS", 1)
BARW H 0 A_Changeflag("SHOOTABLE",0)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,45,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,90,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,135,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,180,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,225,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,270,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,315,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
TNT1 A 0 A_Explode(50, 160, 1,0,128)
TNT1 A 0 A_Stop
NAPA EFGHIJKLMNOPQRS 1
BARW F 0 A_Fadeout(0.5)
Goto Death
Pain:
BARW F 0 A_TransferPointer(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TRACER, AAPTR_TARGET)
Goto Spawn2
Death:
TNT1 A 0 A_PlaySoundEx("weapon/Burstpop", "Weapon")
BARW G 3
stop
}
}


actor KnuxMineSpawner   
{
+MISSILE
+NOTARGETSWITCH
+NOBLOOD
+DONTBLAST
+SKYEXPLODE
+ThruActors
+BOUNCEONWALLS
+BOUNCEONCEILINGS
Obituary "$OB_KnuxMINE"
radius 8
height 8
scale 2.5
speed 0
states
{
Spawn:
BARW H 0
BARW H 0 ThrustthingZ(0,50,1,0)
BARW H 3
goto spawn+1
Death:
BARW H 0 A_SpawnitemEx("KnuxMine",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
stop
}
}

	actor KnuxMineSpawner_BLUE : KnuxMineSpawner
	{translation "103:103=205:205", "195:195=199:199" , "4:4=196:196"
	states
	{
	Death:
	BARW H 0 A_SpawnitemEx("KnuxMine_BLUE",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
	stop
	}
	}

	actor KnuxMineSpawner_RED : KnuxMineSpawner
	{translation "103:103=171:171", "195:195=45:45" , "4:4=170:170"
	states
	{
	Death:
	BARW H 0 A_SpawnitemEx("KnuxMine_RED",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
	stop
	}
	}

	actor KnuxMineSpawner_GREEN : KnuxMineSpawner
	{translation "103:103=104:104", "195:195=131:131" , "4:4=138:138"
	states
	{
	Death:
	BARW H 0 A_SpawnitemEx("KnuxMine_GREEN",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
	stop
	}
	}

	actor KnuxMineSpawner_YELLOW : KnuxMineSpawner
	{translation "103:103=299:299", "195:195=223:223" , "4:4=214:214"
	states
	{
	Death:
	BARW H 0 A_SpawnitemEx("KnuxMine_YELLOW",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
	stop
	}
	}


actor KnuxMine  
{
+MISSILE
+NOBLOOD
+DONTBLAST
+SEEKERMISSILE
+NOTARGETSWITCH
+SHOOTABLE
+NOGRAVITY
+NOCLIP
+LOOKALLAROUND
Obituary "$OB_KNUXMINE"
radius 8
height 8
speed 0
health 10
scale 2.5
Var int user_tally;
Args 96 , 32
states
{
Spawn:
BARW J 0
BARW J 0 ACS_NamedExecuteAlways("ObjectTally",0)
BARW I 1 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_TARGET)
BARW I 10 
BARW J 1 A_PlaySoundEX("weapon/flashstopper","Weapon")
BARW IJIJIJ 3 A_FadeTo(0.3)
BARW J 0 
BARW J 0 A_Jump(256,"Scan")
Goto Scan
Scan:
BARW J 1 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT)
BARW J 0 A_JumpifTracercloser(Args[1],"KnuxThrust")
BARW J 0 A_Jumpif(user_tally >= 4,"Perish")
BARW J 0  A_LookEx(LOF_NOSOUNDCHECK, 1, Args[0], Args[0], 360, "Targetcheck")
BARW J 0 A_Jump(256,"Scan")
Goto Scan
Targetcheck:
BARW J 1 A_Jumpifcloser(Args[0],"Targetcheck2")
BARW J 0 A_Jump(256,"Scan")
Goto Scan
Targetcheck2:
BARW J 0 A_JumpIf(IsPointerEqual(AAPTR_TARGET,AAPTR_TRACER) == True, "Scan")//You Are The Owner
Goto Explode
KnuxThrust:
BARW J 0 A_Spawnitemex("KnuxExplosionSelf",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
BARW J 0 A_GiveInventory("KnuxMineThrust",1,AAPTR_TRACER)
Goto Explode+7
Explode:
BARW J 0 ACS_NamedExecuteAlways("ObjectDeathTally",0)
BARW J 1 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT)
BARW J 0 A_Changeflag("SHOOTABLE",0)
BARW J 0 A_FadeTo(1.0,1.0)
BARW I 10 A_PlaySoundEX("weapon/BombAlarm","Soundslot6")
TNT1 A 0 A_StopsoundEX("Soundslot6")
TNT1 A 0 A_Spawnitemex("KnuxExplosion",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,90,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,180,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,270,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 1
Stop
Death:
BARW J 0 ACS_NamedExecuteAlways("ObjectDeathTally",0)
Goto Perish
Perish:
BARW J 0 A_FadeTo(1.0,1.0)
HARD BCDE 3
BARW J 0
stop
}
}

actor KnuxMine_BLUE : KnuxMine
{
Designatedteam 0
states
{

Targetcheck2:
BARW J 0 A_Jumpifintargetinventory("LightTeamFlag",1,"Scan") //You Are A Teammate
BARW J 0 A_Jumpifintargetinventory("CossackTeamFlag",1,"Scan") //You Are A Teammate
BARW J 0 A_Jumpifintargetinventory("KingTeamFlag",1,"Scan") //You Are A Teammate
Goto Explode
}
}

actor KnuxMine_RED : KnuxMine
{
Designatedteam 1
states
{
Targetcheck2:
BARW J 0 A_Jumpifintargetinventory("WilyTeamFlag",1,"Scan") //You Are A Teammate
BARW J 0 A_Jumpifintargetinventory("CossackTeamFlag",1,"Scan") //You Are A Teammate
BARW J 0 A_Jumpifintargetinventory("KingTeamFlag",1,"Scan") //You Are A Teammate
Goto Explode
}
}

actor KnuxMine_GREEN : KnuxMine
{
Designatedteam 2
states
{
Targetcheck2:
BARW J 0 A_Jumpifintargetinventory("WilyTeamFlag",1,"Scan") //You Are A Teammate
BARW J 0 A_Jumpifintargetinventory("LightTeamFlag",1,"Scan") //You Are A Teammate
BARW J 0 A_Jumpifintargetinventory("KingTeamFlag",1,"Scan") //You Are A Teammate
Goto Explode
}
}

actor KnuxMine_YELLOW : KnuxMine
{
Designatedteam 3
states
{
Targetcheck2:
BARW J 0 A_Jumpifintargetinventory("WilyTeamFlag",1,"Scan") //You Are A Teammate
BARW J 0 A_Jumpifintargetinventory("LightTeamFlag",1,"Scan") //You Are A Teammate
BARW J 0 A_Jumpifintargetinventory("CossackTeamFlag",1,"Scan") //You Are A Teammate
Goto Explode
}
}


actor KnuxExplosion 
{
PROJECTILE
+NOINTERACTION
+NOCLIP
+DONTBLAST
+FORCEXYBILLBOARD
Speed 0
//
Damagetype "KnuxMine"
Obituary "$OB_KNUXMINE"
radius 8
height 8
scale 3.5
States
{
Spawn:
NAPC A 0 
NAPC D 0 A_Explode(25, 128, 1,0,80)
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
ASEX ABCDEF 1 
stop
}
}

actor KnuxExplosionSelf : KnuxExplosion
{
States
{
Spawn:
NAPC A 0 
NAPC D 0 A_Explode(5, 128, 1,0,80)
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
ASEX ABCDEF 1 
stop
}
}

actor KnuxFoamCloud : BasicProjectile
{
Obituary "$OB_BUBBLEARROW"
+FORCEXYBILLBOARD
+HITTRACER
+BOUNCEONWALLS
damagetype "BubbleArrow"
Speed 10
Damage (0)
radius 12
height 8
scale 2.0
Args 17
ReactionTime 17
States
{
Spawn:
BARW A 0
BARW RST 2 A_SpawnItemEx("BubbleArrowTrailS", random(-15,15), random(-15,15), random(-15,15), 0, 0, Random(1, 3),0,1)
BARW A 0 A_CountDown
Loop

Crash:
TNT1 A 1 
TNT1 A 0 A_JumpIf(IsPointerEqual(AAPTR_TRACER,AAPTR_NULL) == False, "FoamCheck")
Goto Death

FoamCheck:
TNT1 A 0 A_JumpIfInventory("KnuxFoamSlow",1,"Death",AAPTR_TRACER)
TNT1 A 0 A_ChangeFlag("NOINTERACTION",True)
TNT1 A 0 A_ChangeFlag("NOCLIP",True)
Goto FoamStick

FoamStick:
TNT1 A 0 A_GiveInventory("KnuxFoamSlow",1,AAPTR_TRACER)
BARW RR 1 A_Warp(AAPTR_TRACER,0,0,10,0,WARPF_COPYINTERPOLATION)
BARW RR 0 A_SpawnItemEx("BubbleArrowTrailS", random(-15,15), random(-15,15), random(-15,15), 0, 0, Random(1, 3),0,1)
BARW SS 1 A_Warp(AAPTR_TRACER,0,0,10,0,WARPF_COPYINTERPOLATION)
BARW SS 0 A_SpawnItemEx("BubbleArrowTrailS", random(-15,15), random(-15,15), random(-15,15), 0, 0, Random(1, 3),0,1)
BARW TT 1 A_Warp(AAPTR_TRACER,0,0,10,0,WARPF_COPYINTERPOLATION)
BARW TT 0 A_SpawnItemEx("BubbleArrowTrailS", random(-15,15), random(-15,15), random(-15,15), 0, 0, Random(1, 3),0,1)
TNT1 A 0 A_SetArg(0,Args[0]-1)
TNT1 A 0 A_JumpIfInventory("CutterFlag",999,"Death",AAPTR_TRACER)
TNT1 A 0 A_JumpIfInventory("IsDead",1,"Death",AAPTR_TRACER)
TNT1 A 0 A_JumpIf(Args[0] > 0, "FoamStick")
Goto Death

Death:
TNT1 A 0 A_Stop
stop

Terminate:
TNT1 A 0 
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor KnuxMineHelper : BasicHelper
{
+MISSILE
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",999,"DeathTake")
TNT1 A 1 
TNT1 A 0 A_GivetoTarget("KnuxAmmoPickup",1)
Goto Spawn
Death:
DeathTake:
TNT1 A 0
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Effect Actors|------------------------------
//[+]========================================================================[+]

actor BubbleArrowTrail
{
scale 2.5
-SOLID
+NOGRAVITY
+CLIENTSIDEONLY
height 0
radius 0
States
{
Spawn:
TNT1 A 2
BARW D 10 
Death:
BARW E 3
stop
}
}

actor BubbleArrowTrailS : BubbleArrowTrail
{
States
{
Spawn:
TNT1 A 2
BUBS E 10
Goto Death
}
}
//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]

actor BubbleArrowShooter : BasicTeamColorPickup
{
states
{
FireX:
BUST B 1 A_FireCustomMissile("BubbleArrow",0,0,8,0)
goto Done
//---
FireB:
BUST B 1 A_FireCustomMissile("BubbleArrow_BLUE",0,0,8,0)
goto Done
FireR:
BUST B 1 A_FireCustomMissile("BubbleArrow_RED",0,0,8,0)
goto Done
FireG:
TNT1 A 0 A_FireCustomMissile("BubbleArrow_GREEN",0,0,8,0)
goto Done
FireY:
TNT1 A 0 A_FireCustomMissile("BubbleArrow_YELLOW",0,0,8,0)
goto Done
}
}

actor BubbleArrow2Shooter : BasicTeamColorPickup
{
states
{
FireX:
BUST B 1 A_FireCustomMissile("BubbleArrow2",0,0,8,0)
goto Done
//---
FireB:
BUST B 1 A_FireCustomMissile("BubbleArrow2_BLUE",0,0,8,0)
goto Done
FireR:
BUST B 1 A_FireCustomMissile("BubbleArrow2_RED",0,0,8,0)
goto Done
FireG:
TNT1 A 0 A_FireCustomMissile("BubbleArrow2_GREEN",0,0,8,0)
goto Done
FireY:
TNT1 A 0 A_FireCustomMissile("BubbleArrow2_YELLOW",0,0,8,0)
goto Done
}
}

actor BubbleArrow3Shooter : BasicTeamColorPickup
{
states
{
FireX:
BUST B 1 A_FireCustomMissile("BubbleArrow3",0,0,8,0)
goto Done
//---
FireB:
BUST B 1 A_FireCustomMissile("BubbleArrow3_BLUE",0,0,8,0)
goto Done
FireR:
BUST B 1 A_FireCustomMissile("BubbleArrow3_RED",0,0,8,0)
goto Done
FireG:
TNT1 A 0 A_FireCustomMissile("BubbleArrow3_GREEN",0,0,8,0)
goto Done
FireY:
TNT1 A 0 A_FireCustomMissile("BubbleArrow3_YELLOW",0,0,8,0)
goto Done
}
}

actor BubbleArrow4Shooter : BasicTeamColorPickup
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInTargetInventory("LightTeamFlag",1,"FireB")
TNT1 A 0 A_JumpIfInTargetInventory("WilyTeamFlag",1,"FireR")
TNT1 A 0 A_JumpIfInTargetInventory("CossackTeamFlag",1,"FireG")
TNT1 A 0 A_JumpIfInTargetInventory("KingTeamFlag",1,"FireY")
TNT1 A 0 A_JumpIfInTargetInventory("IsInGame",1,"FireX")
TNT1 A 0
Done:
TNT1 A 0
stop
FireX:
BUST B 0 A_CustomMissile("BubbleArrow4",32+sin(-pitch)*48,0,0,CMF_AIMDIRECTION |CMF_OFFSETPITCH,-pitch)
goto Done
//---
FireB:
BUST B 0 A_CustomMissile("BubbleArrow4_BLUE",32+sin(-pitch)*48,0,0,CMF_AIMDIRECTION |CMF_OFFSETPITCH,-pitch)
goto Done
FireR:
BUST B 0 A_CustomMissile("BubbleArrow4_RED",32+sin(-pitch)*48,0,0,CMF_AIMDIRECTION |CMF_OFFSETPITCH,-pitch)
goto Done
FireG:
BUST B 0 A_CustomMissile("BubbleArrow4_GREEN",32+sin(-pitch)*48,0,0,CMF_AIMDIRECTION |CMF_OFFSETPITCH,-pitch)
goto Done
FireY:
BUST B 0 A_CustomMissile("BubbleArrow4_YELLOW",32+sin(-pitch)*48,0,0,CMF_AIMDIRECTION |CMF_OFFSETPITCH,-pitch)
goto Done
}
}

actor BubbleArrow5Shooter : BubbleArrow4Shooter
{
states
{
FireX:
BUST B 0 A_CustomMissile("BubbleArrow5",32+sin(-pitch)*48,0,0,CMF_AIMDIRECTION |CMF_OFFSETPITCH,-pitch)
goto Done
//---
FireB:
BUST B 0 A_CustomMissile("BubbleArrow5_BLUE",32+sin(-pitch)*48,0,0,CMF_AIMDIRECTION |CMF_OFFSETPITCH,-pitch)
goto Done
FireR:
BUST B 0 A_CustomMissile("BubbleArrow5_RED",32+sin(-pitch)*48,0,0,CMF_AIMDIRECTION |CMF_OFFSETPITCH,-pitch)
goto Done
FireG:
BUST B 0 A_CustomMissile("BubbleArrow5_GREEN",32+sin(-pitch)*48,0,0,CMF_AIMDIRECTION |CMF_OFFSETPITCH,-pitch)
goto Done
FireY:
BUST B 0 A_CustomMissile("BubbleArrow5_YELLOW",32+sin(-pitch)*48,0,0,CMF_AIMDIRECTION |CMF_OFFSETPITCH,-pitch)
goto Done
}
}


actor KnuxMineShooter : BasicTeamColorPickup
{
states
{
FireX:
BUST B 1 A_FireCustomMissile("KnuxMineSpawner",0,0,8,0)
goto Done
//---
FireB:
BUST B 1 A_FireCustomMissile("KnuxMineSpawner_BLUE",0,0,8,0)
goto Done
FireR:
BUST B 1 A_FireCustomMissile("KnuxMineSpawner_RED",0,0,8,0)
goto Done
FireG:
TNT1 A 0 A_FireCustomMissile("KnuxMineSpawner_GREEN",0,0,8,0)
goto Done
FireY:
TNT1 A 0 A_FireCustomMissile("KnuxMineSpawner_YELLOW",0,0,8,0)
goto Done
}
}

