
actor NadeSpam : CorporalWeaponBase
{
Tag "$TAG_NADESPAM"
Weapon.AmmoUse 1
Weapon.AmmoGive 16
weapon.ammotype "NadeAmmoClass"
Weapon.SlotNumber 3
inventory.icon "NADEI"
Obituary "$OB_NADESPAM"
dropitem "NadeSpamWepCS"

States
{
Ready:
NADE B 0 ACS_ExecuteAlways(998,0,413)
TNT1 A 4 A_WeaponReady(WRF_NOPRIMARY)
NADE I 2 Offset(0,64) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
NADE I 2 A_WeaponReady(WRF_NOPRIMARY)
NADE J 2 Offset(0,64) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
NADE J 2 A_WeaponReady(WRF_NOPRIMARY)
Ready2:
TNT1 A 0
NADE B 1 A_WeaponReady(WRF_ALLOWRELOAD)
//NADE B 1 A_GiveInventory("NadeAmmoClass",1)
loop
Reload:
NADE I 3
TNT1 A 10 A_WeaponReady(WRF_NOPRIMARY)
TNT1 A 0 A_GiveInventory("NadeAmmoClass",1)
TNT1 A 5 A_WeaponReady
TNT1 A 0 A_JumpIfInventory("NadeAmmoClass",16,"ReloadEnd")
goto Reload+2
ReloadEnd:
//lol
goto KnifeEnd
Fire: 
NADE B 0 A_JumpIfNoAmmo("NoAmmo") 
NADE B 0 A_AlertMonsters
NADE B 0 A_GiveInventory ("NadeSpamM1")
NADE B 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
NADE CDEFGH 1
NADE H 0 A_JumpIfInventory("NadeAmmoClass",1,"NadeRefire")
goto Reload+1
NadeRefire:
NADE IJ 1
NADE C 0 A_Refire 
Goto Ready2+1
AltFire:
TNT1 A 0 A_JumpIfInventory("CorpKnifeCooldown",1,"Ready2")
TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH) //OffsetReset
NADE I 2
goto Knife
KnifeEnd:
NADE H 0 A_JumpIfInventory("NadeAmmoClass",1,"KnifeEnd2")
goto Reload+1
KnifeEnd2:
NADE IJ 1
Goto Ready2+1
NoAmmo:
NADE B 1 //'<'
Goto Reload
}
}

Actor NadeAmmoClass : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 16
}

actor NadeBall
{
PROJECTILE
Obituary "%o was blasted by %k's Nadespam."
Radius 2
Height 2
scale 2.5
Damage (2) //total: 8
- NOGRAVITY
speed 30
+HEXENBOUNCE
bouncefactor 0.8
wallbouncefactor 0.4 //Faku
reactiontime 15
bouncecount 5
damagetype "NadeSpam"
+MTHRUSPECIES
States
{
Spawn:
NADE Z 0
NADE Z 0 A_CountDown
NADE Z 16
Goto Spawn+1
Death:
NADE Z 0 A_Stop
NADE Z 0 A_PlaySoundEx("weapon/napalm", "Weapon")
NADE Z 0 A_ChangeFlag("NOGRAVITY",1)
NADE Z 0 A_Explode(9, 128, 0)
NAPA EFGHIJKLMNOPQRS 2
stop
}
}
actor NadeBall_BLUE : NadeBall {translation "128:128=205:205", "132:132=74:74", "210:210=205:205"}
actor NadeBall_RED : NadeBall {translation "128:128=171:171", "132:132=41:41", "210:210=171:171"}
actor NadeBall_GREEN : NadeBall {translation "128:128=104:104", "132:132=128:128", "210:210=104:104"}
actor NadeBall_GOLD : NadeBall {translation "128:128=229:229", "132:132=232:232", "210:210=229:229"}

actor NadeSpamM1 : BasicTeamColorPickup
{
states
{
FireX:
TNT1 A 0 A_FireCustomMissile("NadeBall", 0, 1, -8, 0)
goto Done
//---
FireB:
TNT1 A 0 A_FireCustomMissile("NadeBall_BLUE", 0, 1, -8, 0)
goto Done
FireR:
TNT1 A 0 A_FireCustomMissile("NadeBall_RED", 0, 1, -8, 0)
goto Done
FireG:
TNT1 A 0 A_FireCustomMissile("NadeBall_GREEN", 0, 1, -8, 0)
goto Done
FireY:
TNT1 A 0 A_FireCustomMissile("NadeBall_GOLD", 0, 1, -8, 0)
goto Done
}
}