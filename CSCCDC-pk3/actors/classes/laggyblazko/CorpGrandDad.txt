actor CorpGrandDad : CorporalWeaponBase
{
const int grandRecoil = 65536*3/360;
Tag "$TAG_GRANDDAD"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 8
Weapon.AmmoUse2 1
Weapon.AmmoGive2 24
Weapon.SlotNumber 2
weapon.ammotype1 "GrandAmmo"
weapon.ammotype2 "ShelbyvilleRound"
inventory.icon "COW1I" //Must replace
dropitem "NadeSpamWepCS"
states
{
Ready:
COW1 B 0 ACS_ExecuteAlways(998,0,413)
TNT1 A 0 A_TakeInventory("ReloadGrand")
COW1 B 0 A_JumpIfNoAmmo("UnloadEnd") 
COW1 B 0 A_JumpIfInventory("GrandUnloaded",1,"UnloadEnd")
TNT1 A 4 A_WeaponReady(WRF_NOPRIMARY)
COW1 B 2 Offset(-128,160) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
COW1 B 2 Offset(-96,128) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
COW1 B 3 Offset(-64,96) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
COW1 B 3 Offset(-32,64) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH) //OffsetReset
Ready2: 
COW1 B 1 A_WeaponReady(WRF_ALLOWRELOAD)
COW1 B 0 A_JumpIfInventory ("ForceReload",1,"Reload")
//NADE B 1 A_GiveInventory("NadeAmmoClass",1)
loop
Fire: 
COWB B 0 A_TakeInventory("GrandAutofire",6)
COW1 B 0 A_JumpIfInventory("ReloadGrand",1,"SlowEnd")
COW1 B 0 A_AlertMonsters
COW1 B 0 A_PlaySoundEx("Corp/GrandFire","Weapon")
COW1 B 0 A_light2
COW1 C 0 A_GiveInventory("FakeLongTracerShot_1")
COW1 C 2 Bright A_FireBullets(0,0,1,13,"GrandPuff",FBF_NORANDOM|FBF_USEAMMO)
COW1 C 0 ACS_NamedExecuteAlways("CorpRecoil",0, random(-1*grandRecoil,grandRecoil) , random(-1*grandRecoil,grandRecoil) ,3) 
COW1 B 0 A_JumpIfNoAmmo("NoAmmo")
COW1 B 0 A_FireCustomMissile("ShelbyvilleCasing",random(-25,-40),0,2,4,0,random(30,60))
COW1 D 2 A_light0
COW1 C 0 A_Refire 
Goto Ready2
Hold:
COW1 B 0 A_JumpIfInventory("GrandAutofire",6,"fire")
COW1 B 1 A_GiveInventory("GrandAutofire",1)
COW1 B 0 A_Refire
Goto Ready2
AltFire:
COW1 B 0 A_JumpIfInventory("UnloadGrand",1,"Knife")//Skip animation
COW1 B 1 Offset(-64,64)
COW1 B 1 Offset(-128,128)
TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH) //OffsetReset
goto Knife
KnifeEnd:
TNT1 A 1 A_WeaponReady(WRF_NOPRIMARY)
Goto Ready+1

//Manual reload
Reload:
COW1 B 0 A_JumpIfInventory ("GrandAmmo",8,"Hold")
COW1 B 0 A_JumpIfInventory ("ShelbyvilleRound",1,"Unload")
Goto Hold
Unload:
COW1 B 3 Offset(-64,64)A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
TNT1 A 0 A_GiveInventory("UnloadGrand")
COW1 B 3 Offset(-128,128)A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH) //OffsetReset
TNT1 A 0 A_PlaySound("Corp/GrandChamber","Weapon")
TNT1 A 20 A_WeaponReady(WRF_NOPRIMARY)
TNT1 A 5 A_PlaySound("Corp/GrandUnload","Weapon")
TNT1 A 0 A_TakeInventory("UnloadGrand")
TNT1 A 0 A_GiveInventory("GrandUnloaded")
UnloadEnd: //From knife/switch if it's out of ammo or was unloaded
COW1 D 0 A_JumpIfInventory("ShelbyvilleRound",8,"PreQuick")
//Slow reload: If no 8 round clip is ready
TNT1 A 10 A_WeaponReady(WRF_NOPRIMARY)
TNT1 A 0 A_GiveInventory("ReloadGrand")
TNT1 A 0 A_TakeInventory("GrandUnloaded")
SlowReload:
TNT1 A 0 ACS_NamedExecuteAlways("ReloadWeapon",0,2,1)//Reloads one by one
TNT1 A 0 A_PlaySound("Corp/GrandLoad","Weapon")
TNT1 A 20 A_WeaponReady
//Keep until no spare rounds are left
//Or it's manually canceled
//Or it's fully loaded (by picking up another Grand Dad)
COW1 D 0 A_JumpIfInventory("GrandAmmo",8,"SlowEnd")
COW1 D 0 A_JumpIfInventory("ShelbyvilleRound",1,"SlowReload")
SlowEnd:
COW1 D 0 A_TakeInventory("ReloadGrand")
COW1 D 0 A_PlaySound("Corp/GrandChamber","Weapon")
COW1 B 2 Offset(-128,160) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
COW1 B 2 Offset(-96,128) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
COW1 B 3 Offset(-64,96) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
COW1 B 3 Offset(-32,64) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH) //OffsetReset
Goto Ready2
PreSlow: //From Running out of ammo and not having 8 rounds.
COW1 B 3 Offset(-64,64) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
COW1 B 3 Offset(-128,128) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH) //OffsetReset
TNT1 A 5 A_GiveInventory("ReloadGrand")
goto SlowReload

//Quick Reload: If a full clip is ready
PreQuick: //From manual unload, knife use or weapon switch
COW1 D 2 Offset(-128,160) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
COW1 D 2 Offset(-96,128) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
COW1 D 3 Offset(-64,96) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
COW1 D 3 Offset(-32,64) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH) //OffsetReset
QuickReload:
COW1 D 4 A_WeaponReady(WRF_NOPRIMARY)
//Too late to switch/knife now
COW1 EF 3 
TNT1 A 0 A_TakeInventory("GrandUnloaded")
COW1 G 6 A_PlaySound("Corp/GrandLoad","Weapon")
COW1 H 6 ACS_NamedExecuteAlways("ReloadWeapon",0,2)
COW1 I 3 A_PlaySound("Corp/GrandChamber","Weapon")
Goto Hold
NoAmmo:
COW1 D 0 A_FireCustomMissile("GrandClip",-30,0,2,4,0,0)
COW1 D 4 A_light0
COW1 B 0 A_JumpIfInventory ("ShelbyvilleRound",8,"QuickReload")
COW1 B 0 A_JumpIfInventory ("ShelbyvilleRound",1,"PreSlow")
TNT1 A 1 A_TakeInventory("CorpGrandDad",1)
stop
}
}

Actor GrandAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 8
}

Actor ShelbyvilleRound : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 24
}

actor GrandPuff : TommyPuff
{
Obituary "%o met %k's Grand Dad."
DamageType "GrandDad"
}


actor UnloadGrand: Inventory
{
inventory.maxamount 1
}

actor ReloadGrand: Inventory //Only used to cancel slow reloads by firing.
{
inventory.maxamount 1
}

actor GrandUnloaded: Inventory 
{
inventory.maxamount 1
}

actor GrandClip
{
PROJECTILE
+DOOMBOUNCE
+THRUACTORS 
-NOGRAVITY
SeeSound "Corp/GrandClip"
DeathSound "Corp/GrandClip"
damagetype "FragGrenade"
Obituary "$OB_FRAGGRENADE"
bouncefactor 0.4
wallbouncefactor 0.4
Speed 5
Damage 0
height 4
radius 2
scale 2.5
States
{
Spawn:
TNT1 A 0
TNT1 A 0 ThrustThingZ(0,15,0,0)
TNT1 A 0 A_PlaySoundEx("Corp/GrandEject","Voice")
COW1 KJKLMJKLMJKLMJKLMJZLZJZLZ 4
Stop
Death:
COW1 K 36
COW1 KZKZKZKZ 4
Stop
}
}

actor GrandDadCooldown : Inventory
{
}


//Copied code from the original fakeBullet

actor FakeLongBullet : FakeTommyBullet
{
states
{
Spawn2:
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-5,0,(sin(pitch)*5),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-10,0,(sin(pitch)*10),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-15,0,(sin(pitch)*15),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-20,0,(sin(pitch)*20),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-25,0,(sin(pitch)*25),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-30,0,(sin(pitch)*30),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-35,0,(sin(pitch)*35),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-40,0,(sin(pitch)*40),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-45,0,(sin(pitch)*45),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-50,0,(sin(pitch)*50),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-55,0,(sin(pitch)*55),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-60,0,(sin(pitch)*60),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-65,0,(sin(pitch)*75),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-70,0,(sin(pitch)*70),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-75,0,(sin(pitch)*75),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-80,0,(sin(pitch)*80),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-85,0,(sin(pitch)*85),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-90,0,(sin(pitch)*90),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-95,0,(sin(pitch)*95),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("FakeBulletFx",cos(pitch)*-100,0,(sin(pitch)*100),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0
TNT1 A 1 
Goto Spawn2
}
}


actor FakeLongBullet_BLUE : FakeLongBullet {translation "192:192=205:205", "198:198=74:74" , "196:196=196:196"}
actor FakeLongBullet_RED : FakeLongBullet {translation "192:192=54:54", "198:198=42:42" , "196:196=170:170"}
actor FakeLongBullet_GREEN : FakeLongBullet {translation "192:192=104:104", "198:198=128:128" , "196:196=138:138"}
actor FakeLongBullet_GOLD : FakeLongBullet {translation "192:192=229:229", "198:198=232:232" , "196:196=214:214"}

actor FakeLongTracerShot_1 : BasicTeamColorPickup
{
states
{
FireX:
TNT1 A 0 A_FireCustomMissile("FakeLongBullet",0,0,3,4,0,0)
stop
//---
FireB:
TNT1 A 0 A_FireCustomMissile("FakeLongBullet_BLUE",0,0,3,4,0,0)
stop
FireR:
TNT1 A 0 A_FireCustomMissile("FakeLongBullet_RED",0,0,3,4,0,0)
stop
FireG:
TNT1 A 0 A_FireCustomMissile("FakeLongBullet_GREEN",0,0,3,4,0,0)
stop
FireY:
TNT1 A 0 A_FireCustomMissile("FakeLongBullet_GOLD",0,0,3,4,0,0)
stop
}
}


actor ShelbyvilleCasing : FatBulletCasing
{
SeeSound "Corp/Casing4"
DeathSound "Corp/CasingEnd2"
states
{
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("Silence","Voice")
COW1 NOPQNOPQNOPQNOPQNOPQNOPQNOPQNOPQ 4
Stop
}
}

Actor GrandAutofire : Inventory
{
inventory.amount 1
inventory.maxamount 6
}
