actor TommyGun : CorporalWeaponBase
{
Tag "TommyGun"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 30
Weapon.AmmoGive2 90
Weapon.SlotNumber 1
Obituary "%o was put to sleep with the fishes by %k's Tommy Gun."
weapon.ammotype1 "TommyAmmo"
weapon.ammotype2 "FatBullets"
inventory.icon "TOMMYI" 
dropitem "NadeSpamWepCS"
States
{
Ready:
TOMY B 0 ACS_ExecuteAlways(998,0,413)
TOMY B 0 A_JumpIfInventory("NoMagTommy",1,"ReadyStartupNoMag")
TNT1 A 3 A_WeaponReady(WRF_NOPRIMARY)
TOMY B 2 Offset(0,96) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
TOMY B 2 Offset(0,64) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
goto Ready2
ReadyStartupNoMag:
TNT1 A 3 A_WeaponReady(WRF_NOPRIMARY)
TOMY M 2 Offset(0,96) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
TOMY M 2 Offset(0,64) A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
goto Ready2
Ready2:
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("NoMagTommy",1,"NoMag")
TOMY B 3 A_WeaponReady(WRF_NOPRIMARY)
TNT1 A 0 A_JumpIfInventory("CockTommy",1,"Cocking")
TOMY K 2 A_WeaponReady(WRF_NOPRIMARY)
TOMY C 1 A_WeaponReady(WRF_ALLOWRELOAD)
TOMY C 0 A_JumpIfInventory ("ForceReload",1,"Reload")
Goto Ready2+5
Fire: 
TOMY C 0 A_JumpIfNoAmmo("YesReload")
TOMY D 0 //A_FireCustomMissile("FakeTommyBullet",0,0,3,4,0,0)//First tracer is accurate
TOMY C 0 A_AlertMonsters
TOMY C 0 A_PlaySoundEx("weapon/TommyFire","Weapon")
TOMY C 0 A_light2
TOMY D 2 Bright A_FireBullets(2.5,2.5,1,4,"TommyPuff",FBF_NORANDOM|FBF_USEAMMO)
TOMY D 0 A_FireCustomMissile("FatBulletCasing",random(-65,-55),0,2,4,0,random(0,15))
TOMY C 1 A_light0
TOMY C 0 A_Refire
Goto Ready2+5
Hold:
TOMY C 0 A_JumpIfNoAmmo("YesReload")
TOMY D 0 //A_FireCustomMissile("FakeTommyBullet",fRandom(-2.5,2.5),0,3,4,0,fRandom(-2.5,2.5))
goto Fire+2
Altfire:
TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH) //OffsetReset
TOMY L 0 A_JumpIfInventory("NoMagTommy",1,"MidRelPreKnife")
TOMY L 0 A_JumpIfInventory("CockTommy",1,"MidRelPreKnife")
TOMY LZ 2
goto Knife
KnifeEnd:
TOMY L 0 A_JumpIfInventory("NoMagTommy",1,"MidRelPostKnife")
TOMY L 0 A_JumpIfInventory("CockTommy",1,"MidRelPostKnife")
TOMY ZL 2
Goto ready2+5
MidRelPreKnife:
TOMY MZ 2
goto Knife
MidRelPostKnife:
TOMY ZM 2
goto Ready2+1
Reload:
TOMY F 0 A_JumpIfInventory("TommyAmmo",30,"NoFire")//If your mag is full gud bai
YesReload:
TOMY K 0 A_JumpIfInventory("FatBullets",1,1)//If you have extra bullets keep moving.
Goto NoFire
TOMY E 3
TOMY F 6
TOMY G 3 A_PlaySoundEx("weapon/TommyClip","Weapon")
TOMY H 0 A_GiveInventory("NoMagTommy")
TOMY J 6
NoMag:
TOMY J 6 A_WeaponReady(WRF_NOPRIMARY)
TOMY H 0 A_TakeInventory("NoMagTommy")
TOMY G 3
TOMY F 2 A_PlaySoundEx("weapon/TommyClip","Weapon")
TOMY F 0 A_JumpIfInventory("TommyAmmo",1,"NoCocking")
TOMY H 0 A_GiveInventory("CockTommy")
Cocking:
TOMY F 4 A_WeaponReady(WRF_NOPRIMARY)
TOMY H 6 
TOMY I 3 A_PlaySoundEx("weapon/TommyLoad","Weapon")
TOMY F 3 ACS_NamedExecuteAlways("ReloadWeapon",0,1)
TOMY EK 3
TOMY H 0 A_TakeInventory("CockTommy")
Goto Ready2+5
NoCocking:
TOMY E 3 ACS_NamedExecuteAlways("ReloadWeapon",0,1)
TOMY K 3
Goto Ready2+5
NoFire:
TOMY C 1
Goto Ready2+5
}
}

actor TommyAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 30
}

actor FatBullets : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 90
}


actor TommyPuff // : HitscanPuffBase //breaks things somehow.
{
	+NOGRAVITY
	+MTHRUSPECIES
	+PUFFONACTORS
	+PUFFGETSOWNER
	Damagetype "TommyGun"
	attacksound "Weapon/TommyRicochet"
	scale 2.5 
	Obituary "%o was put to sleep with the fishes by %k's Tommy Gun."
	States
	{
		Spawn:
			MMFX D 2
			TNT1 A 16
			Stop
	}
}


actor CorpKnifePuff : TommyPuff
{
/*
-MTHRUSPECIES
-ALWAYSPUFF
-PUFFONACTORS
-PUFFGETSOWNER
*/
Obituary "%o got shut up by %k."
//Obituary "$OB_CORPKNIFE"
DamageType "CorpKnife"
}





actor TommyFlag : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

/*
//Work on this another time.
actor TommyGunBot : TommyGun
{
States
{
Deselect:
TNT1 A 0 A_SelectWeapon("NadeSpamBot")
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
TNT1 A 1 A_Lower
Loop
}
}
*/


actor CorpHUD : Inventory
{
	inventory.amount 1
	inventory.maxamount 1
}

actor CorpReload : CustomInventory
{
+INVBAR
inventory.amount 1
inventory.maxamount 1
inventory.pickupmessage "Cover me, I'm reloading."
inventory.icon "MAGAICON"
Inventory.PickupSound "item/1up"
states{
Use:
TNT1 A 0
TNT1 A 0 ACS_NamedExecuteAlways("ForceReload")
fail
}
}

actor ForceReload : Once
{
}



actor NoMagTommy : Inventory
{
inventory.maxamount 1
}


actor CockTommy : Inventory
{
inventory.maxamount 1
}

/*actor FakeTommyBullet : FakeBullet
{
states
{
Spawn:
TNT1 A 0
TNT1 AA 1 ACS_NamedExecuteAlways("PitchMatch",0)
TNT1 A 0 A_scalevelocity(3.875)
TNT1 A 1 //Small distance before tracers appear
TNT1 A 0 A_scalevelocity(4.0)
goto Spawn2
}
}*/

actor FatBulletCasing
{
PROJECTILE
+DOOMBOUNCE
+THRUACTORS
+CLIENTSIDEONLY
-NOGRAVITY
SeeSound "Corp/Casing0"
DeathSound "Corp/CasingEnd1"
damagetype "FragGrenade"
Obituary "$OB_FRAGGRENADE"
bouncefactor 0.2
wallbouncefactor 0.2
Speed 10
Damage 0
height 2
radius 1
scale 0.75
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("Silence","Voice")
TOMY VWXYVWXYVWXYVWXYVWXYVWXYVWXYVWXY 4
Stop
Death:
TNT1 A 1
Stop
}
}
