actor MeteorWep : BaseMM8BDMWep
{
Weapon.AmmoUse 28 
Weapon.AmmoGive 56
Weapon.SlotNumber 6  
Inventory.Pickupmessage "Battle Chip slot in! Meteor!"
weapon.ammotype "MeteorAmmo" 
inventory.icon "METEI"
tag "Meteor"
States
{
Spawn:
METE A 1
loop
Ready:
METE B 0 ACS_ExecuteAlways(998,0,DYE_UKIYAMA) 
METE B 1 A_WeaponReady 
Goto Ready+1 
Deselect: 
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
METE B 1 A_Lower
Loop
Select: 
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
METE B 1 A_Raise
Loop
Fire:
METE B 0 A_JumpIfNoAmmo("NoAmmo") 
METE B 1 
METE B 0 A_JumpIfInventory("MeteorCounter", 1, "NoAmmo")
METE B 0 A_TakeInventory("MeteorAmmo",28)
METE B 0 ACS_ExecuteAlways(978,0) 
METE B 0 A_PlaySoundEx("weapon/magnetmissile","Weapon") 
METE B 0 A_SpawnItemEx("MeteorStaff",10,0,13,0,0,0,0,SXF_TRANSFERTRANSLATION)
METE CDE 6
METE E 15
METE B 2
METE B 0 A_Refire 
Goto Ready+1
NoAmmo: 
METE B 1 ACS_Execute(979,0)
Goto Ready+1 
}
}

actor MeteorStaff 
{
PROJECTILE
+SHOOTABLE
-NOBLOCKMAP
+RIPPER
-SOLID
+NoGravity
+DontFall
+NOTARGETSWITCH
+noblood
+noblooddecals
+NoTargetSwitch
+HexenBounce
+DONTBLAST
mass 99999
damagefactor "Meteor",0.0 
damagefactor "UkiTornado",0.0 
Radius 10 
Height 70
scale 2.5 
damage (0) 
+FORCEYBILLBOARD 
speed 0 
reactiontime 300
States
{
Spawn:
METE HGFIJ 2 A_GiveToTarget("MeteorCounter", 1)
Goto Spawn2
Spawn2: 
TNTA A 0 A_SpawnitemEX ("MeteorSpawner",random(500,-500),random(500,-500),40,0,0,0,0,SXF_TRANSFERTRANSLATION)
METE JKLMNO 2 A_CountDown
TNTA A 0 A_Stop
TNTA A 0 A_SpawnitemEX ("MeteorSpawner",random(500,-500),random(500,-500),40,0,0,0,0,SXF_TRANSFERTRANSLATION)
loop
Death:
METE J 0 A_ChangeFlag("Shootable",0)
METE JIHGF 2 A_TakeFromTarget("MeteorCounter", 999)
stop
}
}

actor MeteorSpawner
{
+NOGRAVITY
+MISSILE
+SKYEXPLODE
Radius 5
Height 5
scale 2.5
damage (0)
+FORCEXYBILLBOARD
+NoClip
+NoBlockMap
States
{
Spawn:
TNT1 A 1 ThrustThingZ(0,9999,0,0)
TNT1 A 1
TNT1 A 0 A_ChangeFlag("NoClip",0)
loop
Death:
TNT1 A 0 A_SpawnitemEX("Meteor",0,0,0,10,0,-20,0,SXF_TRANSFERTRANSLATION)
TNT1 A 5
stop
}
}

actor Meteor
{
Obituary "%o was turned into a crater by %k's Meteors."
Projectile
-FORCEXYBILLBOARD
Damagetype "Meteor"
+DontBlast
Radius 5
Height 5
damage (0)
scale 2.5
States
{
Spawn:
METE P 8 A_ScaleVelocity(1.1)
loop
Death:
CBOM K 0 A_PlaySoundEx("weapon/MeteorLand", "Weapon")
METE Q 2 A_Explode (40,150,0)
METE RST 2
stop
}
}

actor MeteorCounter : Inventory
{
Inventory.amount 1
Inventory.Maxamount 999
}

actor MeteorAmmo : Ammo 
{
inventory.amount 1
inventory.maxamount 56
}

actor MeteorWepCS : MeteorWep replaces MeteorWep
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Log("Copywep did a thing!")
goto "Super::Spawn"
}
}