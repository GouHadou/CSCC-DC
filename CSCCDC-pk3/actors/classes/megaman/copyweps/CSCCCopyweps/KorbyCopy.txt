actor CometWep : BaseMM8BDMWep
{
Weapon.AmmoUse 5
Weapon.AmmoGive 28
Weapon.SlotNumber 5
weapon.ammotype "CometAmmo"
Obituary "$OB_COMET"
Inventory.Pickupmessage "$PU_COMET"
inventory.icon "COMETIC"
tag "$TAG_COMET"
States
{
Spawn:
COMZ Z 1
loop
Ready:
COME Q 0 ACS_ExecuteAlways(998,0,DYE_KORBY) 
COME Q 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 A 0 A_TakeInventory("WeaponCharge",999)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
TNT1 A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
COME Q 1 A_Raise
Loop
Fire:
COME Q 0 A_JumpIfNoAmmo("NoAmmo")
COME Q 0 A_TakeInventory("CometFlag3", 999)
COME RS 4
COME R 0 A_FireCustomMissile("CometCrosshair", 0, 1, 8)
goto Hold
Hold:
COME TU 4
COME S 0 A_JumpIfInventory("IsBot", 1, "BotCheck")
COME S 0 A_JumpIfInventory("CometFlag3", 10, 2)
COME S 0 A_ReFire
COME Q 0 A_GiveInventory("CometFlag",1)
COME SRQ 4
COME A 0 A_ClearRefire
COME A 0 A_TakeInventory("CometFlag",999)
Goto Ready+1
Altfire:
COME Q 0 A_JumpIfNoAmmo("NoAmmo")
COME Q 0 A_TakeInventory("CometFlag3", 999)
COME RS 4
COME R 0 A_FireCustomMissile("CometCrosshair", 0, 1, 8)
COME TU 4
COME Q 0 A_GiveInventory("CometFlag",1)
COME SRQ 4
COME A 0 A_TakeInventory("CometFlag",999)
Goto Ready+1
BotCheck:
COME TUTUTUTUTUTU 4 A_Jump(8, "BotFire")
Goto BotFire
BotFire:
COME Q 0 A_GiveInventory("CometFlag",1)
COME SRQ 4
COME A 0 A_ClearRefire
COME A 0 A_TakeInventory("CometFlag",999)
Goto Ready+1
NoAmmo:
COME Q 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor CometAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
}

actor CometFlag : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor CometFlag2 : CometFlag
{
inventory.maxamount 5
}

actor CometFlag3 : CometFlag
{
inventory.maxamount 10
}


actor CometCrosshairSpawner
{
+NOINTERACTION
+DONTBLAST
PROJECTILE
Damage (0)
Height 1
Radius 1
Scale 2.5
gravity 900.0
speed 0
States
{
Spawn:
COME Z 0 A_SpawnItemEX("CometCrosshair",0,0,0,10,0,0,0)
stop
Death:
COME Z 0
stop
}
}

actor CometCrosshair
{
Obituary "%o WAS SLAIN BY %k's CROSSHAIR?"
PROJECTILE
+RIPPER
+THRUACTORS
+DONTBLAST
Damage (0)
Height 8
Radius 8
+FLOORHUGGER 
+DOOMBOUNCE
Scale 2.5
speed 10
translation "198:198=118:118", "192:192=100:100"

States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("LightTeamFlag",1,"BFire")
TNT1 A 0 A_JumpIfInTargetInventory("WilyTeamFlag",1,"RFire")
TNT1 A 0 A_JumpIfInTargetInventory("CossackTeamFlag",1,"GFire")
TNT1 A 0 A_JumpIfInTargetInventory("KingTeamFlag",1,"YFire")
Goto LoopJump
BFire:
TNT1 A 0 Thing_SetTranslation(0, 230)
Goto LoopJump
RFire:
TNT1 A 0 Thing_SetTranslation(0, 231)
Goto LoopJump
GFire:
TNT1 A 0 Thing_SetTranslation(0, 232)
Goto LoopJump
YFire:
TNT1 A 0 Thing_SetTranslation(0, 233)
Goto LoopJump
LoopJump:
TNT1 A 0 A_Jump(256, "SpawnLoop")
Goto SpawnLoop
SpawnLoop:
COME A 0 A_JumpIfInventory("CometFlag2",4,"Spawn2")
COME AAAA 1 A_JumpIfInTargetInventory("CometFlag",1,"COMET")
COME A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
COME BBBB 1 A_JumpIfInTargetInventory("CometFlag",1,"COMET")
COME B 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
COME CCCC 1 A_JumpIfInTargetInventory("CometFlag",1,"COMET")
COME C 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
COME DDDD 1 A_JumpIfInTargetInventory("CometFlag",1,"COMET")
COME D 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
COME EEEE 1 A_JumpIfInTargetInventory("CometFlag",1,"COMET")
COME E 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
COME AAAA 1 A_JumpIfInTargetInventory("CometFlag",1,"COMET")
COME A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
COME A 0 A_GiveInventory("CometFlag2",1)
loop
Spawn2:
TNT1 A 0 A_SpawnItemEx("CometCrosshair2", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
Goto Death
COMET:
TNT1 A 0 A_SpawnItemEx("CometCrosshairSpawn", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
Goto Death
Death:
COME A 0
stop
}
}

actor CometCrosshair2 : CometCrosshair
{
States
{
Spawn:
COME A 0 A_SetArg(0, Args[0]+1)
COME A 0 A_Stop
COME AAAA 1 A_JumpIfInTargetInventory("CometFlag",1,"COMET")
COME A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
COME BBBB 1 A_JumpIfInTargetInventory("CometFlag",1,"COMET")
COME B 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
COME CCCC 1 A_JumpIfInTargetInventory("CometFlag",1,"COMET")
COME C 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
COME DDDD 1 A_JumpIfInTargetInventory("CometFlag",1,"COMET")
COME D 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
COME EEEE 1 A_JumpIfInTargetInventory("CometFlag",1,"COMET")
COME E 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
COME AAAA 1 A_JumpIfInTargetInventory("CometFlag",1,"COMET")
COME A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
COME A 0 A_GiveToTarget("CometFlag3", 1)
COME A 0 A_JumpIf(Args[0] >= 15, "Death") //If somehow this sticks around
loop
}
}

actor CometCrosshairSpawn : CometCrosshair
{
States
{
Spawn:
COME A 0 A_TakeFromTarget("CometFlag",999)
COME ABCD 4 A_SpawnItemEx("CometFall", Random(-96, 96),Random(-96,96),1024,Random(-5, 5),0,-40,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
COME AABCDA 4
stop
}
}

actor CometFall
{
PROJECTILE
+NOINTERACTION
+NOCLIP
radius 16
+RANDOMIZE
damage (70)
damagetype "Comet"
height 8
scale 2.5
States
{
Spawn:
COME Z 0 A_PlaySound("Weapon/CometFall")
COME Z 0 A_SpawnItemEX("CometTrail",0,0,0,0,0,0, 0, SXF_TRANSFERTRANSLATION)
COME Z 0 A_SpawnItemEX("CometTrail",0,0,16,0,0,0)
COME Z 1 A_JumpIf(ceilingz-z > 8, "SpawnChange")
Loop
SpawnChange:
COME Z 0 A_ChangeFlag("NOINTERACTION", 0)
COME Z 0 A_ChangeFlag("NOCLIP", 0)
Goto Spawn2
Spawn2:
COME Z 0 A_PlaySound("Weapon/CometFall")
COME Z 0 A_SpawnItemEX("CometTrail",0,0,0,0,0,0, 0, SXF_TRANSFERTRANSLATION)
COME Z 0 A_SpawnItemEX("CometTrail",0,0,16,0,0,0, 0, SXF_TRANSFERTRANSLATION)
COME Z 1 bright
loop
XDeath:
ASEX ABCDEF 2 bright
stop
Death:
ASEX A 0
ASEX A 0 A_Explode(40,200,0)
ASEX ABCDEF 2 bright
stop
}
}

actor CometTrail : CometFall
{
damage 0
-RANDOMIZE
+CLIENTSIDEONLY
renderstyle add
alpha 0.8
States
{
Spawn:
TNT1 A 2
COME Z 3 A_FadeOut
Goto Spawn+1
Death:
TNT1 A 0
stop
}
}

actor CometWepCS : CometWep replaces CometWep
{
States
{
CSCC_DefaultSpawn:
TNT1 A 0 A_SetTranslucent(1.0, 0)
goto Spawn+3
CSCC_TranslucentSpawn:
TNT1 A 0 A_SetTranslucent(0.5, 0)
goto Spawn+3
Spawn:
TNT1 A 0
TNT1 A 0 ACS_NamedExecuteAlways("CSACS_FakeWeaponPickup",0,0)
TNT1 A 0 ACS_NamedExecuteAlways("CSACS_WeaponDisplay",0,0)
COMZ Z 5
goto Spawn+2
}
}