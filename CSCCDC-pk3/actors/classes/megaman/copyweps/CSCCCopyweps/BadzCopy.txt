actor OrbitalMissileWep : BaseMM8BDMWep
{
Weapon.AmmoUse 1
Weapon.AmmoGive 28
Weapon.SlotNumber 7
Inventory.Pickupmessage "$PU_ORBITALMISSILE"
Obituary "$OB_ORBITALMISSILE"
Tag "$TAG_ORBITALMISSILE"
weapon.ammotype "OrbitalMissileAmmo"
inventory.icon "ORBMSL"
States
{
Spawn:
ORBD X 1
loop
Ready:
ORBD A 0 ACS_ExecuteAlways(998,0,DYE_ORBITALMISSILE)
ORBD A 1 A_WeaponReady
Goto Ready+1
Deselect:
ORBD A 0 A_JumpIfInventory("ShieldCheck",1,"RemoveShield")
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
ORBD A 1 A_Lower
Loop
RemoveShield:
ORBD A 0 A_TakeInventory("ShieldCheck", 1)
ORBD A 0 A_TakeInventory("BadzStackCount",999)
ORBD A 0 A_TakeInventory("OrbitalMissileAmmo",5)
Goto Deselect+1
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
ORBD A 1 A_Raise
Loop
Fire:
ORBD A 0 A_JumpifInventory("ShieldCheck",1,"Release")
ORBD A 0 A_JumpIfNoAmmo("NoAmmo")
ORBD A 0 A_SpawnItemEx("OrbitalMissileWatcher")
ORBD A 0 A_PlaySoundEx("weapon/magnetmissile","Weapon")
ORBD A 1 A_GiveInventory("ShieldCheck",1)
ORBD A 0 A_GiveInventory("BadzStackCount",1)
ORBD A 0 A_SpawnItemEx("OrbitalMissileCopy",0,0,32)
ORBD A 0 A_SpawnItemEx("OrbitalMissileCopy",0,0,32,0,0,0,120)
ORBD A 0 A_SpawnItemEx("OrbitalMissileCopy",0,0,32,0,0,0,240)
TNT1 A 0 A_JumpIfInventory("RuneSpread",1,"FireSpread")
FireEnd:
ORBD ABC 6
ORBD A 0 A_Refire
Goto Ready+1
FireSpread:
ORBD A 0 A_SpawnItemEx("OrbitalMissileCopy",0,0,32,0,0,0,15)
ORBD A 0 A_SpawnItemEx("OrbitalMissileCopy",0,0,32,0,0,0,-15)
ORBD A 0 A_SpawnItemEx("OrbitalMissileCopy",0,0,32,0,0,0,135)
ORBD A 0 A_SpawnItemEx("OrbitalMissileCopy",0,0,32,0,0,0,105)
ORBD A 0 A_SpawnItemEx("OrbitalMissileCopy",0,0,32,0,0,0,255)
ORBD A 0 A_SpawnItemEx("OrbitalMissileCopy",0,0,32,0,0,0,225)
goto FireEnd
Release:
ORBD A 0 A_TakeInventory("ShieldCheck",1)
ORBD A 0 A_TakeInventory("BadzStackCount",999)
ORBD A 0 A_TakeInventory("OrbitalMissileAmmo",5)
ORBD ABC 3
ORBD A 30
goto Ready+1
NoAmmo:
ORBD A 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor OrbitalMissileAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 28
}

actor OrbitalMissileWatcher : BasicHelper
{
States{
Spawn:
TNT1 A 4
TNT1 A 10 A_JumpIfInTargetInventory("BadzStackCount",1,"Exist")
stop
Exist:
TNT1 A 0 A_GiveInventory("CutterFlag",1)
TNT1 A 1 A_JumpIfInventory("CutterFlag",7,"AmmoDrop")
goto Spawn
AmmoDrop:
TNT1 A 0 A_TakeInventory("CutterFlag",7)
TNT1 A 1 A_TakeFromTarget("OrbitalMissileAmmo",1)
TNT1 A 5 A_JumpIfInTargetInventory("OrbitalMissileAmmo",1,"Spawn")
TNT1 A 0 A_TakeFromTarget("ShieldCheck",1)
TNT1 A 0 A_TakeFromTarget("BadzStackCount",1)
goto Spawn
}
}

actor OrbitalMissileCopy : OrbitalMissile
{
Translation "210:210=204:204", "220:220=132:132"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetUserVar("user_dist",0)
TNT1 A 0 A_SetUserVar("user_stack",106)
Orbit:
SPRE D 1 A_Warp(AAPTR_TARGET,0,user_dist,32,angle,WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION)
TNT1 A 0 A_SetAngle(angle+12)
TNT1 A 0 A_SetUserVar("user_dist",user_dist+5)
TNT1 A 0 A_JumpIf(user_dist >= user_stack, "RangeSet")
SPRE E 1 A_Warp(AAPTR_TARGET,0,user_dist,32,angle,WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION)
TNT1 A 0 A_SetAngle(angle+12)
TNT1 A 0 A_SpawnItemEx("OrbitalMissileTrailCopy",-5,0,0,0,2)
TNT1 A 0 A_SetUserVar("user_dist",user_dist+5)
TNT1 A 0 A_JumpIf(user_dist >= user_stack, "RangeSet")
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
SPRE E 1 A_JumpIfInTargetInventory("ShieldCheck",1,"Orbit")
goto Death
RangeSet:
TNT1 A 0 A_SetUserVar(user_dist,user_stack)
OkPerfect:
SPRE D 1 A_Warp(AAPTR_TARGET,0,user_dist,32,angle,WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION)
TNT1 A 0 A_SetAngle(angle+12)
SPRE E 1 A_Warp(AAPTR_TARGET,0,user_dist,32,angle,WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION)
TNT1 A 0 A_SetAngle(angle+12)
TNT1 A 0 A_SpawnItemEx("OrbitalMissileTrailCopy",-5,0,0,0,2)
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
SPRE E 1 A_JumpIfInTargetInventory("ShieldCheck",1,"OkPerfect")
goto Death
Death:
TNT1 A 2 A_SpawnItemEx("OrbitalMissileReleaseCopy",0,0,0,0,0,(0.20944*user_dist))
stop
}
}

actor OrbitalMissileTrailCopy : OrbitalMissileTrail{
Translation "196:196=204:204", "197:197=132:132"
Damage (13)
}

actor OrbitalMissileReleaseCopy : OrbitalMissileRelease
{
Translation "210:210=204:204", "220:220=132:132"
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetUserVar(user_thatfast,momz)
TNT1 A 1 A_ScaleVelocity(-1.0)
TNT1 A 0 A_SpawnItemEx("PunyExplosion")
TNT1 A 0 A_JumpIf(user_thatfast<5,"Death")
TNT1 A 0 ThrustThingZ(0,0,0,1)
SpinSpinSpin:
SPRE D 1 A_ChangeVelocity(user_thatfast,0,0,CVF_RELATIVE|CVF_REPLACE)
SPRE D 0 A_SetAngle(angle+12)
SPRE D 0 A_GiveInventory("CutterFlag",1)
SPRE E 1 A_ChangeVelocity(user_thatfast,0,0,CVF_RELATIVE|CVF_REPLACE)
SPRE D 0 A_SetAngle(angle+12)
SPRE D 0 A_SpawnItemEx("OrbitalMissileTrailCopy",0,0,0,0,2)
SPRE D 0 A_JumpIfInventory("CutterFlag",52,"OkBye")
loop
OkBye:
TNT1 A 0 A_SpawnItemEx("PunyExplosion")
Whee:
SPRE DE 1 A_SpawnItemEx("OrbitalMissileTrailCopy")
loop
}}

actor OrbitalMissileWepCS : OrbitalMissileWep replaces OrbitalMissileWep
{
States
{
CSCC_DefaultSpawn:
TNT1 A 0 A_SetTranslucent(1.0, 0)
goto Spawn+3
CSCC_TranslucentSpawn:
TNT1 A 0 A_SetTranslucent(0.5, 0)
goto Spawn+3
Spawn:
TNT1 A 0
TNT1 A 0 ACS_NamedExecuteAlways("CSACS_FakeWeaponPickup",0,0)
TNT1 A 0 ACS_NamedExecuteAlways("CSACS_WeaponDisplay",0,0)
ORBD X 5
goto Spawn+2
}
}