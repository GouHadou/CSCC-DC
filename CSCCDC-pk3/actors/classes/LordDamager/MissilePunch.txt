actor MissilePunchWep : CSCCWepBase 
{
Weapon.AmmoUse 1
Weapon.SlotNumber 1
Obituary "$OB_MISSILEPUNCH"
Inventory.Pickupmessage "$PU_MISSILEPUNCH"
weapon.ammotype "MissilePunchAmmo"
inventory.icon "LOSICA0"
Tag "$TAG_MISSILEPUNCH"
dropitem "SteelPunchWepCS"
States
{
Spawn:
WEA2 S 1
loop
Ready:
MSPN A 1 ACS_ExecuteAlways(998,0,DYE_LORDDAMAGERCOL)
MSPN A 0 A_GunFlash
goto Ready2
ReadyWait:
MSPN A 1 A_WeaponReady
goto Ready2
Ready2:
MSPN A 1 A_WeaponReady(WRF_ALLOWRELOAD)
MSPN A 0 A_JumpIfInventory("LDRamming",1,"PreRamming")
MSPN A 0 A_jumpifinventory("MissilePunchAmmo",1,"Ready2")
Goto Reload
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
MSPN A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
MSPN A 1 A_Raise
Loop

Fire:
MSPN A 0 SetPlayerProperty(0,0,0)
MSPN A 0 A_TakeInventory("LDRamEarlyEnd",1)
MSPN A 0 A_JumpIfNoAmmo("NoAmmo") 
MSPN A 0 A_PlaySoundEx("weapon/MagnetMissile","Weapon")
MSPN DE 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY|WRF_NOSECONDARY)
TNT1 A 0 A_FireCustomMissile("PowerMissile",0,1,-8,0)
MSPN EAAAAAAAAA 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY|WRF_NOSECONDARY)
MSPN A 0 A_Refire
Goto Ready2

AltFire:
MSPN A 0 SetPlayerProperty(0,0,0)
MSPN A 0 A_TakeInventory("LDRamEarlyEnd",1)
MSPN A 2
MSPN A 0 A_PlaySoundEx("Weapon/napalm","Weapon")
MSPN A 0 A_FireCustomMissile("MissilePunch",0,0,-2,-8)
TNT1 A 0 A_FireCustomMissile("MissilePunchFX",0,0,-2,-8)
TNT1 A 0 A_FireCustomMissile("MissilePunchFX2",0,0,-2,-8)
MSPN BCCCCCBAAAAA 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY|WRF_NOSECONDARY)
MSPN A 1 A_Jump(256,"Ready2")
wait

Reload:
MSPN B 0 A_JumpIfInventory("MissilePunchAmmo",0,"ReadyWait")
MSPN B 1 Offset(0,32)
MSPN B 1 Offset(0,46)
MSPN B 1 Offset(0,58)
MSPN B 1 Offset(0,70)
MSPN B 1 Offset(0,82)
MSPN A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
MSPN B 45 A_WeaponReady(WRF_NOBOB|WRF_NOPRIMARY)
MSPN B 0 A_Giveinventory("MissilePunchAmmo",5)
MSPN B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
MSPN B 1 Offset(0,82)
MSPN B 1 Offset(0,70)
MSPN B 1 Offset(0,58)
MSPN B 1 Offset(0,46)
MSPN B 1 Offset(0,32)
MSPN B 0 A_TakeInventory("LDRamming",99)
MSPN B 1 A_Refire("Fire")
Goto Ready2

PreRamming:
MSPN A 0 A_GiveInventory("RamAmmoBuffer")
MSPN A 0 SetPlayerProperty(0,1,0)
MSPN C 15 A_PlaySoundEx("weapon/pcharge","Weapon")
MSPN A 0 A_TakeInventory("LDRamming",1)
MSPN A 0 A_TakeInventory("LDRamEarlyEnd",1)
Ramming:
MSPN A 0 A_JumpIfInventory("LDRamEarlyEnd",1,"BounceOff")
MSPN C 1 A_WeaponReady(WRF_NOSWITCH)
MSPN A 0 A_Recoil(-3)
MSPN A 0 A_FireCustomMissile("LDRamChecker",0,0,0,0)
MSPN A 0 A_FireCustomMissile("LDRamAttack",0,0,0,0)
MSPN A 0 A_GiveInventory("RamAmmoBuffer")
MSPN A 0 A_PlaySoundEx("misc/gutsgboost","Voice")
MSPN A 0 A_TakeInventory("LDRamEnergy",1)
MSPN A 0 A_JumpIfInventory("LDRamEnergy",2,"Ramming")
goto RamEnd
BounceOff:
MSPN A 0 A_PlaySoundEx("misc/quake","SoundSlot7")
MSPN A 0 A_Recoil(15)
MSPN A 0 ThrustThingZ(0, 32, 0, 0)
MSPN A 0 A_GiveInventory("LDRamEarlyEnd",1)
MSPN A 0 A_TakeInventory("JumpCancler",1)
RamEnd:
MSPN A 0 SetPlayerProperty(0,0,0)
MSPN A 0 A_TakeInventory("LDRamEarlyEnd",1)
MSPN AAAAAAAAAAAAAAAAA 1 A_WeaponReady(WRF_NOSWITCH)
MSPN A 1 A_Jump(256,"Ready2")
wait

NoAmmo:
MSPN A 1
Goto Reload
Flash:
TNT1 A 7
TNT1 A 0 A_GiveInventory("LaserCannonAmmo",3)
TNT1 A 0 A_JumpIfInventory("RamAmmoBuffer",1,2)
TNT1 A 0 A_GiveInventory("LDRamEnergy",1)
TNT1 A 0
loop
}
}

//Inventory and Flags
actor MissilePunchAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 5
}

actor SmogCannonAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 50
}

actor LDRamEnergy : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 56
}

Actor RamAmmoBuffer : Powerup {inventory.amount 1 inventory.maxamount 1 powerup.duration 20}
Actor BeamAmmoBuffer : Powerup {inventory.amount 1 inventory.maxamount 1 powerup.duration 8}

actor PowerMissile : BasicFastProjectile
{
Obituary "$OB_MISSILEPUNCH"
PROJECTILE
+SKYEXPLODE
+DONTBLAST
+BLOODSPLATTER
Radius 8
Height 8
damage (20)
damagetype "PMissile"
speed 65
scale 4.5
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode")
SpawnLoop:
LOMS AB 2 A_PlaySoundEx("weapon/superboost","Weapon")
loop
Death:
TNT1 A 0 A_SetScale(2.5)
TNT1 A 0 A_PlaySoundEx("weapon/hyperbomb", "Weapon")
TNT1 A 0 A_Explode(10,160,0,0,80)
BOMB BDFHJLNOQ 1
stop
}
}

//Altfire
actor MissilePunch : BasicProjectile
{
PROJECTILE
Obituary "$OB_MISSILEPUNCH2"
//+MTHRUSPECIES
+DONTBLAST
+THRUGHOST
damage (35)
damagetype "DamagerFist"
height 32
radius 24
Speed 90
scale 2.5
states
{
Spawn:
TNT1 A 0
TNT1 A 1
stop
}
}

actor MissilePunchFX 
{
translation "192:192=194:194","198:198=241:241"
PROJECTILE
+NOINTERACTION
+NOCLIP
+CLIENTSIDEONLY
radius 8
height 8
damage (0)
scale 3.5
Speed 90
Reactiontime 35
Renderstyle "Translucent"
Alpha 1.0
states
{
Spawn:
TNT1 A 0
TNT1 A 1 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_Stop
GIGF CD 1 
GIGF CDCDC 1 A_FadeOut(0.25)
stop
}
}

actor MissilePunchFX2 : MissilePunchFX
{
translation "192:192=194:194","198:198=241:241"
scale 2.5
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_GiveInventory("ProjectileCode",1)
GIGF D 0 A_Stop

BEWA B 1 A_Fadeout
BEWA B 0 A_SetScale(2.6,2.6)

BEWA B 1 A_Fadeout
BEWA B 0 A_SetScale(2.8,2.8)

BEWA B 1 A_Fadeout
BEWA B 0 A_SetScale(3.0,3.0)

BEWA B 1 A_Fadeout
BEWA B 0 A_SetScale(3.2,3.2)

BEWA B 1 A_Fadeout
BEWA B 0 A_SetScale(3.4,3.4)

BEWA B 1 A_Fadeout
BEWA B 0 A_SetScale(3.6,3.6)
stop
}
}

//Item
actor LDRamActivator : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
inventory.icon "LDRMI0"
//+INVENTORY.UNDROPPABLE copy robot wills it this way
+COUNTITEM
+INVBAR
states
{
Spawn:
TNT1 A 0
loop
Use:
TNT1 A 0 A_JumpIfInventory("LDRamEnergy",28,"StartRamming")
TNT1 A 0 A_PlaySoundEx("weapon/noammo","SoundSlot6")
fail
StartRamming:
TNT1 A 0 A_GiveInventory("LDRamming",1)
fail
}
}

Actor LDRamming : Inventory{}
Actor LDRamEarlyEnd : Inventory{}

actor LDRamAttack
{
PROJECTILE
translation "4:4=194:194", "225:225=241:241"
+MTHRUSPECIES
Obituary "$OB_LDRAM"
radius 1
height 1
speed 80
damage(0)
damagetype "LDRam"
scale 1.0
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_GiveInventory("ProjectileCode",1)
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_Stop
CKIC DEF 1 A_Explode(2, 50, 0)
stop
}
}

actor LDRamChecker
{
PROJECTILE
+MTHRUSPECIES
+THRUACTORS
+DONTREFLECT
+DONTSPLASH
Radius 12
Height 12
Speed 40
states
{
Spawn:
TNT1 A 0
TNT1 A 4
stop
Death:
TNT1 A 0 A_Stop
TNT1 A 0 A_JumpIf(CeilingZ - FloorZ <=48 , "XDeath")
TNT1 A 0 A_CheckFloor("XDeath")
TNT1 A 0 A_CheckCeiling("XDeath")
TNT1 A 0 A_GiveToTarget("LDRamEarlyEnd",1)
stop
XDeath:
Crash:
Floor:
TNT1 A 0
stop
}
}