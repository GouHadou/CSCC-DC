actor StoneCannonWep : CSCCWepBase
{
Weapon.AmmoUse 7
Weapon.AmmoGive 28
Weapon.SlotNumber 6
Obituary "$OB_STONECANNON"
weapon.ammotype "StoneCannonAmmo"
inventory.icon "POWSSI"
DropItem "PowerStoneWepCS"
tag "$TAG_STONECANNON"
States
{
Spawn:
HNCN E 1
stop
Ready:
HCAN B 0 ACS_ExecuteAlways(998,0,DYE_GIZMO)
Ready2:
HCAN B 0 A_JumpIfNoAmmo("Ready3")
HCAN B 7 A_WeaponReady
HCAN B 0 A_GiveInventory("GizmoAllGiver",1)
Goto Ready2
Ready3:
HCAN B 7 A_WeaponReady(WRF_NOFIRE)
HCAN B 0 A_GiveInventory("GizmoAllGiver",1)
HCAN B 0 A_JumpIfNoAmmo("Ready3")
Goto Ready2
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
HCAN B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
HCAN B 1 A_Raise
Loop
Altfire:
Fire:
HCAN B 0 A_JumpIfNoAmmo("NoAmmo")
HCAN B 0 A_PlaySoundEx("weapon/stonec","Weapon")
HCAN J 0 A_GiveInventory("StoneCannonShot_M1")
HCAN J 0 A_ChangeVelocity(-Cos(pitch)*7, 0, sin(pitch)*7,CVF_RELATIVE)
HCAN J 5
HCAN B 26
HCAN B 0 A_Refire
Goto Ready2
NoAmmo:
HCAN B 1 //ACS_Execute(979,0)
Goto Ready2
}
}

actor StoneCannonAmmo : Ammo 
{
inventory.amount 1
inventory.maxamount 28 
}

actor StoneCannonShot : BasicProjectile
{
Obituary "$OB_STONECANNON"
PROJECTILE
Radius 30
Height 30
scale 3.5
damage (34)
speed 45
-NOGRAVITY
damagetype "GizmoStone"
States
{
Spawn:
TNT1 A 0
STCN D 0 ThrustThingZ(0,20,0,1)
STCN D 1
goto Spawn+2
Death:
MMFX B 0 A_Stop
MMFX B 0 A_ChangeFlag("NOGRAVITY",1)
MMFX B 0 A_SpawnItemEx("StoneCannonBit",0,0,0,28,0,0,45*1,1)
MMFX B 0 A_SpawnItemEx("StoneCannonBit",0,0,0,28,0,0,45*2,1)
MMFX B 0 A_SpawnItemEx("StoneCannonBit",0,0,0,28,0,0,45*3,1)
MMFX B 0 A_SpawnItemEx("StoneCannonBit",0,0,0,28,0,0,45*4,1)
MMFX B 0 A_SpawnItemEx("StoneCannonBit",0,0,0,28,0,0,45*5,1)
MMFX B 0 A_SpawnItemEx("StoneCannonBit",0,0,0,28,0,0,45*6,1)
MMFX B 0 A_SpawnItemEx("StoneCannonBit",0,0,0,28,0,0,45*7,1)
MMFX B 0 A_SpawnItemEx("StoneCannonBit",0,0,0,28,0,0,45*8,1)
MMFX BCDE 2
stop
}
}
actor StoneCannonShot_B : StoneCannonShot {Translation  "46:46=244:244", "4:4=205:205", "210:215=205:205"}
actor StoneCannonShot_R : StoneCannonShot {Translation  "46:46=191:191", "4:4=171:171", "210:215=171:171"}
actor StoneCannonShot_G : StoneCannonShot {Translation  "46:46=135:135", "4:4=104:104", "210:215=104:104"}
actor StoneCannonShot_Y : StoneCannonShot {Translation  "46:46=234:234", "4:4=229:229", "210:215=229:229"}

actor StoneCannonBit : BasicProjectile
{
PROJECTILE
+THRUACTORS
Radius 6
Height 6
damage (10)
scale 2.5
damagetype "GizmoStone"
Obituary "$OB_STONECANNON"
speed 28
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Jump(256,"Look1","Look2","Look3")
Look1:
STCN A 0
goto Continue
Look2:
STCN B 0
goto Continue
Look3:
STCN C 0
goto Continue
Continue:
"####" "#" 3
"####" "#" 0 A_ChangeFlag(THRUACTORS, false)
"####" "#" 4 
goto Continue+2
}
}

actor StoneCannonShot_M1 : BasicTeamColorPickup
{
states
{
FireX:
TNT1 A 0 A_FireCustomMissile("StoneCannonShot",0,1,8,0)
goto Done
FireB:
TNT1 A 0 A_FireCustomMissile("StoneCannonShot_B",0,1,8,0)
goto Done
FireR:
TNT1 A 0 A_FireCustomMissile("StoneCannonShot_R",0,1,8,0)
goto Done
FireG:
TNT1 A 0 A_FireCustomMissile("StoneCannonShot_G",0,1,8,0)
goto Done
FireY:
TNT1 A 0 A_FireCustomMissile("StoneCannonShot_Y",0,1,8,0)
goto Done
}
}