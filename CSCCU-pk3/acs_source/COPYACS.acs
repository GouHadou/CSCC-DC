#library "COPYACS"
#include "wepacs.acs"

#define MAX_MORPHS 4

#DEFINE CSACS_COPYCOPYWEP 137
#DEFINE CSACS_COPYBOTFIX 138
#DEFINE CSACS_COPYCOLORS 139
#DEFINE CSACS_COPYMORPH 140
#DEFINE CSACS_COPYSPEED 141
#DEFINE CSACS_TAKEWEAPON 152


str CopyMorphs[MAX_MORPHS][3] = 
{
{"FusionBombClassWep", "Cold Fusion", "ColdFusionClassItems"},
{"FIR", "Korby", "KorbyClassItems"},
{"LIT", "Korby", "KorbyClassItems"},
{"ICE", "Korby", "KorbyClassItems"}
};

Script "GetPlayerWeapon" (int TargetTID)
{
str PlayerWeapon;
SetActivator(TargetTID);
PlayerWeapon = GetWeapon();
int ReturnThis = -1;
For(int i = 1; i < MAX_WEAPONS_GLOBAL; i++)
	{
	//printbold(i:TargetTID, s:" - ", s:weapons_ammo[i][0], s:" - ", s:PlayerWeapon);
	if(StrIcmp(weapons_ammo[i][0], PlayerWeapon) == 0)
		{
		ReturnThis = i;
		break;
		}
	}
SetResultValue(ReturnThis);
}

Script "CopyMorph" (int MorphMode)
{

int CopyPlayer;
int CopyTarget;
str CopyClass;
str CopyWeapon;
str WeaponCopied; // Mega Man
str CopyItems;
str CopyName;
str FinalMorph;
bool CopyWepCheck = false;
Bool MorphCheck = false;



If(MorphMode == 0)
	{
	CopyPlayer = ACS_NamedExecuteWithResult("GetTarget2", AAPTR_Target);
	CopyTarget = ACS_NamedExecuteWithResult("GetTarget2", AAPTR_Tracer);
	if(CopyPlayer == 0 || CopyTarget == 0){terminate;}
	SetActivator(CopyTarget);
	CopyWeapon = GetWeapon();
	FinalMorph = StrParam(s:CopyWeapon, s:"Morph"); 
	SetActivator(CopyPlayer);
	for(int i = 0; i < MAX_MORPHS; i++) 
		{
		if(StrIcmp(CopyMorphs[i][0], CopyWeapon)==0) 
			{
			CopyName = CopyMorphs[i][1];
			CopyItems = CopyMorphs[i][2];
			MorphCheck = true;
			}
		}
	}
If(MorphMode !=1){WeaponCopied = ACS_NamedExecuteWithResult("GetPlayerWeapon", CopyTarget);}
If(WeaponCopied > 0)
	{
	CopyWepCheck = true;
	MorphMode = 2;
	}
if(MorphCheck == False && CopyWepCheck == False){MorphMode = 2;}

int HealthBefore = GetActorProperty(0, APROP_HEALTH);
int MaxHealthBefore = GetActorProperty(0, APROP_SpawnHealth);
Int HealthRatio = FixedDiv(HealthBefore<<16, MaxHealthBefore<<16);
int MaxHealthAfter;
int TotalHealthAfter;

Int PlayerPitch = GetActorPitch(0);
int KilledMeStockNum = CheckInventory("KilledMeStock");
int FusionStockNum = CheckInventory("FusionStock");

int XVel = GetActorVelX(0);
int YVel = GetActorVelY(0);

bool UnderwaterFlag = CheckInventory("IsUnderWater");
If(HealthBefore <= 0){terminate;}
If(CheckInventory("PowerInvulnerable")){TakeInventory("PowerInvulnerable", 1);} 
TakeInventory("CopyRemover", 1);
If(MorphCheck != 2){SetPlayerProperty(0,1,4);}


Switch(MorphMode)
	{
	Case 0:
	ClearInventory();
	SetHudSize(600,380,0);
	HudMessage(s:"\cgYou \cicopied \ckthe \cd", s:CopyName, s:" \chclass\ct!";HUDMSG_FADEOUT,2,CR_ORANGE,300.4,50.0,2.0,0.5);
	MorphActor(CopyPlayer, FinalMorph, "", 0, 258, "", "");
	GiveInventory(CopyItems, 1);
	GiveInventory("MorphFlag", 1);
	GiveInventory("CopyMorphTimer", 35);
	ACS_NamedExecuteAlways("CopyMorphTimer", 0);
	break;
	Case 1:
	UnmorphActor(ActivatorTID(), 0);
	ClearInventory();
	GiveInventory("CanCopyWeapons", 1);
	TakeInventory("TsuPowerChanger",1);
	TakeInventory("TsuSpeedChanger",1);
	TakeInventory("TsuSpreadChanger",1);
	TakeInventory("LDRamActivator", 1);
	TakeInventory("SentinelTriggerItem",1);
	TakeInventory("OilBoat",999);
	SetPlayerProperty(0, 0, 16);
	SetPlayerProperty(0, 0, 3);
	SetPlayerProperty(0, 0, 4);
	SetPlayerProperty(0, 0, 0);
	SetHudSize(600,380,0);
	HudMessage(s:"\ccCopy ability expired!";HUDMSG_FADEOUT,2,CR_ORANGE,300.4,50.0,2.0,0.5);
	GiveInventory("CopyWeapon", 1);
	SetWeapon("CopyWeapon");
	GiveInventory("CopyAmmo", 14);
	break;
	Case 2:
	SetHudSize(600,380,0);
	If(WeaponCopied < 0)
		{
		HudMessage(s:"\ccUnable to copy weapon from ", n:CopyTarget - 999, s:"!";HUDMSG_FADEOUT,2,CR_ORANGE,300.4,50.0,2.0,0.5);
		terminate;
		}
		
	HudMessage(s:"\cgYou \cicopied \ck", s:weapons_ammo[WeaponCopied][2], s:"\cd from \ch", n:CopyTarget - 999, s:"\ct!";HUDMSG_FADEOUT,2,CR_ORANGE,300.4,50.0,2.0,0.5);

	If(CheckInventory(weapons_ammo[WeaponCopied][0]))
		{
		GiveInventory(weapons_ammo[WeaponCopied][1], 255);
		SetWeapon(weapons_ammo[WeaponCopied][0]);
		}
	Else
		{
		GiveInventory(weapons_ammo[WeaponCopied][0], 1);
		SetWeapon(weapons_ammo[WeaponCopied][0]);
		}
	break;
	}
	

If(ClassifyActor(0) & ACTOR_BOT)
	{
	GiveInventory("Monsterblock", 1);
	GiveInventory("IsBot", 1);
	}
MaxHealthAfter = GetActorProperty(0, APROP_SpawnHealth);
SetActorVelocity(0, XVel/2, YVel/2, GetActorVelZ(0), 0, 0);
TotalHealthAfter = FixedMul(HealthRatio, MaxHealthAfter);
If(TotalHealthAfter > 0){SetActorProperty(0, APROP_HEALTH, TotalHealthAfter);}
TakeInventory("NoColorFlag", 1);
GiveInventory("KilledMeStock", KilledMeStockNum);
If(FusionStockNum <= KilledMeStockNum){FusionStockNum++;}
GiveInventory("FusionStock", FusionStockNum);
GiveInventory("CopyRemover", 1);
If(UnderWaterFlag == TRUE){GiveInventory("LowGravity", 1);}
Else{GiveInventory("NormalGravity", 1);}
SetActorPitch(0, PlayerPitch);
Delay(5);

If((XVel == 0.0 && YVel == 0.0) && GetActorVelZ(0) < 0.0){ThrustThing(GetActorAngle(0) >> 8, 1, 0, 0);} //Protects against getting stuck in walls

SetPlayerProperty(0,0,4);
}

Script "CopyMorphTimer" (void)
{
int TimerRemaining;
While(CheckInventory("CopyMorphTimer") && GetActorProperty(0, APROP_HEALTH) > 0)
	{
	TimerRemaining = CheckInventory("CopyMorphTimer");
	If(GetGamemodeState() == 3)
		{
		If(GetGamemodeState() == 3){Delay(35);}
		TakeInventory("CopyMorphTimer", 35);
		}
	SetFont("BIGFONT");
	SetHudSize(600,380,0);
	HudMessage(s:"\cgTime \ciLeft \ck: \cd", i:TimerRemaining-1;HUDMSG_FADEOUT,3,CR_ORANGE,300.4,10.0,2.0,0.5);
	TakeInventory("CopyMorphTimer", 1);
	Delay(35);
	}
HudMessage(s:"";HUDMSG_FADEOUT,3,CR_ORANGE,300.4,10.0,2.0,0.0);
If(!CheckWeapon("CopyWeapon")){ACS_NamedExecuteAlways("CopyMorph", 0, 1);}
}
Script "GetTarget2" (int Target)
{

SetActivator(0, Target);
int ReturnTID = ActivatorTID();
SetResultValue(ReturnTID);
}

//Best colors ever
Script "CSACS_COPYCOLORS" (void)
{

Delay(1);
Int CopyColor = 402;
If(ACS_ExecuteWithResult(975, 1)==1){terminate;}
While(GetActorProperty(0,APROP_HEALTH)>0 && !CheckInventory("IsDead"))
	{
	If(!CheckInventory("NoColorFlag")){Thing_SetTranslation(0, CopyColor);}
	CopyColor++;
	If(CopyColor>407){CopyColor = 401;}
	Delay(4);
	}
}

//removes all CopyWep inventory. Used by Korby, should be unneeded
Script CSACS_TAKEWEAPON (void)
{
int wp = 0;
For(wp = 0; wp < MAX_WEAPONS_GLOBAL; wp++)
	{
	If(CheckInventory(weapons_ammo[wp][0])){/*printbold(s:weapons_ammo[wp][0]);*/TakeInventory(weapons_ammo[wp][0], 1);}
	}
}