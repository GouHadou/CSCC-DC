//Basic actors, some of them inherit from here for easier management.

Actor BaseMM8BDMWepTM : BaseCSCCWep  {+INVENTORY.UNDROPPABLE +WEAPON.CHEATNOTWEAPON inventory.icon "TNT1A0"}


actor BasicExplosion
{
Obituary "%o told %k they are OP and should be nerfed."
DamageType "Normal"
-SOLID
+DONTBLAST
+NOGRAVITY
+MISSILE
+RIPPER
+SKYEXPLODE
+NOINTERACTION
+DONTSPLASH
+THRUGHOST
+THRUACTORS
+DONTREFLECT
height 1
radius 1
Damage(0)
Speed 0
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_Explode(5000,32,0)
stop
}
}

Actor BasicHelper
{
+NOINTERACTION
-SOLID
+MISSILE
radius 1
height 1
+NOGRAVITY
+DONTBLAST
+DONTREFLECT
renderstyle none
States
{
Spawn:
TNT1 A 1
stop
Death:
TNT1 A 1
stop
}
}

actor BasicClientSide
{
+CLIENTSIDEONLY
+NOINTERACTION
+NOGRAVITY
-SOLID
radius 2
height 2
scale 2.5
}

actor BasicNotClientSide : BasicClientSide
{
-CLIENTSIDEONLY
}

actor TeamColor_CI : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("LightTeamFlag",1,"FireB")
TNT1 A 0 A_JumpIfInventory("WilyTeamFlag",1,"FireR")
TNT1 A 0 A_JumpIfInventory("CossackTeamFlag",1,"FireO")
TNT1 A 0 A_JumpIfInventory("KingTeamFlag",1,"FireP")
TNT1 A 0 A_Jump(256,"FireX")
TNT1 A 0
goto Done
Done:
TNT1 A 0
stop
}
}

actor Nudge : CustomInventory
{
states
{
Pickup:
//TNT1 A 0 A_Stop
TNT1 A 0 A_Jumpif(Momx == 0 && Momy == 0, "Pickup2")
stop
Pickup2:
TNT1 A 0 A_Recoil(-1)
stop
}
}

actor ReSolidifier : BasicHelper
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_CheckFlag("SOLID","Spawn3",AAPTR_TARGET)
TNT1 A 0 A_GiveToTarget("UnSolid",1)
Spawn2:
TNT1 A 0
TNT1 A 0 A_GiveToTarget("ReSolidifierCheckPickup",1)
TNT1 A 1
TNT1 A 0 A_JumpIfInTargetInventory("UnSolid",1,"Spawn2a")
TNT1 A 0 A_CheckFlag("SOLID","Spawn3",AAPTR_TARGET)
goto SuperDeath
Spawn2a:
TNT1 A 0 A_JumpIfInTargetInventory("StaminaFlag",1,"Spawn2")
TNT1 A 0 A_GiveInventory("WeaponCharge",1)
TNT1 A 0 A_JumpIfInventory("WeaponCharge",10,"Spawn2H")
goto Spawn2
Spawn2H:
TNT1 A 0 A_GiveToTarget("StopBeingUnSolidNoob",1)
TNT1 A 0 A_TakeInventory("WeaponCharge",100)
goto Spawn2
Spawn3:
TNT1 A 1 A_GiveToTarget("StaminaFlag",5)
TNT1 A 0 A_JumpIfInTargetInventory("StaminaFlag",496,2)
TNT1 A 0 A_CheckFlag("SOLID","Spawn3",AAPTR_TARGET)
TNT1 A 0
stop
SuperDeath:
TNT1 A 1
stop
Death:
TNT1 A 1
stop
}
}

actor UnSolid : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor ReSolidifierCheckPickup : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_SpawnItemEx("ReSolidifierCheck",0,0,0,momx,momy,momz,0,8,0)
stop
}
}

actor StopBeingUnSolidNoob : CustomInventory
{
states
{
Pickup:
TNT1 A 0 DamageThing(1,15)
stop
}
}

actor ReSolidifierCheck
{
PROJECTILE
+DONTSPLASH
+SKYEXPLODE
+DONTBLAST
+DONTREFLECT
+THRUGHOST
Damage (0)
radius 24
height 84
States
{
Spawn:
TNT1 A 0
TNT1 A 1
TNT1 A 0 A_GiveToTarget("ReSolidify",1)
stop
Death:
XDeath:
TNT1 A 0 A_TakeFromTarget("StaminaFlag",2)
stop
}
}

actor ReSolidify : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("UnSolid",1,"Pickup2")
stop
Pickup2:
TNT1 A 0 A_ChangeFlag("SOLID",1)
TNT1 A 0 A_TakeInventory("UnSolid",1)
stop
}
}

Actor MovementHalt : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_Stop
TNT1 A 0 A_ScaleVelocity(0.0)
stop
}
}