actor HilGunWep : BaseCSCCWep 
{
Weapon.AmmoUse 1
Weapon.AmmoGive 28
Weapon.SlotNumber 1
Obituary "%o was pierced by energy from %k's HilArrows."
Inventory.Pickupmessage "Power up! HilArrow!"
weapon.ammotype "HilRayAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
+WEAPON.ALT_AMMO_OPTIONAL
+WEAPON.CHEATNOTWEAPON
+INVENTORY.UNDROPPABLE
-WEAPON.DONTBOB
Scale 2.0
DropItem "GeminiLaserWep"
States
{
Spawn:
WEAP M 1
WEAP M 1 A_SpawnItemEx("GeminiLaserWep")
stop
Ready:
HRAY A 0 A_JumpIfInventory("Once",1,"Ready2")
HRAY A 0 ACS_ExecuteAlways(330,0,0)
HRAY A 0 A_GiveInventory("Once",1)
goto Ready2
Ready2:
HRAY A 0 ACS_ExecuteAlways(991,0,433)
HRAY A 5 A_WeaponReady
HRAY A 5 A_WeaponReady //Dont ask why, just roll with it
HRAY A 0 A_GiveInventory("HilRayAmmo",1)
Goto Ready2+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
HRAY A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
HRAY A 1 A_Raise
Loop
Fire:
HRAY A 0 A_JumpIfNoAmmo("NoAmmo")
HRAY A 0 A_PlaySoundEx("weapon/mbuster","Weapon")
HRAY A 0 A_FireCustomMissile("HilArrow",0,1,8,5)
HRAY A 0 A_FireCustomMissile("HilArrow",20,0,8,5)
HRAY A 0 A_FireCustomMissile("HilArrow",-20,0,8,5)
HRAY A 0 A_FireCustomMissile("HilArrow",40,0,8,5)
HRAY A 0 A_FireCustomMissile("HilArrow",-40,0,8,5)
HRAY BCA 2
HRAY A 0 A_Refire
Goto Ready2+1
NoAmmo:
HRAY A 1 ACS_Execute(979,0)
Goto Ready2+1
}
}


actor HilRayAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 28
}

actor HilArrow
{
Obituary "%o was pierced by energy from %k's HilArrows."
PROJECTILE
radius 7
height 7
scale 2.5
speed 35
Damage (3)
damagetype "GeminiLaser"
States
{
Spawn:
HARR A 1
loop
}
}

actor FlashField : CentaurFlash
{
-SOLID
+NOGRAVITY
PROJECTILE
damage 0
radius 1
height 1
damagetype "CentaurFlash"
obituary "%o was blasted by %k's Flash Field."
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_Explode(5, 1000,0)
stop
}
}

actor FlashFieldIII : CentaurFlash
{
-SOLID
+NOGRAVITY
PROJECTILE
damage 0
radius 1
height 1
damagetype "CentaurFlash"
obituary "%o was blasted by %k's Flash Field."
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_Explode(5, 1000,0)
stop
}
}

actor FieldFX2
{
scale 2.5
-SOLID
+NOGRAVITY
+CLIENTSIDEONLY
States
{
Spawn:
HGFF A 4
stop
}
}

actor FieldFX3
{
scale 2.5
-SOLID
+NOGRAVITY
+CLIENTSIDEONLY
States
{
Spawn:
HGFF B 2
stop
}
}

actor HilFlashWep : BaseCSCCWep
{
Weapon.AmmoUse 1
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Obituary "%o was blasted by %k's Flash Field."
Inventory.Pickupmessage "Power up! Flash Field!"
weapon.ammotype "HilRayAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
+WEAPON.ALT_AMMO_OPTIONAL
+WEAPON.CHEATNOTWEAPON
+INVENTORY.UNDROPPABLE
-WEAPON.DONTBOB
Scale 2.0
DropItem "GeminiLaserWep"
States
{
Spawn:
WEAP M 1
stop
Ready:
HRAY A 0 A_JumpIfInventory("Once",1,"Ready2")
HRAY A 0 ACS_ExecuteAlways(330,0,0)
HRAY A 0 A_GiveInventory("Once",1)
goto Ready2
Ready2:
HRAY A 0 ACS_ExecuteAlways(991,0,433)
HRAY A 10 A_WeaponReady
HRAY A 0 A_GiveInventory("HilRayAmmo",1)
Goto Ready2+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
HRAY A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
HRAY A 1 A_Raise
Loop
Fire:
HRAY A 1 A_JumpIfInventory("HilRayAmmo",5,"FieldLV1")
HRAY A 2
goto Ready2+1
FieldLV1:
HRAY A 0 A_TakeInventory("WeaponCharge",999)
HRAY A 0 A_JumpIfNoAmmo("NoAmmo")
HRAY BC 2
HRAY A 0 A_TakeInventory("HilRayAmmo",5)
HRAY C 0 A_PlaySoundEx("weapon/centaurflash","Weapon")
HRAY C 0 A_FireCustomMissile("FlashField",0,0,8,0)
HRAY C 0 //A_GiveInventory("Repulsion",1)
HRAY C 11 ACS_ExecuteAlways(976,0)
HRAY C 3
HRAY C 0 A_Refire
HRAY CB 3
goto Ready2+1
Fire2:
HRAY A 0 A_TakeInventory("WeaponCharge",999)
HRAY A 0 A_JumpIfNoAmmo("NoAmmo")
HRAY A 0 A_JumpIfInventory("HilRayAmmo",9,"Fire2r")
HRAY A 0 A_JumpIfInventory("HilRayAmmo",5,"FieldLV1")
goto Ready2+1
Fire2r:
HRAY A 0 A_JumpIfNoAmmo("NoAmmo")
HRAY BC 2
HRAY A 0 A_TakeInventory("HilRayAmmo",9)
HRAY C 0 A_PlaySoundEx("weapon/centaurflash","Weapon")
HRAY C 0 A_FireCustomMissile("FlashField",0,0,8,0)
HRAY C 0 A_GiveInventory("Repulsion",1)
HRAY C 6 ACS_ExecuteAlways(976,0)
HRAY C 0 A_PlaySoundEx("weapon/centaurflash","Weapon")
HRAY C 0 A_FireCustomMissile("FlashField",0,0,8,0)
HRAY C 0 A_GiveInventory("Repulsion",1)
HRAY C 11 ACS_ExecuteAlways(976,0)
HRAY C 3
HRAY C 0 A_Refire
HRAY CB 3
Goto Ready2+1
Fire3:
HRAY A 0 A_TakeInventory("WeaponCharge",999)
HRAY A 0 A_JumpIfNoAmmo("NoAmmo")
HRAY A 0 A_JumpIfInventory("HilRayAmmo",13,"Fire3r")
HRAY A 0 A_JumpIfInventory("HilRayAmmo",9,"Fire2r")
HRAY A 0 A_JumpIfInventory("HilRayAmmo",5,"FieldLV1")
goto Ready2+1
Fire3r:
HRAY A 0 A_JumpIfNoAmmo("NoAmmo")
HRAY BC 2
HRAY A 0 A_PlaySoundEx("weapon/napalm", "Weapon2")
HRAY A 1
HRAY A 0 A_TakeInventory("HilRayAmmo",13)
HRAY C 0 A_PlaySoundEx("weapon/centaurflash","Weapon")
HRAY C 0 A_FireCustomMissile("FlashFieldIII",0,0,8,0)
HRAY C 0 A_GiveInventory("Repulsion",1)
HRAY C 3 ACS_ExecuteAlways(976,0)
HRAY C 0 A_PlaySoundEx("weapon/centaurflash","Weapon")
HRAY C 0 A_FireCustomMissile("FlashFieldIII",0,0,8,0)
HRAY C 0 A_GiveInventory("Repulsion",1)
HRAY C 3 ACS_ExecuteAlways(976,0)
HRAY C 0 A_PlaySoundEx("weapon/centaurflash","Weapon")
HRAY C 0 A_FireCustomMissile("FlashFieldIII",0,0,8,0)
HRAY C 0 A_GiveInventory("Repulsion",1)
HRAY C 11 ACS_ExecuteAlways(976,0)
HRAY C 3
HRAY C 0 A_Refire
HRAY CB 3
Goto Ready2+1
Hold:
HRAY C 0 A_JumpIfInventory("WeaponCharge",32,"Hold4")
HRAY C 0 A_JumpIfInventory("WeaponCharge",18,"Hold2")
HRAY C 1 A_GiveInventory("WeaponCharge",1)
HRAY C 0 A_Refire
Goto Ready2+1
Hold2:
HRAY C 0 A_JumpIfInventory("WeaponCharge",32,"Hold5")
HRAY C 0 A_JumpIfInventory("WeaponCharge",19,"Hold3")
HRAY C 0 A_PlaySoundEx("weapon/pcharge","Weapon")
HGNC AA 1
Goto Hold3
Hold3:
HGNC A 0 A_JumpIfInventory("WeaponCharge",32,"Hold5")
HGNC A 1 A_SpawnItemEx("FieldFX2", 0, 0, 0, momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
HGNC A 3 A_SpawnItemEx("FieldFX2", 0, 0, 0, momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
HGNC A 0 A_GiveInventory("WeaponCharge",2)
HGNC A 0 A_Refire
Goto Fire2
Hold4:
HGNC A 0 A_Jump(224, "Hold5")
HGNC A 0 ACS_ExecuteAlways(974,0)
Goto Hold5
Hold5:
HGNC B 1 A_SpawnItemEx("FieldFX3", 0, 0, 0, momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
HGNC B 1 A_SpawnItemEx("FieldFX3", 0, 0, 0, momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
HGNC B 0 A_Refire
Goto Fire3
NoAmmo:
HRAY A 1 ACS_Execute(979,0)
Goto Ready2+1
}
}



actor FlashMissileIGWep : BaseCSCCWep
{
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Obituary "%o was nuked by %k's Flash Missile."
Inventory.Pickupmessage "You have no idea what you're doing..."
weapon.ammotype "HilRayAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
+WEAPON.NOAUTOAIM
+WEAPON.CHEATNOTWEAPON
+INVENTORY.UNDROPPABLE
-WEAPON.DONTBOB
Scale 2.0
States
{
Spawn:
TNT1 A 1
stop
Ready:
HRAY A 1 A_GunFlash
HRAY A 0 ACS_ExecuteAlways(998,0,433)
HRAY A 40 A_WeaponReady(WRF_NOSWITCH)
HRAY A 0 A_GiveInventory("HilRayAmmo",1)
Goto Ready+2
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
HRAY A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
HRAY A 1 A_Raise
Loop
Fire:
HRAY A 0 A_PlaySoundEx("weapon/mirrordeflect2","Weapon")
HRAY A 0 A_FireCustomMissile("FlashMissile",0,0,8,6)
HRAY BC 15
HRAY A 5
HRAY A 0 A_Refire
Goto Ready+2
AltFire:
HARY A 0 A_JumpIfInventory("HilRayAmmo",7,"ArrowShot")
goto Ready+2
ArrowShot:
HRAY A 0 A_JumpIfNoAmmo("NoAmmo")
HRAY A 0 A_TakeInventory("HilRayAmmo",7)
HRAY A 0 A_PlaySoundEx("weapon/gemini","Weapon")
HRAY A 0 A_FireCustomMissile("DetonationArrow",0,0,8,6)
HRAY BC 6
HRAY A 25
HRAY A 0 A_Refire
Goto Ready+2
Flash:
TNT1 A 1 ACS_ExecuteAlways(301,0,0)
loop
NoAmmo:
HRAY A 1 ACS_Execute(979,0)
Goto Ready+2
}
}

actor FlashMissile
{
PROJECTILE
+SHOOTABLE
-NOBLOCKMAP
scale 2.5
mass 999999
health 1
damagefactor "Normal", 0.0
damagefactor "Instagib", 0.0
damagefactor "Detonation", 100.0
Speed 30
Radius 7
Height 7
Damage (0)
damagetype "Instagib"
States
{
Spawn:
FLMI A 1 A_SpawnItemEx("FlashMissileTrail",-14,0,0,0,0,0,0)
loop
Death:
FLMI A 0 A_Stop
FLMI A 0 A_PlaySoundEx("weapon/astrocrush","Voice")
FLMI A 0 A_Explode(300,320,0)
FLMI A 0 A_SpawnItemEx("FlashMissileXPL1",0,0,0,0,0,0,0)
stop
Death.Detonation:
FLMI A 0 A_Stop
FLMI A 0 A_PlaySoundEx("weapon/astrocrush","Voice")
FLMI A 0 A_Explode(300,450,0)
FLMI A 0 A_SpawnItemEx("FlashMissileXPL2",0,0,0,0,0,0,0)
stop
}
}

actor FlashMissileTrail
{
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
+FORCEXYBILLBOARD
-SOLID
scale 2.5
height 0
radius 0
States
{
Spawn:
FLMI B 5
stop
}
}

actor FlashMissileXPL1
{
scale 2.5
-SOLID
+NOGRAVITY
+CLIENTSIDEONLY
States
{
Spawn:
FLMI CDEFG 3
stop
}
}

actor FlashMissileXPL2
{
scale 2.5
-SOLID
+NOGRAVITY
+CLIENTSIDEONLY
States
{
Spawn:
FLMI HIJKL 3
stop
}
}

actor DetonationArrow
{
PROJECTILE
+RIPPER
scale 2.5
Speed 120
Radius 7
Height 7
Damage (1)
damagetype "Detonation"
States
{
Spawn:
HARR A 1
loop
}
}