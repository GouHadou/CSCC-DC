actor FrostTowerBoss : Weapon
{
Obituary "%o was toppled by %k's Frost Tower."
Weapon.AmmoUse 8
Weapon.AmmoGive 0
weapon.ammotype "WalrusAmmo"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
States
{
Spawn:
TNT1 A 1
stop
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
TNT1 A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
TNT1 A 1 A_Raise
Loop

Ready:
BUST B 0 ACS_ExecuteAlways(991,0,15)
BUST B 0 A_JumpIfInventory("BigSpawnFlag",1,"AltFire2")
BUST B 0 A_GunFlash
BUST B 0 A_JumpIfInventory("WalrusBreathCD",1,"FireNoAmmoSlide")
Ready2:
BUST B 0 A_JumpIfInventory("WalrusAmmo",8,"Ready3")
ICES D 1 A_WeaponReady(WRF_NOPRIMARY)
BUST B 0 A_GiveInventory("WalrusAmmo",2)
loop
Ready3:
ICES D 1 A_WeaponReady
BUST B 0 A_GiveInventory("WalrusAmmo",2)
loop
Fire:
ICES D 3
//ICES D 0 A_FireCustomMissile("FrostTowerBreath",0,1,8,0,0,0)
goto Hold+1
Hold:
ICES D 0 A_JumpIfNoAmmo("FireNoAmmo")
ICES D 0 A_PlaySoundEx("weapon/iceslasher","Weapon")
ICES D 0 A_FireCustomMissile("FrostTowerBreath",random(-5,5),1,8,0,0,random(-5,5))
ICES E 2
ICES D 0 A_FireCustomMissile("FrostTowerBreath2",random(-25,25),0,8,0,0,random(-10,10))
ICES F 2
ICES D 0 A_FireCustomMissile("FrostTowerBreath2",random(-25,25),0,8,0,0,random(-10,10))
ICES D 2
ICES D 0 A_Refire
FireNoAmmo:
ICES D 10
Goto Ready2

AltFire:
BUST B 0 ACS_ExecuteAlways(400,0,1)
ICES D 1
BUST B 0 A_GiveInventory("WalrusAmmo",1)
goto Ready2
AltFire2:
BUST B 0 ACS_ExecuteAlways(400,0,1)
ICES D 1
BUST B 0 A_GiveInventory("WalrusAmmo",1)
loop

FireNoAmmoSlide:
ICES D 1
ICES D 0 A_JumpIfInventory("WalrusBreathCD",1,"FireNoAmmoSlide")
goto Ready2

Flash:
TNT1 A 0 A_SetTranslucent (1.0, 0)
stop
}
}

actor FrostTowerBossSlide : FrostTowerBoss
{
states
{
Ready:
BUST B 0 ACS_ExecuteAlways(991,0,15)
BUST B 0 A_JumpIfInventory("BigSpawnFlag",1,"AltFireSpawn")
goto Altfire
Altfire:
BUST B 0 A_JumpIfInventory("WalrusSliding",1,"AltFire2")
CKIC C 1 A_GiveInventory("WalrusAmmo",1)
//A_GunFlash needs a tic before to work properly in this case for some reason
BUST B 0 A_GunFlash
CKIC CBBAA 1 A_GiveInventory("WalrusAmmo",1)
goto Ready2
Altfire2:
CKIC A 1 A_GiveInventory("WalrusAmmo",1)
BUST B 0 A_GunFlash
CKIC ABBCC 1 A_GiveInventory("WalrusAmmo",1)
BUST B 16
goto Ready2
AltfireSpawn:
BUST B 1 A_GiveInventory("WalrusAmmo",1)
BUST B 0 A_GunFlash
goto Ready2


Ready2:
BUST B 0 A_JumpIfInventory("WalrusAmmo",8,"Ready3")
BUST B 0 A_JumpIfInventory("WalrusSlidingStart",1,"Ready2Slide")
BUST B 0 A_JumpIfInventory("WalrusSlideHitFlag",1,"Ready2Slide")
CKIC C 1 A_WeaponReady(WRF_NOPRIMARY)
BUST B 0 A_GiveInventory("WalrusAmmo",1)
loop
Ready2Slide:
BUST B 0 A_JumpIfInventory("WalrusSlidingStart",1,"Ready2SlideS")
CKIC C 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY)
BUST B 0 A_GiveInventory("WalrusAmmo",1)
goto Ready2
Ready3:
BUST B 0 A_JumpIfInventory("WalrusSlidingStart",1,"Ready3Slide")
BUST B 0 A_JumpIfInventory("WalrusSlideHitFlag",1,"Ready3Slide")
CKIC C 1 A_WeaponReady
BUST B 0 A_GiveInventory("WalrusAmmo",1)
loop
Ready3Slide:
CKIC C 1 A_WeaponReady(WRF_NOSECONDARY)
BUST B 0 A_GiveInventory("WalrusAmmo",1)
goto Ready3

Fire:
ICES D 3
goto Hold+1
Hold:
ICES D 0 A_JumpIfNoAmmo("FireNoAmmo")
ICES D 0 A_PlaySoundEx("weapon/iceslasher","Weapon")
ICES D 0 A_FireCustomMissile("FrostTowerBreath",random(-5,5),1,8,0,0,random(-5,5))
ICES D 0 A_GiveInventory("WalrusBreathCD",1)
ICES E 3
ICES D 0 A_FireCustomMissile("FrostTowerBreath2",random(-25,25),0,8,0,0,random(-10,10))
ICES D 0 A_GiveInventory("WalrusBreathCD",1)
ICES F 3
ICES D 0 A_FireCustomMissile("FrostTowerBreath2",random(-25,25),0,8,0,0,random(-10,10))
ICES D 0 A_GiveInventory("WalrusBreathCD",1)
ICES D 3
ICES D 0 A_Refire
FireNoAmmo:
ICES D 1
ICES D 0 A_JumpIfInventory("WalrusBreathCD",1,"FireNoAmmo")
goto Ready2

Flash:
TNT1 A 0 A_JumpIfInventory("BigSpawnFlag",1,"SlideEndSpawn")
TNT1 A 0 A_JumpIfInventory("WalrusSliding",1,"SlideCancel")
SlideStartUp:
TNT1 A 0 A_GiveInventory("WalrusSlidingStart",1)
TNT1 A 0 A_GiveInventory("WalrusSliding",1)
TNT1 A 0 A_TakeInventory("WalrusSlideHitFlag",1)
TNT1 A 0 A_TakeInventory("WalrusSlideAngle",255)
TNT1 A 0 ACS_ExecuteAlways(403,0,0)
TNT1 A 5
TNT1 A 0 A_GiveInventory("WalrusSlideAngle",angle*256/360)
SlideGo:
TNT1 A 1 A_GiveInventory("WalrusSlideThrust2",1)
TNT1 A 1 A_GiveInventory("WalrusSlideThrust",1)
TNT1 A 1 A_GiveInventory("WalrusSlideThrust2",1)
TNT1 A 1 A_GiveInventory("WalrusSlideThrust",1)
TNT1 A 1 A_GiveInventory("WalrusSlideThrust2",1)
TNT1 A 1 A_GiveInventory("WalrusSlideThrust",1)
TNT1 A 1 A_GiveInventory("WalrusSlideThrust2",1)
TNT1 A 1 A_GiveInventory("WalrusSlideThrust",1)
SlideGo2:
TNT1 A 1 A_GiveInventory("WalrusSlideThrust2",1)
TNT1 A 0 A_JumpIfHealthLower(1,"SlideCancel")
TNT1 A 0 A_SpawnItemEx("WalrusSlideHitCheck", 0, 0, 24, momx*1.1, momy*1.1, momz*1.1, 0, SXF_ABSOLUTEMOMENTUM, 0)
TNT1 A 0 A_JumpIfInventory("WalrusSlideHitFlag",1,"SlideCrash")
TNT1 A 1 A_GiveInventory("WalrusSlideThrust",1)
TNT1 A 0 A_JumpIfHealthLower(1,"SlideCancel")
TNT1 A 0 A_SpawnItemEx("WalrusSlideHitCheck", 0, 0, 24, momx*1.1, momy*1.1, momz*1.1, 0, SXF_ABSOLUTEMOMENTUM, 0)
TNT1 A 0 A_JumpIfInventory("WalrusSlideHitFlag",1,"SlideCrash")
loop

SlideCancel:
TNT1 A 0 A_FireCustomMissile("FrostTowerB",0,0,36,48)
TNT1 A 0 A_FireCustomMissile("FrostTowerB",0,0,-36,48)
TNT1 A 10
TNT1 A 10 ACS_ExecuteAlways(403,0,1)
goto SlideEnd
SlideCrash:
TNT1 A 0 A_TakeInventory("WalrusSlidingStart",1)
GUTP A 0 A_PlaySoundEx("misc/hugequake2","Weapon")
TNT1 A 0 A_FireCustomMissile("WalrusSlideHit",0,0,0,0)
TNT1 A 10
TNT1 A 10 ACS_ExecuteAlways(403,0,1)
goto SlideEnd

SlideEnd:
TNT1 A 0 A_TakeInventory("WalrusSliding",1)
TNT1 A 0 A_TakeInventory("WalrusSlideHitFlag",1)
//Player.AttackZOffset 8
//Height 56 / 2 = 28
//MaxStepHeight 48
TNT1 A 0 A_FireCustomMissile("WalrusSpaceCheck",0,0,0,12)
TNT1 A 1
SlideEndLoop:
TNT1 A 0 A_JumpIfInventory("InSomething",1,2)
TNT1 A 0 ACS_ExecuteAlways(400,0,0)
TNT1 A 0 A_FireCustomMissile("WalrusSpaceCheck",0,0,0,12)
TNT1 A 1
goto SlideEndLoop

SlideEndSpawn:
TNT1 A 0 A_TakeInventory("BigSpawnFlag",1)
TNT1 A 0 A_FireCustomMissile("WalrusSpaceCheck",0,0,0,12)
TNT1 A 1
goto SlideEndLoop
}
}

actor WalrusAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 300
}

actor FrostTowerBreath
{
//Translation "10:140=75:75"
Translation "10:200=75:75"
PROJECTILE
//+RIPPER
//Rippers don't enter XDeath states on +DONTRIP actors, FuuF
Speed 30
damagetype "FrostTowerBuild"
Radius 25
Height 25
Damage (3)
scale 6.25
reactiontime 15
States
{
Spawn:
BLIZ C 0
BLIZ C 1 A_CountDown
loop
Death:
BLIZ C 0 A_SpawnItemEx("FrostTowerBSpawn")
XDeath:
BLIZ C 1
stop
}
}

actor FrostTowerBreath2 : FrostTowerBreath
{
damagetype "FrostTowerB"
reactiontime 30
Radius 10
Height 10
+RIPPER
scale 2.5
states
{
Death:
BLIZ C 0
goto XDeath
}
}

actor FrostTowerBSpawn
{
Translation "10:200=75:75"
PROJECTILE
+NOTARGETSWITCH

+NOGRAVITY

+RIPPER
+THRUGHOST
+GHOST
Damage(0)

-NOBLOCKMAP
+SHOOTABLE
//+DONTRIP
+NODAMAGETHRUST
+NOBLOOD
//+DONTSPLASH
mass 0x7FFFFFFF

painchance 256

+NOTARGETSWITCH

radius 10
height 10
scale 5.0
states
{
Spawn:
TNT1 A 0
//Don't use A_Countdown
ICEW A 1 A_GiveInventory("CutterFlag",1)
TNT1 A 1 A_GiveInventory("CutterFlag",1)
TNT1 A 0 A_JumpIfInventory("CutterFlag",25,"Death")
loop
Pain.FrostTowerBuild:
TNT1 A 0 A_SpawnItemEx("FrostTowerBSpawn2")
stop
Death:
TNT1 A 0 A_SpawnItemEx("FrostTowerB")
stop
}
}

actor FrostTowerBSpawn2 : FrostTowerBSpawn
{
radius 15
height 15
scale 7.5
states
{
Pain.FrostTowerBuild:
TNT1 A 0 A_SpawnItemEx("FrostTowerBSpawn3")
stop
Death:
TNT1 A 0 A_SpawnItemEx("FrostTowerB2")
stop
}
}

actor FrostTowerBSpawn3 : FrostTowerBSpawn
{
radius 20
height 20
scale 10.0
states
{
Pain.FrostTowerBuild:
TNT1 A 0 A_SpawnItemEx("FrostTowerBSpawn4")
stop
Death:
TNT1 A 0 A_SpawnItemEx("FrostTowerB3")
stop
}
}

actor FrostTowerBSpawn4 : FrostTowerBSpawn3
{
radius 25
height 25
scale 12.5
states
{
Pain.FrostTowerBuild:
Death:
TNT1 A 0 A_SpawnItemEx("FrostTowerB4")
stop
}
}

actor FrostTowerB
{
Translation "10:200=75:75"
PROJECTILE
-NOGRAVITY
+NOEXPLODEFLOOR
+MOVEWITHSECTOR

+GHOST

mass 0x7FFFFFFF
-NOBLOCKMAP
+SHOOTABLE
+NOTARGETSWITCH
+DONTRIP

+NOPAIN
Health 200
damagefactor "FrostTowerBuild", 0.0
damagefactor "FrostTowerB", 0.0
damagefactor "Fire", 0.1 //SeekerMissiles

damage (20)
//+INVULNERABLE
//+NORADIUSDMG
scale 5.0

//gravity 0.8
gravity 1.6

speed 30
radius 10
height 10
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_JumpIf(z-floorz>0,"Fall")
TNT1 A 0 A_Jump(256,"OnGround")
//TNT1 A 0 A_CheckFloor("OnGround")
TNT1 A 1
loop

Fall:
ICEW A 0 A_JumpIf(z-floorz==0,"RespawnOnGround")
ICEW A 1
loop

RespawnOnGround:
ICEW A 0 A_SpawnItemEx("FrostTowerB")
ShardFX:
ICEW A 0
stop

OnGround:
ICEW A 0 A_JumpIf(z-floorz>0,"SemiShatterAndFall")
ICEW A 0 A_JumpIfCloser(100,"Shatter0")
ICEW A 1 DamageThing(1)
loop

Shatter0:
ICEW A 0 A_FaceTarget
ICEW A 0 A_Setangle(angle-180.0)
Shatter:
ICEW A 0 A_CustomMissile("FrostTowerBBall",7.5+random(-3,3),random(-3,3),random(-15,15),2,random(10,20))
ICEW A 0 A_CustomMissile("FrostTowerBBall",7.5+random(-3,3),random(-3,3),random(-15,15),2,random(10,20))
stop

SemiShatterAndFall:
ICEW A 0 A_CustomMissile("FrostTowerBBall",7.5+random(-3,3),random(-3,3),random(0,359),2,random(10,20))
ICEW A 0 A_CustomMissile("FrostTowerBBall",7.5+random(-3,3),random(-3,3),random(0,359),2,random(10,20))
Death:
ICEW A 0
stop
}
}

actor FrostTowerB2 : FrostTowerB
{
radius 15
height 15
scale 7.5
states
{
RespawnOnGround:
ICEW A 0 A_SpawnItemEx("FrostTowerB2")
goto ShardFX

OnGround:
ICEW A 0 A_JumpIf(z-floorz>0,"SemiShatterAndFall")
ICEW A 0 A_JumpIfCloser(125,"Shatter0")
ICEW A 1 DamageThing(1)
loop

Shatter0:
ICEW A 0 A_FaceTarget
ICEW A 0 A_Setangle(angle-180.0)
Shatter:
ICEW A 0 A_CustomMissile("FrostTowerBBall",10.0+random(-4,4),random(-4,4),random(-20,20),2,random(10,25))
ICEW A 0 A_CustomMissile("FrostTowerBBall",10.0+random(-4,4),random(-4,4),random(-20,20),2,random(10,25))
ICEW A 0 A_CustomMissile("FrostTowerBBall",10.0+random(-4,4),random(-4,4),random(-20,20),2,random(10,25))
goto "Super::Shatter"

SemiShatterAndFall:
ICEW A 0 A_CustomMissile("FrostTowerBBall",10.0+random(-4,4),random(-4,4),random(0,359),2,random(10,25))
ICEW A 0 A_CustomMissile("FrostTowerBBall",10.0+random(-4,4),random(-4,4),random(0,359),2,random(10,25))
ICEW A 0 A_CustomMissile("FrostTowerBBall",10.0+random(-4,4),random(-4,4),random(0,359),2,random(10,25))
Death:
ICEW A 0 A_SpawnItemEx("FrostTowerB")
stop
}
}

actor FrostTowerB3 : FrostTowerB2
{
radius 20
height 20
scale 12.5
states
{
RespawnOnGround:
ICEW A 0 A_SpawnItemEx("FrostTowerB3")
goto ShardFX

OnGround:
ICEW A 0 A_JumpIf(z-floorz>0,"SemiShatterAndFall")
ICEW A 0 A_JumpIfCloser(150,"Shatter0")
ICEW A 1 DamageThing(1)
loop

Shatter0:
ICEW A 0 A_FaceTarget
ICEW A 0 A_Setangle(angle-180.0)
Shatter:
ICEW A 0 A_CustomMissile("FrostTowerBBall",12.5+random(-5,5),random(-5,5),random(-25,25),2,random(10,30))
ICEW A 0 A_CustomMissile("FrostTowerBBall",12.5+random(-5,5),random(-5,5),random(-25,25),2,random(10,30))
ICEW A 0 A_CustomMissile("FrostTowerBBall",12.5+random(-5,5),random(-5,5),random(-25,25),2,random(10,30))
ICEW A 0 A_CustomMissile("FrostTowerBBall",12.5+random(-5,5),random(-5,5),random(-25,25),2,random(10,30))
goto "Super::Shatter"

SemiShatterAndFall:
ICEW A 0 A_CustomMissile("FrostTowerBBall",12.5+random(-5,5),random(-5,5),random(0,359),2,random(10,30))
ICEW A 0 A_CustomMissile("FrostTowerBBall",12.5+random(-5,5),random(-5,5),random(0,359),2,random(10,30))
ICEW A 0 A_CustomMissile("FrostTowerBBall",12.5+random(-5,5),random(-5,5),random(0,359),2,random(10,30))
ICEW A 0 A_CustomMissile("FrostTowerBBall",12.5+random(-5,5),random(-5,5),random(0,359),2,random(10,30))
Death:
ICEW A 0 A_SpawnItemEx("FrostTowerB2")
stop
}
}

actor FrostTowerB4 : FrostTowerB3
{
radius 25
height 25
scale 15.0
states
{
RespawnOnGround:
ICEW A 0 A_SpawnItemEx("FrostTowerB4")
goto ShardFX

OnGround:
ICEW A 0 A_JumpIf(z-floorz>0,"SemiShatterAndFall")
ICEW A 0 A_JumpIfCloser(175,"Shatter0")
ICEW A 1 DamageThing(1)
loop

Shatter0:
ICEW A 0 A_FaceTarget
ICEW A 0 A_Setangle(angle-180.0)
Shatter:
ICEW A 0 A_CustomMissile("FrostTowerBBall",15.0+random(-6,6),random(-6,6),random(-30,30),2,random(10,35))
ICEW A 0 A_CustomMissile("FrostTowerBBall",15.0+random(-6,6),random(-6,6),random(-30,30),2,random(10,35))
ICEW A 0 A_CustomMissile("FrostTowerBBall",15.0+random(-6,6),random(-6,6),random(-30,30),2,random(10,35))
ICEW A 0 A_CustomMissile("FrostTowerBBall",15.0+random(-6,6),random(-6,6),random(-30,30),2,random(10,35))
ICEW A 0 A_CustomMissile("FrostTowerBBall",15.0+random(-6,6),random(-6,6),random(-30,30),2,random(10,35))
goto "Super::Shatter"

SemiShatterAndFall:
ICEW A 0 A_CustomMissile("FrostTowerBBall",15.0+random(-6,6),random(-6,6),random(0,359),2,random(10,35))
ICEW A 0 A_CustomMissile("FrostTowerBBall",15.0+random(-6,6),random(-6,6),random(0,359),2,random(10,35))
ICEW A 0 A_CustomMissile("FrostTowerBBall",15.0+random(-6,6),random(-6,6),random(0,359),2,random(10,35))
ICEW A 0 A_CustomMissile("FrostTowerBBall",15.0+random(-6,6),random(-6,6),random(0,359),2,random(10,35))
ICEW A 0 A_CustomMissile("FrostTowerBBall",15.0+random(-6,6),random(-6,6),random(0,359),2,random(10,35))
Death:
ICEW A 0 A_SpawnItemEx("FrostTowerB3")
stop
}
}

actor FrostTowerBBall
{
PROJECTILE
-NOGRAVITY
+THRUGHOST
Damage (15)
damagetype "FrostTowerB"
Height 10
Radius 10
speed 45
//gravity 0.8
gravity 1.6
Scale 2.5
Translation "198:198=75:75","87:87=75:75","200:255=4:4"
States
{
Spawn:
KNIG A 0
KNIG A 2
KNIG A 0 A_ChangeFlag("THRUGHOST",0)
Spawn2:
KNIG A 1
loop
Death:
TNT1 A 0 A_SpawnItemEx("FrostTowerBBall2")
XDeath:
TNT1 A 0
stop
}
}

actor FrostTowerBBall2 : FrostTowerBBall
{
+NOGRAVITY
+MOVEWITHSECTOR
reactiontime 150
states
{
Spawn:
KNIG A 0
KNIG A 1 A_CountDown
loop
Death:
TNT1 A 0
goto XDeath
}
}

actor WalrusSlideHitCheck
{
PROJECTILE
+DONTSPLASH
+DONTBLAST
+NOEXPLODEFLOOR
+THRUGHOST
//Radius 16
//Height 56
//MaxStepHeight 24
Radius 12
Height 32
damage 0
States
{
Spawn:
TNT1 A 0
TNT1 A 1
stop
Death:
TNT1 A 0 A_GiveToTarget("WalrusSlideHitFlag",1)
Stop
}
}

actor WalrusSpaceCheck : SpaceCheck
{
//Radius 32
//Height 112
//MaxStepHeight 48

radius 32
height 64
}

actor WalrusSlideHit
{
PROJECTILE
+DONTSPLASH
+DONTBLAST
Speed 0
radius 0
height 0
Obituary "%o was slammed by %k's Body Slam."
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Explode(35,240,0)
TNT1 A 7
TNT1 A 0 A_Explode(25,180,0)
TNT1 A 7
TNT1 A 0 A_Explode(15,120,0)
TNT1 A 7
TNT1 A 0 A_Explode(5,60,0)
stop
}
}

actor WalrusSliding : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor WalrusMotion : PowerSpeed
{
//Speed 0.1 //Pfft morphed actor
Speed 1.0
Powerup.Duration 5
}

actor WalrusSlideHitFlag : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor WalrusSlideAngle : Inventory
{
inventory.amount 1
inventory.maxamount 255
}

actor WalrusSlideAngleAdjust : Inventory
{
inventory.amount 1
inventory.maxamount 10
}

actor WalrusSlideThrust : CustomInventory
{
states
{
Pickup:
TNT1 A 0 ACS_ExecuteAlways(402,0,ACS_ExecuteWithResult(401),angle*256/360,3)
Thrust:
TNT1 A 0 A_GiveInventory("WalrusMotion",1)
TNT1 A 0 A_CheckFloor("GroundThrust")
TNT1 A 0 ThrustThing(ACS_ExecuteWithResult(401), 0.75, 0, 0)
stop
GroundThrust:
TNT1 A 0 ThrustThing(ACS_ExecuteWithResult(401), 2, 0, 0)
stop
}
}

actor WalrusSlideThrust2 : CustomInventory
{
states
{
Pickup:
TNT1 A 0 ACS_ExecuteAlways(402,0,ACS_ExecuteWithResult(401),angle*256/360,3)
Thrust:
TNT1 A 0 A_GiveInventory("WalrusMotion",1)
TNT1 A 0 A_CheckFloor("GroundThrust")
TNT1 A 0 ThrustThing(ACS_ExecuteWithResult(401), 0.75, 0, 0)
stop
GroundThrust:
TNT1 A 0 ThrustThing(ACS_ExecuteWithResult(401), 1.5, 0, 0)
stop
}
}

actor WalrusBreathCD : PowerDamage
{
powerup.duration 18
damagefactor "normal", 1.0
}

actor WalrusSlidingStart : PowerDamage
{
powerup.duration 60
damagefactor "normal", 1.0
}

actor FrostTowerWep : Base8BDMWeapon
{
//$Category MM8BDM-Weapons
Weapon.AmmoUse 1
Weapon.AmmoGive 28
Weapon.SlotNumber 4
Obituary "%o was toppled over by %k's Frost Tower."
Inventory.Pickupmessage "Power up! Frost Tower!"
weapon.ammotype "FrostTowerAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
inventory.icon "ICESI"
Scale 2.0
States
{
Spawn:
WEAP P 1
loop
Ready:
ICES D 0 ACS_ExecuteAlways(998,0,15)
ICES D 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
ICES D 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
ICES D 1 A_Raise
Loop
Fire:
ICES D 0 A_JumpIfNoAmmo("NoAmmo")
ICES D 0 A_PlaySoundEx("weapon/iceslasher","Weapon")
ICES D 0 A_FireCustomMissile("IceSlasher",0,1,8,0)
ICES EFD 3
ICES D 0 A_Refire
Goto Ready+1
NoAmmo:
ICES D 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor FrostTowerAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
}