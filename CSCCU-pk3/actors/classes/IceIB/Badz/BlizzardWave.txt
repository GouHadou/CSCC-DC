Actor BlizzardWaveBoss : MegaBuster
{
Weapon.AmmoUse 20
Weapon.AmmoGive 1
Weapon.SlotNumber 1
Obituary "%o couldn't stop %k's Blizzard Wave."
weapon.ammotype "BlizzardEnergyAmmo"
weapon.ammotype2 "SnowStormAmmo"
Inventory.PickupMessage "You found a legendary blade from the Land of Elsewhere! But do you know how to use it?"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
Scale 2.0
States
{
Spawn:
BLIZ A 1
stop
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BLIZ A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
BWAV A 1 A_Raise
Loop
Ready:
BWAV A 0 ACS_ExecuteAlways (998,0,280)
BWAV A 0 A_GunFlash
BWAV A 1 A_WeaponReady(WRF_NOSWITCH)
Goto Ready+2
Flash:
TNT1 A 0 A_JumpIfInventory("BadzInversionFlag",1,"InvertedMode")
TNT1 A 5 A_GiveInventory("BlizzardEnergyAmmo",3)
Goto Flash
InvertedMode:
TNT1 A 5 A_GiveInventory("SnowStormAmmo",3)
TNT1 A 0 A_JumpIfInventory("BadzInversionFlag",1,"InvertedMode")
Goto Flash
Fire:
BWAV A 0 A_PlaySoundEx("weapon/mirrordeflect2","Weapon")
BWAV A 0 A_JumpIfInventory("BadzInversionFlag",1,"InvertedFire")
BWAV D 2 A_FireCustomMissile("BlizzardWaveProjectile",-9,0,8,0)
BWAV E 2 A_FireCustomMissile("BlizzardWaveProjectile",-3,0,8,0)
BWAV F 2 A_FireCustomMissile("BlizzardWaveProjectile",3,0,8,0)
BWAV G 2 A_FireCustomMissile("BlizzardWaveProjectile",9,0,8,0)
TNT1 A 20
BWAV CB 2
Goto Ready+2
InvertedFire:
BWAV D 2 A_FireCustomMissile("BlizzardWaveProjectile2",-9,0,8,0)
BWAV E 2 A_FireCustomMissile("BlizzardWaveProjectile2",-3,0,8,0)
BWAV F 2 A_FireCustomMissile("BlizzardWaveProjectile2",3,0,8,0)
BWAV G 2 A_FireCustomMissile("BlizzardWaveProjectile2",9,0,8,0)
TNT1 A 20
BWAV CB 2
Goto Ready+2
AltFire:
BWAV A 0 A_JumpIfInventory("DecoyFlag2",1,"DoNothing")
BWAV A 0 A_GiveInventory("DecoyFlag1",1)
BWAV A 0 A_GiveInventory("DecoyFlag2",1)
TNT1 A 0 A_SpawnItemEx("NegaSwordmanLegs",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM)
BWAV ABC 2
BWAV A 0 SetPlayerProperty(0,1,4)
BWAV A 0 A_ChangeFlag(NOGRAVITY,1)
BWAV A 0 A_PlaySoundEx("weapon/mirrordeflect2","Weapon")
BWAV A 0 A_SpawnItemEx("GlitteringWhirlwind",0,0,0,momx*1.5,momy*1.5,0,0,SXF_ABSOLUTEMOMENTUM)
TNT1 A 35 A_Stop
BWAV A 0 A_ChangeFlag(NOGRAVITY,0)
TNT1 A 0 A_SpawnItemEx("TeleportFog",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
BWAV A 0 A_TakeInventory("DecoyFlag1",1)
BWAV A 0 SetPlayerProperty(0,0,4)
BWAV CBA 2
DoNothing:
BWAV A 7
Goto Ready+2
}
}

Actor BlizzardWaveProjectile
{
PROJECTILE
Radius 10
Height 10
scale 2.5
damage (7)
speed 32
damagetype "BlizzardAttack"
States
{
Spawn:
FCRA D 1 A_JumpIfInTargetInventory("SnowStormAmmo",3,"Blizzard")
loop
Blizzard:
FCRA D 1 A_TakeFromTarget("SnowStormAmmo",3)
FCRA D 0 A_SpawnItemEx("BlizzardBit",random(-100,100),random(-100,100),random(-30,30),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM)
Goto Spawn
Death:
FCRA ABC 4
stop
}
}

actor BlizzardWaveProjectile2 : BlizzardWaveProjectile
{
States
{
Spawn:
FCRA D 1 A_JumpIfInTargetInventory("BlizzardEnergyAmmo",3,"Blizzard")
loop
Blizzard:
FCRA D 1 A_TakeFromTarget("BlizzardEnergyAmmo",3)
FCRA D 0 A_SpawnItemEx("BlizzardBit",random(-100,100),random(-100,100),random(-30,30),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM)
Goto Spawn
}
}

Actor BlizzardBit : BlizzardWaveProjectile
{
PROJECTILE
Radius 4
Height 4
damage (3)
Scale 1.0
States
{
Spawn:
FCRA CB 2
FCRA A 2 A_ScaleVelocity(0.9)
FCRA D 4 
Goto Spawn+3
}
}

Actor NegaSwordmanLegs
{
Radius 16
Height 27
Scale 2.5
+NOBLOCKMONST
+REFLECTIVE
States
{
Spawn:
SWOL A 6
SWOL A 35 A_ChangeFlag("SOLID",1)
SWOL A 0 A_SpawnItemEx("TeleportFog",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

actor GlitteringWhirlwind 
{
PROJECTILE
+RIPPER
+DONTREFLECT
+DONTBLAST
+CANBOUNCEWATER
Translation "5:255=203:203"
BounceType "Hexen"
BounceFactor 1.0
RenderStyle "Translucent"
Damagetype "BlizzardAttack"
Obituary "%o couldn't stop %k's glittering whirlwind."
Alpha 0.9
Radius 48
Height 20
Damage (4)
Scale 6.5
Speed 0
ReactionTime 245
States
{
Spawn:
TORH FGH 1 A_CountDown
TORH F 0 A_SpawnItemEx("FreezeFX",random(-100,100),random(-100,100),random(-100,100))
loop
Death:
TNT1 A 1 A_TakeFromTarget("DecoyFlag2",1)
TORH FGHFGHFGH 1 A_FadeOut
stop
}
}

actor BlizzardEnergyAmmo : Ammo 
{
inventory.amount 1
inventory.maxamount 350
}

actor SnowStormAmmo : Ammo 
{
inventory.amount 1
inventory.maxamount 350
}

actor BadzInversionFlag : Ammo
{
inventory.amount 1
inventory.maxamount 1
}

actor DecoyFlag1 : Ammo
{
inventory.amount 1
inventory.maxamount 1
}


actor DecoyFlag2 : Ammo
{
inventory.amount 1
inventory.maxamount 1
}

actor BadzInversionItem : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
inventory.icon "BWAVI"
+INVENTORY.UNDROPPABLE
+COUNTITEM
+INVBAR
states
{
Spawn:
TNT1 A 0
loop
Use:
TNT1 A 0 A_JumpIfInventory("BadzInversionFlag",1,"RemoveFlag")
TNT1 A 0 A_JumpIfInventory("SnowStormAmmo",3,"OhNoYouDont")
TNT1 A 0 A_PlaySoundEx("misc/gravitywarn","Weapon")
TNT1 A 1 A_GiveInventory("BadzInversionFlag",1)
fail
RemoveFlag:
TNT1 A 0 A_JumpIfInventory("BlizzardEnergyAmmo",3,"OhNoYouDont")
TNT1 A 0 A_PlaySoundEx("misc/gravitywarn","Weapon")
TNT1 A 1 A_TakeInventory("BadzInversionFlag",1)
fail
OhNoYouDont:
TNT1 A 1
fail
}
}