actor BlizzardSwordBoss : MegaBuster
{
Weapon.AmmoUse 4
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Obituary "%o was slashed and frozen by %k's Blizzard Sword."
Inventory.Pickupmessage "It's cold that you would cheat yourself this weapon."
weapon.ammotype "BlizzardKnightAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
+WEAPON.NOAUTOAIM
//inventory.icon "COOWI"
Scale 2.0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 //A_SpawnItemEx("BlizzardSlashWepDropped")
stop
/*Ready:
BSWD A 0 ACS_ExecuteAlways(998,0,2828)
BSWD A 6 A_WeaponReady
BSWD A 0 A_GiveInventory("BlizzardKnightAmmo",1)
Goto Ready+1*/
Ready:
BSWD A 0 ACS_ExecuteAlways(998,0,2828)
BSWD A 0 A_JumpIfInventory("ShieldCheck",1,"ReadyWithShield")
BSWD A 0 A_JumpIfInventory("BlizzardKnightAmmo",4,"Ready2")
BSWD A 6 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
BSWD A 0 A_GiveInventory("BlizzardKnightAmmo",1)
Goto Ready+2
Ready2:
BSWD A 0 A_JumpIfInventory("BlizzardKnightAmmo",28,"Ready3")
BSWD A 6 A_WeaponReady(WRF_NOSECONDARY|WRF_NOSWITCH)
BSWD A 0 A_GiveInventory("BlizzardKnightAmmo",1)
loop
Ready3:
BSWD A 6 A_WeaponReady(WRF_NOSWITCH)
BSWD A 0 A_GiveInventory("BlizzardKnightAmmo",1)
loop
ReadyWithShield:
BSWD A 0 A_JumpIfInventory("BlizzardKnightAmmo",4,"Ready2WithShield")
BSWD A 10 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSWITCH)
BSWD A 0 A_GiveInventory("BlizzardKnightAmmo",1)
loop
Ready2WithShield:
BSWD A 10 A_WeaponReady(WRF_NOSWITCH)
BSWD A 0 A_GiveInventory("BlizzardKnightAmmo",1)
loop
Deselect:
TNT1 A 0 A_SelectWeapon("BlizzardSwordBoss")
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BSWD A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
BSWD A 1 A_Raise
Loop
Fire:
BSWD A 0 A_JumpIfInventory("IsBot",1,"BotUseShield")
NormalFire:
BSWD A 0 A_JumpIfNoAmmo("NoAmmo")
ContinueFire:
BSWD A 0 A_PlaySoundEx("weapon/iceslasher","Weapon")
BSWD A 0
BSWD A 0 A_CheckFloor("GroundSwing")
Goto AirSwing
GroundSwing:
BSWD C 1 A_FireCustomMissile("BlizzardSwordAttack",50,1,-4,0,0,-10)
//BSWD C 0 A_SpawnItemEx("FSSParkSpawnerB",32,0,8,0,0,0,0,0,0)
BSWD D 1 A_FireCustomMissile("BlizzardSwordAttack",25,0,-2,0,0,-5)
BSWD E 0 A_FireCustomMissile("BlizzardSwordAttack",0,0,0,0,0,0)
BSWD E 1 A_FireCustomMissile("BlizzardSlash",0,0,0,0,0,0)
BSWD F 1 A_FireCustomMissile("BlizzardSwordAttack",-25,0,2,0,0,10)
BSWD G 1 A_FireCustomMissile("BlizzardSwordAttack",-50,0,4,0,0,20)
Goto Finish
AirSwing:
BSWD J 1 A_FireCustomMissile("BlizzardSwordAttackB",-50,1,4,0,0,20)
BSWD K 1 A_FireCustomMissile("BlizzardSwordAttackB",-25,0,2,0,0,10)
BSWD L 0 A_FireCustomMissile("BlizzardSwordAttackB",0,0,0,0,0,0)
BSWD L 1 A_FireCustomMissile("BlizzardSlash",0,0,0,0,0,0)
BSWD M 1 A_FireCustomMissile("BlizzardSwordAttackB",25,0,-2,0,0,-5)
BSWD N 1 A_FireCustomMissile("BlizzardSwordAttackB",50,0,-4,0,0,-10)
Goto Finish
Finish:
TNT1 A 14
BSWD HI 2
BSWD A 10
Goto Ready+1
BotUseShield:
BSWD A 0 A_JumpIfInventory("ShieldCheck", 1,"NormalFire")
BSWD A 0 A_JumpIfInventory("BlizzardKnightAmmo",28,1)
Goto NormalFire
AltFire:
BSWD A 0 A_JumpIfInventory("ShieldCheck", 1,"ShieldThrow")
BSWD A 0 A_JumpIfInventory("BlizzardKnightAmmo",28,1)
Goto NoAmmo
BSWD A 0 A_PlaySoundEx("weapon/blizzardshield","Weapon")
BSWD A 0 A_GiveInventory("ShieldCheck",1)
BSWD A 0 A_GiveInventory("BlizzardArmor",1)
BSWD A 0 A_TakeInventory("BlizzardKnightAmmo",28)
BSWD A 0 A_SpawnItemEx("BlizzardShieldWatcher")
BSWD CDEFGHI 1
BSWD A 0 A_Refire
Goto Ready+1
ShieldThrow:
BSWD A 0 A_GiveInventory("BlizzardDone",1)
BSWD JK 1
BSWD A 0 A_TakeInventory("BlizzardDone",1)
//BSWD A 0 A_PlayWeaponSound("weapon/junkshield")
Throwing:
BSWD L 1 A_FireCustomMissile("BlizzardShield",0,0,0,0)
BSWD MN 1
BSWD HI 2
BSWD A 10
Goto Ready+1
AltHold:
BSWD A 6
BSWD A 0 A_Refire
Goto Ready+1
NoAmmo:
BSWD A 0
Goto Ready+1
}
}

actor BlizzardKnightAmmo : Ammo
{
+IGNORESKILL
inventory.amount 1
inventory.maxamount 28
}

actor BlizzardSwordAttack
{
PROJECTILE
+RIPPER
+DONTBLAST
damage (0)
height 5
radius 5
Speed 16
scale 2.5
states
{
Spawn:
TNT1 A 0
TNT1 AAAAA 1 A_SpawnItemEx("BlizzardSwordTrail")
Goto Death
Death:
TNT1 A 0
stop
}
}

actor BlizzardSwordAttackB : BlizzardSwordAttack
{
States
{
Spawn:
TNT1 A 0
TNT1 AAAAA 1 A_SpawnItemEx("BlizzardSwordTrailB")
Goto Death
}
}

actor BlizzardSwordTrail
{
Obituary "%o was slashed and frozen by %k's Blizzard Sword."
+FORCEXYBILLBOARD
//seesound "weapon/firestorm"
PROJECTILE
+DONTBLAST
//+RIPPER
height 8
radius 8
damage (4)
damagetype "BlizzardSword"
scale 2.5 
States
{
Spawn:
FCRA E 0 
FCRA E 0 A_ChangeVelocity(0, -15, 0, CVF_RELATIVE | CVF_REPLACE)
FCRA EDB 4
stop
}
}

actor BlizzardSwordTrailB : BlizzardSwordTrail
{
States
{
Spawn:
FCRA E 0 
FCRA E 0 A_ChangeVelocity(0, 15, 0, CVF_RELATIVE | CVF_REPLACE)
FCRA EDB 4
stop
}
}

actor BlizzardSlash
{
PROJECTILE
-DONTSPLASH
-NOGRAVITY
+FORCEXYBILLBOARD
height 5
radius 8
damage (16)
speed 45
scale 3
Translation "210:210=196:196"
Obituary "%o was slashed from afar by %k's Blizzard Slash."
damagetype "BlizzardSword"
States
{
Spawn:
SCLA J 0
SCLA J 0 A_PlaySoundEx("weapon/slashclaw","Weapon")
SCLA J 2 A_CheckFloor("Death")
Goto Spawn+2
Death:
TNT1 AAAA 0 A_SpawnItemEx("BlizzardSlashBit",0,0,0,random(-10,10),random(-10,10),random(10,15),random(0,360))
TNT1 A 1 A_PlaySoundEx("weapon/icewavebreak","Weapon")
stop
}
}

actor BlizzardSlashBit
{
damage 0
+RIPPER
-NOGRAVITY
+CLIENTSIDEONLY
-SOLID
scale 2.5
//+NOINTERACTION
Height 2
Radius 2
States
{
Spawn:
FCRA E 4
TNT1 A 2
FCRA E 4
TNT1 A 2
FCRA E 4
TNT1 A 2
FCRA E 4
TNT1 A 2
FCRA E 4
TNT1 A 2
stop
}
}

actor BlizzardArmor : BasicArmorPickup
{
Armor.Savepercent 75
Armor.Saveamount 100
States
{
Spawn:
PLAY A 1
loop
}
}

actor BlizzardShieldWatcher
{
Height 0
Radius 0
+NOINTERACTION
-SOLID
+NOGRAVITY
+MISSILE
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("BlizzardDone",1,"Death")
TNT1 A 1 A_GiveToTarget("BlizzardShieldGiveFX1",1)
TNT1 A 1 A_GiveToTarget("BlizzardShieldGiveFX1",1)
TNT1 A 0 A_JumpIfInTargetInventory("BlizzardDone",1,"Death")
TNT1 A 1 A_GiveToTarget("BlizzardShieldGiveFX2",1)
TNT1 A 1 A_GiveToTarget("BlizzardShieldGiveFX2",1)
TNT1 A 0 A_JumpIfInTargetInventory("BasicArmor",1,1)
Goto Break
TNT1 A 0 A_JumpIfInTargetInventory("BlizzardDone",1,"Death")
TNT1 A 1 A_GiveToTarget("BlizzardShieldGiveFX3",1)
TNT1 A 1 A_GiveToTarget("BlizzardShieldGiveFX3",1)
TNT1 A 0 A_JumpIfInTargetInventory("BlizzardDone",1,"Death")
TNT1 A 1 A_GiveToTarget("BlizzardShieldGiveFX4",1)
TNT1 A 1 A_GiveToTarget("BlizzardShieldGiveFX4",1)
TNT1 A 0 A_JumpIfInTargetInventory("BasicArmor",1,"Spawn")
goto Break
Break:
TNT1 A 1 A_GiveToTarget("BlizzardShieldDebrisSpawner",1)
Death:
TNT1 A 1 A_TakeFromTarget("ShieldCheck",1)
TNT1 A 0 A_TakeFromTarget("BasicArmor",100)
stop
}
}

actor BlizzardShieldDebrisSpawner : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 1
loop
Use:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("weapon/centaurflash","Weapon")
TNT1 AAAA 1 A_SpawnItemEx("BlizzardSlashBit",0,0,0,random(-10,10),random(-10,10),random(10,15),random(0,360))
stop
}
}

actor BlizzardShieldGiveFX1 : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 1
loop
Use:
TNT1 A 1 A_SpawnItemEx("BlizzardShieldFX1",0,0,0,momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
stop
}
}

actor BlizzardShieldGiveFX2 : BlizzardShieldGiveFX1
{
States
{
Use:
TNT1 A 1 A_SpawnItemEx("BlizzardShieldFX2",0,0,0,momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
stop
}
}

actor BlizzardShieldGiveFX3 : BlizzardShieldGiveFX1
{
States
{
Use:
TNT1 A 1 A_SpawnItemEx("BlizzardShieldFX3",0,0,0,momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
stop
}
}

actor BlizzardShieldGiveFX4 : BlizzardShieldGiveFX1
{
States
{
Use:
TNT1 A 1 A_SpawnItemEx("BlizzardShieldFX4",0,0,0,momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
stop
}
}

actor BlizzardShieldFX1
{
scale 2.5
- SOLID
+NOGRAVITY
+CLIENTSIDEONLY
States
{
Spawn:
ICCI A 1
ICCI A 1
stop
}
}

actor BlizzardShieldFX2 : BlizzardShieldFX1
{
States
{
Spawn:
ICCI B 1
ICCI B 1
stop
}
}

actor BlizzardShieldFX3 : BlizzardShieldFX1
{
States
{
Spawn:
ICCI C 1
ICCI C 1
stop
}
}

actor BlizzardShieldFX4 : BlizzardShieldFX1
{
States
{
Spawn:
ICCI D 1
ICCI D 1
stop
}
}


actor BlizzardShield
{
scale 2.5
PROJECTILE
Damagetype "BlizzardSword"
Obituary "%o was frozen over by %k's Blizzard Shield."
Damage (28)
+DONTBLAST
Speed 20
Radius 12
Height 12
States
{
Spawn:
ICCI ABCD 2
loop
}
}

actor BlizzardDone : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor BlizzardSlashWep : MegaBuster 28000
{
//$Category MM8BDM-Weapons
Weapon.AmmoUse 2
Weapon.AmmoGive 112
Weapon.SlotNumber 4
Obituary "%o was slashed from afar by %k's Blizzard Slash."
Inventory.Pickupmessage "Power up! Blizzard Slash!"
weapon.ammotype "BlizzardSlashAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
inventory.icon "BSLWI"
Scale 2.0
States
{
Spawn:
BSLA Z 1
loop
Ready:
BSLA A 0 ACS_ExecuteAlways(998,0,54)
BSLA A 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BSLA A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
BSLA A 1 A_Raise
Loop
Fire:
BSLA A 0 A_JumpIfNoAmmo("NoAmmo")
BSLA BC 1
BSLA A 0 A_PlaySoundEx("weapon/slashclaw","Weapon")
BSLA A 0 A_FireCustomMissile("BlizzardSlash",0,1,11,-8)
BSLA DE 2
BSLA F 4
BSLA G 15
BSLA CBA 2 A_WeaponReady
Goto Ready+1
NoAmmo:
BSLA A 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor BlizzardSlashAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
}