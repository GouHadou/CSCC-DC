actor NoColorFlag : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor MorphFlag : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor ColorsOnce : Once
{
+INVENTORY.UNDROPPABLE
}

actor WeaponCopyFlag : Inventory
{
inventory.amount 1
inventory.maxamount 9999
}

actor CopyWeapon : BaseMM8BDMWep
{
Weapon.AmmoUse 4
Weapon.AmmoGive 28
Weapon.SlotNumber 1
Obituary "%k won by defeating %o!"
weapon.ammotype "CopyAmmo"
Inventory.Pickupmessage "THE ULTIMATE IN COPY TECHNOLOGY!" 
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
+WEAPON.CHEATNOTWEAPON
+INVENTORY.UNDROPPABLE
dropitem "CopyRandomWeapon"
Scale 2.0
States
{
Spawn:
WEAP X 1
stop
Ready:
//COPY A 0 A_JumpIfInventory("MorphFlag", 1, "Reset")
CPYB A 0 ACS_ExecuteAlways(998,0,401)
CPYB A 0 A_JumpIfInventory("ColorsOnce", 1, "Ready2")
CPYB A 0 ACS_ExecuteAlways(981,0)
CPYB A 0 ACS_ExecuteAlways(CSACS_COPYCOLORS, 0)
CPYB A 0 A_SpawnItemEx("YokuPrismWatcher")
CPYB A 0 A_GiveInventory("ColorsOnce", 1)
Goto Ready2
Reset:
COPY A 1 ACS_ExecuteAlways(CSACS_COPYMORPH, 0)
Goto Ready2
Ready2:
CPYB A 0 A_TakeInventory("NoColorFlag", 1)
CPYB AAAA 1 A_WeaponReady
CPYB A 0 A_GiveInventory("CopyAmmo",1)
CPYB BBBB 1 A_WeaponReady
CPYB A 0 A_GiveInventory("CopyAmmo",1)
CPYB CCCC 1 A_WeaponReady
CPYB A 0 A_GiveInventory("CopyAmmo",1)
CPYB DDDD 1 A_WeaponReady
CPYB A 0 A_GiveInventory("CopyAmmo",1)
CPYB EEEE 1 A_WeaponReady
CPYB A 0 A_GiveInventory("CopyAmmo",1)
CPYB FFFF 1 A_WeaponReady
CPYB A 0 A_GiveInventory("CopyAmmo",1)
CPYB GGGG 1 A_WeaponReady
CPYB A 0 A_GiveInventory("CopyAmmo",1)
CPYB A 0 A_ClearReFire
Goto Ready2
Deselect:
TNT1 A 0 A_GiveInventory("NoColorFlag", 1)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
CPYB A 1 A_Lower
Loop
Select:
TNT1 A 0 A_TakeInventory("NoColorFlag", 1)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
CPYB A 1 A_Raise
Loop
AltFire:
CPYB A 0 A_JumpIfInventory("CopyAmmo", 8, "AltFireFire")
Goto NoAmmo
AltFireFire:
CPYB I 0 A_PlaySoundEx("weapon/mbuster","Weapon")
CPYB I 0 A_TakeInventory("CopyAmmo", 8)
CPYB I 0 A_FireCustomMissile("CopyAltShot",0,0,8,0)
CPYB JKIDEFG 3 
CPYB I 0 A_Refire
Goto Ready2
Fire:
CPYB A 0 A_JumpIfNoAmmo("NoAmmo") 
CPYB I 0 A_PlaySoundEx("weapon/mbuster","Weapon")
CPYB I 0 A_FireCustomMissile("CopyMainShot",0,1,8,0)
CPYB JKIDEFG 3 
CPYB I 0 A_Refire
Goto Ready2
NoAmmo:
CPYB H 1 A_JumpIfInventory("IsBot", 1, "GiveBotAmmo")
Goto Ready2
GiveBotAmmo:
CPYB H 1 A_GiveInventory("CopyAmmo", 28)
Goto Ready2
Flash:
TNT1 A 1
stop
NoFlash:
TNT1 A 1
stop
}
}

actor CopyMainShot : Megashot
{
speed 80
damagetype "CopyMain"
Obituary "%o\c- was killed by %k's\c- Copy Bullet. Seriously? What's wrong with you %o\c-?"
damage (2)
}

actor CopyAltShot 
{
PROJECTILE
+DONTBLAST
Radius 5
Height 5
scale 2.5
damagetype "CopyAlt"
Obituary "%o\c- was killed by %k's\c- Copy Bullet. Seriously? What's wrong with you %o\c-?"
damage (2)
speed 40
+SEEKERMISSILE
reactiontime 60
States
{
Spawn:
BUST A 0
BUST AA 1 A_SpawnItemEx("CopyAltShotFX")
BUST A 0 A_SeekerMissile(0, 2, SMF_LOOK|SMF_PRECISE, 256)
BUST A 0 A_CountDown
loop
Death:
TNT1 A 1
stop
}
}

actor CopyAltShotFX 
{
Radius 0
Height 0
Scale 2.5
+NOINTERACTION
+NOCLIP
+CLIENTSIDEONLY
RenderStyle "Translucent"
Alpha 0.7
States
{
Spawn:
TNT1 A 1
BUST A 1 A_Fadeout(0.1)
Goto Spawn+1
}
}

actor CopyAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 28
}

actor CopyRemover : CustomInventory 
{
inventory.amount 1
inventory.maxamount 1
//Inventory.RespawnTics 350
inventory.pickupmessage ""
inventory.icon "BANISHI"
Inventory.PickupSound "item/1up"
+INVBAR
+COUNTITEM
+INVENTORY.UNDROPPABLE
scale 2.0
States
{
Spawn:
EBAL EH 6
loop
Use:
EBAL E 0 A_JumpIfInventory("MorphFlag", 1, "Success")
EBAL E 0 A_JumpIfInventory("CopyWeapon", 1, "OkNormal")
EBAL E 0 A_GiveInventory("CopyWeapon", 1)
EBAL E 0 A_SelectWeapon("CopyWeapon")
Goto OkNormal
OkNormal:
EBAL E 0 
fail
Success:
EBAL E 0 ACS_ExecuteAlways(CSACS_COPYMORPH, 0, 999)
fail
}
}

actor CopyRandomWeapon
{
+NOINTERACTION
States
{
Spawn:
TNT1 A 0
TNT1 A 0 ACS_NamedExecuteAlways("CopyRandomWeapon", 0)
TNT1 A 2
stop
}
}