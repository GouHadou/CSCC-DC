actor LaserFlag : Once {}
actor RoaderFlag : Once {}
actor EggTossFlag: Once {}
actor SpikeFlag : Once {}
actor MineFlag : Once {}
actor PressFlag : Once {}
actor MetalPressFlag : Once {}
actor IceBlokFlag : Once {}
actor SpinDashFlag : Inventory
{
inventory.amount 1
inventory.maxamount 45
}

actor MutantYoshiTongue : BaseCSCCWep
{
//Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 4
Obituary "%o was eaten by %k."
Inventory.Pickupmessage "Power up! NOT Thunder Claw!"
weapon.ammotype2 "SpinDashAmmo"
inventory.pickupsound "weapon/weaponup"
dropitem "EggTossWepGiver"
+WEAPON.AMMO_OPTIONAL
+WEAPON.CHEATNOTWEAPON
Scale 2.0
States
{
Spawn:
WEA4 G 0 
stop
Ready:
YNOS D 0 
YNOS D 0 A_JumpIfInventory("Once", 1, "Ready2")
YNOS D 0 A_GiveInventory("Once", 1)
YNOS D 0 ACS_ExecuteAlways(998,0,WCCS_MUTANTYOSHI)
Goto Ready2
Ready2:
YNOS D 0 A_JumpIfInventory("EggTossFlag", 1, "EggThrow")
YNOS D 1 A_WeaponReady
Goto Ready2
Deselect:
TNT1 A 0 A_SelectWeapon("MutantYoshiTongueX")
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
YNOS D 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
YNOS D 1 A_Raise
Loop
BotCheck:
YNOS D 0 A_Jump(128, 1)
Goto Fire+1
YNOS D 0 A_Jump(64, "BotAlt")
YNOS D 0 A_JumpIfInventory("EggFlag", 1, "BotEgg")
Goto Fire+1
BotAlt:
YNOS D 0 A_ClearRefire
TNT1 A 0 SetPlayerProperty(0, 1, 0)
YNOS D 0 A_Jump(192, 2)
YNOS D 4 ThrustThingZ(0, Random(50, 75), 0, 0)
YNOS D 0 A_SetArg(0, 25)
BotCharge:
TNT1 A 1 A_JumpIf(!(z-floorz<=0 || momz==0), "Thok")
TNT1 A 0 A_PlaySoundEx("myoshi/spindash","Weapon")
TNT1 AAA 1 A_SetArg(0, Args[0]+1)
TNT1 A 1 A_JumpIf(!(z-floorz<=0 || momz==0), "Thok")
TNT1 A 0 A_PlaySoundEx("myoshi/spindash","Weapon")
TNT1 AAA 1 A_SetArg(0, Args[0]+1)
TNT1 A 0 A_ChangeFlag("THRUACTORS", 1)
TNT1 A 0 A_JumpIf(Args[0] >= 75, 2)
TNT1 A 0 A_JumpIf(Args[0] < 35, "BotCharge")
TNT1 A 0 A_TakeInventory("SpindashFlag",999)
TNT1 A 1 A_GiveInventory("PunkSpinning",1)
TNT1 A 0 A_Stop
Goto RollBegin

BotEgg:
YNOS D 0 A_TakeInventory("EggFlag", 1)
YNOS D 0 A_GiveInventory("EggTossFlag", 1)
Goto Ready2
Fire:
YNOS D 0 A_JumpIfInventory("IsBot", 1, "BotCheck")
YNOS D 0 A_JumpIfInventory("Heartburn", 1, "Spit.Flames")
YNOS D 0 A_JumpIfInventory("LaserFlag", 1, "Spit.ForceBeam")
YNOS D 0 A_JumpIfInventory("RoaderFlag", 1, "Spit.Tire")
YNOS D 0 A_JumpIfInventory("SpikeFlag", 1, "Spit.Spike")
YNOS D 0 A_JumpIfInventory("MineFlag", 1, "Spit.Mine")
YNOS D 0 A_JumpIfInventory("IceBlokFlag", 1, "Spit.IceBlock")
YNOS D 0 A_JumpIfInventory("PressFlag", 1, "Spit.PressBall")
YNOS D 0 A_JumpIfInventory("MetalPressFlag", 1, "Spit.MetalPress")
YNOS D 0 A_PlaySoundEx("weapon/tongue","Weapon")
YNOS D 0 A_FireCustomMissile("Tongue",0,1,0,0)
YNOS IB 3
YNOS C 5
YNOS CCCCCCCCCC 1 A_JumpIfInventory("TongueReturnFlag", 1, "Returning")
YNOS CCCCCCCCCC 1 A_JumpIfInventory("TongueReturnFlag", 1, "Returning")
YNOS CCCCCCCCCC 1 A_JumpIfInventory("TongueReturnFlag", 1, "Returning")
Returning: 
YNOS BI 4
YNOS D 8 A_TakeInventory("TongueReturnFlag", 1)
YNOS D 0 A_Refire
Goto Ready+1
Spit.Flames:
YNOS A 0 A_PlaySoundEx("myoshi/firebreath","Weapon")
YNOS A 0 A_FireCustomMissile("HeartBurnStorm",15)
YNOS A 0 A_FireCustomMissile("HeartBurnStorm",0)
YNOS A 0 A_FireCustomMissile("HeartBurnStorm",-15)
YNOS BCB 6
YNOS A 10
YNOS A 0 A_TakeInventory("Heartburn",1)
Goto Ready+1
Spit.ForceBeam:
YNOS A 0 A_PlaySoundEx("myoshi/firebreath","Weapon")
YNOS A 0 A_TakeInventory("LaserFlag",1)
YNOS BC 4
YNOS C 0 SetPlayerProperty(0,1,0)
YNOS C 0 SetPlayerProperty(0,1,4)
YNOS C 1 A_FireCustomMissile("YoshiBeam",0,0,0,-2)
YNOS CCC 1 A_FireCustomMissile("NoLagYoshiBeam",0,0,0,-2)
YNOS C 1 A_FireCustomMissile("YoshiBeam",0,0,0,-2)
YNOS CCC 1 A_FireCustomMissile("NoLagYoshiBeam",0,0,0,-2)
YNOS C 1 A_FireCustomMissile("YoshiBeam",0,0,0,-2)
YNOS CCC 1 A_FireCustomMissile("NoLagYoshiBeam",0,0,0,-2)
YNOS C 1 A_FireCustomMissile("YoshiBeam",0,0,0,-2)
YNOS CCC 1 A_FireCustomMissile("NoLagYoshiBeam",0,0,0,-2)
YNOS C 1 A_FireCustomMissile("YoshiBeam",0,0,0,-2)
YNOS CCC 1 A_FireCustomMissile("NoLagYoshiBeam",0,0,0,-2)
YNOS C 1 A_FireCustomMissile("YoshiBeam",0,0,0,-2)
YNOS CCC 1 A_FireCustomMissile("NoLagYoshiBeam",0,0,0,-2)
YNOS C 1 A_FireCustomMissile("YoshiBeam",0,0,0,-2)
YNOS CCC 1 A_FireCustomMissile("NoLagYoshiBeam",0,0,0,-2)
YNOS C 1 A_FireCustomMissile("YoshiBeam",0,0,0,-2)
YNOS CCC 1 A_FireCustomMissile("NoLagYoshiBeam",0,0,0,-2)
YNOS C 1 A_FireCustomMissile("YoshiBeam",0,0,0,-2)
YNOS CCC 1 A_FireCustomMissile("NoLagYoshiBeam",0,0,0,-2)
YNOS C 1 A_FireCustomMissile("YoshiBeam",0,0,0,-2)
YNOS CCC 1 A_FireCustomMissile("NoLagYoshiBeam",0,0,0,-2)
YNOS C 0 SetPlayerProperty(0,0,0)
YNOS C 0 SetPlayerProperty(0,0,4)
YNOS B 5
Goto Ready+1
Spit.Tire:
YNOS A 0 A_PlaySoundEx("myoshi/spit","Weapon")
YNOS A 0 A_FireCustomMissile("RoaderTire",0)
YNOS BCB 6
YNOS A 10
YNOS A 0 A_TakeInventory("RoaderFlag",1)
Goto Ready+1
Spit.Spike:
YNOS A 0 A_PlaySoundEx("myoshi/spit","Weapon")
YNOS A 0 A_FireCustomMissile("YoshiSpike",0)
YNOS BCB 6
YNOS A 10
YNOS A 0 A_TakeInventory("SpikeFlag",1)
Goto Ready+1
Spit.Mine:
YNOS A 0 A_PlaySoundEx("myoshi/spit","Weapon")
YNOS A 0 A_FireCustomMissile("YoshiMine",0)
YNOS BCB 6
YNOS A 10
YNOS A 0 A_TakeInventory("MineFlag",1)
Goto Ready+1
Spit.Iceblock:
YNOS A 0 A_PlaySoundEx("myoshi/spit","Weapon")
YNOS A 0 A_FireCustomMissile("YoshiIceblock",0)
YNOS BCB 6
YNOS A 10
YNOS A 0 A_TakeInventory("IceBlokFlag",1)
Goto Ready+1
Spit.PressBall:
YNOS A 0 A_PlaySoundEx("myoshi/spit","Weapon")
YNOS A 0 A_FireCustomMissile("YoshiPressBall",0)
YNOS BCB 6
YNOS A 10
YNOS A 0 A_TakeInventory("PressFlag",1)
Goto Ready+1
Spit.MetalPress:
YNOS A 0 A_PlaySoundEx("myoshi/spit","Weapon")
YNOS A 0 A_FireCustomMissile("YoshiMetalPress",0)
YNOS BCB 6
YNOS A 10
YNOS A 0 A_TakeInventory("MetalPressFlag",1)
Goto Ready+1
EggThrow:
YNOS A 0 A_TakeInventory("EggTossFlag", 1)
YNOS A 0 A_PlaySoundEx("weapon/eggtoss","Weapon")
YNOS A 0 A_FireCustomMissile("YoshiEgg",0,1,8,0)
YNOS AEFGH 2
YNOS A 4 A_ClearRefire
YNOS AID 2
Goto Ready+1
AltFire:
YNOS JK 1 A_JumpIf(!(z-floorz<=0 || momz==0), "Thok")
TNT1 A 0 A_TakeInventory("SpindashFlag",999)
TNT1 A 1 A_SetArg(0, 30)
TNT1 A 0 SetPlayerProperty(0, 1, 0)
//TNT1 A 0 A_ChangeFlag("THRUACTORS", 1)

goto AltHold
AltHold:
TNT1 A 1 A_JumpIf(!(z-floorz<=0 || momz==0), "Thok")
TNT1 A 0 A_PlaySoundEx("myoshi/spindash","Weapon")
TNT1 A 0 A_SpawnItemEx("DashMelee")
TNT1 A 3 A_SetArg(0, Args[0]+3)
TNT1 A 0 A_GiveInventory("SpinDashFlag", 3)
TNT1 A 1 A_JumpIf(!(z-floorz<=0 || momz==0), "Thok")
TNT1 A 0 A_PlaySoundEx("myoshi/spindash","Weapon")
TNT1 A 0 A_SpawnItemEx("DashMelee")
TNT1 A 3 A_SetArg(0, Args[0]+3)
TNT1 A 0 A_GiveInventory("SpinDashFlag", 3)
TNT1 A 0 A_Refire
TNT1 A 0 A_TakeInventory("SpindashFlag",999)
TNT1 A 1 A_GiveInventory("PunkSpinning",1)
TNT1 A 0 A_Stop
TNT1 A 0 A_JumpIf(Args[0] > 75, "RollTooHigh")
Goto RollBegin
RollTooHigh:
TNT1 A 0 A_SetArg(0, 75)
Goto RollBegin

RollBegin:
TNT1 A 0 A_ClearRefire
TNT1 A 0 A_PlaySoundEX("myoshi/release","SoundSlot5")
TNT1 A 0 A_SetArg(0, Args[0])
TNT1 A 0 A_TakeInventory("SpindashFlag",999)
TNT1 A 0 A_ChangeVelocity(Args[0], 0, momz, CVF_REPLACE|CVF_RELATIVE)
TNT1 A 0 A_ChangeFlag("THRUACTORS", 1)
Goto RollContinue
RollContinue:
TNT1 A 0 A_SpawnItemEx("YoshiShadow", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
TNT1 A 1 A_SpawnItemEx("DashMelee")
TNT1 A 0 A_ChangeVelocity(Args[0]/10, 0, 0, CVF_RELATIVE)
TNT1 A 0 A_FireBullets (0, 0, 1, 0, "BounceChecker", 0, 43)
TNT1 A 0 A_JumpIfInventory("Crashed", 1, "RapidSlow")
TNT1 A 0 A_SetArg(0, Args[0]-2)
TNT1 A 0 A_JumpIf(Args[0] <= 0, "RapidSlow")
loop
RapidSlow:
TNT1 A 0 A_SetArg(0, Args[0]-10)
TNT1 A 0 A_JumpIf(Args[0] <= 0, "StopDash")
Goto RollContinue
StopDash:
TNT1 A 0 A_GiveInventory("PunkStopping",1)
TNT1 A 0 A_TakeInventory("PunkSpinning",999)
TNT1 A 1 A_SpawnItemEx("DashChecker")
YNOS KJ 2
YNOS A 0 A_TakeInventory("Crashed",999)
YNOS A 0 A_TakeInventory("PunkStopping",999)
TNT1 A 0 SetPlayerProperty(0, 0, 0)
Goto FinishDash
FinishDash:
YNOS A 1 //A_SpawnItemEx("DashChecker")
Goto Ready+1
Thok:
TNT1 A 0 A_ClearRefire
TNT1 A 0 A_ChangeFlag("SOLID", 0)
TNT1 A 0 A_TakeInventory("SpindashFlag",999)
TNT1 A 0 A_GiveInventory("PunkSpinning",1)
TNT1 A 0 SetPlayerProperty(0,1,0)
TNT1 A 0 A_ChangeVelocity(momx*4, momy*4, (momz/5)+6, CVF_REPLACE)
TNT1 A 0 A_PlaySoundEX("myoshi/thok","SoundSlot5")
TNT1 A 0 A_SetArg(0, 31)
Goto ThokContinue
ThokContinue:
TNT1 A 1 A_SpawnItemEx("DashMelee")
TNT1 A 0 A_SpawnItemEx("YoshiShadow", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_FireBullets (0, 0, 1, 0, "BounceChecker", 0, 43)
TNT1 A 0 A_CheckFloor("StopDash2")
TNT1 A 0 A_JumpIfInventory("Crashed", 1, "RapidSlow2")
TNT1 A 0 A_SetArg(0, Args[0]-1)
TNT1 A 0 A_JumpIf(Args[0] <= 0, "StopDash2")
loop
RapidSlow2:
TNT1 A 0 A_SetArg(0, Args[0]-4)
TNT1 A 0 A_JumpIf(Args[0] <= 0, "StopDash2")
Goto ThokContinue
StopDash2:
TNT1 A 0 A_GiveInventory("PunkStopping",1)
TNT1 A 0 A_TakeInventory("PunkSpinning",999)
TNT1 A 1 A_SpawnItemEx("DashChecker")
YNOS KJ 2
TNT1 A 0 SetPlayerProperty(0,0,0)
YNOS A 0 A_TakeInventory("Crashed",999)
YNOS A 0 A_TakeInventory("PunkStopping",999)
Goto FinishDash
NoAmmo:
YNOS A 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor MutantYoshiTongueX : MutantYoshiTongue
{
States
{
Select:
TNT1 A 0 A_SelectWeapon("MutantYoshiTongue")
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
YNOS D 1 A_Raise
Loop
}
}
actor SpinDashAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
}

actor Tongue
{
+MISSILE
+NOGRAVITY
+SKYEXPLODE
+DONTBLAST
+SHOOTABLE
+DONTRIP
+PIERCEARMOR
+FORCEXYBILLBOARD
+NOTARGETSWITCH
+DONTREFLECT
species "Tongue"
Obituary "%o was eaten by %k."
damage (10)
damagetype "Tongue"
health 999
height 20
radius 24
Speed 28
scale 2.5
painchance 256
Damagefactor "Beam", 0.1
Damagefactor "Spiney", 0.1
Damagefactor "FlingDamage", 0.0
mass 9999
States
{
Spawn:
TUN1 A 1 A_SpawnItemEx("TongueReturn2")
TUN1 A 0 A_JumpIf(Args[1] >= 10, "Death")
TUN1 A 0 A_SetArg(1, Args[1] + 1)
loop
Pain:
TUN1 A 0 A_ScaleVelocity(0.8)//A_PlaySound("myoshi/swallow")
TUN1 A 0 A_JumpIf(Args[1] <=0, "Spawn")
TUN1 A 0 A_SetArg(1, Args[1] - 1)
Goto Spawn
GiveEggs:
TNT1 A 0 A_CustomMissile("TongueReturn",0,0,0,0)
Goto GiveEggs2
GiveEggs2:
TNT1 A 0 A_Stop
TNT1 A 0 A_JumpIfInTargetInventory("EggFlag", 7, "Done")
TNT1 A 2 A_GiveToTarget("MutantYoshiEgg",1)
TNT1 A 0 A_SetArg(0, Args[0] - 13)
TNT1 A 0 A_JumpIf(Args[0] > 0, "GiveEggs2")
stop
Pain.YoshiStorm:
Pain.FireTrap:
Pain.OilFlame:
Pain.FirePillar:
Pain.BurnWave:
TUN1 A 0 A_GiveToTarget("Heartburn",1)
TUN1 A 0 A_GiveToTarget("MutantYoshiNoEgg",1)//A_PlaySound("myoshi/NoEgg")
Goto Return
Pain.YoshiTire:
Pain.TurboRoader: 
TUN1 A 0 A_GiveToTarget("RoaderFlag",1)
TUN1 A 0 A_GiveToTarget("MutantYoshiNoEgg",1)//A_PlaySound("myoshi/NoEgg")
Goto Return
Pain.YoshiSpike:
Pain.Spiney: 
TUN1 A 0 A_ChangeFlag("NOINTERACTION", 1)
TUN1 A 0 A_GiveToTarget("SpikeFlag",1)
TUN1 A 0 A_GiveToTarget("MutantYoshiNoEgg",1)//A_PlaySound("myoshi/NoEgg")
Goto Return
Pain.YoshiIceblock:
Pain.Iceblock: 
TUN1 A 0 A_ChangeFlag("NOINTERACTION", 1)
TUN1 A 0 A_GiveToTarget("IceBlokFlag",1)
TUN1 A 0 A_GiveToTarget("MutantYoshiNoEgg",1)//A_PlaySound("myoshi/NoEgg")
Goto Return
GiveLaser:
Pain.Beam:
TUN1 A 0 A_GiveToTarget("LaserFlag",1)
TUN1 A 0 A_GiveToTarget("MutantYoshiNoEgg",1)//A_PlaySound("myoshi/NoEgg")
Goto Return
Pain.YoshiMine:
Pain.PirateMine:
TUN1 A 0 A_ChangeFlag("NOINTERACTION", 1)
TUN1 A 0 A_GiveToTarget("MineFlag",1)
TUN1 A 0 A_GiveToTarget("MutantYoshiNoEgg",1)//A_PlaySound("myoshi/NoEgg")
Goto Return
Pain.YoshiPressBall:
Pain.PressBall:
TUN1 A 0 A_GiveToTarget("PressFlag",1)
TUN1 A 0 A_GiveToTarget("MutantYoshiNoEgg",1)//A_PlaySound("myoshi/NoEgg")
Goto Return
Pain.YoshiMetalPress:
Pain.MetalPress:
TUN1 A 0 A_GiveToTarget("MetalPressFlag",1)
TUN1 A 0 A_GiveToTarget("MutantYoshiNoEgg",1)//A_PlaySound("myoshi/NoEgg")
Goto Return
Death:
TUN1 A 0 A_ChangeFlag("NOINTERACTION", 1)
TUN1 A 0 A_SetArg(0, 999 - health)
TUN1 A 0 A_JumpIf(Args[0] >= 255, "GiveLaser")
TUN1 A 0 A_JumpIf(Args[0] > 0, "GiveEggs")
Goto Return
Return:
TNT1 A 0 A_CustomMissile("TongueReturn",0,0,0,0)
Goto Done
Done:
TNT1 A 0 
stop
}
}

Actor TongueReturn
{
radius 0
height 0
speed 40
damage (0)
scale 2.5
species "tongue"
PROJECTILE
+RIPPER
-SHOOTABLE
+SKYEXPLODE
+DONTSPLASH
+NOINTERACTION
+THRUSPECIES
+MTHRUSPECIES
+NOTARGETSWITCH
+DONTBLAST
states
{
Spawn:
TUN1 A 1 A_FaceTarget
TUN1 A 0 A_JumpIfCloser(60,"Return")
TUN1 A 1 A_FaceTarget
TUN1 A 0 A_JumpIfCloser(60,"Return")
TUN1 A 1 A_FaceTarget
TUN1 A 0 A_JumpIfCloser(60,"Return")
TUN1 A 1 A_FaceTarget
TUN1 A 0 A_JumpIfCloser(60,"Return")
TNT1 A 0 A_CustomMissile("TongueReturn",0,0,0,0)
stop
Return:
TUN1 A 0 A_GiveToTarget("TongueReturnFlag",1)
stop
}
}



Actor TongueReturn2 : Tongue
{
Damage (0)
+NOCLIP
+THRUSPECIES
+NOPAIN
-SHOOTABLE
species "tongue"
//+CLIENTSIDEONLY
States
{
Spawn:
TNT1 A 0
TNT1 A 1
TNT1 A 0 A_SpawnItemEx("TongueFX")
TNT1 A 0 A_JumpIfCloser(64, "Death")
TNT1 A 0 A_CustomMissile("TongueReturn2", 0, 0, 0, 0)
stop
Death:
TNT1 A 0 
stop
}
}

Actor TongueFX 
{
+NOINTERACTION
+THRUSPECIES
species "tongue"
//+CLIENTSIDEONLY
Scale 2.5
States
{
Spawn:
TNT1 A 1
TNT1 A 0 A_JumpIfCloser(32, "Death")
TUN2 A 2 
stop
Death:
TNT1 A 0
stop
}
}

actor PunkSpinning : Inventory
{
inventory.amount 1
inventory.maxamount 999
}

actor PunkStopping : Inventory
{
inventory.amount 1
inventory.maxamount 999
}

actor KnuckleSandwich : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor Heartburn : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor EggFlag : Ammo
{
inventory.amount 1
inventory.maxamount 7
+INVENTORY.IGNORESKILL
}

actor YoshiCrosshair : BulletPuff
{
+DONTBLAST
+CLIENTSIDEONLY
scale 2.5
alpha 255
States
{
  Spawn:
    CRS1 AB 1
    // Intentional fall-through
  Melee:
    CRS1 AB 1
    stop

}
}

actor BounceChecker : BulletPuff
{
+PUFFGETSOWNER
States
{
Spawn:
TNT1 A 1
stop
Crash:
TNT1 A 1
TNT1 A 0 A_GiveToTarget("Crashed",1)
stop
}
}

actor YoshiEgg : FastProjectileBase
{
PROJECTILE
damagetype "YoshiEgg"
Speed 80
Damage (0)
radius 5
height 5
scale 2.5
//DeathSound "Burst"
Obituary "%o was scrambled by %k's Egg Toss."
States
{
Spawn:
EGG0 A 1
loop
Death:
EGG0 A 0 A_SpawnItemEx("EggFirework",0,0,9,0,0,32)
EGG0 A 0 A_Explode(50, 128, 0)
EGG0 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
NAPA FGHIJKLMNOPQRS 1 
stop
}
}

actor EggFirework
{
-SOLID
+NOGRAVITY
+NOINTERACTION
scale 2.5
States
{
Spawn:
DOMP G 1
DOMP G 0 A_Stop
DOMP H 4 A_SpawnItemEx("BrightFireworkExp",random(-10,10),random(-10,10),10)
DOMP H 4 A_SpawnItemEx("BrightFireworkExp",random(-10,10),random(-10,10),0)
DOMP H 4 A_SpawnItemEx("BrightFireworkExp",random(-10,10),random(-10,10),-10)
stop
}
}

actor EggTossInv : CustomInventory 
{
inventory.amount 1
inventory.maxamount 1
inventory.icon "EGGWI"
Inventory.PickupSound ""
+INVBAR
+COUNTITEM
scale 2.0
States
{
Spawn:
EGGW A 1 
loop
Use:
EGGW A 0 A_JumpIfInventory("EggTossFlag", 1, "Nope")
EGGW A 0 A_JumpIfInventory("EggFlag", 1, "Success")
Goto Nope
Success:
EGGW A 0 A_TakeInventory("EggFlag", 1)
EGGW A 0 A_GiveInventory("EggTossFlag", 1)
fail
Nope:
TNT1 A 0 
fail
Death:
TNT1 A 0
stop
}
}

actor YoshiBeam
{
PROJECTILE
+MTHRUSPECIES
Damage (20)
+RIPPER
damagetype "Beam"
renderstyle "Translucent"
Translation "47:47 = 110:110", "217:217 = 138:138"
scale 2.5
Alpha 0.85
+DONTBLAST
+FORCEXYBILLBOARD
+FOILINVUL
radius 16
height 8
obituary "%o was not quick enough to avoid %k's beams."
Speed 32
States
{
Spawn:
QBEA A 10
loop
}
}

actor NoLagYoshiBeam : YoshiBeam
{
damage 0
+FORCEXYBILLBOARD
+BLOODLESSIMPACT
+CLIENTSIDEONLY
Alpha 0.5
States
{
Spawn:
QBEA A 10
loop
}
}

actor YoshiShadow
{
+NOGRAVITY
+NOINTERACTION
+CLIENTSIDEONLY
Renderstyle "Add"
+FORCEXYBILLBOARD
Alpha 0.95
Scale 2.5
Height 1
Radius 1
States
{
Spawn:
TNT1 A 1
MYOS X 1 A_Fadeout(0.1)
Goto Spawn+1
}
}

actor HeartBurnStorm
{
PROJECTILE
+DONTBLAST
+MTHRUSPECIES
damagetype "YoshiStorm"
Obituary "%o was melted by %k's Heartburn Storm."
Translation "227:227 = 110:110", "229:229 = 138:138"
Radius 6
Height 7
scale 2.5
damage (40)
speed 40
States
{
Spawn:
FIRE ABC 2
loop
Death:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
stop
}
}

actor RoaderTire
{
PROJECTILE
+MTHRUSPECIES
+DOOMBOUNCE
+CANBOUNCEWATER
SeeSound "weapon/coilbounce1"
Damage (35)
height 16
radius 12
speed 25
damagetype "YoshiTire"
Obituary "%o was tired out by %k's Roader Tire."
bouncefactor 1.0
wallbouncefactor 1.0
bouncecount 8
scale 2.0
-NOGRAVITY
States
{
Spawn:
YTIR A 1 
loop
Death:
YTIR A 0 A_Stop
YTIR A 0 A_ChangeFlag("NOGRAVITY",1)
MMFX BCDE 2
stop
}
}

actor YoshiSpike
{
+DONTBLAST
+MTHRUSPECIES
PROJECTILE
-NOGRAVITY
+THRUGHOST
Gravity 0.75
Height 8
Radius 16
scale 2.5
Damage (55)
Speed 32
Obituary "%o was spiked by %k."
damagetype "YoshiSpike"
//Translation "0:4 = 110:110"
states
{
Spawn:
BUBS D 1
loop
Death:
BUBS D 0 A_SpawnItemEx("YoshiSpikeStick")
stop
XDeath:
BUBS D 0 
stop
}
}

actor YoshiSpikeStick : YoshiSpike
{
+NOGRAVITY
ReactionTime 300
states
{
Spawn:
BUBS D 1 A_CountDown
Loop

XDeath:
Death:
MMFX BCDE 2
stop
}
}

actor YoshiMine
{
PROJECTILE
+MTHRUSPECIES
Radius 32
Height 32
scale 2.5
damagetype "YoshiMine"
Obituary "%o was sunk by %k's Mine."
damage (0)
speed 50
+EXPLODEONWATER
States
{
Spawn:
PMIN A 1
loop
Death:
PMIN A 0 A_Stop
PMIN A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
PMIN A 0 A_Explode(48, 160, 1)
NAPA EFGHIJKLMNOPQRS 1
stop
}
}

actor YoshiIceBlock
{
PROJECTILE
+MTHRUSPECIES
Damage (70)
damagetype "YoshiIceblock"
Obituary "%o was crushed by %k's Ice block."
Height 30
Radius 30
-NOGRAVITY
Speed 30
scale 2.5
Translation "60:60 = 110:110", "70:70 = 110:110", "196:196 = 138:138"
States
{
Spawn:
IMAC I 0
IMAC I 0 ThrustThingZ(0,20,0,1)
IMAC I 1
Goto Spawn+2

Death:
IMAC I 0 A_PlaySoundEx("weapon/icewavebreak", "Voice")
TNT1 AA 0 A_SpawnItemEx("IceBlockShard1",Random(-32, 32),Random(-32, 32),Random(-8, 16)+32,random(2,8),random(2,8),random(5,10),random(0,360), SXF_TRANSFERTRANSLATION)
TNT1 AA 0 A_SpawnItemEx("IceBlockShard2",Random(-32, 32),Random(-32, 32),Random(-8, 16)+32,random(2,8),random(2,8),random(5,10),random(0,360), SXF_TRANSFERTRANSLATION)
TNT1 AA 0 A_SpawnItemEx("IceBlockShard3",Random(-32, 32),Random(-32, 32),Random(-8, 16)+32,random(2,8),random(2,8),random(5,10),random(0,360), SXF_TRANSFERTRANSLATION)
TNT1 AA 0 A_SpawnItemEx("IceBlockShard4",Random(-32, 32),Random(-32, 32),Random(-8, 16)+32,random(2,8),random(2,8),random(5,10),random(0,360), SXF_TRANSFERTRANSLATION)
TNT1 AA 0 A_SpawnItemEx("IceBlockShard1",Random(-32, 32),Random(-32, 32),Random(-8, 16)+32,random(2,8),random(2,8),random(5,10),random(0,360), SXF_TRANSFERTRANSLATION)
stop
}
}




actor YoshiIceBlockDamager : IceBlockDamager
{
Damage(45)
}

actor YoshiPressBall
{
PROJECTILE
+MTHRUSPECIES
Damage (70)
damagetype "YoshiPressBall"
Obituary "%o was crushed by %k's Press Ball."
Height 30
Radius 30
-NOGRAVITY
Speed 30
scale 2.5
Translation "198:198 = 110:110", "192:192 = 138:138"
States
{
Spawn:
YPRS B 0
YPRS B 0 ThrustThingZ(0,20,0,1)
YPRS B 1
Goto Spawn+2
Death:
MMFX B 0 A_Changeflag("NOGRAVITY", 1)
MMFX B 0 A_Stop
MMFX BCDE 2
stop
}
}

actor YoshiMetalPress
{
PROJECTILE
+MTHRUSPECIES
Damage (70)
damagetype "YoshiMetalPress"
Obituary "%o was crushed by %k's Metal Press."
Height 30
Radius 30
-NOGRAVITY
Speed 30
scale 2.5
Translation "198:198 = 110:110", "192:192 = 138:138"
States
{
Spawn:
YPRS A 0
YPRS A 0 ThrustThingZ(0,20,0,1)
YPRS A 1
Goto Spawn+2
Death:
MMFX B 0 A_Changeflag("NOGRAVITY", 1)
MMFX B 0 A_Stop
MMFX BCDE 2
stop
}
}


actor DashMelee 
{
Obituary "%k mowed %o down with the Spin Dash."
damagetype "Spindash"
PROJECTILE
+RIPPER
+DONTBLAST
Radius 8
Height 8
Damage 0
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_Explode(20, 64, 0, 0, 48)
stop
Death:
TNT1 A 0
stop
}
}

actor SpinDashProtect : PowerProtection
{
Powerup.Duration 10
damagefactor "Spindash", 0.0
}

 
Actor MutantYoshiEgg : CustomInventory
{
+AUTOACTIVATE
States
{
Use:
TNT1 A 0 
TNT1 A 0 A_PlaySoundEx("myoshi/swallow", "Voice")
TNT1 A 0 A_GiveInventory("EggFlag", 1)
stop
}
}

Actor MutantYoshiNoEgg : CustomInventory
{
+AUTOACTIVATE
States
{
Use:
TNT1 A 0 
TNT1 A 0 A_PlaySoundEx("myoshi/noegg", "Voice")
stop
}
}

actor Crashed : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor TongueReturnFlag : Powerup
{
Powerup.Duration 35
}

