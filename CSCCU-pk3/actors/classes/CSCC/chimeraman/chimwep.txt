actor ChimeraBuster : BaseCSCCWep
{
Weapon.AmmoUse 3
Weapon.AmmoGive 0
Weapon.SlotNumber 1
Obituary "%o was ambushed by %k's Dashing Orb."
weapon.ammotype "ChimeraAmmo"
inventory.pickupsound "weapon/weaponup"
Inventory.Pickupmessage "Power Up! Crystal Eye!"
+WEAPON.AMMO_OPTIONAL
+WEAPON.CHEATNOTWEAPON
+INVENTORY.UNDROPPABLE
DropItem "CrystalEyeWepGiver"
Scale 2.0
States
{
Ready:
CHUD A 0 ACS_ExecuteAlways(991,0,WCCS_CHIMERAMAN)
CHUD A 5 A_WeaponReady(WRF_NOSWITCH)
CHUD A 0 A_GiveInventory("ChimeraAmmo",1)
TNT1 A 0 A_JumpIfInventory("throwing",1,"DoFlash")
goto Ready+1
DoFlash:
CHUD A 1 A_GunFlash("Flash",1)
goto Ready+2
Deselect:
TNT1 A 0 A_SelectWeapon("ChimeraBusterBot")
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
TNT1 A 1 A_Lower
Loop
Select:
TNT1 A 0 A_SelectWeapon("ChimeraBusterBot")
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
CHUD A 1 A_Raise
Loop
Fire:
SNKW A 0 A_JumpIfInventory("IsBot", 1, "BotCheck")
CHUD A 0 A_JumpIfNoAmmo("No")
CHUD A 0 A_Jump(16,"FireCrit")
CHUD ABC 2
CHUD A 0 A_PlaySoundEx("misc/gutslift","Weapon")
CHUD C 0 A_FireCustomMissile("DashingOrb",0,1,8,0)
CHUD B 2
CHUD A 12
CHUD A 0 A_Refire
Goto Ready+1
FireCrit:
CHUD ABC 2
CHUD A 0 A_PlaySoundEx("misc/vanishblock","Weapon")
CHUD C 0 A_FireCustomMissile("DashingOrbCrit",0,1,8,0)
CHUD B 2
CHUD A 12
CHUD A 0 A_Refire
Goto Ready+1
BotCheck:
CHUD A 0 A_Jump(16,"FireCrit")
CHUD A 0 A_Jump(16,"FuseBlast")
CHUD A 0 A_Jump(16,"Dice")
CHUD ABC 2
CHUD A 0 A_PlaySoundEx("misc/gutslift","Weapon")
CHUD C 0 A_FireCustomMissile("DashingOrb",0,0,8,0)
CHUD B 2
CHUD A 12
CHUD A 0 A_Refire
Goto Ready+1
Dice:
CHUD A 1 A_GiveInventory("throwing",1)
Goto Ready+1
AltFire:
CHUD A 0 A_JumpIfInventory("ChimeraAmmo", 14, "FuseBlast")
CHUD A 1
Goto Ready+1
FuseBlast:
CHUD A 0 A_TakeInventory("ChimeraAmmo",14)
CHUD A 0 A_Jump(16,"AltFireCrit")
CHUD ABC 2
CHUD A 0 A_PlaySoundEx("weapon/magnetmissile","Weapon")
CHUD C 0 A_FireCustomMissile("FuseBlast",0,1,8,0)
CHUD B 2
CHUD A 12
CHUD A 0 A_Refire
Goto Ready+1
AltFireCrit:
CHUD ABC 2
CHUD A 0 A_PlaySoundEx("weapon/junkshield","Weapon")
CHUD C 0 A_FireCustomMissile("FuseBlastCrit",0,1,8,0)
CHUD B 2
CHUD A 12
CHUD A 0 A_Refire
Goto Ready+1
No:
CHUD A 2 ACS_Execute(979,0)
Goto Ready+1

Flash:
TNT1 A 0 A_JumpIfInventory("IsDead",1,"NoFlash")
TNT1 A 1 
TNT1 A 0 A_JumpIfInventory("throwing",1,"Flash.throw")
TNT1 A 1 A_JumpIfInventory("IsDead",1,"NoFlash")
TNT1 A 0 A_JumpIfInventory("throwing",1,"Flash.throw")
loop

Flash.throw:
YAMS A 0 A_TakeInventory("throwing")
CHU2 D 2
CHU2 A 0 A_PlaySoundEx("weapon/junkend","Weapon")
CHU2 C 0 A_FireCustomMissile("ChimeraDiceRoll",0,0,8,0)
CHU2 EFGH 2
goto Flash
NoFlash:
TNT1 A 0
stop
}
}

actor DashingOrb
{
Obituary "%o was ambushed by %k's Dashing Orb."
PROJECTILE
+HEXENBOUNCE
+DONTSPLASH
Radius 7
Height 7
scale 2.5
speed 40
damagetype "DashingOrb"
bouncefactor 1.5
wallbouncefactor 1.5
damage (20)//25
bouncecount 10
seesound "weapon/crackerthrow"
States
{
Spawn:
DSHO AB 2
loop
Death:
DSHO EF 2
stop
}
}

actor DashingOrbCrit : DashingOrb
{
damage (40)//50
Obituary "A Critical Hit! %o was ambushed by %k's Dashing Orb."
States
{
Spawn:
DSHO CD 2
loop
}
}

actor FuseBlast
{
PROJECTILE
-NOGRAVITY
+RIPPER
+BOUNCEONACTORS
+DONTSPLASH
Radius 24
Height 24
scale 2.5
speed 20
damagetype "FuseBlast"
+HEXENBOUNCE
seesound "weapon/coilbounce2"
Obituary "%k dropped the f-bomb on %o."
bouncefactor 0.7
wallbouncefactor 0.7
States
{
Spawn:
FBLA A 2
FBLA B 35
FBLA C 1
FBLA B 4
FBLA C 1
FBLA B 4
FBLA C 1
FBLA B 4
FBLA C 1
FBLA B 4
FBLA C 1
FBLA B 4
FBLA C 1
FBLA B 4
FBLA CBCBC 1
FBLA A 0 A_Stop
FBLA A 0 A_PlaySoundEx("misc/hugequake", "Weapon")
FBLA A 0 A_Explode(80, 300, 0)//FBLA A 0 A_Explode(100, 300, 0)
FBLA A 0 A_SpawnItemEx("FuseBlastExplosion",0,0,32)
stop
Death:
TNT1 A 0
Stop
}
}

actor FuseBlastCrit : FuseBlast
{
Obituary "A Critical Hit! %k dropped the f-bomb on %o."
seesound "misc/hugequake2"
States
{
Spawn:
FBLA D 2
FBLA EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 1 A_SpawnItemEx("FuseBlastCritTrail",-2)
FBLA FEEEEFEEEEFEEEEFEEEEFEEEEFEEEEFEFEF 1 A_SpawnItemEx("FuseBlastCritTrail",-2)
FBLA F 0 A_Stop
FBLA F 0 A_PlaySoundEx("misc/hugequake", "Weapon")
FBLA F 0 A_Explode(100, 400, 0)//FBLA F 0 A_Explode(150, 400, 0)
FBLA F 0 A_SpawnItemEx("FuseBlastCritExplosion")
stop
Death:
TNT1 A 0
Stop
}
}

actor FuseBlastCritTrail
{
Scale 2.5
+NOGRAVITY
+NOINTERACTION
States
{
Spawn:
FBLA FFFFEEEDDD 1 A_FadeOut(0.1)
loop
}
}

actor FuseBlastExplosion
{
Scale 10.0
+NOGRAVITY
+NOINTERACTION
States
{
Spawn:
NAPA EFGHIJKLMNOPQRS 1
stop
}
}

actor FuseBlastCritExplosion : FuseBlastExplosion
{
Scale 13.0
}

actor ChimeraAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 28
}

actor ChimeraBusterBot : ChimeraBuster
{
States
{
Deselect:
TNT1 A 0 A_SelectWeapon("ChimeraBusterBot")
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
TNT1 A 1 A_Lower
Loop
Fire:
CHUD A 0 A_JumpIfNoAmmo("Pause")
CHUD A 0 A_Jump(16,"FireCrit")
CHUD A 0 A_Jump(128,"Altfire")
CHUD ABC 2
CHUD A 0 A_PlaySoundEx("misc/gutslift","Weapon")
CHUD C 0 A_FireCustomMissile("DashingOrb",0,1,8,0)
CHUD B 2
CHUD A 12
Goto Ready+1
Pause:
CHUD A 30
CHUD A 0 A_GiveInventory("ChimeraAmmo",14)
Goto Ready+1
Altfire:
CHUD A 0 A_JumpIfInventory("ChimeraAmmo", 14, "FuseBlast")
CHUD A 1
Goto Ready+1
FuseBlast:
CHUD ABC 2
CHUD A 0 A_TakeInventory("ChimeraAmmo",14)
CHUD A 0 A_Jump(16,"AltFireCrit")
CHUD A 0 A_PlaySoundEx("weapon/magnetmissile","Weapon")
CHUD C 0 A_FireCustomMissile("FuseBlast",0,1,8,0)
CHUD B 2
CHUD A 12
Goto Ready+1
}
}

/*
actor ChimeraDice : BaseCSCCWep
{
Weapon.AmmoUse 1
Weapon.SlotNumber 1
Obituary "%o was loaded by %k's Dice Roll."
weapon.ammotype "ChimeraAmmo"
inventory.pickupsound "weapon/weaponup"
Inventory.Pickupmessage "Power Up! Loaded Dice!"
+WEAPON.AMMO_OPTIONAL
+WEAPON.CHEATNOTWEAPON
+INVENTORY.UNDROPPABLE
Scale 2.0
States
{
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
TNT1 A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
CHU2 A 1 A_Raise
Loop
Ready:
CHU2 D 2
CHU2 A 0 A_PlaySoundEx("weapon/junkend","Weapon")
CHU2 C 0 A_FireCustomMissile("ChimeraDiceRoll",0,0,8,0)
CHU2 EFGH 2
TNT1 A 0 A_GiveInventory("ChimeraBuster")
TNT1 A 0 A_SelectWeapon("ChimeraBuster")
Goto Deselect
}
}*/

actor ChimeraDiceThrow : CustomInventory
{
inventory.amount 1
inventory.maxamount 6
inventory.pickupmessage "Test your luck!"
inventory.icon "CHIDI"
Inventory.PickupSound "item/1up"
DropItem "CrystalEyeWep"
+INVBAR
+COUNTITEM
scale 2.0
states
{
Use:
TNT1 A 0 A_GiveInventory("throwing")
stop
}
}

actor throwing : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor ChimeraDiceRoll
{
+MISSILE
Radius 5
Height 5
scale 2.5
damage (0)
damagetype "Dice"
+DONTSPLASH
+DONTBLAST
+NOTARGETSWITCH
+HEXENBOUNCE
+BOUNCEONACTORS
speed 20
bouncecount 2
bouncefactor 0.1
wallbouncefactor 0.1
States
{
Spawn:
DICE A 1 
loop
Death:
DICE A 0 A_Jump(256,"One","Two","Three","Four","Five","Six")
stop
One:
DICE B 2
DICE A 0 A_SpawnItemEx("ChimeraDiceOne",0,0,5,0,0,0)
DICE A 0 A_SpawnItemEx("SmallHealth")
MMFX BCDE 2
stop
Two:
DICE B 2 
DICE A 0 A_SpawnItemEx("ChimeraDiceTwo",0,0,5,0,0,0)
DICE A 0 A_SpawnItemEx("ChimeraBanish",0,0,32,60,0,0,random(45,135))
DICE A 0 A_SpawnItemEx("ChimeraBanish",0,0,32,60,0,0,random(225,315))
MMFX BCDE 2
stop
Three:
DICE B 2
DICE A 0 A_SpawnItemEx("ChimeraDiceThree",0,0,5,0,0,0)
MMFX BCD 2 A_SpawnItemEx("ChimeraFirework",random(-16,16),random(-16,16),random(64,80))
MMFX E 2
stop
Four:
DICE B 2
DICE A 0 A_SpawnItemEx("ChimeraDiceFour",0,0,5,0,0,0)
DICE A 0 A_SpawnItemEx("SparkShock",0,0,32,35,0,0,0)
DICE A 0 A_SpawnItemEx("SparkShock",0,0,32,35,0,0,90)
DICE A 0 A_SpawnItemEx("SparkShock",0,0,32,35,0,0,180)
DICE A 0 A_SpawnItemEx("SparkShock",0,0,32,35,0,0,270)
MMFX BCDE 2
stop
Five:
DICE B 2
DICE A 0 A_SpawnItemEx("ChimeraDiceFive",0,0,5,0,0,0)
DICE A 0 A_GiveToTarget("ChimeraTruco",1)
MMFX BCDE 2
stop
Six: 
DICE B 2
DICE A 0 A_SpawnItemEx("ChimeraDiceSix",0,0,5,0,0,0)
DICE A 0 A_GiveToTarget("ChimeraJackpot",1)
MMFX BCDE 2
stop
}
}

actor ChimeraDiceOne
{
+NOGRAVITY
+NOINTERACTION
+FloorHugger
reactiontime 20
scale 2.5
states
{
Spawn:
DICE CI 2 A_Countdown
loop
Death:
DICE OPQ 2
stop
}
}

actor ChimeraDiceTwo : ChimeraDiceOne
{
states
{
Spawn:
DICE DJ 2 A_Countdown
loop
}
}

actor ChimeraDiceThree : ChimeraDiceOne
{
states
{
Spawn:
DICE EK 2 A_Countdown
loop
}
}

actor ChimeraDiceFour : ChimeraDiceOne
{
states
{
Spawn:
DICE FL 2 A_Countdown
loop
}
}

actor ChimeraDiceFive : ChimeraDiceOne
{
states
{
Spawn:
DICE GM 2 A_Countdown
loop
}
}

actor ChimeraDiceSix : ChimeraDiceOne
{
states
{
Spawn:
DICE HN 2 A_Countdown 
loop
}
}

actor ChimeraBanish : TelOtherFX1
{
Scale 2.5
+HEXENBOUNCE
Bouncecount 8
BounceFactor 1
WallBounceFactor 1
states
{
Spawn:
GEMI A 1 A_SpawnItemEx("GeminiTrail",0,0,0,40)
loop
}
}

actor ChimeraFirework
{
PROJECTILE
Radius 1
Height 1
+RIPPER
Damage(0)
Scale 2.5
Damagetype "Bright"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Explode(5,512,0)
DOMP HIJKLMN 2
stop
}
}

actor ChimeraTruco : CustomInventory
{
inventory.amount 1
inventory.maxamount 0
states
{
Pickup:
TNT1 A 0 A_SpawnItemEx("ChimeraTrucoProjectile",0,0,32,60)
TNT1 A 0 A_PlaySoundEx("weapon/noisecrush2","Weapon")
stop
}
}

actor ChimeraTrucoProjectile : NoiseCrush2
{
Damage(10)
DamageType "Wind"
}

actor ChimeraJackpot : PowerupGiver
{
inventory.maxamount 0
+INVENTORY.AUTOACTIVATE
//Powerup.Color 00 00 50, 0.3
Powerup.Color GoldMap
Powerup.Duration -5
Powerup.Type Invulnerable
}

actor DiceCheck : CustomInventory
{
inventory.amount 1
inventory.maxamount 0
states
{
Pickup:
TNT1 A 0 
TNT1 A 0 A_JumpIfInventory("ChimeraBuster", 1, "YesDice")
stop
YesDice:
TNT1 A 0 A_GiveInventory("ChimeraDiceThrow", 1)
stop
}
}
