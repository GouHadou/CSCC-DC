actor LegoMissilesBoss : MegaBuster
{
Weapon.AmmoUse 1
Weapon.AmmoUse2 2
Weapon.AmmoGive 5
Weapon.SlotNumber 1
Obituary "%o was obliterated by %k's Missile Launcher."
Inventory.Pickupmessage "Power Up! Sticky Bombs!"
weapon.ammotype "LegoWeaponAmmo"
weapon.ammotype2 "LegoWeaponAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
+WEAPON.CHEATNOTWEAPON
Scale 2.0
DropItem "StickyBombWepGiver"
States
{
Spawn:
WEA3 L 0
WEA3 L 1
stop
Ready:
LEHU A 0 ACS_ExecuteAlways(998,0,418)
LEHU A 0 A_JumpIfNoAmmo("Ready2")
LEHU A 1 A_WeaponReady(WRF_ALLOWRELOAD)
Goto Ready+1
Ready2:
LEHU D 5 //A_WeaponReady(WRF_NOSECONDARY)
Goto NoAmmo
Deselect:
LEHU A 0 A_SelectWeapon("LegoStickiesBoss")
LEHU A 2 offset(30,30)
LEHU A 2 offset(72,72)
LEHU A 2 offset(90,90)
TNT1 A 1
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
LEHU A 1 A_Lower
Goto Deselect+4
Select:
TNT1 A 1
LEHU A 2 offset(90,90)
LEHU A 2 offset(72,72)
LEHU A 2 offset(30,30)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
LEHU A 1 A_Raise
goto Select+3
Fire:
LEHU A 0 A_JumpIfNoAmmo("NoAmmo")
LEHU A 0 A_PlaySoundEx("weapon/legomissileshot","Weapon")
LEHU A 0 A_FireCustomMissile("LegoMissile1",0,1,8,0)
LEHU A 0 A_JumpIfNoAmmo("FireEndNoAmmo")
LEHU BC 6
LEHU A 10 A_PlaySoundEx("weapon/adapterreturn", "Weapon")
LEHU A 0 //A_Refire
Goto Ready+1
AltFire:
LEHU D 0 A_GunFlash
LEHU A 0 A_JumpIfNoAmmo("AltNoAmmo")
LEHU A 15
Goto Ready+1
AltNoAmmo:
LEHU D 15
Goto Ready+1
FireEndNoAmmo:
LEHU BE 6
LEHU D 13
Goto Ready+1
Reload:
NoAmmo:
LEHU D 1 //ACS_Execute(979,0)
LEHU D 3 offset(30,30)
LEHU D 3 offset(72,72)
LEHU D 3 offset(95,95)
LEHU D 8 A_PlaySoundEx("weapon/wrapfire","Weapon")
LEHU D 0 A_GiveInventory("LegoWeaponAmmo",1)
LEHU D 0 A_JumpIfInventory("LegoWeaponAmmo",5,"ReloadEnd")
goto NoAmmo+4
ReloadEnd:
LEHU D 3 offset(0,95)
LEHU B 3 offset(0,72)
LEHU C 3 offset(0,30)
LEHU A 0 A_PlaySoundEx("weapon/adapterreturn", "Weapon")
Goto Ready+1
Flash:
LEHU JIH 1
LEHU F 0 A_PlaySoundEx("weapon/legobuttonpress","Weapon")
LEHU F 3 A_GiveInventory("GyroFlag",1)
LEHU GF 3 A_TakeInventory("GyroFlag",999)
LEHU HIJ 1
stop
}
}



actor LegoWeaponAmmo : Ammo
{
+INVENTORY.IGNORESKILL
Inventory.amount 1
Inventory.maxamount 5
}

actor LegoStickyAmmo : Ammo
{
+INVENTORY.IGNORESKILL
Inventory.amount 1
Inventory.maxamount 10
}

actor LegoMissile1
{
Obituary "%o was obliterated by %k's Missile Launcher."
PROJECTILE
+FORCEYBILLBOARD
Radius 6
Height 7
scale 2.5
speed 65
//seesound "weapon/legomissileshot2"
damage (0)
damagetype "LegoMissile"
States
{
Spawn:
LROC AABB 2 A_SpawnItemEx("LegoRocketTrail")
loop
Death:
LROC A 0 A_Stop
LROC A 0 A_PlaySoundEx("weapon/legomissileexplode", "Weapon")
LROC A 0 A_Explode(35, 135)
LMEX A 0 A_SpawnItemEx("LegoRocketExplosion")
stop
}
}


actor LegoRocketTrail
{
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
+FORCEXYBILLBOARD
-SOLID
scale 2.5
height 0
radius 0
States
{
Spawn:
LETR ABCDEF 1
stop
}
}

actor LegoRocketExplosion : LegoRocketTrail//Better
{
States
{
Spawn:
LMEX ABCDEFG 1
stop
}
}

actor LegoStickiesBoss : MegaBuster
{
Weapon.AmmoUse 1
Weapon.AmmoGive 10
Weapon.SlotNumber 5
Obituary "%o was surprised by %k's Sticky bombs."
Inventory.Pickupmessage "Power Up! Sticky Bombs!"
weapon.ammotype "LegoStickyAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
+WEAPON.CHEATNOTWEAPON
DropItem "StickyBombWepGiver"
Scale 2.0
States
{
Spawn:
TNT1 A 0
TNT1 A 1 
stop
Ready:
LEHU D 0 ACS_ExecuteAlways(998,0,418)
LEHU D 1 A_WeaponReady
Goto Ready+1
Deselect:
LEHU D 0 A_SelectWeapon("LegoMissilesBoss")
LEHU D 2 offset(30,30)
LEHU D 2 offset(72,72)
LEHU D 2 offset(95,95)
TNT1 A 1
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
LEHU D 1 A_Lower
goto Deselect+4
Select:
TNT1 A 1
LEHU D 2 offset(95,95)
LEHU D 2 offset(72,72)
LEHU D 2 offset(30,30)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
LEHU D 1 A_Raise
goto Select+3
Fire:
LEHU D 0 A_JumpIfNoAmmo("NoAmmo")
LEHU D 0 A_PlaySoundEx("weapon/pshot","Weapon")
LEHU D 0 A_JumpIf(ACS_ExecuteWithResult(630,0)>0, "TeamStickies")
LEHU D 0 A_FireCustomMissile("LegoStickyBomb",0,1,8,0)
LEHU BE 6
LEHU D 7
LEHU I 0 A_Refire
Goto Ready+1
AltFire:
LEHU D 0 A_GunFlash
LEHU D 19
Goto Ready+1
TeamStickies:
LEHU D 0 A_JumpIf(ACS_ExecuteWithResult(630,0)==1, "LightSticky")
LEHU D 0 A_JumpIf(ACS_ExecuteWithResult(630,0)==2, "WilySticky")
LEHU D 0 A_JumpIf(ACS_ExecuteWithResult(630,0)==3, "CossackSticky")
LEHU D 0 A_JumpIf(ACS_ExecuteWithResult(630,0)==4, "MrXSticky")
goto Fire+4//Failsafe
LightSticky:
LEHU D 0 A_FireCustomMissile("LegoStickyBomb",0,1,8,0)
goto Fire+5
WilySticky:
LEHU D 0 A_FireCustomMissile("LegoStickyBombW",0,1,8,0)
goto Fire+5
CossackSticky:
LEHU D 0 A_FireCustomMissile("LegoStickyBombC",0,1,8,0)
goto Fire+5
MrXSticky:
LEHU D 0 A_FireCustomMissile("LegoStickyBombX",0,1,8,0)
goto Fire+5
NoAmmo:
LEHU D 1 ACS_Execute(979,0)
Goto Ready+1
Flash:
LEHU JIH 1
LEHU F 0 A_PlaySoundEx("weapon/legobuttonpress","Weapon")
LEHU F 3 A_GiveInventory("GyroFlag",1)
LEHU GF 3 A_TakeInventory("GyroFlag",999)
LEHU HIJ 1
stop
}
}

actor LegoStickyBomb
{
Obituary "%o was surprised by %k's Sticky bombs."
PROJECTILE
Radius 6
Height 7
scale 2.5
-NOGRAVITY
+RIPPER
+FORCEXYBILLBOARD
+SKYEXPLODE
speed 60
damage (0)
gravity 1.5
States
{
Spawn:
LSTB E 1
loop
Death:
LSTB E 0 A_PlaySoundEx("weapon/crashland", "Weapon")
LSTB E 0 A_SpawnItemEx("LegoStickyMine")
stop
}
}

actor LegoStickyMine : LegoStickyBomb
{
+NOGRAVITY
+NOEXPLODEFLOOR
Reactiontime 65
damagetype "LegoStickyBomb"
States
{
Spawn:
LSTB A 0
LSTB A 0 A_SpawnItemEx("StickyBombWave")
LSTB A 0 A_JumpIfInTargetInventory("GyroFlag",1,"Death")
LSTB A 1 A_JumpIfInTargetInventory("IsDead",1,"Fadeout")
LSTB A 0 A_JumpIfInTargetInventory("GyroFlag",1,"Death")
LSTB A 1 A_JumpIfInTargetInventory("IsDead",1,"Fadeout")
LSTB A 0 A_JumpIfInTargetInventory("GyroFlag",1,"Death")
LSTB A 1 A_JumpIfInTargetInventory("IsDead",1,"Fadeout")
LSTB A 0 A_JumpIfInTargetInventory("GyroFlag",1,"Death")
LSTB B 1 A_JumpIfInTargetInventory("IsDead",1,"Fadeout")
LSTB A 0 A_JumpIfInTargetInventory("GyroFlag",1,"Death")
LSTB B 1 A_JumpIfInTargetInventory("IsDead",1,"Fadeout")
LSTB A 0 A_JumpIfInTargetInventory("GyroFlag",1,"Death")
LSTB B 1 A_JumpIfInTargetInventory("IsDead",1,"Fadeout")
LSTB A 0 A_SpawnItemEx("StickyBombWave")
LSTB A 0 A_JumpIfInTargetInventory("GyroFlag",1,"Death")
LSTB C 1 A_JumpIfInTargetInventory("IsDead",1,"Fadeout")
LSTB A 0 A_JumpIfInTargetInventory("GyroFlag",1,"Death")
LSTB C 1 A_JumpIfInTargetInventory("IsDead",1,"Fadeout")
LSTB A 0 A_JumpIfInTargetInventory("GyroFlag",1,"Death")
LSTB C 1 A_JumpIfInTargetInventory("IsDead",1,"Fadeout")
LSTB A 0 A_JumpIfInTargetInventory("GyroFlag",1,"Death")
LSTB D 1 A_JumpIfInTargetInventory("IsDead",1,"Fadeout")
LSTB A 0 A_JumpIfInTargetInventory("GyroFlag",1,"Death")
LSTB D 1 A_JumpIfInTargetInventory("IsDead",1,"Fadeout")
LSTB A 0 A_JumpIfInTargetInventory("GyroFlag",1,"Death")
LSTB D 1 A_JumpIfInTargetInventory("IsDead",1,"Fadeout")
LSTB A 0 A_CountDown
loop
Death:
LSFX A 0 A_PlaySoundEx("weapon/legobombexplode","Weapon")
LSFX A 0 A_GiveToTarget("LegoStickyAmmo",1)
LSFX A 0 A_Explode(25,150)
LSFX DEFGHI 1
stop
Fadeout:
LSTB E 20
LSTB E 1 A_Fadeout
Goto Fadeout+1
}
}



actor StickyBombWave 
{
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
+FORCEXYBILLBOARD
-SOLID
scale 2.5
height 0
radius 0
States
{
Spawn:
LSFX CBA 2
stop
}
}

actor LegoStickyBombW : LegoStickyBomb
{
Translation "195:207=171:187"
States
{
Death:
LSTB E 0 A_PlaySoundEx("weapon/crashland", "Weapon")
LSTB E 0 A_SpawnItemEx("LegoStickyMineW")
stop
}
}

actor LegoStickyBombC : LegoStickyBomb
{
Translation "195:207=208:223"
States
{
Death:
LSTB E 0 A_PlaySoundEx("weapon/crashland", "Weapon")
LSTB E 0 A_SpawnItemEx("LegoStickyMineC")
stop
}
}

actor LegoStickyBombX : LegoStickyBomb
{
Translation "195:207=250:254"
States
{
Death:
LSTB E 0 A_PlaySoundEx("weapon/crashland", "Weapon")
LSTB E 0 A_SpawnItemEx("LegoStickyMineX")
stop
}
}

actor LegoStickyMineW : LegoStickyMine
{
Translation "195:207=171:187"
}

actor LegoStickyMineC : LegoStickyMine
{
Translation "195:207=208:223"
}

actor LegoStickyMineX : LegoStickyMine
{
Translation "195:207=250:254"
}

actor StickyBombWep : MegaBuster//Compatable DoomED number here
{
Weapon.AmmoUse 1
Weapon.AmmoGive 15
Weapon.SlotNumber 5
Obituary "%o was blasted from afar by %k's Sticky bombs."
Inventory.Pickupmessage "Power up! Sticky Bombs!"
weapon.ammotype "StickyBombAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
inventory.icon "SBLWI"
Scale 2.0
States
{
Spawn:
SBLW I 1 
loop
Ready:
SBLW A 0 ACS_ExecuteAlways(998,0,418)
SBLW A 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
SBLW A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
SBLW A 1 A_Raise
Loop
Fire:
SBLW A 0 A_JumpIfNoAmmo("NoAmmo")
SBLW A 0 //A_JumpIfInventory("StickyFlag",5,"NoAmmo")
SBLW A 0 A_PlaySoundEx("weapon/pshot","Weapon")
SBLW A 0 A_FireCustomMissile("StickyBomb",0,1,8,0)
SBLW A 0 //A_GiveInventory("StickyFlag",1)
SBLW BC 6
SBLW A 13
SBLW A 0 A_Refire
Goto Ready+1
NoAmmo:
SBLW A 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor StickyBombAmmo : Ammo
{
+INVENTORY.IGNORESKILL
Inventory.amount 1
Inventory.maxamount 15
}

actor StickyFlag : Inventory
{
Inventory.amount 1
Inventory.maxamount 5
}

actor StickyBomb : LegoStickyBomb
{
States
{
Death:
LSTB E 0 A_PlaySoundEx("weapon/crashland", "Weapon")
LSTB E 0 A_SpawnItemEx("StickyMine")
stop
}
}

actor StickyMine : LegoStickyMine
{
-RIPPER
reactiontime 30
states
{
Spawn:
LSTB A 0
LMSB AAABBBCCCDDD 1 A_JumpIfInTargetInventory("IsDead",1,"FadeOut")
LSTB A 0 A_CountDown
loop
Death:
LSFX A 0 A_PlaySoundEx("weapon/legobombexplode","Weapon")
LSFX A 0 A_Explode(40,125,0)
LSFX DEFGHI 1
stop
Fadeout:
LMSB E 20
LMSB E 1 A_FadeOut
Goto Fadeout+1
}
}