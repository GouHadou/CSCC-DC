actor PsychicProwess : BaseCSCCWep
{
Weapon.AmmoUse 1
Weapon.AmmoGive 28
Weapon.SlotNumber 1
Obituary "%k snapped %o's mind with %p Psychic Prowess."
weapon.ammotype "PsychicAmmo"
Inventory.Pickupmessage "MY MIND. TO YOUR MIND."
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
+WEAPON.CHEATNOTWEAPON
+INVENTORY.UNDROPPABLE
Scale 2.0
States
{
Spawn:
CFWP Z 0
CFWP Z 1 
stop
Ready:
SERG A 0 ACS_ExecuteAlways(991,0,502)
SERG A 0 A_GunFlash
SERG A 1 A_WeaponReady
Goto Ready+2
Flash:
TNT1 A 10
TNT1 A 0 A_GiveInventory("PsychicAmmo", 1)
loop
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
TNT1 A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
SLSR B 1 A_Raise
Loop
Fire:
SERG B 0 A_JumpIfInventory("IsBot", 1, "BotFire")
SERG B 0 A_JumpIfNoAmmo("NoAmmo")
SERG B 5
SERG B 0 A_PlaySoundEx("weapon/SolarL","Weapon")
SERG C 2 A_GiveInventory("SergioMainfire",1)
SERG C 9
SERG B 0 A_ReFire
SERG B 5
Goto Ready+2
Hold:
SERG B 0 A_PlaySoundEx("weapon/SolarL","Weapon")
SERG C 2 A_GiveInventory("SergioMainfire",1)
SERG C 9
SERG B 0 A_ReFire
SERG B 5
goto Ready+2
Altfire:
SERG A 0 A_JumpIfInventory("PsychicAmmo", 14, "Wall")
SERG A 0
Goto NoAmmo
Wall:
SERG B 5
SERG D 0 A_PlaySoundEx("weapon/CentaurFlash","Weapon")
SERG D 0 A_TakeInventory("PsychicAmmo",14)
SERG D 0 A_SpawnItemEX("PsyWallLayerSpawn",120,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
SERG D 0 A_SpawnItemEX("PsyWallLayerSpawn",120,0,68,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
SERG D 0 A_SpawnItemEX("PsyWallLayerSpawn",120,0,132,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
SERG D 0 A_SpawnItemEX("PsyWallLayerSpawn",120,0,204,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
SERG D 5
SERG B 5
goto Ready+2
NoAmmo:
SERG A 1 //ACS_Execute(979,0) this sound is the worst
Goto Ready+2
BotFire:
SERG B 0 A_Jump(50, "Wall")
SERG B 5
SERG B 0 A_PlaySoundEx("weapon/SolarL","Weapon")
SERG C 2 A_GiveInventory("SergioMainfire",1)
SERG C 9
SERG B 0 A_ReFire
SERG B 5
Goto Ready+2
}
}

actor PsychicAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 28
}

actor SergioWallChecker : Inventory
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 10
}

actor WallDeathCounter : Inventory
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 899
}

actor PsyShot
{
Obituary "%k snapped %o's mind with %p Psychic Prowess."
PROJECTILE 
Radius 5
Height 5
scale 2.5 
damagetype "PsyShot"
+FORCEXYBILLBOARD 
+MTHRUSPECIES
speed 20
translation "204:204=36:36", "61:61=39:39"
damage (15)
States
{
Spawn:
SLSR E 0 A_JumpIf(z-floorz==0, "Death")
NCRU DCBA 2
loop
Death:
SLSR E 0
stop
}
}
actor PsyShotB : PsyShot {translation "204:204=205:205", "61:61=74:74"}
actor PsyShotR : PsyShot {translation "204:204=171:171", "61:61=41:41"}
actor PsyShotG : PsyShot {translation "204:204=104:104", "61:61=128:128"}
actor PsyShotY : PsyShot {translation "204:204=229:229", "61:61=232:232"}

actor PsyWallPiece : HexBarrierBase
{
+SOLID
+THRUACTORS
+RIPPER
health 100
Radius 24
Height 48
alpha 0.9
renderstyle Translucent
//-NOBLOCKMAP
Damage(0)
scale 2.5
speed 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 ACS_NamedExecuteAlways("PlayerSpeciesSynch",0)
SGWS CBA 2 
SGWL ABAB 2
SGWL A 0 A_ChangeFlag("THRUACTORS", 0)
Spawn2:
SGWL D 0 A_JumpIFHealthLower(50,3)
SGWL C 0 A_JumpIFHealthLower(75,2)
SGWL B 0
SGWL "#" 0
SGWL "#" 1
SGWL "#" 0 A_GiveToTarget("SergioWallChecker",1)
SGWL "#" 0 A_JumpIfInventory("WallDeathCounter",898,"Expire")
SGWL "#" 0 A_GiveInventory("WallDeathCounter",2)
SGWL "#" 0 A_Stop
SGWL "#" 0 //A_FadeOut(0.002)
goto Spawn2
Pain:
SGWL A 0 A_PlaySoundEx("weapon/mirrorabsorb","Auto")
SGWL A 0 A_ChangeFlag("INVULNERABLE", 1)
SGWL A 2
SGWL A 0 A_ChangeFlag("INVULNERABLE", 0)
goto Spawn2
GoToSpawn:
PSHH A 0
goto Spawn2
Death:
PSHH A 0 A_JumpIf(Health >= 100, "Death2")
TNT1 A 1 A_PlaySoundEx("weapon/CentaurFlash","Weapon")
TNT1 AAAAA 0 A_SpawnItemEx("LightScreenSpark", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359))
Death2:
PSHH A 0 A_TakeFromTarget("SergioWallChecker",1)
stop
Expire:
SGWL A 0 A_ChangeFlag("SHOOTABLE", 0)
SGWS A 0 A_TakeFromTarget("SergioWallChecker",1)
SGWS ABC 2 
stop
}
}

actor PsyWallLayerSpawn
{
  Radius 1
  Height 1
  +MISSILE
  +NOGRAVITY
  +NOINTERACTION
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 1
    TNT1 D 0 A_SpawnItemEX("PsyWallPiece",0,68,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS)
    TNT1 A 0 A_SpawnItemEX("PsyWallPiece",0,-68,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS)
    TNT1 A 0 A_SpawnItemEX("PsyWallPiece",0,136,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS)
    TNT1 A 0 A_SpawnItemEX("PsyWallPiece",0,-136,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS)
    TNT1 A 0 A_SpawnItemEX("PsyWallPiece",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS)
    stop
  }
}

actor SergioMainfire : CustomInventory
{
States
{
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("LightTeamFlag",1,"FireB")
TNT1 A 0 A_JumpIfInventory("WilyTeamFlag",1,"FireR")
TNT1 A 0 A_JumpIfInventory("CossackTeamFlag",1,"FireG")
TNT1 A 0 A_JumpIfInventory("KingTeamFlag",1,"FireY")
TST1 A 0 A_FireCustomMissile("Psyshot",0,0,8,0)
stop
FireB:
TST1 A 0 A_FireCustomMissile("PsyshotB",0,0,8,0)
stop
FireR:
TST1 A 0 A_FireCustomMissile("PsyshotR",0,0,8,0)
stop
FireG:
TST1 A 0 A_FireCustomMissile("PsyshotG",0,0,8,0)
stop
FireY:
TST1 A 0 A_FireCustomMissile("PsyshotY",0,0,8,0)
stop
}
}