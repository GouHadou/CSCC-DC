actor CyberPackWep : FireStarterWep
{
Weapon.SlotNumber 3
Obituary "%o was hacked and bombed by %k."
weapon.ammotype "CyberPackAmmo"
inventory.icon "CBPKI0"
Scale 2.0
Tag "Cyber Pack"
States
{
Spawn:
CBPK I 1
loop
Deselect:
TNT1 A 0 A_TakeInventory("CPActive",999)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
TNT1 A 0 A_TakeInventory("FireStarterAmmo",2)
TNT1 A 0 A_TakeInventory("CyberPackAmmo",2)
TNT1 A 0 A_TakeInventory("BombJumperAmmo",2)
TNT1 A 0 A_TakeInventory("MagicPackAmmo",2)
FKKB A 1 A_Lower
goto Deselect+1
Select:
TNT1 A 0 A_GiveInventory("CPActive",1)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
FKKB A 1 A_Raise
goto Select+1
Ready:
CPKB A 0 ACS_ExecuteAlways(998,0,805)
CPKB A 0 A_Gunflash
CPKB A 0 A_JumpIfInventory("User1_Trigger",1,"BombJump")
CPKB A 1 A_WeaponReady
Goto Ready+1
Fire:
CPKB D 0 A_JumpIfInventory("SmashFlag",1,"FinalSmash")
CPKB D 0 A_JumpIfInventory("CyberPackAmmo",56,"XPloderUse")
CPKB D 0 A_PlaySoundEX("baku/baku5","Weapon")
CPKB D 0 A_FireCustomMissile("XSplitter",random(-3,3),0,8,0,0,random(-3,3))
CPKB D 8
Goto Ready+1
XploderUse:
CPKB D 0 A_TakeInventory("CyberPackAmmo",56)
CPKB D 0 A_PlaySoundEx("weapon/BNBombToss","Weapon")
CPKB D 0 A_FireCustomMissile("Xploder",0,0,8,0)
CPKB D 2
CPKB C 2
CPKB B 2
CPKB A 10
Goto Ready+1
Altfire:
CPKB A 0 A_JumpIfNoAmmo("NoAmmo")
CPKB A 0 A_JumpIfInventory("CyberPackAmmo",280,"ZeroDividerUse")
CPKB B 1
goto Ready+1
FinalSmash:
CPKB B 25 A_PlaySoundEx("baku/finalsmash1","Voice")
CPKB D 0 A_FireCustomMissile("ClusterFuckBomb",random(-5,5),0,8,0,0,random(-5,5))
CPKB D 0 A_TakeInventory("SmashFlag",999)
CPKB D 2
CPKB C 2
CPKB B 2
CPKB A 14
goto Ready+1
ZeroDividerUse:
CPKB D 0 A_PlaySoundEX("weapon/BNBombToss","Weapon")
CPKB D 0 A_TakeInventory("CyberPackAmmo",280,0)
CPKB D 0 A_FireCustomMissile("ZeroDivider",0,0,8,0)
CPKB D 2
CPKB C 2
CPKB B 2
CPKB A 28
goto Ready+1
BombJump:
TNT1 A 0
CPKB A 1
CPKB A 0 A_JumpIfInventory("BombJumperAmmo",300,"BombJumpUse")
CPKB A 0 A_Giveinventory("InputClearer")
goto NoAmmo
BombJumpUse:
CPKB A 0
CPKB A 0 A_TakeInventory("BombJumperAmmo",999)
CPKB A 0 A_PlaySoundEx("weapon/hyperbomb", "Weapon")
CPKB A 0 A_SpawnItemEX("BombJumpBoom")
CPKB A 0 A_ChangeVelocity(17,0,17,CVF_RELATIVE|CVF_REPLACE)
CPKB A 0 A_Recoil(-20)
CPKB A 0 A_Giveinventory("InputClearer")
CPKB A 1
goto Ready+1
Flash:
TNT1 A 0
TNT1 A 1
TNT1 A 0 A_GiveInventory("MagicPackAmmo",1)
TNT1 A 0 A_GiveInventory("BombJumperAmmo",1)
TNT1 A 0 A_GiveInventory("FireStarterAmmo",1)
TNT1 A 0 A_GiveInventory("CyberPackAmmo",1)
TNT1 A 39
loop
NoAmmo:
CPKB A 0 ACS_Execute(979,0)
Goto Ready+1
}
}

actor CyberPackAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 280
}

actor Xploder
{
PROJECTILE
Radius 4
Height 4
scale 2.5
damagetype "HyperBomb"
Obituary "How?"
damage (0)
- NOGRAVITY
+HEXENBOUNCE
+CANBOUNCEWATER
speed 50
Bouncefactor 0.7
Reactiontime 35
States
{
Spawn:
BMPK B 1
BMPK B 0 A_CountDown
loop
Death:
TNT1 A 0 A_Stop
TNT1 A 0 A_SpawnItemEX("XSplitterSpawner")
stop
}
}

Actor XSplitterSpawner
{
Projectile
Radius 1
Height 1
Scale 2.5
damagetype "HyperBomb"
obituary "How the fuck did %o die to a spawner?"
damage (0)
+RIPPER
Speed 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEX("baku/baku1","Weapon")
TNT1 A 0 A_SpawnItemEX("XSplitter",0,0,0,30,0,0,0)
TNT1 A 0 A_SpawnItemEX("XSplitter",0,0,0,30,0,0,90)
TNT1 A 0 A_SpawnItemEX("XSplitter",0,0,0,30,0,0,180)
TNT1 A 0 A_SpawnItemEX("XSplitter",0,0,0,30,0,0,270)
TNT1 A 12
TNT1 A 0 A_PlaySoundEX("baku/baku1","Weapon")
TNT1 A 0 A_SpawnItemEX("XSplitter",0,0,0,30,0,0,45)
TNT1 A 0 A_SpawnItemEX("XSplitter",0,0,0,30,0,0,135)
TNT1 A 0 A_SpawnItemEX("XSplitter",0,0,0,30,0,0,225)
TNT1 A 0 A_SpawnItemEX("XSplitter",0,0,0,30,0,0,315)
stop
Death:
TNT1 A 0
stop
}
}

actor XSplitter
{
PROJECTILE
translation "192:192=4:4", "198:198=210:210"
+LOOKALLAROUND
+SKYEXPLODE
+DONTBLAST
+RIPPER
damagetype "HyperBomb"
Obituary "%o was split into pieces by %k's X-Ploder."
Speed 25
Damage (4)
radius 20
height 20
scale 2.5
States
{
Spawn:
TNT1 A 0
BAEX BCDEFG 1
stop
}
}

actor ZeroDivider
{
PROJECTILE
Radius 4
Height 4
scale 2.5
damagetype "HyperBomb"
Obituary "How?"
damage (0)
+HEXENBOUNCE
+CANBOUNCEWATER
speed 40
Bouncefactor 0.7
Reactiontime 3
States
{
Spawn:
ZRDV IJKL 2
ZRDV I 0 A_CountDown
loop
Death:
ZRDV MMNN 2
TNT1 A 0 A_Stop
TNT1 A 0 A_SpawnItemEX("DividerHole")
stop
}
}

actor DividerHole
{
PROJECTILE
+NOTARGETSWITCH
+FORCEXYBILLBOARD
+RIPPER
+NOCLIP
Obituary "%o was eaten up by %k's rip in the time space continuum because they divided by zero."
Radius 20
Height 20
Scale 2.5
Damage(20)
Damagetype "HyperBomb"
Speed 0
States
{
Spawn:
DKBH A 0
DKBH A 0 A_PlaySoundEx("misc/darkenergyspawn","Weapon")
DKBH ABCABCABCABC 3
DKBH C 1
DKBH C 0 A_PlaySoundEX("misc/gutsgboost","Weapon")
goto HoleOpen
HoleOpen:
DKBH D 0 A_JumpIfInventory("CutterFlag",7,"Death")
DKBH D 0 ACS_ExecuteAlways(247,0,35,400)
DKBH DEF 3
DKBH D 0 A_Explode(7,128,0,0,64)
DKBH D 0 A_GiveInventory("CutterFlag",1)
loop
Death:
DKBH DEF 4 A_FadeOut(0.13)
loop
}
}
