actor RinGougi1 : Megabuster
{
Weapon.SlotNumber 1
scale 2.0
Obituary "%o was knocked out by %k!"
Inventory.Pickupmessage ""
+WEAPON.NOAUTOAIM
Tag "Shot Putt / Phasethrough Dash"
States
{
Spawn:
TNT1 A 1
loop
Ready:
RINH A 0 ACS_ExecuteAlways(998,0,DYE_RIN)
RINH A 0 A_GunFlash
RINH A 0 A_JumpIfInventory("DemonRinEnergy",1,"Ready2")
RINH A 1 A_WeaponReady
Goto Ready+2

Ready2:
DMRH A 0 A_JumpIfInventory("DemonRinEnergy",1,"Ready2.b")
goto GoBack
Ready2.b:
DMRH A 0 ACS_NamedExecuteAlways("MCSpeedSetScript",0,5)
DMRH A 0 ACS_ExecuteAlways(991,0,754)
DMRH A 0 A_TakeInventory("DemonRinEnergy",1)
DMRH A 1 A_WeaponReady
DMRH A 0 A_JumpIfInventory("DemonRinEnergy",1,"Ready2.b")
goto GoBack

GoBack:
RINH A 0 ACS_ExecuteAlways(991,0,753)
RINH A 0 ACS_NamedExecuteAlways("MCSpeedSetScript",0,0)
RINH A 0 SetPlayerProperty(0,0,3)
RINH A 0 A_SetTranslucent(1.0,0)
RINH A 0 A_ChangeFlag("SHOOTABLE",1)
RINH A 0 A_ChangeFlag("THRUACTORS",0)
RINH A 0 A_TakeInventory("KnightFlag",999)
RINH A 0 A_TakeInventory("DemonRinAnim",1)
RINH A 0 A_TakeInventory("DemonRinDashAnim",1)
Goto Ready+2

Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
RINH A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
RINH A 1 A_Raise
Loop

Fire: //Shot Putt
RINH A 0 A_JumpIfInventory("DemonRinEnergy",1,"UltFire")
RINH A 0 A_JumpIfInventory("RinPhaseDash",1,"SetFire")
RINH A 0 A_JumpIfInventory("SmashFlag",1,"Ultimate")
RINH A 0 A_PlaySoundEx("Rin/ShotPutS","Weapon")
RINH TU 5
RINH A 0 A_PlaySoundEx("Rin/ShotPutC","Weapon")
RINH V 7 A_FireCustomMissile("ShotPut",0,0,8,0)
RINH UT 3
RINH A 0 A_Refire
Goto Ready+2

SetFire: //Crushing Knee
RINH A 0 A_GiveInventory("RinCheckPhase",1)
RINH A 0 A_SetTranslucent(1.0,0)
RINH A 0 A_TakeInventory("RinPhaseDash",1)
RINH A 0 A_TakeInventory("CutterFlag",999)
RINH A 0 A_GiveInventory("RinKneeAnim",1)
RINH A 0 A_TakeInventory("RinDashAnim",1)
RINH A 0 A_SpawnItemEx("ReSolidifier")
RINH A 0 A_PlaySoundEx("Rin/PowerKnee","Weapon")
RINH A 0 A_SpawnItemEX("RinKneeFX",35,0,24,10)
RINH A 0 A_Recoil(-30)
RINH RRRRRRRRRRR 1 A_SpawnItemEx("RinKneeDamager",42,-8,16,MomX,MomY,MomZ,0,SXF_ABSOLUTEMOMENTUM)
RINH A 0 ACS_NamedExecuteAlways("MCSpeedSetScript",0,0)
RINH A 0 A_TakeInventory("RinKneeAnim",1)
RINH A 0 A_GiveInventory("RinPhaseDash1")
RINH A 0 A_GiveInventory("RinPhaseDash2")
RINH A 0 A_Refire
Goto Ready+2

Uppercut: //Rin Uppercut
RINH A 0 ACS_ExecuteAlways(991,0,753)
RINH A 0 A_TakeInventory("RUCCharge",999)
RINH A 0 A_PlaySoundEx("Rin/UltUpprcut2","Weapon")
RINH FGH 1 A_Recoil(-3)
RINH A 0 ThrustThingZ(0,70,0,0)
RINH A 0 A_SpawnItemEx("RinUppercut",0,0,30,0,0,0,0,SXF_ABSOLUTEMOMENTUM)
RINH I 10 A_GiveInventory("RinUppercutAnim",1)
RINH A 0 A_TakeInventory("RinUpperChargeAnim",1)
RINH A 0 A_TakeInventory("RinUppercutAnim",1)
Goto Ready+2

Altfire: //Phasethrough Dash
RINH A 0 A_JumpIfInventory("DemonRinEnergy",1,"UltAlt")
RINH A 0 A_JumpIfInventory("RUCCharge",30,"Uppercut")
RINH A 0 A_JumpIfInventory("RinPhaseDash2",1,"NoAmmo")
RINH A 0 A_JumpIfInventory("RinPhaseDash1",1,"NoAmmo")
RINH A 0 A_GiveInventory("RinPhaseDash",1)
RINH A 0 A_GiveInventory("RinDashAnim",1)
RINH A 0 A_TakeInventory("UnSolid",1)
RINH A 0 A_ChangeFlag("SOLID",0)
RINH A 0 A_GiveInventory("Nudge",1)
//doesn't seem like enough for a dash ~bika
RINH A 0 A_Recoil(-50)
RINH A 0 A_SetTranslucent(0.4,0)
goto AltLoop
AltLoop:
RINH A 0 A_JumpIfInventory("CutterFlag",15,"AltEnd")
RINH A 0 A_CheckFloor("AltLoop2")
RINH A 0 ACS_NamedExecuteAlways("MCSpeedSetScript",0,3)
RINH A 0 A_SpawnItemEx("RinPhaseDamager",0,0,28)
RINH A 0 A_SpawnItemEx("RinPhaseFX",0,0,28)
RINU F 1 A_WeaponReady(WRF_NOSECONDARY)
RINH A 0 A_GiveInventory("CutterFlag",1)
loop
AltLoop2:
RINH A 0 ACS_NamedExecuteAlways("MCSpeedSetScript",0,2)
RINH A 0 A_SpawnItemEx("RinPhaseDamager",0,0,28)
RINH A 0 A_SpawnItemEx("RinPhaseFX",0,0,28)
RINU F 1 A_WeaponReady(WRF_NOSECONDARY)
RINH A 0 A_GiveInventory("CutterFlag",1)
goto AltLoop
AltEnd:
RINH A 0 ACS_NamedExecuteAlways("MCSpeedSetScript",0,0)
RINH A 0 A_TakeInventory("RinPhaseDash",1)
RINH A 0 A_TakeInventory("RinDashAnim",1)
RINH A 0 A_TakeInventory("CutterFlag",999)
RINH A 0 A_SpawnItemEx("ReSolidifier")
RINH A 0 A_SetTranslucent(1.0,0)
RINH YXWAA 2
RINH A 0 A_GiveInventory("RinPhaseDash1")
RINH A 0 A_GiveInventory("RinPhaseDash2")
RINH A 0 A_Refire
Goto Ready+1

Ultimate:
RINH A 0 A_TakeInventory("SmashFlag",1)
RINH A 0 A_GiveInventory("DemonRinAnim",1)
RINH A 0 A_ChangeFlag("SHOOTABLE",0)
RINH A 1 Offset(0,60)
RINH A 1 Offset(0,90)
RINH A 0 A_PlaySoundEx("weapon/coilexplode","Weapon")
RINH A 0 A_SpawnItemEx("RinUltFX",0,0,0,0,0,0,0,SXF_ABSOLUTEMOMENTUM)
RINH A 1 Offset(0,120)
RINH A 0 A_Jump(100,"Ultimate2")
RINH A 0 ACS_ExecuteAlways(991,0,753)
RINH A 0 ACS_NamedExecuteAlways("MCSpeedSetScript",0,5)
RINH A 0 SetPlayerProperty(0,1,3)
RINH A 0 A_TakeInventory("RinPhaseDash1",1)
RINH A 0 A_TakeInventory("RinPhaseDash2",1)
TNT1 A 3
DMRH A 1 Offset(0,90)
DMRH A 1 Offset(0,60)
DMRH A 1 Offset(0,32)
DMRH A 0 A_GiveInventory("DemonRinEnergy",200)
Goto Ready2
Ultimate2:
DMRH A 0 A_SpawnItemEx("RinSkirt",0,0,0,random(-15,15),random(-15,15),random(5,25),random(-256,256),SXF_TRANSFERTRANSLATION)
DMRH A 0 A_SpawnItemEx("RinGlove1",0,0,0,random(-15,15),random(-15,15),random(5,25),random(-256,256),SXF_TRANSFERTRANSLATION)
DMRH A 0 A_SpawnItemEx("RinGlove2",0,0,0,random(-15,15),random(-15,15),random(5,25),random(-256,256),SXF_TRANSFERTRANSLATION)
DMRH A 0 A_SpawnItemEx("RinShoe1",0,0,0,random(-15,15),random(-15,15),random(5,25),random(-256,256),SXF_TRANSFERTRANSLATION)
DMRH A 0 A_SpawnItemEx("RinShoe2",0,0,0,random(-15,15),random(-15,15),random(5,25),random(-256,256),SXF_TRANSFERTRANSLATION)
goto Ultimate+9

UltFire: //Demon Blast
DMRH A 0 A_TakeInventory("DemonRinEnergy",6)
DMRH A 0 A_PlaySoundEx("Rin/Kouhadou","Weapon")
DMRH A 0 A_FireCustomMissile("DemonBlast",0,0,8,0)
DMRH BBCB 2
DMRH A 0 A_Refire
Goto Ready2
UltAlt: //Demon Dash
DMRH A 0 A_GiveInventory("DemonRinDashAnim",1)
DMRH A 0 A_TakeInventory("UnSolid",1)
DMRH A 0 A_ChangeFlag("SOLID",0)
DMRH A 0 A_GiveInventory("Nudge",1)
DMRH A 0 A_SetTranslucent(0.4,0)
DMRH A 0 A_Recoil(-6)
DMRH A 0 A_TakeInventory("DemonRinEnergy",1)
DMRH D 1 A_SpawnItemEx("DemonDasher",0,0,30)
DMRH A 0 A_Refire
DMRH A 0 A_TakeInventory("DemonRinDashAnim",1)
DMRH A 0 A_SpawnItemEx("ReSolidifier")
DMRH A 0 A_SetTranslucent(1.0,0)
Goto Ready2

NoAmmo:
RINH A 1 SetPlayerProperty(0,0,0)
Goto Ready+2

Flash:
TNT1 A 0 A_JumpIfInventory("RinCheckPhase",1,"PhaseFlash")
TNT1 A 0 A_CheckFloor("FloorFlash")
TNT1 A 1 A_JumpIfInventory("DemonRinEnergy",1,"DemonFlash")
loop
FloorFlash:
TNT1 A 0 A_TakeInventory("RinPhaseDash1",1)
goto Flash+2
PhaseFlash:
TNT1 A 1 ACS_NamedExecuteAlways("MCSpeedSetScript",0,0)
goto Flash+1

DemonFlash:
TNT1 A 0 ACS_ExecuteAlways(991,0,754)
TNT1 A 1 A_GiveInventory("KnightFlag",1)
TNT1 A 0 A_JumpIfInventory("KnightFlag",20,"DemonFlash2")
TNT1 A 0 A_JumpIfInventory("DemonRinEnergy",1,"DemonFlash")
goto GoBackFlash
DemonFlash2:
TNT1 A 0 ACS_ExecuteAlways(991,0,754)
TNT1 A 1 A_TakeInventory("KnightFlag",999)
TNT1 AAA 0 A_SpawnItemEx("DemonLaser",random(-500,500),random(-500,500))
TNT1 A 0 A_JumpIfInventory("DemonRinEnergy",1,"DemonFlash")
goto GoBackFlash
GoBackFlash:
RINH A 0 ACS_ExecuteAlways(991,0,753)
RINH A 0 ACS_NamedExecuteAlways("MCSpeedSetScript",0,0)
RINH A 0 SetPlayerProperty(0,0,3)
RINH A 0 A_SetTranslucent(1.0,0)
RINH A 0 A_ChangeFlag("SHOOTABLE",1)
RINH A 0 A_ChangeFlag("THRUACTORS",0)
RINH A 0 A_TakeInventory("KnightFlag",999)
RINH A 0 A_TakeInventory("DemonRinAnim",1)
RINH A 0 A_TakeInventory("DemonRinDashAnim",1)
goto Flash
}
}

//Inventory and Flags
actor DemonRinEnergy : Ammo //For the Ultimate
{
inventory.amount 1
inventory.maxamount 200
}

//Mainfire
Actor ShotPut : MegaShot
{
-NOGRAVITY
-DONTBLAST
+HEXENBOUNCE
+CANBOUNCEWATER
-BOUNCEONACTORS
+FORCEXYBILLBOARD
+USEBOUNCESTATE
DamageType "ShotPut"
Obituary "%o was crushed under %k's Shot Putt."
Speed 60
Radius 15
Height 15
Damage (10)
gravity 3
BounceFactor 0.9
WallBounceFactor 0.9
reactiontime 100
States
{
Spawn:
TNT1 A 0
STPT A 0 A_SpawnItemEx("HardKnuckleFX")
STPT AAAAA 1 A_Countdown
Loop
Crash:
XDeath:
Death:
STPT A 0 A_SetTranslucent(0.85)
STPT A 0 A_JumpIfInventory("CutterFlag",20,"DeathStop")
STPT A 1 A_Stop
TNT1 A 1 A_Stop
TNT1 A 0 A_GiveInventory("CutterFlag",1)
GoTo Death+1
DeathStop:
TNT1 A 0 A_TakeInventory("CutterFlag",999)
Stop
Bounce:
TNT1 A 0 A_PlaySoundEx("Rin/ShotPutL1","Body")
TNT1 AAA 0 A_SpawnItemEx("ShotPutRockFX",0,0,0,Random(-15,15),Random(-7,7),random(-15,15))
goto Spawn
}
}

Actor ShotPutRockFX
{
PROJECTILE
-NOGRAVITY
Damagetype "ShotPut"
Obituary "%o was knocked out by debris spawned by %k's Shot Putt."
Damage (5)
radius 5
height 5
scale 1.5
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Jump(256,"One","Two")
goto One
One:
DROC A 1 A_CheckFloor("Death")
DROC A 0 A_CheckCeiling("Death")
Loop
Two:
DROC B 1 A_CheckFloor("Death")
DROC B 0 A_CheckCeiling("Death")
Loop
Death:
TNT1 A 0
Stop
}
}

//Altfire
actor RinPhaseDamager
{
PROJECTILE
+DONTBLAST
+DONTSPLASH
+SKYEXPLODE
+THRUGHOST
+THRUACTORS
+NOINTERACTION
damagetype "Rin"
Obituary "%o was ran over by a phased-through %k."
Damage (0)
radius 1
height 1
scale 2.5
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_Explode(3,68,0,0,68)
stop
}
}

actor RinPhaseFX : HardKnuckleFX
{
scale 1.75
States
{
Spawn:
TNT1 A 0
ASEX EEEEE 1 A_Fadeout
stop
}
}

//Alt -> Main
Actor RinKneeFX : HardKnuckleFX
{
+FORCEYBILLBOARD
Alpha 1.0
Scale 1.0
Translation "0:255=0:0"
RenderStyle Translucent
States
{
Spawn:
TNT1 A 0
ASEX E 1
ASEX E 0 A_SetTranslucent(0.85)
ASEX E 1 A_SetScale(1.2,1.2)
ASEX E 0 A_SetTranslucent(0.70)
ASEX E 1 A_SetScale(1.4,1.4)
ASEX E 0 A_SetTranslucent(0.55)
ASEX E 1 A_SetScale(1.6,1.6)
ASEX E 0 A_SetTranslucent(0.40)
ASEX E 1 A_SetScale(1.8,1.8)
ASEX E 0 A_SetTranslucent(0.25)
ASEX E 1 A_SetScale(2.0,2.0)
ASEX E 0 A_SetTranslucent(0.10)
ASEX E 1 A_SetScale(2.2,2.2)
Stop
}
}

Actor RinKneeDamager : FastProjectile //: OmegaSaber
{
PROJECTILE
+RIPPER
+DONTBLAST
+NOCLIP
+FORCEXYBILLBOARD
Speed 70
Radius 5
Height 5
Scale 8.5
DamageType "PowerKnee"
Obituary "%o had their head OC'd by %k's knee."
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_Explode(5,68,0,0,68)
Stop
}
}

//Ultimate
actor RinUltFX : HardKnuckleFX
{
translation "4:208=0:0", "209:212=42:42"
scale 2.5
States
{
Spawn:
TNT1 A 0
ASEX ABCDEF 2 A_Warp(AAPTR_TARGET,0,0,30,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
stop
}
}

actor DemonLaser
{
PROJECTILE
+FLOORHUGGER
+SKYEXPLODE
+DONTBLAST
+DONTSPLASH
+THRUGHOST
+THRUACTORS
damagetype "DemonLaser"
Obituary "%o was caught in %k's deadly laser beam."
Speed 0
Damage (0)
radius 1
height 1
scale 10
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("trapmine/laser","Weapon")
DMLA EEEEE 1 A_Explode(5,100,0,0,50)
DMLA EEEEE 1 A_Fadeout
stop
}
}

//Mainfire ~Ult Ver
Actor DemonBlast
{
PROJECTILE
Translation "192:192=237:237", "198:198=239:239", "214:214=56:56", "210:210=236:236", "208:208=238:238"
DamageType "Kouhadou"
Obituary "%o could not withstand the strength of %k's Demon Blast"
Renderstyle "add"
Speed 100
Radius 25
Height 25
Damage (0)
Scale 4.0
States
{
Spawn:
TNT1 A 0
CHBU G 0 A_SpawnItemEX("KouhadouWavesFXS",35,0,0)
CHBU GG 0 A_SpawnItemEX("KouhadouFX1",Random(-20,20),Random(-20,20),Random(-20,20))
CHBU GG 1 A_SpawnItemEX("KouhadouFX2")
loop
Death:
TNT1 A 0 A_Explode(12,190,0,0,190)
TNT1 A 0 A_SetScale(5)
ASEX ABCDEF 1
Stop
}
}

//Altfire ~Ult Ver~
actor DemonDasher : RinPhaseDamager
{
translation "209:212=38:38"
scale 1.75
States
{
Spawn:
TNT1 A 0
ASEX EEEEE 1 A_Explode(6,68,0,0,68)
ASEX EEEEEEEEEE 1 A_Fadeout(0.5)
stop
}
}