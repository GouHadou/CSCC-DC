
//[+]========================================================================[+]
//----------------------------|Thunder Tower|------------------------------------
//[+]========================================================================[+]
// While Frailer than other buildings, the thunderTower can pack a 
// mean punch, it charges up energy over time before finally unleashing it on the first thing to come near it.
//[+]========================================================================[+]
actor ThunderTower
{
+SOLID
+Shootable
+MISSILE
//+NOCLIP
+NODAMAGETHRUST
+DONTBLAST
//+STANDSTILL  //doesn't seem to do anything
+DONTBLAST
+SKYEXPLODE
+DONTSEEKINVISIBLE//
+SEEKERMISSILE
+NoTargetSwitch
+FLOORHUGGER
+NOBLOOD
Damagefactor "SentryShot", 0
DamageFactor "Dynashock", 0.0
speed 1
scale 2.5
Radius 20
Height 70
Health 80
gravity 999
reactiontime 256
PainChance 256
states
{
Spawn:
TUNW A 0
TUNW A 0 A_GiveToTarget("SentryGoingup")
TRET A 0 A_GiveInventory("IsThunderTower",1) 
TUNW A 1 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_DEFAULT)
TRET A 0 A_ChangeFlag("Missile",false)
TUNW A 0 A_GiveInventory("ThunderTowerHPView",Health,AAPTR_MASTER)
Goto Charge1

Charge1: //[+]=======|Charging Phase|======[+]
TUNW D 0 A_Jumpifinventory("Weaponcharge",100,"Charge5")
TUNW C 0 A_Jumpifinventory("Weaponcharge",75,"Charge4")
TUNW B 0 A_Jumpifinventory("Weaponcharge",50,"Charge3")
TUNW A 0 A_Jumpifinventory("Weaponcharge",25,"Charge2")
TUNW A 1
TUNW A 0 A_GiveInventory("Weaponcharge",1) 
TUNW A 0 A_Jumpifintargetinventory("ThunderTowerReclaim",1,"Checkreturn")
TUNW A 0 A_CheckFlag("NOGRAVITY", "Terminate", AAPTR_MASTER)
goto Charge1
Charge2:
TUNW B 1
TUNW B 0 A_Jumpifinventory("Weaponcharge",50,"Charge3")
TUNW B 0 A_GiveInventory("Weaponcharge",1) 
TUNW B 0 A_Jumpifintargetinventory("ThunderTowerReclaim",1,"Checkreturn")
TUNW B 0 A_CheckFlag("NOGRAVITY", "Terminate", AAPTR_MASTER)
goto Charge2
Charge3:
TUNW C 1
TUNW C 0 A_Jumpifinventory("Weaponcharge",75,"Charge4")
TUNW C 0 A_GiveInventory("Weaponcharge",1) 
TUNW C 0 A_Jumpifintargetinventory("ThunderTowerReclaim",1,"Checkreturn")
TUNW C 0 A_CheckFlag("NOGRAVITY", "Terminate", AAPTR_MASTER)
goto Charge3
Charge4:
TUNW D 1
TUNW D 0 A_Jumpifinventory("Weaponcharge",100,"Charge5")
TUNW D 0 A_GiveInventory("Weaponcharge",1) 
TUNW D 0 A_Jumpifintargetinventory("ThunderTowerReclaim",1,"Checkreturn")
TUNW D 0 A_CheckFlag("NOGRAVITY", "Terminate", AAPTR_MASTER)
goto Charge4
Charge5: //[+]=======|Seeking Phase|======[+]
TUNW E 0
TUNW E 0 A_Jumpifintargetinventory("ThunderTowerReclaim",1,"Checkreturn")
TRET A 0 A_ChangeFlag("Missile",true)
TUNW E 0 A_SeekerMissile(0,0,SMF_LOOK,256,100)
TUNW E 1 A_Changevelocity(0,0,0,CVF_REPLACE) //Prevents movement
TUNW E 0 A_JumpIfTargetInLOS ("PowerCheck",0,JLOSF_PROJECTILE,600)
TRET A 0 A_ChangeFlag("Missile",false)
TUNW E 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_NULL)
TUNW E 0 A_CheckFlag("NOGRAVITY", "Terminate", AAPTR_MASTER)
goto Charge5

PowerCheck:
"----" A 1 A_Jumpifinventory("Weaponcharge",100,"TargetCheck")
goto Charge1

TargetCheck: //Visibility & detectability
TUNW E 1
TUNW E 0 A_CheckFlag("INVISIBLE","Charge1",AAPTR_TRACER)
TUNW E 0 A_CheckFlag("SHADOW","Charge1",AAPTR_TRACER)
TUNW E 0 A_Jumpifintargetinventory("ReachedFullInvis",1,"Charge1",AAPTR_TRACER) 

//====Check if we are looking at our maker
TUNW E 0 A_JumpIf(IsPointerEqual(AAPTR_TRACER, AAPTR_MASTER) == TRUE, "Charge1")
TUNW E 0 A_Jump(256,"TargetCheck2")
Goto TargetCheck2

TargetCheck2: //Team allaiance
//TUNW E 1 A_Jumpifintargetinventory("WilyTeamFlag",1,"Missile") //found an enemy!
TUNW E 0 A_checkflag("IsMonster","Missile",AAPTR_TRACER) //found an enemy!
TUNW E 0 A_Jump(256,"Missile")
Goto Charge1


Missile: //[+]=======|Firing Phase|======[+]
TUNW F 0 
TUNW F 0 A_Jumpifintargetinventory("ThunderTowerReclaim",1,"Checkreturn")
TUNW F 0 A_PlaySoundEx("weapon/lightningbolt","Weapon")
TUNW F 2 A_SpawnItemEx("LightningStartFX1",4,0,92)
TUNW F 2 A_SpawnItemEx("LightningStartFX2",4,0,92)
TUNW F 1 A_SpawnItemEx("ThunderTowerBullet",30,0,40,cos(-pitch)*64,0,sin(-pitch)*64,0,SXF_TRANSFERPOINTERS)
TUNW F 1 A_Takeinventory("WeaponCharge",999)
TUNW FEDCBA 1 A_Jumpifintargetinventory("ThunderTowerReclaim",1,"Checkreturn")
TUNW F 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_NULL)
TUNW F 0 A_CheckFlag("NOGRAVITY", "Terminate", AAPTR_MASTER)
Goto Charge1 

Checkreturn: //[+]=======|Tower Pickup Phase|======[+]
TUNW A 1 A_JumpIfTargetInLOS("Recall",30,JLOSF_TARGETLOS,200)
TUNW A 0 //A_SpawnItemEX("PickupIndicator",0,0,40,0,0,0,0)
//TUNW A 0 A_JumpIfTargetInLOS ("Missile", 0, JLOSF_PROJECTILE,1000  )
Goto Charge1
Recall:
TNT1 A 0 A_Changeflag("SHOOTABLE",0)
TFOG A 1 A_Takefromtarget("ThunderTowerActive",9)
TNT1 A 0 A_Takefromtarget("ThunderTowerHPView",9999)
TNT1 A 0 A_GiveToTarget("Movingthis")
TNT1 A 0 A_GiveToTarget("Electrammo",50)
TFOG BC 2
Stop

//[+]=======|Pain Phase|======[+]

Pain.DynaShock:
TUNW D 0 A_GiveInventory("Weaponcharge",10)
TUNW A 1 A_Changevelocity(0,0,0,CVF_REPLACE) //Prevents movement
//---Health Check---
TUNW A 0 A_Takefromtarget("ThunderTowerHPView",9999)
TUNW A 0 A_Givetotarget("ThunderTowerHPView",Health)
//---
//TUNW A 0 A_jumpifinventory("SentryStalling",1,"Stall")
//TUNW A 0 A_JumpIfTargetInLOS ("Missile", 0, JLOSF_PROJECTILE,1000  )
//TUNW A 1 A_jumpifinventory("WeaponCharge",130,"See")
Goto Charge1
Pain:
TUNW A 1 A_Changevelocity(0,0,0,CVF_REPLACE) //Prevents movement
//---Health Check---
TUNW F 0 A_TakeInventory("ThunderTowerHPView",9999,AAPTR_MASTER)
TUNW F 0 A_GiveInventory("ThunderTowerHPView",Health,AAPTR_MASTER)
//---
TNT1 A 0 A_PlaySoundEx("misc/mm4pain", "Voice")
Goto Charge1

Death:
TNT1 A 0 A_Takefromtarget("ThunderTowerHPView",9999)
TNT1 A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
TNT1 A 1 A_SpawnItemEx("TopExplosion")
TNT1 A 0 A_Takefromtarget("ThunderTowerActive",9)
TNT1 A 0 A_GiveToTarget("SentryDown")
stop
Terminate:
TNT1 A 0
stop
}
}


	actor ThunderTower_R : ThunderTower
	{
	translation "244:244=191:191", "75:75=43:43", "74:74=41:41", "205:205=171:171", "196:196=170:170"
	Designatedteam 1
	//Species "REDMember"
	States
	{
	TargetCheck2:
	TRET A 0// A_Jumpifintargetinventory("RespawnCamera",1,"Charge1") //can't shoot what you can't see,
	TRET A 0 A_Jumpifintargetinventory("LightTeamFlag",1,"Missile") //found an enemy!
	TRET A 0 A_checkflag("IsMonster","Missile",AAPTR_TRACER) //found an enemy!
	Goto Charge1
	}
	}

	actor ThunderTower_B : ThunderTower
	{
	Designatedteam 0
	//Species "BLUEMember"
	States
	{
	TargetCheck2:
	TRET A 0// A_Jumpifintargetinventory("RespawnCamera",1,"Charge1") //can't shoot what you can't see,
	TRET A 0 A_Jumpifintargetinventory("WilyTeamFlag",1,"Missile") //found an enemy!
	TRET A 0 A_checkflag("IsMonster","Missile",AAPTR_TRACER) //found an enemy!
	Goto Charge1
	}
	}

actor ThunderTowerBullet
{
Obituary "$OB_TREBLESENTRY"
PROJECTILE
+DONTBLAST
+NODAMAGETHRUST
+BLOODSPLATTER
+NOTargetSwitch
+SEEKERMISSILE
+NOINTERACTION
+FloorHugger
Speed 60
Damage (35)
scale 2.5
Height 10
Radius 10
Damagetype "SentryShot"
Obituary "%o was struck by %k 's Thunder Tower"
States
{
Spawn:
TNT1 A 1
TNT1 A 1 A_Warp(AAPTR_TRACER,0,0,16,0,WARPF_NOCHECKPOSITION)//A_SeekerMissile(90,90,SMF_LOOK,20,30)
TNT1 A 1 A_JumpifTracerCloser(100,"Strike")
loop
Strike:
TNT1 A 1 A_Stop
LIBO G 0 A_Explode(45, 210, 0,0,210)
TNT1 AAAAAAAAAAAAAA 1 A_SpawnItemEx("LightningBolt_Visual",random(-80,80),random(-80,80))
stop
Death:
TNT1 A 0 
Stop
}
}


actor LightningBolt_Visual
{
PROJECTILE
+THRUACTORS
+NOEXPLODEFLOOR
+FORCEYBILLBOARD
+FLOORHUGGER
+DONTBLAST
+CLIENTSIDEONLY
Damage (0)
DamageType "LightningBolt"
height 32
radius 16
scale 2.5
states
{
Spawn:
LIBO G 0
LIBO G 1 A_SpawnItemEx("LightningExtend", 0, 0, 355)
LIBO G 0 A_PlaySoundEx("weapon/lightningbolt","Weapon")
LIBO H 1
LIBO I 0 A_SpawnItemEx("LightningBoltFX", Random(32, 96), Random(32, 96), 32)
LIBO I 0 A_SpawnItemEx("LightningBoltFX", Random(-32, -96), Random(-32, -96), 64)
LIBO I 0 A_SpawnItemEx("LightningBoltFX", Random(32, 96), Random(32, 96), 96)
LIBO I 0 A_SpawnItemEx("LightningBoltFX", Random(-32, -96), Random(-32, -96), 128)
LIBO I 0 A_SpawnItemEx("LightningBoltFX", Random(32, 96), Random(32, 96), 160)
LIBO I 0 A_SpawnItemEx("LightningBoltFX", Random(-32, -96), Random(-32, -96), 192)
LIBO IJK 1
Stop
}
}

actor ThunderTowerMarker  // 
{
PROJECTILE
//+NOINTERACTION
+NOGRAVITY
//+FLOORHUGGER
+NOTARGETSWITCH
height 30
Radius 80
damage (0)
reactiontime 1
scale 1.5
RenderStyle "ADD"
Alpha 1
States
{
Spawn:
TNT1 A 0
TUNG A 2
stop
Crash:
XDeath:
TUNG B 0 A_Changeflag("NOINTERACTION",1)
TUNG B 1 //A_GiveToTarget("SpawnDeny")
TNT1 A 1 A_SpawnitemEx("ThunderTowerMarker2", 0, 0, 0, momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
Stop
Death:
TUNG B 0 A_Changeflag("NOINTERACTION",1)
TUNG B 1// A_GiveToTarget("SpawnDeny")
TNT1 A 1 A_SpawnitemEx("ThunderTowerMarker2", 0, 0, 0, momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
stop
}
}

actor ThunderTowerMarker2  // 
{
PROJECTILE
+NOINTERACTION
+NOGRAVITY
//+FLOORHUGGER
+NOTARGETSWITCH
height 30
Radius 80
damage (0)
reactiontime 1
scale 1.5
RenderStyle "ADD"
Alpha 1
States
{
Spawn:
TNT1 A 0
TUNG B 2
stop
stop
}
}

actor ThunderTowerActive : Inventory // anti-spam
{
inventory.amount 1
inventory.maxamount 1
}

actor ThunderTowerReclaim : Powerup // anti-spam
{
inventory.amount 1
inventory.maxamount 1
Powerup.Duration 3
}


actor ThunderTowerHPView : Inventory // anti-spam
{
inventory.amount 1
inventory.maxamount 80
}


actor IsThunderTower : Inventory // anti-spam
{
inventory.amount 1
inventory.maxamount 1
}

actor ThunderTowerPlanter : CustomInventory  //places a sentry based on the team.
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 0 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 0 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
//---otherwise, just place a blank one
GDTB A 0 A_SpawnItemEX("ThunderTower",120,0,0,0,0,0,0,SXF_SETMASTER)
stop
TeamRedFire:
GDTB A 0 A_SpawnItemEX("ThunderTower_R",120,0,0,0,0,0,0,SXF_SETMASTER)
TNT1 A 0
stop
TeamBlueFire:
GDTB A 0 A_SpawnItemEX("ThunderTower_B",120,0,0,0,0,0,0,SXF_SETMASTER)
TNT1 A 0
stop
}
}