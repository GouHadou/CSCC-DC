//Numbers that -might- make balancing easier

const int VIZ_LASER_DMG = 10;
const int VIZ_DASH_LIMIT = 3;
const int VIZ_DASH_SPEED = 120;
const int VIZ_DASH_DMG = 70;
const int VIZ_DASH_RADIUS = 120;
const int VIZ_PAD_ID = 5000;

actor VoltLaserWep : MegaBuster
{
//$Category MM8BDM-Weapons
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Obituary "%o's memories were scrambled by %k's Voltron Splicers."
Inventory.Pickupmessage "Eye I. Laser!"
weapon.ammotype "VoltLaserAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
+WEAPON.ALT_AMMO_OPTIONAL
Scale 2.0
Dropitem "VoltWallWepDropped"
States
{
Spawn:
WEA3 R 0
stop
Ready:
OBUS A 0 ACS_ExecuteAlways(998,0,600)
OBUS A 0 A_GunFlash
OBUS A 1 A_WeaponReady
Goto Ready+2
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
OBUS A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
OBUS A 1 A_Raise
Loop
Fire:
OBUS A 0 A_JumpIfInventory("PlatformFlag", 1, "Fire2")
OBUS A 0 A_PlaySoundEx("weapon/thunderbolt","Weapon")
OBUS A 0 A_FireCustomMissile("BlastLaser1",0,0,8,0)
OBUS A 0 A_FireCustomMissile("BlastLaser2",0,0,8,0)
OBUS A 0 A_FireCustomMissile("BlastLaser3",0,0,8,0)
OBUS BCCCCBB 2
OBUS A 0 A_Refire
OBUS A 14
Goto Ready+2
Fire2:
OBUS A 0 A_PlaySoundEx("weapon/thunderbolt","Weapon")
OBUS A 0 A_TakeInventory("PlatformFlag", 1)
OBUS A 0 A_SpawnItemEx("VoltRingShot1", 0, 0, 32, 20, 0, 0, 0, SXF_TRANSFERTRANSLATION)
OBUS A 0 A_SpawnItemEx("VoltRingShot1", 0, 0, 32, 20, 0, 0, 90, SXF_TRANSFERTRANSLATION)
OBUS A 0 A_SpawnItemEx("VoltRingShot1", 0, 0, 32, 20, 0, 0, 180, SXF_TRANSFERTRANSLATION)
OBUS A 0 A_SpawnItemEx("VoltRingShot1", 0, 0, 32, 20, 0, 0, 270, SXF_TRANSFERTRANSLATION)
OBUS BCCCCBB 2
OBUS A 14
Goto Ready+2
Altfire:
OBUS A 0 A_JumpIfInventory("VizPadTimer", 1, "PadReturn")
OBUS A 0 A_JumpIfInventory("VoltLaserAmmo", 9, "AltfireDash")
Goto Ready+2
AltfireDash:
OBUS A 1 A_ChangeFlag(FLOORHUGGER, 1)
OBUS A 0 A_SpawnItemEx("VizPad", 0, 0, -(z-floorz), 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
OBUS A 0 A_SpawnItemEx("VizImage", 0, 0, -(z-floorz), 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
OBUS A 0 Thing_Remove(tid+VIZ_PAD_ID)
OBUS A 0 A_GiveInventory("VizPadTimer", 175)
OBUS A 0 A_GiveInventory("PlatformFlag", 1)
OBUS A 0 A_ChangeFlag(THRUACTORS, 1)
OBUS A 0 SetPlayerProperty(0, 1, 4)
OBUS A 0 A_PlaySoundEx("viz/dash", "weapon")
AltfireLoop:
OBUS A 1 A_ChangeVelocity(VIZ_DASH_SPEED, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OBUS A 0 A_JumpIf(momx==0 || momy==0, "AltfireEnd")
OBUS A 0 A_JumpifInventory("CutterFlag", VIZ_DASH_LIMIT, "AltfireEnd")
OBUS A 0 A_GiveInventory("CutterFlag", 1)
loop
AltfireEnd:
OBUS A 0 A_PlaySoundEx("viz/dashland", "weapon")
OBUS A 0 A_SpawnItemEx("VisionDistorter", 0, 0, 32)
OBUS A 0 A_TakeInventory("VoltLaserAmmo", 9)
OBUS A 1 A_Stop
OBUS A 0 A_TakeInventory("CutterFlag", 999)
OBUS A 0 A_ChangeFlag(THRUACTORS, 0)
OBUS A 0 A_ChangeFlag(FLOORHUGGER, 0)
OBUS A 0 SetPlayerProperty(0, 0, 4)
OBUS A 12 A_WeaponReady(WRF_NOSECONDARY)
Goto Ready+2
PadReturn:
OBUS A 1 A_ChangeFlag(FLOORHUGGER, 1)//Anti-pit stuff...can't use this above pits or you ded 
									 //[meanwhile you can still abuse bits right next to the wall...oh well]
OBUS A 0 A_SpawnItemEx("VisionDistorter", 0, 0, 32)
OBUS A 0 A_GiveInventory("PadExplodeFlag", 1)
OBUS A 0 A_SpawnItemEx("VizImage", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
OBUS A 0 Thing_Move(tid, tid+VIZ_PAD_ID, 1)
OBUS A 0 A_ChangeFlag(FLOORHUGGER, 0)
OBUS A 0 ThrustThingZ(0, 5, 0, 0)
OBUS A 0 A_PlaySoundEx("viz/teleport", "body")
OBUS A 0 A_SpawnItemEx("VisionDistorter", 0, 0, 32)
OBUS A 0 A_ScaleVelocity(0)
OBUS A 6 A_WeaponReady(WRF_NOSECONDARY)
OBUS A 12 A_WeaponReady(WRF_NOSECONDARY)
Goto Ready+2
Flash:
TNT1 A 0 A_JumpIfInventory("VizPadTimer", 1, 2)
TNT1 A 0 A_GiveInventory("VoltLaserAmmo", 1)
TNT1 A 35
loop
NoAmmo:
OBUS A 1 ACS_Execute(979,0)
Goto Ready+2
}
}

actor PlatformFlag : Once {}

actor VoltLaserAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 28
}

actor BlastLaserTrail
{
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
+FORCEXYBILLBOARD
-SOLID
renderstyle translucent
alpha 0.6
scale 2.5
height 0
radius 0
states
{
Spawn:
OBE1 ABCDEF 1
stop
}
}

actor BlastLaser1
{
Obituary "%o's memories were scrambled by %k's Voltron Splicers."
PROJECTILE
damagetype "BlastSplicer"
+PIERCEARMOR
+FORCEXYBILLBOARD
renderstyle add
scale 2.5
Speed 40
Radius 4
Height 8
Damage (VIZ_LASER_DMG)
reactiontime 10
States
{
Spawn:
OBE1 A 0 A_BishopMissileWeave
OBE1 A 1 A_SpawnItemEx("BlastLaserTrail")
OBE1 A 0 A_CountDown
loop
Death:
OBE1 GHIJ 3
stop
}
}

actor BlastLaser2 : BlastLaser1
{
states
{
Spawn:
TNT1 AAAAAAAAAAA 0 A_BishopMissileWeave//Totally not jacking ideas from Terminus...nope...
goto Super::Spawn
}
}

actor BlastLaser3 : BlastLaser1
{
states
{
Spawn:
TNT1 AAAAAAAAAAAAAAAAAAAAA 0 A_BishopMissileWeave
goto Super::Spawn
}
}

actor VisionDistorter
{
PROJECTILE
damage 0
radius 32
height 64
speed 0
damagetype "FlashStopper"
obituary "%o was blinded by %k's Visual Distorter."
renderstyle none
States
{
Spawn:
PLAY A 0
PLAY A 0 A_Explode(VIZ_DASH_DMG, VIZ_DASH_RADIUS, 0)
PLAY AAAAAAAAAA 0 A_SpawnItemEx("VisionDistorterExplosionFX", 0, 0, 12, VIZ_DASH_RADIUS/12, 0, random2(VIZ_DASH_RADIUS/12), random(0, 359))
PLAY A 5
stop
Death:
PLAY H 2 A_GiveToTarget("VisionDistorterFixer1", 1)
PLAY H 0 A_GiveToTarget("VisionDistorterFixer2", 1)
stop
}
}

actor VisionDistorterFixer1 : CustomInventory
{
+INVENTORY.AUTOACTIVATE
States
{
Use:
TNT1 A 0
TNT1 A 0 A_ChangeFlag(THRUACTORS, 1)
TNT1 A 0 ThrustThingZ(0, 64, 0, 0)
stop
}
}


actor VisionDistorterFixer2 : VisionDistorterFixer1
{
States
{
Use:
TNT1 A 0
TNT1 A 0 A_ChangeVelocity(momx, momy, 0, CVF_REPLACE)
TNT1 A 0 A_ChangeFlag(THRUACTORS, 0)
stop
}
}

actor VisionDistorterExplosionFX : BlastLaserTrail
{
renderstyle add
alpha 1.0
states
{
Spawn:
TNT1 A 1
ODAS ABCD 3
stop
}
}

actor VizPad
{
+FLOORHUGGER
+RIPPER
+NOTARGETSWITCH
+MISSILE
-SOLID
damage (0)
Scale 2.5
Height 2
Radius 1
States
{
Spawn:
OPA1 A 0
OPA1 A 0 Thing_ChangeTID(0, VIZ_PAD_ID+ACS_ExecuteWithResult(257, 0))
OPA1 DCB 2
Spawn2:
OPA1 A 2 A_TakeFromTarget("VizPadTimer", 2)
OPA1 A 0 A_JumpIfInTargetInventory("PadExplodeFlag", 1, "Death")
OPA1 A 0 A_JumpIfInTargetInventory("VizPadTimer", 1, "Spawn2")
goto Death
Death:
OPA1 A 0 A_TakeFromTarget("PlatformFlag", 1)
OPA1 A 0 A_TakeFromTarget("VizPadTimer", 999)
OPA1 A 0 A_TakeFromTarget("PadExplodeFlag", 1)
OPA1 ABCD 2
stop
}
}

actor VizPadTimer : inventory
{
inventory.amount 1
inventory.maxamount 175
}

actor PadExplodeFlag : Once {}

actor VizImage : BlastLaserTrail
{
alpha 1.0
States
{
Spawn:
OPTM A 1 A_Fadeout(0.1)
wait
}
}

const int VIZ_RING_TURNSPEED = 14;

actor VoltRingDamager
{
PROJECTILE
+RIPPER
+DONTBLAST
obituary "%o was disintegrated by %k's Discharge Field."
Radius 10
Height 40
scale 2.5
damage (5)
speed 32
States
{
Spawn:
TNT1 A 1
stop
}
}

actor VoltRingShot1
{
PROJECTILE
+RIPPER
+DONTBLAST
obituary "%o was disintegrated by %k's Discharge Field."
Radius 10
Height 10
scale 2.5
damage (18)
speed 32
States
{
Spawn:
OFI1 B 6
OFI1 A 0 A_Stop
OFI1 AB 3
OFI1 A 0 A_SpawnItemEx("VoltRingShot2R", 0, 0, 0, 0, 0, 0, 90, SXF_TRANSFERTRANSLATION)
OFI1 A 0 A_SpawnItemEx("VoltRingShot2L", 0, 0, 0, 0, 0, 0, -90, SXF_TRANSFERTRANSLATION)
stop
}
}

actor VoltRingShot2R
{
PROJECTILE
+NOINTERACTION
+DONTBLAST
Radius 10
Height 40
scale 2.5
damage (5)
speed 32
reactiontime 10
States
{
Spawn:
OFI1 B 0
OFI1 CDE 2
SpawnLoop:
OFI1 F 0 A_SetAngle(angle+VIZ_RING_TURNSPEED)
OFI1 F 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 FF 1 A_SpawnItemEx("VoltRingDamager")
OFI1 G 0 A_SetAngle(angle+VIZ_RING_TURNSPEED)
OFI1 G 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 GG 1 A_SpawnItemEx("VoltRingDamager")
OFI1 H 0 A_SetAngle(angle+VIZ_RING_TURNSPEED)
OFI1 H 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 HH 1 A_SpawnItemEx("VoltRingDamager")
OFI1 I 0 A_SetAngle(angle+VIZ_RING_TURNSPEED)
OFI1 I 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 II 1 A_SpawnItemEx("VoltRingDamager")
OFI1 I 0 A_CountDown
loop
Death:
OFI1 F 0 A_SetAngle(angle+VIZ_RING_TURNSPEED)
OFI1 F 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 F 1 A_Fadeout
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 F 1 A_Fadeout
OFI1 G 0 A_SetAngle(angle+VIZ_RING_TURNSPEED)
OFI1 G 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 G 1 A_Fadeout
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 G 1 A_Fadeout
OFI1 H 0 A_SetAngle(angle+VIZ_RING_TURNSPEED)
OFI1 H 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 H 1 A_Fadeout
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 H 1 A_Fadeout
OFI1 I 0 A_SetAngle(angle+VIZ_RING_TURNSPEED)
OFI1 I 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 I 1 A_Fadeout
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 I 1 A_Fadeout
loop
}
}

actor VoltRingShot2L : VoltRingShot2R
{
States
{
Spawn:
OFI1 B 0
OFI1 CDE 2
SpawnLoop:
OFI1 F 0 A_SetAngle(angle-VIZ_RING_TURNSPEED)
OFI1 F 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 FF 1 A_SpawnItemEx("VoltRingDamager")
OFI1 G 0 A_SetAngle(angle-VIZ_RING_TURNSPEED)
OFI1 G 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 GG 1 A_SpawnItemEx("VoltRingDamager")
OFI1 H 0 A_SetAngle(angle-VIZ_RING_TURNSPEED)
OFI1 H 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 HH 1 A_SpawnItemEx("VoltRingDamager")
OFI1 I 0 A_SetAngle(angle-VIZ_RING_TURNSPEED)
OFI1 I 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 II 1 A_SpawnItemEx("VoltRingDamager")
OFI1 I 0 A_CountDown
loop
Death:
OFI1 F 0 A_SetAngle(angle-VIZ_RING_TURNSPEED)
OFI1 F 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 F 1 A_Fadeout
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 F 1 A_Fadeout
OFI1 G 0 A_SetAngle(angle-VIZ_RING_TURNSPEED)
OFI1 G 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 G 1 A_Fadeout
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 G 1 A_Fadeout
OFI1 H 0 A_SetAngle(angle-VIZ_RING_TURNSPEED)
OFI1 H 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 H 1 A_Fadeout
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 H 1 A_Fadeout
OFI1 I 0 A_SetAngle(angle-VIZ_RING_TURNSPEED)
OFI1 I 0 A_ChangeVelocity(10, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 I 1 A_Fadeout
OFI1 F 0 A_SpawnItemEx("VoltRingDamager")
OFI1 I 1 A_Fadeout
loop
}
}

actor VoltWallWep : MegaBuster //Copyweps, yeeeeee
{
//$Category MM8BDM-Weapons
Weapon.AmmoUse 1
Weapon.AmmoGive 28
Weapon.SlotNumber 6
Obituary "%o was energized %k's Volt Wall."
Inventory.Pickupmessage "Power up! Volt Wall!"
weapon.ammotype "VoltWallAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
Inventory.Icon "VWEAP2"
Scale 2.0 
States
{
Spawn:
VICO B 1
loop
Ready:
TSLO A 0 ACS_ExecuteAlways (998,0,96)
TSLO A 1 A_WeaponReady
goto Ready+1
Fire:
TSLO A 0 A_JumpIfNoAmmo("NoAmmo")
SLOT JKLMN 2
TNT1 A 0 A_PlaySoundEx("weapon/sparkshock","Weapon")
TNT1 A 0 A_SpawnItemEx("VoltWall",48,0,32,0,0,0,0,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_TakeInventory("VoltWallAmmo", 4)
TNT1 A 2
TSLO BC 2
TSLO AAAAAAAAAA 1 A_WeaponReady (WRF_NOFIRE)
TSLO AAAAAAAAAA 1 A_WeaponReady (WRF_NOFIRE)
TSLO AAAAAAAAAA 1 A_WeaponReady (WRF_NOFIRE)
goto Ready+1
NoAmmo: 
TSLO A 1 ACS_Execute(979,0)
goto Ready+1
}
}

actor VoltWallAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
}

const int COPYWEP_DIST = 32;
const int COPYWEP_TURNSPEED = 16;

actor VoltWall
{
PROJECTILE
+RIPPER
Radius 10
Height 10
scale 2.5
damage (18)
speed 32
States
{
Spawn:
OFI1 B 0
OFI1 A 0 A_Stop
OFI1 AB 3
OFI1 A 0 A_SpawnItemEx("VoltWall2", COPYWEP_DIST, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
OFI1 A 0 A_SpawnItemEx("VoltWall2", COPYWEP_DIST, 0, 0, 0, 0, 0, 90, SXF_TRANSFERTRANSLATION)
OFI1 A 0 A_SpawnItemEx("VoltWall2", COPYWEP_DIST, 0, 0, 0, 0, 0, 180, SXF_TRANSFERTRANSLATION)
OFI1 A 0 A_SpawnItemEx("VoltWall2", COPYWEP_DIST, 0, 0, 0, 0, 0, 270, SXF_TRANSFERTRANSLATION)
stop
}
}

actor VoltWall2
{
PROJECTILE
Radius 10
Height 40
scale 2.5
damage (17)
damagetype "VoltWall"
speed 32
reactiontime 30
States
{
Spawn:
OFI1 BB 0 A_SetAngle(angle+90)
OFI1 CDE 2
SpawnLoop:
OFI1 F 0 A_SetAngle(angle+COPYWEP_TURNSPEED)
OFI1 F 2 A_ChangeVelocity(5, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 G 0 A_SetAngle(angle+COPYWEP_TURNSPEED)
OFI1 G 2 A_ChangeVelocity(5, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 H 0 A_SetAngle(angle+COPYWEP_TURNSPEED)
OFI1 H 2 A_ChangeVelocity(5, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 I 0 A_SetAngle(angle+COPYWEP_TURNSPEED)
OFI1 I 2 A_ChangeVelocity(5, 0, 0, CVF_RELATIVE|CVF_REPLACE)
OFI1 I 0 A_CountDown
loop
Death:
OFI1 A 0 A_Stop
OFI1 EDCBA 2
stop
}
}

actor VoltSlow : PowerSpeed
{
Speed 0.67
powerup.duration 105
}

actor VoltWeaken : PowerProtection
{
damagefactor "Normal", 1.5
powerup.duration 105
Powerup.Color "55 55 00", 0.4
}