actor TankSnailWeapon : BaseCSCCWep
{
Weapon.AmmoUse 6
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Obituary "%o was slugged to submission by %k's Ballistic Mucus."
Inventory.Pickupmessage "Power up! Ballistic Mucus!"
weapon.ammotype "MucusAmmo"
weapon.ammotype2 "TankSnailHeat"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
Scale 2.0
States
{
Spawn:
TNT1 A 0
TNT1 A 1 //A_SpawnItemEX("MucusCopyWepDropped")
stop
Ready:
BLMU A 0 ACS_ExecuteAlways(998,0,710)
BLMU A 8 A_WeaponReady
BLMU A 0 A_GiveInventory("MucusAmmo",1)
BLMU A 8 A_WeaponReady
BLMU A 0 A_GiveInventory("MucusAmmo",1)
BLMU A 8 A_WeaponReady
BLMU A 0 A_GiveInventory("MucusAmmo",1)
BLMU A 8 A_WeaponReady
BLMU A 0 A_GiveInventory("MucusAmmo",1)
BLMU A 0 A_TakeInventory("TankSnailHeat",2)
Goto Ready+1
Deselect:
TNT1 A 0 A_SelectWeapon("TankSnailWeapon")
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BLMU A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
BLMU A 1 A_Raise
Loop
Fire:
BLMU A 0 A_JumpIfInventory("TankSnailHeat",49,"Overheat")
BLMU A 0 A_JumpIfNoAmmo("NoAmmo")
BLMU AAB 2
BLMU B 0 SetPlayerProperty(0,1,0)
BLMU B 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
BLMU B 2 A_FireCustomMissile("MucusBomb",0,1,0,10)
BLMU B 0 A_GiveInventory("TankSnailHeat",8)
BLMU BBC 2
BLMU C 0 SetPlayerProperty(0,0,0)
BLMU CCCBBAA 2
BLMU A 0 A_Refire
Goto Ready+1
AltFire:
BLMU A 0 A_JumpIfInventory("MucusAmmo",2,"AltFireAttack")
BLMU A 0 A_JumpIfInventory("SnailBoostFlag",1,"FallCheck")
BLMU A 1
goto Ready+1
AltFireAttack:
BLMU A 0 A_JumpIfInventory("TankSnailHeat",56,"Overheat")
BLMU A 0 ThrustThingZ(0, 4, 0, 1)
BLMU A 0 A_PlaySoundEX("weapon/superboost", "Weapon")
BLMU A 1
BLMU A 0 ThrustThingZ(0, 4, 0, 1)
BLMU A 0 A_PlaySoundEX("weapon/superboost", "Weapon")
BLMU A 1
BLMU A 0 ThrustThingZ(0, 3, 0, 1)
BLMU A 0 A_TakeInventory("MucusAmmo", 2)
BLMU A 0 A_GiveInventory("TankSnailHeat", 1)
BLMU A 0 A_GiveInventory("SnailBoostFlag", 1)
BLMU A 0 A_Refire
BLMU A 0 A_TakeInventory("SnailBoostFlag", 1)
BLMU A 0
goto FallCheck
FallCheck:
BLMU A 0 A_TakeInventory("SnailBoostFlag", 1)
BLMU A 1 A_JumpIf(z-floorz>224, "FallSequence")
goto Ready+1
FallSequence:
BLMU A 0 SetPlayerProperty(0,1,0)
BLMU A 0
BLMU A 0 A_SpawnItemEx("SnailCrush",0,0,-10,0,0,-10)
BLMU A 1 A_JumpIf(z-floorz<=0, "SnailSmash")
goto FallContinue
SnailSmash:
BLMU A 0 A_PlaySoundEx("misc/oil", "Weapon")
BLMU A 0 A_FireCustomMissile ("MucusSpread",0,0,0,0)
BLMU A 0 A_FireCustomMissile ("MucusSpread",45,0,0,0)
BLMU A 0 A_FireCustomMissile ("MucusSpread",90,0,0,0)
BLMU A 0 A_FireCustomMissile ("MucusSpread",135,0,0,0)
BLMU A 0 A_FireCustomMissile ("MucusSpread",180,0,0,0)
BLMU A 0 A_FireCustomMissile ("MucusSpread",-45,0,0,0)
BLMU A 0 A_FireCustomMissile ("MucusSpread",-90,0,0,0)
BLMU A 0 A_FireCustomMissile ("MucusSpread",-135,0,0,0)
BLMU A 70
BLMU A 0 SetPlayerProperty(0,0,0)
goto Ready+1
FallContinue:
BLMU A 1
goto FallSequence+1
Overheat:
BLMU A 0 A_JumpIfInventory("SnailBoostFlag",1,"FallCheck")
BLMU A 1 //If Snail's heat meter is too high, his moves fail.
Goto Ready+1
NoAmmo:
BLMU A 1
BLMU A 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor MucusAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 28
}

actor TankSnailHeat : Ammo //Heat value, a major factor in gameplay.
{
+INVENTORY.IGNORESKILL //Would it be dumb if he overheats faster based on difficulty?
inventory.amount 1
inventory.maxamount 56
}

actor SnailBoostFlag : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor MucusBomb
{
PROJECTILE
Radius 6
Height 7
scale 2.5
damage (0)
damagetype "UpperMucus"
+EXPLODEONWATER
speed 50
States
{
Spawn:
BLMU DEDF 3
BLMU F 0 A_SpawnItemEX("MucusDrop",0,0,0,0,0,0,0)
BLMU F 0 A_GiveInventory("MucusDuration",1)
BLMU F 0 A_JumpIfInventory("MucusDuration",25,"End")
BLMU F 0
loop
End:
BLMU F 0
stop
Death:
BLMU D 0 A_PlaySoundEx("misc/oil", "Weapon")
BLMU D 0 A_Explode(33, 128, 0)
BLMU D 0 A_SpawnItemEX("MucusSpread",0,0,0,13,0,0,135)
BLMU D 0 A_SpawnItemEX("MucusSpread",0,0,0,13,0,0,180)
BLMU D 0 A_SpawnItemEX("MucusSpread",0,0,0,13,0,0,-135)
stop
}
}

actor MucusDuration : Inventory //Ballistic Mucus' life span
{
inventory.amount 1
inventory.maxamount 25
}

actor MucusDrop
{
PROJECTILE
Damage (15)
obituary "%o was bombarded by %k's Mucus Droplet."
damagetype "UpperMucus"
Height 5
Radius 5
-NOGRAVITY
Scale 2.0
speed 0
States
{
Spawn:
BLMU G 0
BLMU G 5 A_CheckFloor("Death")
loop
Death:
BLMU G 1 
BLMU G 0 A_JumpIf(z-floorz<=0,"Death2")
loop
Death2:
BLMU G 0 A_PlaySoundEx("weapon/oilshot","Weapon")
BLMU G 1 A_SpawnItemEx("MucusBlob",0,0,0)
stop
}
}

Actor MucusBlob
{
+MISSILE
+NODAMAGETHRUST
+NOBLOOD
+DONTSPLASH
+NOEXPLODEFLOOR
+NOTARGETSWITCH
Damage (1)
obituary "%o got in a sticky situation with %k's Mucus Blob."
damagetype "LowerMucus"
Height 10
Radius 8
scale 2.5
reactiontime 250
painchance 256
states
{
Spawn:
BLMU H 1
Goto Spawn2
Spawn2:
BLMU H 1
BLMU H 0 A_GiveInventory("CutterFlag", 1)
BLMU H 0 A_JumpIf(z-floorz>0,"NewShot")
BLMU H 0 A_JumpIfInventory("CutterFlag", 175, "Death")
loop
NewShot:
BLMU H 0 A_SpawnItemEx("MucusDrop", 0, 0, 0, momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM)
Goto Death 
Death:
TNT1 A 0
stop
}
}

actor MucusSpread : MucusDrop
{
speed 13
States
{
Spawn:
BLMU G 1
BLMU G 0 ThrustThingZ(0, 12, 0, 1)
goto Spawn2
Spawn2:
BLMU G 0
BLMU G 5 A_CheckFloor("Death")
loop
}
}

actor MucusBlind : PowerSpeed //Blinded by direct hit
{
Powerup.Duration 210
Speed 0.6
Powerup.Color Green, 0.5
}

actor MucusStep : PowerSpeed //Stepped on mucus
{
Powerup.Duration 280
Speed 0.3
}

actor SnailCrush
{
PROJECTILE
damage (10)
Radius 15
Height 2
Obituary "%o was dive-bombed by %k."
renderstyle none
States
{
Spawn:
TNT1 A 1
stop
}
}

actor CrushDamage
{
+NOGRAVITY
+MISSILE
+SKYEXPLODE
+NOINTERACTION
+RIPPER
height 1
radius 300
damage (20)
damagetype "UpperMucus"
Obituary "%o was coated in %k's Mucus Splash."
-SOLID
States
{
Spawn:
TNT1 A 1
stop
}
}

actor MucusCopyWep : BaseCSCCWep
{
Weapon.AmmoUse 7
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Obituary "%o was slugged to submission by %k's Ballistic Mucus."
Inventory.Pickupmessage "Power up! Ballistic Mucus!"
weapon.ammotype "MucusAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL 
inventory.icon "BLMUI"
Scale 2.0
States
{
Spawn:
BLMU X 1
loop
Ready:
BLMU I 0 ACS_ExecuteAlways(998,0,710)
BLMU I 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BLMU I 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
BLMU I 1 A_Raise
Loop
Fire:
BLMU I 0 A_JumpIfNoAmmo("NoAmmo")
BLMU IIJ 2
BLMU J 0 SetPlayerProperty(0,1,0)
BLMU J 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
BLMU J 2 A_FireCustomMissile("MucusBomb",0,1,8,0)
BLMU I 0 A_ChangeVelocity(-9, 0, 0, CVF_RELATIVE)
BLMU JJK 2
BLMU K 0 SetPlayerProperty(0,0,0)
BLMU KKKJJII 2
BLMU I 0 A_Refire
Goto Ready+1
NoAmmo:
BLMU I 1 
Goto Ready+1
}
}