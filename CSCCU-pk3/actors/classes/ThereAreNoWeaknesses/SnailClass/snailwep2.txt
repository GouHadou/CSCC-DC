actor TankSnailStart : CustomInventory //Spawn item, creates checker
{
+INVENTORY.AUTOACTIVATE
-COUNTITEM
states
{
Pickup:
TNT1 A 1 A_SpawnItemEx("TankSnailDude")
stop
}
}

actor BreederTrail : BuddyTrail
{
obituary "$OB_SNAILTRAIL"
damagetype "SlugFeet2" //Give victim the SuperSlugFeet item.
Height 3
Radius 18
XScale 2.5
YScale 2.5
States
{
Spawn:
SNTR B 20
stop
}
}

actor TankSnailDude //Activates passive sequence
{
+MISSILE
+NOINTERACTION
+DONTBLAST
states
{
Spawn:
TNT1 A 0
TNT1 A 4 A_Warp(AAPTR_TARGET,-4,0,0,0)
TNT1 A 0 ACS_NamedExecuteAlways("cscc_stillalive",0,0)
TNT1 A 4 A_Warp(AAPTR_TARGET,-4,0,0,0)
TNT1 A 0 ACS_NamedExecuteAlways("cscc_stillalive",0,0)
TNT1 A 0 A_JumpIf(z-floorz<=0,"FloorIt")
loop
FloorIt:
TNT1 A 0 ACS_NamedExecuteAlways("cscc_stillalive",0,0)
TNT1 A 0 A_GiveToTarget("SnailCrawl",1) //see Snail Breeder
goto Spawn
Begone:
TNT1 A 0
stop
}
}

actor YesTankTrail : CustomInventory //Trail spawner
{
+INVENTORY.AUTOACTIVATE
-COUNTITEM
states
{
Pickup:
TNT1 A 3
TNT1 A 0 A_JumpIfInventory("SnailDash",1,"NoUse")//check if dash in effect
TNT1 A 0 A_SpawnItemEx("TankSnailTrail")
TNT1 A 0 A_PlaySoundEx("weapon/OilSlider","Voice")
stop
NoUse:
TNT1 A 0
stop
}
}

actor TankSnailTrail
{
PROJECTILE
Damage (2)
obituary "$OB_SNAILTRAIL"
damagetype "SlugFeet3" //Give victim the SlugFeet3 item.
Height 3
Radius 24
-NOGRAVITY
+FLOORHUGGER
+NOEXPLODEFLOOR
+DONTBLAST
XScale 3.0
YScale 2.5
Translation "192:192=110:110"
speed 0
States
{
Spawn:
SNTR B 20
stop
}
}

actor HotTankTrail : TankSnailTrail
{
Damage (5)
obituary "$OB_SNAILDASH"
damagetype "SlugFeet"
Translation "192:192=42:42"
+NOGRAVITY
-FLOORHUGGER
+RIPPER
States
{
Spawn:
SNTR B 0
SNTR B 0 A_GiveToTarget("SnailDash",1)//prevent trailing
SNTR BB 5
SNTR B 0 A_TakeFromTarget("SnailDash",1)
stop
}
}