//+[==EXECUTE LV 2==]+

actor ExecuteTech3 : CrazyWaveBoss
{
Weapon.AmmoUse 1
Weapon.SlotNumber 4
Obituary "%o had %p life siphoned by %k's Insanity Tech: \ccExecute \c[s8]LV 3\c-."
Tag "$TAG_EXECUTE3"
inventory.icon "EXECS0"
States
{
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
IPID A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
IPID A 1 A_Raise
Loop

//+[== SELL THE TECH ==]+

Altfire:
IPID A 1
IPID A 0
Althold:
IPID A 1 A_JumpIfInventory("SellCharge",100,"SellTech")
IPID A 0 A_GiveInventory("SellCharge",5)
IPID A 0 A_Refire
Goto Ready1
SellTech:
IPID A 0 A_SpawnItemEx("SellFX_Helper")
IPID A 1 Offset(0,25)
IPID A 1 Offset(0,50) A_PlaySoundEx("insanitytech/sell3","Weapon")
IPID A 1 Offset(0,75)
IPID A 1 Offset(0,100) A_GiveInventory("TechAmmo",75)
IPID A 10
IPID A 0 A_JumpIfInventory("ExecuteAmmo3",2,"SellTechX")
IPID A 0 A_TakeInventory("ExecuteAmmo3",99)
IPID A 0 A_TakeInventory("ExecuteTech3",99)
Goto Ready1
SellTechX:
IPID A 0 A_TakeInventory("ExecuteAmmo3",1)
Goto Ready1

//+[== USE THE TECH ==]+

Fire: //Start
ExecuteStart:
IPID A 0 ACS_ExecuteAlways(512,0,1,0)
PEXC IIHH 1
IPID A 0 A_PlaySoundEx("Execute/Charge2","Weapon")
ExecuteCharge:
TNT1 A 0 A_GiveInventory("ExecuteWeakness3",1)
IPID A 0 A_GunFlash
IPID A 0 ACS_ExecuteAlways(512,0,1,4)
PEXC FL 1 A_FireCustomMissile("ExecuteChecker",0,0,8,0)
TNT1 A 0 A_SpawnItemEx("ExecuteRange3",52,0,0,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM,0)
PEXC G 0 A_Refire("ExecuteCharge")
ExecuteCheck:
PEXC G 1 A_JumpIfInventory("CanExecute",1,"ExecuteFire")
Goto ExecuteFail
ExecuteFire:
TNT1 A 0 A_TakeInventory("ExecuteWeakness3",1)
IPID A 0 A_TakeInventory("ExecuteAmmo3",1)
PEXC H 0 A_SpawnItemEx("ExecuteFinisher3",52,0,8)
PEXC G 1 Offset(-48,64)
PEXC G 11 Offset(0,96)
PEXC G 3 Offset(0,128)
IPID A 1 Offset(0,92)
IPID A 1 Offset(0,62)
IPID A 1 A_WeaponReady(WRF_NOFIRE)
IPID A 3
IPID A 0 ACS_ExecuteAlways(991,0,DYE_PHARAOHMAN)
IPID A 0 A_TakeInventory("CanExecute",99)
TakeAll:
IPID A 0 A_JumpIfInventory("ExecuteAmmo3",1,"Ready1")
IPID A 0 A_TakeInventory("ExecuteTech3",99)
IPID A 0
Goto Ready1
ExecuteFail:
TNT1 A 0 A_TakeInventory("ExecuteWeakness3",1)
PEXC G 0 ACS_ExecuteAlways(991,0,DYE_PHARAOHMAN)
PEXC G 0 ACS_ExecuteAlways(512,0,1,0)
IPID A 0 A_TakeInventory("CanExecute",99)
PEXC E 1 Offset(-24,42)
PEXC E 1 Offset(-24,72)
PEXC E 1 Offset(-24,92)
PEXC E 1 Offset(-24,102)
PEXC E 1 Offset(-24,142)
IPID A 1 Offset(0,92)
IPID A 1 Offset(0,62)
IPID A 1 A_WeaponReady(WRF_NOFIRE)
IPID A 3
goto Ready1
Flash:
IPID A 0 A_JumpIfInventory("ExecuteWeakness3",1,"Flash_Execute")
TNT1 A 1
loop
Flash_Execute:
TNT1 A 1 ACS_ExecuteAlways(991,0,DYE_PHARAOHEXECUTE3)
TNT1 A 1 ACS_ExecuteAlways(991,0,DYE_PHARAOHMAN)
Goto Flash
}
}

Actor ExecuteFinisher3
{
PROJECTILE
+DONTBLAST
+THRUGHOST
+NOCLIP
+PIERCEARMOR
Damagetype "ExecuteFinish3"
Scale 1.5
Radius 70
Height 85
Damage(0)
Speed 0
Obituary "%o had %p life siphoned by %k's Insanity Tech: \ccExecute \c[s8]LV 3\c-."
States
{
Spawn:
TNT1 A 0 Radius_Quake(9,45,0,1,0)
TNT1 A 0 A_SpawnItemEx("ExecuteFX3",15,-10,35)
TNT1 A 0 A_PlaySoundEx("Execute/Finish2","Voice")
TNT1 A 1 A_Explode(90,65,0,0,65)
stop
}
}

Actor ExecuteFX3
{
Translation "192:192=174:174", "198:198=0:0", "4:4=176:176"
+SKYEXPLODE
+DONTBLAST
+DONTREFLECT
+DONTSPLASH
+NOGRAVITY
-SOLID
+THRUGHOST
+THRUACTORS
+NOINTERACTION
Scale 1
Radius 4
Height 4
States
{
Spawn:
PEXF A 0
PFBX ABCDEFGHI 1 bright
PFBX IIII 1 bright A_Fadeout
stop
}
}
Actor ExecuteRange3 : ExecuteFX
{
+NOCLIP
Translation "0:255=176:176"
renderstyle add
alpha 0.8
Scale 3.8
States
{
Spawn:
MMFX F 0
MMFX F 1 
stop
}
}

actor ExecuteAmmo3 : Ammo
{
inventory.amount 2
inventory.maxamount 2
}

actor ExecuteWeakness3 : PowerProtection
{
Powerup.Duration 0x7FFFFFFD
damagefactor "Normal", 2.0
}