//+[==EXECUTE LV 2==]+

actor ExecuteTech2 : CrazyWaveBoss
{
Weapon.AmmoUse 1
Weapon.SlotNumber 3
Obituary "%o's matrix disintegrated from %k's Insanity Tech: \ccExecute \c[g1]LV 2\c-."
Tag "$TAG_EXECUTE2"
inventory.icon "EXECA0"
States
{
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
IPID A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
IPID A 1 A_Raise
Loop

//+[== SELL THE TECH ==]+

Altfire:
IPID A 1
IPID A 0
Althold:
IPID A 1 A_JumpIfInventory("SellCharge",100,"SellTech")
IPID A 0 A_GiveInventory("SellCharge",5)
IPID A 0 A_Refire
Goto Ready1
SellTech:
IPID A 0 A_SpawnItemEx("SellFX_Helper")
IPID A 1 Offset(0,25)
IPID A 1 Offset(0,50) A_PlaySoundEx("insanitytech/sell2","Weapon")
IPID A 1 Offset(0,75)
IPID A 1 Offset(0,100) A_GiveInventory("TechAmmo",50)
IPID A 10
IPID A 0 A_JumpIfInventory("ExecuteAmmo2",2,"SellTechX")
IPID A 0 A_TakeInventory("ExecuteAmmo2",99)
IPID A 0 A_TakeInventory("ExecuteTech2",99)
Goto Ready1
SellTechX:
IPID A 0 A_TakeInventory("ExecuteAmmo2",1)
Goto Ready1

//+[== USE THE TECH ==]+

Fire: //Start
ExecuteStart:
IPID A 0 ACS_ExecuteAlways(512,0,1,0)
PEXC IIHH 1
IPID A 0 A_PlaySoundEx("Execute/Charge","Weapon")
ExecuteCharge:
TNT1 A 0 A_GiveInventory("ExecuteWeakness2",1)
IPID A 0 A_GunFlash
IPID A 0 ACS_ExecuteAlways(512,0,1,3)
PEXC FK 1 A_FireCustomMissile("ExecuteChecker",0,0,8,0)
TNT1 A 0 A_SpawnItemEx("ExecuteRange2",50,0,4,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM,0)
PEXC G 0 A_Refire("ExecuteCharge")
ExecuteCheck:
PEXC G 1 A_JumpIfInventory("CanExecute",1,"ExecuteFire")
Goto ExecuteFail
ExecuteFire:
TNT1 A 0 A_TakeInventory("ExecuteWeakness2",1)
IPID A 0 A_TakeInventory("ExecuteAmmo2",1)
PEXC H 0 A_SpawnItemEx("ExecuteFinisher2",50,0,8)
PEXC G 1 Offset(-48,64)
PEXC G 11 Offset(0,96)
PEXC G 3 Offset(0,128)
IPID A 1 Offset(0,92)
IPID A 1 Offset(0,62)
IPID A 1 A_WeaponReady(WRF_NOFIRE)
IPID A 3
IPID A 0 ACS_ExecuteAlways(991,0,DYE_PHARAOHMAN)
IPID A 0 A_TakeInventory("CanExecute",99)
TakeAll:
IPID A 0 A_JumpIfInventory("ExecuteAmmo2",1,"Ready1")
IPID A 0 A_TakeInventory("ExecuteTech2",99)
IPID A 0
Goto Ready1
ExecuteFail:
TNT1 A 0 A_TakeInventory("ExecuteWeakness2",1)
PEXC G 0 ACS_ExecuteAlways(991,0,DYE_PHARAOHMAN)
PEXC G 0 ACS_ExecuteAlways(512,0,1,0)
IPID A 0 A_TakeInventory("CanExecute",99)
PEXC E 1 Offset(-24,42)
PEXC E 1 Offset(-24,72)
PEXC E 1 Offset(-24,92)
PEXC E 1 Offset(-24,102)
PEXC E 1 Offset(-24,142)
IPID A 1 Offset(0,92)
IPID A 1 Offset(0,62)
IPID A 1 A_WeaponReady(WRF_NOFIRE)
IPID A 3
goto Ready1
Flash:
IPID A 0 A_JumpIfInventory("ExecuteWeakness2",1,"Flash_Execute")
TNT1 A 1
loop
Flash_Execute:
TNT1 A 1 ACS_ExecuteAlways(991,0,DYE_PHARAOHEXECUTE2)
TNT1 A 1 ACS_ExecuteAlways(991,0,DYE_PHARAOHMAN)
Goto Flash
}
}

Actor ExecuteFinisher2
{
PROJECTILE
+DONTBLAST
+THRUGHOST
+NOCLIP
+PIERCEARMOR
Damagetype "ExecuteFinish2"
Scale 1.5
Radius 65
Height 80
Damage(0)
Speed 0
Obituary "%o's matrix disintegrated from %k's Insanity Tech: \ccExecute \c[g1]LV 2\c-."
States
{
Spawn:
TNT1 A 0 Radius_Quake(6,45,0,1,0)
TNT1 A 0 A_SpawnItemEx("ExecuteFX2",15,-10,30)
TNT1 A 0 A_PlaySoundEx("Execute/Finish","Voice")
TNT1 A 1 A_Explode(70,60,0,0,60)
stop
}
}

Actor ExecuteFX2
{
Translation "192:192=205:205", "198:198=197:197", "4:4=202:202"
+SKYEXPLODE
+DONTBLAST
+DONTREFLECT
+DONTSPLASH
+NOGRAVITY
-SOLID
+THRUGHOST
+THRUACTORS
+NOINTERACTION
Renderstyle Add
Scale 0.8
Radius 4
Height 4
States
{
Spawn:
PEXF A 0
PFBX ABCDEFGHI 1 bright
PFBX IIII 1 bright A_Fadeout
stop
}
}

Actor ExecuteRange2 : ExecuteFX
{
+NOCLIP
Translation "0:255=192:192"
renderstyle Add
alpha 0.6
Scale 3.4
States
{
Spawn:
MMFX F 0
MMFX F 1 
stop
}
}

actor ExecuteAmmo2 : Ammo
{
inventory.amount 3
inventory.maxamount 3
}

actor ExecuteWeakness2 : PowerProtection
{
Powerup.Duration 0x7FFFFFFD
damagefactor "Normal", 1.8
}