actor CrazyWaveBoss : MegaBuster
{
Weapon.AmmoUse 70
Weapon.AmmoUse2 0
Weapon.AmmoGive 0
Weapon.SlotNumber 1
Obituary "%o was dropped by %k's Insanity Buster."
dropitem "CrazyWaveWepCDropped"
Tag "$TAG_PHARAOHBUSTER"
weapon.ammotype "InsanityAmmo"
weapon.ammotype2 "TechAmmo"
States
{
//+[== SPAWN ==]+
Spawn:
TNT1 A 1
loop
Ready:
IPID A 0 ACS_ExecuteAlways(998,0,DYE_PHARAOHMAN)
IPID A 1 A_GunFlash //Checks for a full Tech Bar
Goto Ready1
Ready1: //The Real Ready State
IPID A 0 ACS_ExecuteAlways(512,0,0,0)
IPID A 0 A_GiveInventory("InsanityAmmo",140)
IPID A 1 A_WeaponReady
IPID A 1 A_TakeInventory("SellCharge",999)
IPID A 1 A_TakeInventory("CantSell",999)
loop
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
IPID A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
IPID A 1 A_Raise
Loop

//+[== PHARAOH MACHINEGUN ==]+

Fire: //Ready that Gun
PHMG A 0 ACS_ExecuteAlways(512,0,1,-3) //Lower Movement Capabilities and Change to Firing animation on skin
PHMG A 0 A_JumpIfNoAmmo("NoAmmo")
PHMG A 0 A_JumpIfInventory("MGFiring",1,"Fire1")
PHMG A 0 A_GiveInventory("MGFiring",1)
PHMG AB 1
PHMG A 0 A_PlaySoundEx("pharaohbuster/ready","Weapon")
PHMG CCC 1
goto Fire1
Fire1: //Fire that Gun
PHMG A 0 A_PlaySoundEx("pharaohbuster/mgfire","Weapon")
PHMG A 0 A_GiveInventory("PharaohBulletTeam",1)
PHMG DDEE 1
PHMG A 0 A_Refire
Goto Unready 
Unready: //Stop firing that Gun
PHMG A 0 ACS_ExecuteAlways(512,0,0,0) //Restore Movement Capabilities
PHMG A 0 A_TakeInventory("MGFiring",1)
PHMG CBA 2
Goto Ready1

//+==[INSANITY TECH: SELECT]==+

Altfire:
IPST D 0 A_JumpIfInventory("TechAmmo",100,1) //Check ammo and proceed if full
Goto NoAmmo
IPST A 0 A_PlaySoundEx("insanitytech/swing","Weapon")
IPST DABE 1
IPST E 1 Offset (40,0)
IPST EEEE 4 Offset (32,0)
IPST E 0 A_TakeInventory("TechAmmo",100) //Take ammo so RNG can't be manipulated
IPST E 0 A_Jump(2,"AscensionGet")
IPST E 0 A_Jump(8,"SanctuaryGet")
IPST E 0 A_Jump(64,"SetsWatchGet")
IPST E 0 A_Jump(256,"FireBoltGet", "ExecuteGet")
Goto Altfire

TechSelected:
IPST C 1 Offset (13,20)
IPST C 1 Offset (26,34)
IPST C 1 Offset (39,48)
Goto Ready1

//+[== INSANITY TECH: READY FOR SELECTION ==]+

Flash: //Plays an alert sound when Tech Bar is full
TNT1 A 0 A_JumpIfInventory("TechAmmo",100,"Flash_TechReady")
TNT1 A 1
loop
Flash_TechReady: //Plays the sound
TNT1 A 1 A_PlaySoundEx("insanitytech/ready","SoundSlot7")
Flash_TechReady_Wait:
TNT1 A 1
TNT1 A 0 A_JumpIfInventory("TechAmmo",100,"Flash_TechReady_Wait")
goto Flash

//+[== NO AMMO ==]+

NoAmmo:
PHMG A 0 ACS_ExecuteAlways(512,0,0,0)
PHMG A 0 A_JumpifInventory("MGFiring",1,"Unready")
IPID A 1
Goto Ready1

// +[== INSANITY TECH: SHOW WHAT POWER YOU GOT ==]+ 

FireBoltGet:
IPTC B 0 A_PlaySoundEx("insanitytech/selected","SoundSlot7")
IPTC B 0 A_GiveInventory("FireBoltAmmo",1)
IPTC B 0 A_GiveInventory("FireBoltTech",1)
IPTC XBXBXBXBXBXBXBXB 1
IPTC B 10
Goto TechSelected
ExecuteGet:
IPTC B 0 A_PlaySoundEx("insanitytech/selected","SoundSlot7")
IPTC B 0 A_GiveInventory("ExecuteAmmo1",1)
IPTC B 0 A_GiveInventory("ExecuteTech1",1)
IPTC XEXEXEXEXEXEXEXE 1
IPTC E 10
Goto TechSelected
SetsWatchGet:
IPTC B 0 A_PlaySoundEx("insanitytech/selected","SoundSlot7")
IPTC B 0 A_GiveInventory("SetsWatchAmmo",1)
IPTC B 0 A_GiveInventory("SetsWatchTech",1)
IPTC XSXSXSXSXSXSXSXS 1
IPTC S 10
Goto TechSelected
SanctuaryGet:
IPTC B 0 A_PlaySoundEx("insanitytech/selected","SoundSlot7")
IPTC B 0 A_GiveInventory("SanctuaryAmmo",1)
IPTC B 0 A_GiveInventory("SanctuaryTech",1)
IPTC XNXNXNXNXNXNXNXN 1
IPTC N 10
Goto TechSelected
AscensionGet:
IPTC B 0 A_PlaySoundEx("insanitytech/selected","SoundSlot7")
IPTC B 0 A_GiveInventory("AscensionAmmo",1)
IPTC B 0 A_GiveInventory("AscensionTech",1)
IPTC XAXAXAXAXAXAXAXA 1
ASTC AABB 2
IPTC A 8
Goto TechSelected
}
}
//Projectiles

actor PharaohBullet : FastProjectile //Parodies R4MI: Pharaoh Shotgun
{
PROJECTILE
+MTHRUSPECIES
//missiletype "PharaohBulletTrail"
//missileheight 8
Obituary "%o was rapidly blinded by %k's Insanity Buster."
Translation "192:192=208:208", "198:198=216:216"
Radius 6
Height 7
damagetype "Insanity"
scale 2.5
damage (5)
speed 10
States
{
Spawn:
TNT1 A 0
TNT1 AA 1 ACS_NamedExecuteAlways("PitchMatch",0)
TNT1 A 0 A_ScaleVelocity(22.5)
Go:
TNT1 A 0 A_SpawnItemEx("PharaohBulletTrail",cos(pitch)*-5,0,(sin(pitch)*5),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletTrail",cos(pitch)*-10,0,(sin(pitch)*10),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletTrail",cos(pitch)*-15,0,(sin(pitch)*15),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletTrail",cos(pitch)*-20,0,(sin(pitch)*20),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletTrail",cos(pitch)*-25,0,(sin(pitch)*25),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletTrail",cos(pitch)*-30,0,(sin(pitch)*30),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletTrail",cos(pitch)*-35,0,(sin(pitch)*35),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletTrail",cos(pitch)*-40,0,(sin(pitch)*40),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletTrail",cos(pitch)*-45,0,(sin(pitch)*45),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletTrail",cos(pitch)*-50,0,(sin(pitch)*50),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletTrail",cos(pitch)*-55,0,(sin(pitch)*55),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletTrail",cos(pitch)*-60,0,(sin(pitch)*60),momX,MomY,MomZ,0,SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
PMAG A 1
loop
Death:
TNT1 A 0 A_Explode(1,20,0,0,20) //Because I cannot aim
TNT1 A 0 A_SpawnItemEx("PharaohBulletFX",0,0,0,random(-10,10),random(-10,10),random(5,10),random(0,360),SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletFX",0,0,0,random(-10,10),random(-10,10),random(5,10),random(0,360),SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletFX",0,0,0,random(-10,10),random(-10,10),random(5,10),random(0,360),SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletFX",0,0,0,random(-10,10),random(-10,10),random(5,10),random(0,360),SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PharaohBulletFX",0,0,0,random(-10,10),random(-10,10),random(5,10),random(0,360),SXF_TRANSFERTRANSLATION)
stop
}
}

Actor PharaohBulletL : PharaohBullet{Translation "192:192=196:196", "198:198=74:74" }
Actor PharaohBulletW : PharaohBullet{Translation "192:192=170:170", "198:198=41:41" }
Actor PharaohBulletC : PharaohBullet{Translation "192:192=138:138", "198:198=128:128" }
Actor PharaohBulletK : PharaohBullet{Translation "192:192=214:214", "198:198=232:232" }

actor PharaohBulletFX : BasicClientSide //Flashy aesthetic effects
{
Translation "192:192=216:216", "198:198=208:208"
Scale 1.5
Gravity 10
Renderstyle Normal
States
{
Spawn:
CRBM A 0
PMGX A 1 BRIGHT ThrustThingz(0,random(-15,15),random(-15,15),0)
PMGX BABABABABA 1 A_Fadeout
stop
}
}

// +[== SELL FX FOR SELLING TECHS ==]+ 

actor SellFX_Helper : BasicClientSide
{
reactiontime 6
States
{
Spawn:
TNT1 A 0
TNT1 AAAAAAA 0 A_SpawnItemEx("SellFX",random(-25,25),random(-25,25),random(-25,25))
TNT1 A 1 A_Countdown
loop
}
}

actor SellFX : BasicClientSide
{
Translation "198:198=112:112"
Renderstyle Translucent
scale 1.5
States
{
Spawn:
TNT1 A 0
SELL A 1 ThrustThingZ(0,10,0,0)
Death:
SELL A 1 A_Fadeout(0.75)
loop
}
}

//Inventory and Flags

actor InsanityAmmo : Ammo
{
inventory.amount 1960
inventory.maxamount 1960
}

actor TechAmmo : Ammo
{
inventory.amount 100
inventory.maxamount 100
}

actor SellCharge : Ammo
{
inventory.amount 100
inventory.maxamount 100
}

actor MGFiring : Inventory{}

actor CantSell : Inventory{}

actor PharaohBulletTeam : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("LightTeamFlag",1,"LightFire")
TNT1 A 0 A_JumpIfInventory("WilyTeamFlag",1,"WilyFire")
TNT1 A 0 A_JumpIfInventory("CossackTeamFlag",1,"CossackFire")
TNT1 A 0 A_JumpIfInventory("KingTeamFlag",1,"KingFire")
goto Default
Default:
CODB A 0 A_FireCustomMissile("PharaohBullet",frandom(-1.8,0.5),1,-10,0,0,frandom(1.8,0.5))
stop
LightFire:
CODB A 0 A_FireCustomMissile("PharaohBulletL",frandom(-1.8,0.5),1,-10,0,0,frandom(1.8,0.5))
stop
WilyFire:
CODB A 0 A_FireCustomMissile("PharaohBulletW",frandom(-1.8,0.5),1,-10,0,0,frandom(1.8,0.5))
stop
CossackFire:
CODB A 0 A_FireCustomMissile("PharaohBulletC",frandom(-1.8,0.5),1,-10,0,0,frandom(1.8,0.5))
stop
KingFire:
CODB A 0 A_FireCustomMissile("PharaohBulletK",frandom(-1.8,0.5),1,-10,0,0,frandom(1.8,0.5))
stop
}
}