actor StormLightningBoss : BaseCSCCWep
{
Inventory.Pickupmessage ""
Obituary "$OB_CHAINLIGHTNING"
inventory.pickupsound "weapon/weaponup"
Weapon.SlotNumber 1
Tag "Spark Marker"
inventory.icon "TBOLSI"
Weapon.AmmoType "StormCAmmo"
Weapon.AmmoType2 "StormCFlightLimit"
Weapon.AmmoUse 20
Weapon.AmmoUse2 80
+WEAPON.AMMO_OPTIONAL
DropItem "RainParadeWepCDropped"
Scale 2.0
States
{
Spawn:
WEA3 R 1
loop
Ready:
TBOL S 0 ACS_ExecuteAlways(998,0,56)
TBOL S 0 A_GunFlash
TBOL S 0 A_GiveInventory("StormCAmmo",1)
TBOL S 1 A_WeaponReady
Goto Ready+2
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
TBOL S 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
TBOL S 1 A_Raise
Loop
Fire:
TBOL S 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
TBOL S 0 A_GiveInventory("StormCMainTeamcolor")
TBOL S 0 A_GiveInventory("SparkMarkCancelFlag")
TBOL TU 5 A_GiveInventory("StormCAmmo",10)
TBOL S 10 A_TakeInventory("SparkMarkCancelFlag")
TBOL S 0 A_Refire
Goto Ready+2
AltFire:
TBOL S 0 A_JumpIfInventory("StormCAmmo",80,1)
goto NoAmmo
TBOL S 0 A_PlaySoundEx("weapon/thunderbolt","Weapon")
TBOL S 0 A_TakeInventory("StormCAmmo",80)
TBOL S 0 A_GiveInventory("StormCAltTeamcolor")
TBOL TU 5
TBOL S 20
TBOL A 0 A_TakeInventory("CutterFlag",999)
TBOL S 0 A_Refire
Goto Ready+2
Flash:
TNT1 A 0 A_TakeInventory("RainParadeCooldown",1)
TNT1 A 0 A_JumpIfInventory("IsDead",1,"NoFlash")
TNT1 A 0 A_JumpIfInventory("StormCFlight",1,"Flight")
goto NoFlight
Flight:
TNT1 A 0 A_JumpIf(z-floorz>=160, "NoFlight")
TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(107,0)==1, "MaybeFlight")
YesFlight:
TNT1 A 1 SetPlayerProperty(0, 1, 3)
TNT1 A 0 A_JumpIfInventory("StormCFlightLimit",100,"Flash")
TNT1 A 0 A_JumpIf(z-floorz==0, "Recharge")
goto Flash
MaybeFlight:
TNT1 A 0 A_TakeInventory("StormCFlightLimit",5)
TNT1 A 0 A_JumpIfInventory("StormCFlightLimit",1,"YesFlight")
goto NoFlight
NoFlight:
TNT1 A 1 SetPlayerProperty(0, 0, 3)
TNT1 A 0 A_JumpIfInventory("StormCFlightLimit",100,"Flash")
TNT1 A 0 A_JumpIf(z-floorz==0, "Recharge")
Goto Flash
Recharge:
TNT1 A 0 A_GiveInventory("StormCFlightLimit",1)
Goto Flash
NoFlash:
TNT1 A 0 SetPlayerProperty(0,0,3)
stop
NoAmmo:
TBOL S 1 ACS_Execute(979,0)
Goto Ready+2
}
}

actor StormCMainTeamcolor : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("LightTeamFlag",1,"LightFire")
TNT1 A 0 A_JumpIfInventory("WilyTeamFlag",1,"WilyFire")
TNT1 A 0 A_JumpIfInventory("CossackTeamFlag",1,"CossackFire")
TNT1 A 0 A_JumpIfInventory("KingTeamFlag",1,"KingFire")
TNT1 A 0 A_Jump(256,"Default")
goto Default
Default:
TNT1 A 0 A_FireCustomMissile("SparkMarkerShot",0,0,8,0)
TNT1 A 0 A_FireCustomMissile("SparkMarkerContainer",0,0,8,0)
stop
LightFire:
TNT1 A 0 A_FireCustomMissile("SparkMarkerShotL",0,0,8,0)
TNT1 A 0 A_FireCustomMissile("SparkMarkerContainerL",0,0,8,0)
stop
WilyFire:
TNT1 A 0 A_FireCustomMissile("SparkMarkerShotW",0,0,8,0)
TNT1 A 0 A_FireCustomMissile("SparkMarkerContainerW",0,0,8,0)
stop
CossackFire:
TNT1 A 0 A_FireCustomMissile("SparkMarkerShotC",0,0,8,0)
TNT1 A 0 A_FireCustomMissile("SparkMarkerContainerC",0,0,8,0)
stop
KingFire:
TNT1 A 0 A_FireCustomMissile("SparkMarkerShotK",0,0,8,0)
TNT1 A 0 A_FireCustomMissile("SparkMarkerContainerK",0,0,8,0)
stop
}
}

actor StormCAltTeamcolor : StormCMainTeamcolor
{
States
{
Default:
TNT1 A 0 A_FireCustomMissile("ChainLightningSpawner",0,0,8,0)
TNT1 A 0 A_FireCustomMissile("ChainLightningFX",0,0,8,0)
stop
LightFire:
TNT1 A 0 A_FireCustomMissile("ChainLightningSpawnerL",0,0,8,0)
TNT1 A 0 A_FireCustomMissile("ChainLightningFXL",0,0,8,0)
stop
WilyFire:
TNT1 A 0 A_FireCustomMissile("ChainLightningSpawnerW",0,0,8,0)
TNT1 A 0 A_FireCustomMissile("ChainLightningFXW",0,0,8,0)
stop
CossackFire:
TNT1 A 0 A_FireCustomMissile("ChainLightningSpawnerC",0,0,8,0)
TNT1 A 0 A_FireCustomMissile("ChainLightningFXC",0,0,8,0)
stop
KingFire:
TNT1 A 0 A_FireCustomMissile("ChainLightningSpawnerK",0,0,8,0)
TNT1 A 0 A_FireCustomMissile("ChainLightningFX",0,0,8,0)
stop
}
}

actor StormCAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 80
}

actor StormCFlight : Once{}

actor StormCFlightLimit : Ammo
{
inventory.amount 1
inventory.maxamount 100
+INVENTORY.IGNORESKILL
}

actor StormCFlightItem : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
inventory.pickupmessage ""
inventory.icon "TORNADBI"
Inventory.PickupSound "item/1up"
scale 2.0
+COUNTITEM
+INVBAR
states
{
Spawn:
TBLO A 1
loop
Use:
WEA3 N 0 A_JumpIfInventory("StormCFlight",1,"Remove")
WEA3 N 0 A_GiveInventory("StormCFlight",1)
fail
Remove:
TNT1 A 0 A_TakeInventory("StormCFlight",1)
fail
}
}

actor SparkMarkCancelFlag : Once{}

actor SparkMarkerShot
{
PROJECTILE
damagetype "SparkMarker"
Obituary "$OB_SPARKMARKER"
Translation "192:192=228:228" 
scale 1.5
Speed 30
Radius 25
Height 16
Damage (6)
States
{
Spawn:
TBOL NMNMNMNMNMNM 1
TBOL N 0 A_GiveInventory("KnightFlag",1)
Flying:
TBOL NM 1 A_JumpIfInTargetInventory("SparkMarkCancelFlag",1,"Reduce")
loop
Reduce:
TBOL N 0 A_GiveInventory("CutterFlag",1)
goto Spawn
Death:
TNT1 A 0 A_PlaySoundEx("weapon/thunderbolt","Weapon")
TNT1 AAAAA 0 A_SpawnItemEx("ThunderTrail",random(-48,48),random(-48,48),random(-48,48),0,0,0,0,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Explode(8,96,0)
TNT1 A 0 A_JumpIfInventory("CutterFlag",5,"GGILose")
TNT1 A 0 A_JumpIfInventory("CutterFlag",4,"OneLeft")
TNT1 A 0 A_JumpIfInventory("CutterFlag",3,"TwoLeft")
TNT1 A 0 A_JumpIfInventory("CutterFlag",2,"ThreeLeft")
TNT1 A 0 A_JumpIfInventory("CutterFlag",1,"FourLeft")
TNT1 A 10 A_SpawnItemEx("SparkMark",0,0,0,0,0,-32,0,SXF_TRANSFERTRANSLATION)
stop
FourLeft:
TNT1 A 10 A_SpawnItemEx("SparkMark4",0,0,0,0,0,-32,0,SXF_TRANSFERTRANSLATION)
stop
ThreeLeft:
TNT1 A 10 A_SpawnItemEx("SparkMark3",0,0,0,0,0,-32,0,SXF_TRANSFERTRANSLATION)
stop
TwoLeft:
TNT1 A 10 A_SpawnItemEx("SparkMark2",0,0,0,0,0,-32,0,SXF_TRANSFERTRANSLATION)
stop
OneLeft:
TNT1 A 10 A_SpawnItemEx("SparkMark1",0,0,0,0,0,-32,0,SXF_TRANSFERTRANSLATION)
stop
GGILose:
TNT1 A 10
stop
}
}

actor SparkMarkerShotL : SparkMarkerShot {Translation "228:228=203:203", "73:73=74:74", "212:212=201:201", "210:210=204:204", "229:229=74:74", "192:192=205:205"}
actor SparkMarkerShotW : SparkMarkerShot {Translation "228:228=172:172", "73:73=41:41", "212:212=172:172", "210:210=171:171", "229:229=41:41", "192:192=171:171"}
actor SparkMarkerShotC : SparkMarkerShot {Translation "228:228=108:108", "73:73=128:128", "212:212=104:104", "210:210=108:108", "229:229=128:128", "192:192=104:104"}
actor SparkMarkerShotK : SparkMarkerShot {Translation "228:228=228:228", "73:73=231:231", "212:212=212:212", "229:229=230:230", "210:210=228:228", "192:192=229:229"}

actor SparkMarkerContainer : SparkMarkerShot{
Damage (0)
Alpha 0.5
Scale 2.0
RenderStyle "Translucent"
States
{
Spawn:
CRYE A 0
CRYE A -1
loop
Death:
MMFX BCDE 2
stop
}}

actor SparkMarkerContainerL : SparkMarkerContainer {Translation "193:193=204:204"}
actor SparkMarkerContainerW : SparkMarkerContainer {Translation "193:193=171:174"}
actor SparkMarkerContainerC : SparkMarkerContainer {Translation "193:193=108:108"}
actor SparkMarkerContainerK : SparkMarkerContainer {Translation "193:193=228:228"}

actor SparkMark
{
+MISSILE
+NODAMAGETHRUST
+NOBLOOD
+DONTSPLASH
+NOEXPLODEFLOOR
+NOTARGETSWITCH
Radius 25
Height 16
Scale 2.0
ReactionTime 140
painchance 256
damagefactor "ChainLightning", 1.0
damagefactor "Normal", 0.0
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_GiveInventory("CutterFlag",5)
Start:
TBOL NM 1
TBOL N 0 A_SpawnItemEx("ThunderTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TBOL N 0 A_CheckFloor("Marked")
TBOL N 0 A_JumpIfInTargetInventory("SparkMarkCancelFlag",1,"RemoveF")
loop
Marked:
TNT1 A 0 A_Stop
TNT1 A 0 ThrustThingZ(0,0,0,1)
TNT1 A 0 A_ChangeFlag("SHOOTABLE",1)
TNT1 A 0 A_JumpIfInTargetInventory("SparkMarkCancelFlag",1,"WaitASec")
ExistLoop:
TBOL OQ 3
TBOL P 0 A_JumpIfInTargetInventory("SparkMarkCancelFlag",1,"Remove1")
TBOL P 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
ExistLoop2:
TBOL PR 3
TBOL O 0 A_JumpIfInTargetInventory("SparkMarkCancelFlag",1,"Remove2")
TBOL O 3 A_JumpIfInTargetInventory("StormLightningBoss",1,"ExistLoop")
goto Death
Remove1:
TBOL P 3 A_TakeInventory("CutterFlag",1)
TBOL R 3
TBOL O 3 A_JumpIfInventory("CutterFlag",1,"ExistLoop")
goto Death
Remove2:
TBOL O 3 A_TakeInventory("CutterFlag",1)
TBOL Q 3
TBOL P 3 A_JumpIfInventory("CutterFlag",1,"ExistLoop2")
goto Death
RemoveF:
TBOL N 0 A_TakeInventory("CutterFlag",1)
TBOL N 1 A_JumpIfInventory("CutterFlag",1,1)
goto Death
TBOL N 1 A_SpawnItemEx("ThunderTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TBOL M 1 A_CheckFloor("Marked")
TBOL N 1 A_SpawnItemEx("ThunderTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TBOL M 1 A_CheckFloor("Marked")
TBOL N 1 A_SpawnItemEx("ThunderTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TBOL M 1 A_CheckFloor("Marked")
TBOL N 1 A_SpawnItemEx("ThunderTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TBOL M 1 A_CheckFloor("Marked")
TBOL N 1 A_SpawnItemEx("ThunderTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TBOL M 1 A_CheckFloor("Marked")
goto Start
WaitASec:
TBOL OQPR 3 
goto ExistLoop
Pain.ChainLightning:
TNT1 A 0 A_SpawnItemEx("SparkMarkLightningMaker",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
Death:
MMFX CDE 2
stop
}}

actor SparkMark4 : SparkMark
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("CutterFlag",4)
goto Start
}
}

actor SparkMark3 : SparkMark
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("CutterFlag",3)
goto Start
}
}

actor SparkMark2 : SparkMark
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("CutterFlag",2)
goto Start
}
}

actor SparkMark1 : SparkMark
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("CutterFlag",1)
goto Start
}
}

actor SparkMarkLightningMaker
{
PROJECTILE
+NOCLIP
+NOINTERACTION
Obituary "$OB_CHAINLIGHTNING"
Radius 1
Height 1
States
{
Spawn:
TNT1 A 0
TNT1 AAAAA 1 A_SpawnItemEx("ThunderTrail",0,0,0,random(-3,3),random(-3,3),random(0,4),0,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Explode(12,128,0)
TNT1 A 5 A_SpawnItemEx("StormLightningBolt",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

actor ChainLightningSpawner : FastProjectile
{
+RIPPER
+DONTBLAST
+THRUGHOST
+SKYEXPLODE
+DONTREFLECT
reactiontime 5
damage (0)
radius 8
height 8
speed 800
Translation "192:192=228:228" 
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_SpawnItemEx("StormLightningBolt",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TNT1 A 3
stop
}
}

actor ChainLightningSpawnerL : ChainLightningSpawner{Translation "210:210=204:204", "229:229=74:74", "192:192=205:205"}
actor ChainLightningSpawnerW : ChainLightningSpawner{Translation "210:210=171:171", "229:229=41:41", "192:192=171:171"}
actor ChainLightningSpawnerC : ChainLightningSpawner{Translation "210:210=108:108", "229:229=128:128", "192:192=104:104"}
actor ChainLightningSpawnerK : ChainLightningSpawner{Translation "229:229=230:230", "210:210=228:228", "192:192=229:229"}

actor StormLightningBolt : LightningBolt
{
Damage (0)
DamageType "ChainLightning"
Obituary "$OB_CHAINLIGHTNING"
states
{
Spawn:
LIBO G 0
LIBO G 1 A_SpawnItemEx("StormLightningExtend",0,0,355,0,0,0,0,SXF_TRANSFERTRANSLATION)
LIBO G 0 A_PlaySoundEx("weapon/lightningbolt","Weapon")
LIBO G 0 A_Explode(15, 128, 0)
TNT1 A 0 A_SpawnItemEx("StormLightningShockwave",0,0,0,18,0,0,0,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("StormLightningShockwave",0,0,0,18,0,0,45,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("StormLightningShockwave",0,0,0,18,0,0,90,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("StormLightningShockwave",0,0,0,18,0,0,135,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("StormLightningShockwave",0,0,0,18,0,0,180,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("StormLightningShockwave",0,0,0,18,0,0,225,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("StormLightningShockwave",0,0,0,18,0,0,270,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("StormLightningShockwave",0,0,0,18,0,0,315,SXF_TRANSFERTRANSLATION)
LIBO HIJK 1
Stop
}
}

Actor StormLightningExtend : LightningExtend
{
States
{
Spawn:
LIBO L 0 
LIBO L 0 A_JumpIf(ceilingz-z <= 32, "Death")
LIBO L 0 A_SpawnItemEx("StormLightningExtend",0,0,160,0,0,0,0,SXF_TRANSFERTRANSLATION)
LIBO LLMN 1
stop
}
}

actor StormLightningShockwave : DuoShockwavePart
{
PROJECTILE
Scale 2.0
+FLOORHUGGER
+CLIENTSIDEONLY
+RIPPER
Damage (0)
Speed 30
Radius 32
Height 32
States
{
Spawn:
DUOR T 1 A_FadeOut(0.1)
loop
}
}

actor ChainLightningFX 
{
PROJECTILE
+DONTREFLECT
Translation "225:225=229:229"
Speed 2
Damage(0)
Radius 8
Height 8
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveToTarget("CutterFlag",1)
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",50,"PleaseStop")
TNT1 A 0 A_SpawnItemEx("ChainLightningFX",momx*16,-momy*16,momz*16,momx,momy,momz,0,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
SPAS ABABABABABAB 2
stop
PleaseStop:
TNT1 A 0
stop
}
}

actor ChainLightningFXL : ChainLightningFX {Translation "225:225=204:204"}
actor ChainLightningFXW : ChainLightningFX {Translation "225:225=172:172"}
actor ChainLightningFXC : ChainLightningFX {Translation "225:225=107:107"}