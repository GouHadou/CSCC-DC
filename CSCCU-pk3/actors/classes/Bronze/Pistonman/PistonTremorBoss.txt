actor PistonTremorBoss : MegaBuster 
{
weapon.ammotype "PistonTremorAmmo"
Inventory.Pickupmessage "Power up! Car Engine-wait."
dropitem "PistonTremorWepCDropped"
Obituary "%o couldn't comprehend why exactly he got killed by running straight into %k's fist."
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
Scale 2.0
States
{
Spawn:
MCAR X 1
loop
Ready:
TNT1 A 3
TNT1 A 0 A_SpawnItemEX("PistonAmmoWatcher")
TNT1 A 0 A_JumpIfInventory("LightTeamFlag",1,"LightColor")
TNT1 A 0 A_JumpIfInventory("WilyTeamFlag",1,"WilyColor")
TNT1 A 0 A_JumpIfInventory("CossackTeamFlag",1,"CossackColor")
TNT1 A 0 A_JumpIfInventory("KingTeamFlag",1,"KingColor")
goto DefaultColor
DefaultColor:
PST1 A 0
goto Ready2
LightColor:
PST2 A 0
goto Ready2
WilyColor:
PST3 A 0
goto Ready2
CossackColor:
PST4 A 0
goto Ready2
KingColor:
PST5 A 0
goto Ready2
Ready2:
"####" E 0 ACS_ExecuteAlways(998,0,DYE_PISTONMAN)
"####" E 0 A_GunFlash
"####" E 0 A_TakeInventory("CutterFlag",999)
"####" E 1 A_WeaponReady
Goto Ready2+2
Deselect:
"####" A 0 A_SelectWeapon("PistonTremorBoss")
"####" AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" E 1 A_Lower
Loop
Select:
"####" AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" E 1 A_Raise
Loop
Fire:
"####" E 0 A_JumpIfInventory("MainBuffer2",1,"NoAmmo")
"####" E 0 A_JumpIfInventory("MainBuffer",1,"NoAmmo")
"####" E 0 A_JumpIfInventory("PistonAltCheck",1,"NoAmmo")
"####" E 0 A_GiveInventory("PistonPunchItem",1)
"####" E 0 A_PlaySoundEx("weapon/magnetmissile","Weapon")
"####" FG 2
"####" G 0 A_JumpIfInventory("CutterFlag",1,"FireCancel")
goto ActualFire
FireCancel:
"####" E 0 A_GiveInventory("MainBuffer",1)
goto Ready2+2
ActualFire:
"####" D 0 A_GiveInventory("PistonFistItem",1)
ActualFireWait:
"####" H 0 A_GiveInventory("MainFistTimer",1)
"####" H 0 A_JumpIfInventory("MainFistReturning",1,1)
"####" H 0 A_JumpIfInventory("MainFistTimer",210,"MainFistBackFail")
"####" H 0 A_JumpIfInventory("MainFistClear",1,"MainFistBack")
"####" H 1 A_WeaponReady(WRF_NOPRIMARY)
goto ActualFireWait
MainFistBack:
"####" E 0 A_PlaySoundEx("weapon/adapterreturn", "Voice")
goto MainFistBack2
MainFistBackFail:
"####" E 0 A_PlaySoundEx("cscc/pistonfail", "Voice")
goto MainFistBack2
MainFistBack2:
"####" E 0 A_GiveInventory("MainBuffer2",1)
"####" E 0 A_TakeInventory("MainFistTimer",999)
"####" E 0 A_TakeInventory("MainFistReturning",999)
goto Ready2+2
Altfire:
"####" E 0 A_JumpIfInventory("AltBuffer2",1,"NoAmmo")
"####" A 0 A_JumpIfInventory("AltBuffer",1,"NoAmmo")
"####" A 0 A_JumpIfInventory("AltBufferAmmo",1,"NoAmmo")
"####" A 0 A_JumpIfInventory("DontAltfire",1,"NoAmmo")
"####" A 0 A_GiveInventory("PistonAltCheck",1)
"####" A 0 A_JumpIfInventory("MainFistTimer",1,"ActualFireWait")
goto Ready2+2
Flash:
TNT1 A 0 A_JumpIfInventory("LightTeamFlag",1,"LightColorF")
TNT1 A 0 A_JumpIfInventory("WilyTeamFlag",1,"WilyColorF")
TNT1 A 0 A_JumpIfInventory("CossackTeamFlag",1,"CossackColorF")
TNT1 A 0 A_JumpIfInventory("KingTeamFlag",1,"KingColorF")
PST1 A 0
goto ColoredFlash
LightColorF:
PST2 A 0
goto ColoredFlash
WilyColorF:
PST3 A 0
goto ColoredFlash
CossackColorF:
PST4 A 0
goto ColoredFlash
KingColorF:
PST5 A 0
goto ColoredFlash
ColoredFlash:
"####" A 0 A_JumpIfInventory("IsDead",1,"UnFlash")
"####" A 0 A_TakeInventory("DontAltfire",999)
"####" A 0 A_TakeInventory("PistonAltCancelCheck",999)
"####" A 1 A_JumpIfInventory("PistonAltCheck",1,"Flash2")
goto ColoredFlash
Flash2:
"####" D 0 A_GiveInventory("DontAltfire",1)
"####" D 0 A_GiveInventory("PistonPunch2Item",1)
"####" D 0 A_PlaySoundEx("weapon/magnetmissile","Weapon")
"####" BC 2
"####" E 0 A_TakeInventory("PistonAltCheck",999)
"####" G 0 A_JumpIfInventory("PistonAltCancelCheck",1,"FlashCancel")
goto ActualFlash
FlashCancel:
"####" E 0 A_GiveInventory("AltBuffer",1)
goto ColoredFlash
ActualFlash:
"####" D 0 A_GiveInventory("PistonFist2Item",1)
ActualFlashWait:
"####" D 1 A_GiveInventory("AltFistTimer",1)
"####" H 0 A_JumpIfInventory("AltFistReturning",1,1)
"####" H 0 A_JumpIfInventory("AltFistTimer",210,"AltFistBackFail")
"####" H 0 A_JumpIfInventory("AltFistClear",1,"AltFistBack")
goto ActualFlashWait
AltFistBack:
"####" E 0 A_PlaySoundEx("weapon/adapterreturn", "Voice")
goto AltFistBack2
AltFistBackFail:
"####" E 0 A_PlaySoundEx("cscc/pistonfail", "Voice")
goto AltFistBack2
AltFistBack2:
"####" E 0 A_GiveInventory("AltBuffer2",1)
"####" E 0 A_TakeInventory("AltFistTimer",999)
"####" E 0 A_TakeInventory("AltFistReturning",999)
goto ColoredFlash
UnFlash:
"####" A 0
stop
NoAmmo:
"####" A 0 A_JumpIfInventory("MainFistTimer",1,"ActualFireWait")
"####" E 1 
Goto Ready2+2
}
}

actor PistonPunch
{
translation "202:202=87:87"
PROJECTILE
damagetype "PistonPunch"
Obituary "%o felt the epicenter of %k's Piston Punch"
+DONTBLAST
+MTHRUSPECIES
damage (13)
Radius 20
Height 20
speed 30
scale 2.5
States
{
Spawn:
PMFX R 0
PMFX R 1
goto FakeDeath
FakeDeath:
PMFX R 0 A_Explode(13,40,0)
goto XDeath2
Death:
XDeath:
PMFX R 0 A_JumpIfInTargetInventory("PistonTremorAmmo",100,"GiveStuff")
goto XDeath2
GiveStuff:
PMFX R 0 A_GiveToTarget("CutterFlag",1)
PMFX R 0 A_GiveToTarget("PistonBonk",1)
goto XDeath2
XDeath2:
PMFX R 0 A_Stop
PMFX R 2
MMFX B 2
PMFX RS 2
stop
}
}

actor PistonPunchL : PistonPunch {translation "202:202=205:205"}
actor PistonPunchW : PistonPunch {translation "202:202=171:171"}
actor PistonPunchC : PistonPunch {translation "202:202=104:104"}
actor PistonPunchK : PistonPunch {translation "202:202=229:229"}

actor PistonPunch2 : PistonPunch
{
radius 28
height 28
scale 3.5
States
{
FakeDeath:
PMFX R 0 A_Explode(13,56,0)
goto XDeath2
Death:
XDeath:
PMFX R 0 A_JumpIfInTargetInventory("PistonTremorAmmo",200,"GiveStuff")
goto XDeath2
GiveStuff:
PMFX R 0 A_GiveToTarget("PistonAltCancelCheck",1)
PMFX R 0 A_GiveToTarget("PistonBonk2",1)
goto XDeath2
}
}

actor PistonPunch2L : PistonPunch2 {translation "202:202=205:205"}
actor PistonPunch2W : PistonPunch2 {translation "202:202=171:171"}
actor PistonPunch2C : PistonPunch2 {translation "202:202=104:104"}
actor PistonPunch2K : PistonPunch2 {translation "202:202=229:229"}


actor PistonBonk : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_TakeInventory("PistonTremorAmmo",100)
TNT1 A 0 A_ChangeVelocity(-Cos(pitch)*30+velx, 0, sin(pitch)*30,CVF_RELATIVE|CVF_REPLACE)
stop
}
}

actor PistonBonk2 : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_TakeInventory("PistonTremorAmmo",200)
TNT1 A 0 A_ChangeVelocity(-Cos(pitch)*40+velx, 0, sin(pitch)*40,CVF_RELATIVE|CVF_REPLACE)
stop
}
}

actor PistonFist
{
PROJECTILE
Speed 50
Damage (15)
translation "192:192=87:87", "198:198=95:95"
damagetype "PistonPunchStun"
Obituary "%o couldn't stand their ground against %k's Piston Tremor."
Radius 7
Height 7
+NOTARGETSWITCH
+MTHRUSPECIES
+DONTREFLECT
+SEEKERMISSILE
-NOGRAVITY
scale 2.5
States
{
Spawn:
PSTP A 0
PSTP A 1 A_SpawnItemEx("PistonCoilFX",-8,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
loop
Death:
PSTP A 0 A_ChangeFlag("NOGRAVITY",1)
PSTP A 0 A_Stop
TNT1 A 0 A_Explode(7,96,0)
TNT1 A 0 A_CheckCeiling("Ceiling")
PSTP A 0 A_SpawnItemEX("PistonExplode",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
PSTP A 0 A_GiveToTarget("MainFistReturning",1)
PSTP A 0 A_RearrangePointers(AAPTR_DEFAULT,AAPTR_DEFAULT,AAPTR_TARGET)
PSTP A 0 A_ChangeFlag("NOINTERACTION",1)
goto Return
Ceiling:
PSTP A 0 A_SpawnItemEX("PistonExplodeC",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
goto Death+4
Return:
PSTP A 0 A_JumpIfInventory("CutterFlag",210,"GiveReturn")
PSTP A 0 A_GiveInventory("CutterFlag",1)
PSTP A 0 A_JumpIfCloser(60,"GiveReturn")
PSTP A 0 A_FaceTarget
PSTP C 1 A_SeekerMissile(90,90,SMF_PRECISE)
loop
GiveReturn:
PSTP A 0 A_GiveToTarget("MainFistClear")
stop
}
}

actor PistonFistL : PistonFist {translation "192:192=205:205", "198:198=74:74"}
actor PistonFistW : PistonFist {translation "192:192=171:171", "198:198=41:41"}
actor PistonFistC : PistonFist {translation "192:192=104:104", "198:198=128:128"}
actor PistonFistK : PistonFist {translation "192:192=229:229", "198:198=232:232"}

actor PistonFist2 : PistonFist
{
scale 3.5
damagetype "PistonPunchStun2"
damage (20)
speed 30
States
{
Spawn:
PSTP A 0
PSTP A 2 A_SpawnItemEx("PistonCoilFX2",-8,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
loop
Death:
PSTP A 0 A_ChangeFlag("NOGRAVITY",1)
PSTP A 0 A_Stop
TNT1 A 0 A_Explode(13,128,0)
TNT1 A 0 A_CheckCeiling("Ceiling")
PSTP A 0 A_SpawnItemEX("PistonExplode2",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
PSTP A 0 A_GiveToTarget("AltFistReturning",1)
PSTP A 0 A_RearrangePointers(AAPTR_DEFAULT,AAPTR_DEFAULT,AAPTR_TARGET)
PSTP A 0 A_ChangeFlag("NOINTERACTION",1)
PSTP A 2
PSTP AA 4 A_SpawnItemEx("PistonCoilFX3",-18,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
goto Return
Ceiling:
PSTP A 0 A_SpawnItemEX("PistonExplode2C",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
goto Death+4
GiveReturn:
PSTP A 0 A_GiveToTarget("AltFistClear")
stop
}
}

actor PistonFist2L : PistonFist2 {translation "192:192=205:205", "198:198=74:74"}
actor PistonFist2W : PistonFist2 {translation "192:192=171:171", "198:198=41:41"}
actor PistonFist2C : PistonFist2 {translation "192:192=104:104", "198:198=128:128"}
actor PistonFist2K : PistonFist2 {translation "192:192=229:229", "198:198=232:232"}

actor PistonCoilFX
{
Translation "202:202=56:56"
PROJECTILE
+THRUACTORS
+SKYEXPLODE
+DONTBLAST
+NOGRAVITY
+NOINTERACTION
+NOTARGETSWITCH
+SEEKERMISSILE
damage (0)
Radius 2
Height 2
speed 50
scale 2.5
reactiontime 210
States
{
Spawn:
TNT1 A 0
PSTP B 0 A_RearrangePointers(AAPTR_DEFAULT,AAPTR_DEFAULT,AAPTR_TARGET)
PSTP B 0 A_FaceTarget
Return:
PSTP B 0 A_Countdown
PSTP B 0 A_JumpIfCloser(60,"Death")
PSTP BB 0 A_SeekerMissile(90,90,SMF_PRECISE)
PSTP B 1 A_JumpIfInTargetInventory("IsDead",1,"Death")
loop
Death:
TNT1 A 1
stop
}
}

actor PistonCoilFX2 : PistonCoilFX
{
scale 3.5
speed 30
}

actor PistonCoilFX3 : PistonCoilFX2
{
States
{
Spawn:
PSTP B 0 
PSTP B 0 A_RearrangePointers(AAPTR_DEFAULT,AAPTR_DEFAULT,AAPTR_TARGET)
PSTP B 0 A_FaceTarget
Return:
PSTP B 0 A_Countdown
PSTP B 0 A_JumpIfCloser(60,"Death")
PSTP BB 0 A_SeekerMissile(90,90,SMF_PRECISE)
PSTP B 1 A_JumpIfInTargetInventory("IsDead",1,"Death")
loop
Death:
TNT1 A 1
stop
}
}

actor PistonTremorFX
{
PROJECTILE
damage 0
+RIPPER
-NOGRAVITY
+CLIENTSIDEONLY
-SOLID
scale 2.5
//+NOINTERACTION
Height 2
Radius 2
States
{
Spawn:
PSTS C 2
TNT1 A 2
loop
}
}

actor PistonTremorFX2
{
PROJECTILE
damage 0
+RIPPER
-NOGRAVITY
+CLIENTSIDEONLY
-SOLID
scale 2.5
//+NOINTERACTION
Height 4
Radius 4
States
{
Spawn:
PSTS D 2
TNT1 A 2
loop
}
}

actor PistonStunFX
{
-SOLID
+NOINTERACTION
height 1
radius 1
scale 2.5
states
{
Spawn:
PSTS AB 4
stop
}
}

actor PistonStunFX2 : PistonStunFX
{
states
{
Spawn:
PSTS ABAB 4
stop
}
}

actor PistonExplode
{
Speed 0
Damage 0
Scale 2.5
Height 0
Radius 0
+NOGRAVITY
-SOLID
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("cscc/pistonhit", "Weapon")
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, 8, 0, 0, random(4,7), 0,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, 8, 0, 0, random(4,7), 45,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, 8, 0, 0, random(4,7), 90,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, 8, 0, 0, random(4,7), -90,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, 8, 0, 0, random(4,7), -45,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, 8, 0, 0, random(4,7), 135,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, 8, 0, 0, random(4,7), -135,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, 8, 0, 0, random(4,7), 180,SXF_TRANSFERTRANSLATION)
TNT1 AAAAAA 0 A_SpawnItemEX("PistonTremorFX",random(0,96),0,0,random(1,5),0,random(5,15),random(0,359),SXF_TRANSFERTRANSLATION)
stop
}
}

actor PistonExplode2 : PistonExplode
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("cscc/pistonhit", "Weapon")
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(7,11), 0,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(7,11), 45,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(7,11), 90,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(7,11), -90,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(7,11), -45,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(7,11), 135,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(7,11), -135,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(7,11), 180,SXF_TRANSFERTRANSLATION)
TNT1 AAAAAA 0 A_SpawnItemEX("PistonTremorFX2",random(0,96),0,0,random(1,5),0,random(5,15),random(0,359),SXF_TRANSFERTRANSLATION)
TNT1 AAAAAA 0 A_SpawnItemEX("PistonTremorFX",random(0,96),0,0,random(1,5),0,random(8,18),random(0,359),SXF_TRANSFERTRANSLATION)
stop
}
}

actor PistonExplodeC : PistonExplode
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("cscc/pistonhit", "Weapon")
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, -8, 0, 0, random(-7,-4), 0,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, -8, 0, 0, random(-7,-4), 45,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, -8, 0, 0, random(-7,-4), 90,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, -8, 0, 0, random(-7,-4), -90,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, -8, 0, 0, random(-7,-4), -45,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, -8, 0, 0, random(-7,-4), 135,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, -8, 0, 0, random(-7,-4), -135,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 96, 0, -8, 0, 0, random(-7,-4), 180,SXF_TRANSFERTRANSLATION)
TNT1 AAAAAA 0 A_SpawnItemEX("PistonTremorFX",random(0,96),0,0,random(1,5),0,random(-15,-5),random(0,359),SXF_TRANSFERTRANSLATION)
stop
}
}

actor PistonExplode2C : PistonExplode
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("cscc/pistonhit", "Weapon")
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(-11, -7), 0,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(-11, -7), 45,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(-11, -7), 90,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(-11, -7), -90,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(-11, -7), -45,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(-11, -7), 135,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(-11, -7), -135,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PistonStunFX", 128, 0, 8, 0, 0, random(-11, -7), 180,SXF_TRANSFERTRANSLATION)
TNT1 AAAAAA 0 A_SpawnItemEX("PistonTremorFX2",random(0,96),0,0,random(1,5),0,random(-15,-5),random(0,359),SXF_TRANSFERTRANSLATION)
TNT1 AAAAAA 0 A_SpawnItemEX("PistonTremorFX",random(0,96),0,0,random(1,5),0,random(-18,-8),random(0,359),SXF_TRANSFERTRANSLATION)
stop
}
}

actor PistonFistItem : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("LightTeamFlag",1,"LightFire")
TNT1 A 0 A_JumpIfInventory("WilyTeamFlag",1,"WilyFire")
TNT1 A 0 A_JumpIfInventory("CossackTeamFlag",1,"CossackFire")
TNT1 A 0 A_JumpIfInventory("KingTeamFlag",1,"KingFire")
goto Default
Default:
TNT1 A 0 A_FireCustomMissile("PistonFist",0,0,-8,0)
stop
LightFire:
TNT1 A 0 A_FireCustomMissile("PistonFistL",0,0,-8,0)
stop
WilyFire:
TNT1 A 0 A_FireCustomMissile("PistonFistW",0,0,-8,0)
stop
CossackFire:
TNT1 A 0 A_FireCustomMissile("PistonFistC",0,0,-8,0)
stop
KingFire:
TNT1 A 0 A_FireCustomMissile("PistonFistK",0,0,-8,0)
stop
}
}

actor PistonFist2Item : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("LightTeamFlag",1,"LightFire")
TNT1 A 0 A_JumpIfInventory("WilyTeamFlag",1,"WilyFire")
TNT1 A 0 A_JumpIfInventory("CossackTeamFlag",1,"CossackFire")
TNT1 A 0 A_JumpIfInventory("KingTeamFlag",1,"KingFire")
goto Default
Default:
TNT1 A 0 A_FireCustomMissile("PistonFist2",0,0,8,0)
stop
LightFire:
TNT1 A 0 A_FireCustomMissile("PistonFist2L",0,0,8,0)
stop
WilyFire:
TNT1 A 0 A_FireCustomMissile("PistonFist2W",0,0,8,0)
stop
CossackFire:
TNT1 A 0 A_FireCustomMissile("PistonFist2C",0,0,8,0)
stop
KingFire:
TNT1 A 0 A_FireCustomMissile("PistonFist2K",0,0,8,0)
stop
}
}


actor PistonPunchItem : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("LightTeamFlag",1,"LightFire")
TNT1 A 0 A_JumpIfInventory("WilyTeamFlag",1,"WilyFire")
TNT1 A 0 A_JumpIfInventory("CossackTeamFlag",1,"CossackFire")
TNT1 A 0 A_JumpIfInventory("KingTeamFlag",1,"KingFire")
goto Default
Default:
TNT1 A 0 A_FireCustomMissile("PistonPunch",0,0,-8,0)
stop
LightFire:
TNT1 A 0 A_FireCustomMissile("PistonPunchL",0,0,-8,0)
stop
WilyFire:
TNT1 A 0 A_FireCustomMissile("PistonPunchW",0,0,-8,0)
stop
CossackFire:
TNT1 A 0 A_FireCustomMissile("PistonPunchC",0,0,-8,0)
stop
KingFire:
TNT1 A 0 A_FireCustomMissile("PistonPunchK",0,0,-8,0)
stop
}
}

actor PistonPunch2Item : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("LightTeamFlag",1,"LightFire")
TNT1 A 0 A_JumpIfInventory("WilyTeamFlag",1,"WilyFire")
TNT1 A 0 A_JumpIfInventory("CossackTeamFlag",1,"CossackFire")
TNT1 A 0 A_JumpIfInventory("KingTeamFlag",1,"KingFire")
goto Default
Default:
TNT1 A 0 A_FireCustomMissile("PistonPunch2",0,0,8,0)
stop
LightFire:
TNT1 A 0 A_FireCustomMissile("PistonPunch2L",0,0,8,0)
stop
WilyFire:
TNT1 A 0 A_FireCustomMissile("PistonPunch2W",0,0,8,0)
stop
CossackFire:
TNT1 A 0 A_FireCustomMissile("PistonPunch2C",0,0,8,0)
stop
KingFire:
TNT1 A 0 A_FireCustomMissile("PistonPunch2K",0,0,8,0)
stop
}
}

actor PistonAmmoWatcher
{
Height 0
Radius 0
+NOINTERACTION
-SOLID
+NOGRAVITY
+MISSILE
+DONTBLAST
States
{
Spawn:
TNT1 A 1
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("PistonTremorBoss",1,1)
goto Death
TNT1 A 0 A_JumpIfInTargetInventory("PistonTremorAmmo",500,"Spawn")
goto AmmoRegen
AmmoRegen:
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("MainBuffer",1,"ResetWait")
TNT1 A 1 A_JumpIfInTargetInventory("AltBuffer",1,"ResetWait")
TNT1 A 0 A_GiveToTarget("PistonTremorAmmo",1)
TNT1 A 0 A_JumpIfInTargetInventory("PistonTremorAmmo",500,"Spawn")
TNT1 A 0 A_JumpIfInTargetInventory("PistonTremorBoss",1,"AmmoRegen")
goto Death
ResetWait:
TNT1 AAAAA 1 A_JumpIfInTargetInventory("IsDead",1,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("PistonTremorBoss",1,"AmmoRegen")
goto Death
Death:
TNT1 A 0
stop
}
}

actor PistonAltCheck : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor PistonAltCancelCheck : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor DontAltfire : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor MainBuffer : Powerup
{
Powerup.Duration 5
}

actor MainBufferAmmo : Powerup
{
Powerup.Duration 15
}

actor MainBuffer2 : Powerup
{
Powerup.Duration 10
}

actor AltBuffer : Powerup
{
Powerup.Duration 5
}

actor AltBufferAmmo : Powerup
{
Powerup.Duration 15
}

actor AltBuffer2 : Powerup
{
Powerup.Duration 10
}

actor MainFistReturning : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor MainFistClear : Powerup
{
Powerup.Duration 5
}

actor MainFistTimer : Inventory
{
inventory.amount 1
inventory.maxamount 210
}

actor AltFistReturning : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor AltFistClear : Powerup
{
Powerup.Duration 5
}

actor AltFistTimer : Inventory
{
inventory.amount 1
inventory.maxamount 210
}

actor PistonStunBuffer : Powerup
{
Powerup.Duration 15
}

actor PistonTremorAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 500
+INVENTORY.IGNORESKILL
}