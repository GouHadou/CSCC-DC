const int BONEHEAD_DMG = 12;
const int BONEHEAD_THROWNDMG = 8;

actor MissedMemoBoss : BaseMM8BDMWep
{
Weapon.AmmoUse 84
Weapon.AmmoGive 84
Weapon.AmmoUse2 175
Weapon.AmmoGive2 175
Weapon.SlotNumber 0
Inventory.Pickupmessage "Somebody missed the memo that they were supposed to be dead."
Obituary "%o got to close to %k's bones."
Tag "Missed Memo"
weapon.ammotype "BoneheadAmmo"
weapon.ammotype2 "MissedMemoAmmo"
inventory.icon "NULLICON"
dropitem ""
States
{
Spawn:
WEA2 X 1
loop
Ready:
BHED A 1 ACS_ExecuteAlways(998,0,1)
BHED A 0 A_GunFlash
Ready1:
BHED A 0 A_JumpIfInventory("BoneheadHasShield",1,2)
BHED A 0 A_JumpIfInventory("BoneheadAmmo",0,1)
goto Ready2
BHED A 0 A_ClearRefire
BHED A 1 A_WeaponReady
Goto Ready1
Ready2:
BHED A 1 A_WeaponReady(WRF_NOPRIMARY)
BHED A 0 A_JumpIfInventory("BoneheadAmmo",0,"Ready1")
Goto Ready2
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BHED A 1 A_Lower
loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
BHED A 1 A_Raise
Loop
Fire:
BHED B 0 A_JumpIfInventory("BoneheadHasShield",1,"Fire2")
BHED B 0 A_JumpIfNoAmmo("NoAmmo")
BHED B 0 A_PlaySoundEx("actor/bonesummon","Weapon")
BHED B 0 A_SpawnItemEX("BoneheadBone1",55,0,0,0,0,0,0,33)
BHED B 0 A_SpawnItemEX("BoneheadBone2",55,0,0,0,0,0,120,33)
BHED B 0 A_SpawnItemEX("BoneheadBone3",55,0,0,0,0,0,240,33)
BHED B 0 A_GiveInventory("BoneheadHasShield",1)
BHED BCDEFGH 2
Goto Ready1
Fire2:
BHED I 0 A_GiveInventory("BoneheadToss",1)
BHED I 0 A_TakeInventory("BoneheadHasShield",999)
BHED I 0 A_TakeInventory("BoneheadAmmo",999)
BHED B 0 A_PlaySoundEx("actor/boneshoot","Weapon")
BHED I 0 A_SpawnItemEx("BoneheadBoneThrown1",0,0,0,cos(-pitch)*20,0,sin(-pitch)*20,0,1)
BHED I 0 A_SpawnItemEx("BoneheadBoneThrown2",0,45,0,cos(-pitch)*20,0,sin(-pitch)*20,0,1)
BHED I 0 A_SpawnItemEx("BoneheadBoneThrown3",0,-45,0,cos(-pitch)*20,0,sin(-pitch)*20,0,1)
BHED IJ 3
BHED K 5
BHED I 3
Goto Ready1
Altfire:
TNT1 A 0 A_JumpIfInventory("MissedMemoAmmo",0,1)
goto NoAmmo
TNT1 A 0 A_PlaySoundEx("actor/boneexplode","Weapon")
TNT1 A 0 A_GiveInventory("BoneheadSpeed",1)
TNT1 A 0 A_ChangeFlag("INVULNERABLE",1)
TNT1 A 0 A_TakeInventory("MissedMemoAmmo",999)
TNT1 A 0 A_SetTranslucent(0.0)
TNT1 A 0 A_GiveInventory("InvisibleFlag",1)
TNT1 A 0 A_SpawnItemEx("BoneheadHeadFX", 0, 0, 28, 0, 0, 15,0,1)
TNT1 AAAAAAA 0 A_SpawnItemEx("BoneheadFX", 0, 0, 28, 256/24, 0, random2(512/24), random(0, 180),1)
TNT1 AAAAAAA 0 A_SpawnItemEx("BoneheadFX", 0, 0, 28, 256/24, 0, random2(512/24), random(181, 359),1)
TNT1 A 55 
TNT1 A 5 A_GiveInventory("BoneheadBones",1)
TNT1 A 0 A_ChangeFlag("INVULNERABLE",0)
TNT1 A 0 A_TakeInventory("InvisibleFlag",1)
TNT1 A 0 A_SetTranslucent(1.0)
TNT1 A 0 A_TakeInventory("BoneheadSpeed",1)
TNT1 A 0 ACS_NamedExecuteAlways("CSACS_BoneheadHealth",0,1)
BHED GHA 2
Goto Ready1
NoAmmo:
BHED A 1
Goto Ready1
Flash:
TNT1 A 0 A_JumpIfInventory("IsDead",1,"NoFlash")
TNT1 A 0 A_GiveInventory("BoneAmmoGive2",1)
TNT1 A 1 A_GiveInventory("BoneAmmoGive",1)
Goto Flash
NoFlash:
TNT1 A 0
stop
}
}

actor MissedMemoAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 175
}

actor BoneheadAmmo : Ammo
{
+INVENTORY.IGNORESKILL
inventory.amount 1
inventory.maxamount 84
}

actor BoneheadHasShield : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor BoneheadRegen : Inventory
{
inventory.amount 1
inventory.maxamount 105
}

actor BoneheadFakeHP : Inventory
{
inventory.amount 1
inventory.maxamount 45
}

actor BoneheadToss : Powerup
{
Powerup.Duration 3
}

actor BoneheadFX
{
PROJECTILE
+CLIENTSIDEONLY
+BOUNCEONFLOORS
+BOUNCEONWALLS
+BOUNCEONCEILINGS
+THRUACTORS
-NOGRAVITY
Speed 20
scale 2.5
States
{
Spawn:
BNHD A 0
BNHD A 0 A_Jump(128,"Spawn2")
BNHD ABCD 3
goto Death2
Spawn2:
BNHD CDAB 3
goto Death
Death:
BNHD CDABC 3 A_FadeOut(0.2)
stop
Death2:
BNHD ABCDA 3 A_FadeOut(0.2)
stop
}
}

actor BoneheadHeadFX
{
PROJECTILE
+CLIENTSIDEONLY
+BOUNCEONFLOORS
+BOUNCEONWALLS
+BOUNCEONCEILINGS
+THRUACTORS
-NOGRAVITY
Speed 20
scale 2.5
States
{
Spawn:
BNHD A 0
BNHD EEEEEEEEEEEE 1 A_SetAngle(angle+30)
goto Death
Death:
BNHD E 0 A_FadeOut(0.2)
BNHD EEE 1 A_SetAngle(angle+30)
BNHD E 0 A_FadeOut(0.2)
BNHD EEE 1 A_SetAngle(angle+30)
BNHD E 0 A_FadeOut(0.2)
BNHD EEE 1 A_SetAngle(angle+30)
BNHD E 0 A_FadeOut(0.2)
BNHD EEE 1 A_SetAngle(angle+30)
BNHD E 0 A_FadeOut(0.2)
BNHD EEE 1 A_SetAngle(angle+30)
stop
}
}

actor BoneheadReturnFX
{
PROJECTILE
+THRUACTORS
+SKYEXPLODE
+DONTBLAST
+NOGRAVITY
+NOINTERACTION
+NOTARGETSWITCH
+SEEKERMISSILE
reactiontime 70
renderstyle "translucent"
alpha 0.0
damage (0)
Radius 2
Height 2
speed 40
scale 2.5
States
{
Spawn:
TNT1 A 0
BNHD ABCDA 1 A_FadeIn(0.2)
BNHD D 0 A_RearrangePointers(AAPTR_DEFAULT,AAPTR_DEFAULT,AAPTR_TARGET)
BNHD D 0 A_FaceTarget
goto Spawn2
Spawn2:
BNHD A 0 A_JumpIf(CallACS("core_targetexists")==0,"Death")
BNHD B 0 A_JumpIfCloser(30,"Death")
BNHD BB 0 A_SeekerMissile(90,90,SMF_PRECISE)
BNHD B 1 A_Countdown
BNHD A 0 A_JumpIf(CallACS("core_targetexists")==0,"Death")
BNHD C 0 A_JumpIfCloser(30,"Death")
BNHD CC 0 A_SeekerMissile(90,90,SMF_PRECISE)
BNHD C 1 A_Countdown
BNHD A 0 A_JumpIf(CallACS("core_targetexists")==0,"Death")
BNHD D 0 A_JumpIfCloser(30,"Death")
BNHD DD 0 A_SeekerMissile(90,90,SMF_PRECISE)
BNHD D 1 A_Countdown
BNHD A 0 A_JumpIf(CallACS("core_targetexists")==0,"Death")
BNHD A 0 A_JumpIfCloser(30,"Death")
BNHD AA 0 A_SeekerMissile(90,90,SMF_PRECISE)
BNHD A 1 A_Countdown
loop
Death:
TNT1 A 1
stop
}
}

actor BoneheadInvis : PowerInvisibility
{
Powerup.Duration -30
Powerup.Strength 100
Powerup.Mode "Translucent"
}

actor BoneheadSpeed : PowerSpeed
{
Powerup.Duration -30
Speed 2.0
+POWERSPEED.NOTRAIL
}

actor BoneheadBone1
{
Obituary "%o was boned by %k's Bone Barrier."
damagetype "BoneHead"

+MISSILE
+NOGRAVITY
+NOCLIP
+DONTSPLASH

+NOBLOOD
+SHOOTABLE
+INVULNERABLE
+NODAMAGE

+DONTBLAST
+NOTARGETSWITCH
+EXPLODEONDEATH
+DONTREFLECT
+DONTDRAIN
+DONTRIP

+THRUSPECIES
species "DumbBone"

damage (0)
height 56
radius 32
scale 2.5
States
{
Spawn:
BNHD F 0
BNHD F 0 A_JumpIfInTargetInventory("BoneheadSpeed",1,"Die")
BNHD F 0 A_JumpIf(CallACS("core_targetexists")==0,"Die")
BNHD F 0 A_JumpIfInTargetInventory("BoneheadToss",1,"Death")
BNHD F 0 A_TakeFromTarget("BoneheadAmmo",1)
BNHD F 0 A_JumpIfInTargetInventory("BoneheadAmmo",3,2)
BNHD F 1 A_Jump(256,"Die")
wait
BNHD F 0 Thing_SetTranslation(0,CallACS("core_copytranslation"))
BNHD F 0 A_Warp(AAPTR_TARGET,55,0,0,8,28)
BNHD F 1 A_Explode(BONEHEAD_DMG,20,0,0,20)
BNHD F 0 A_JumpIfInTargetInventory("BoneheadSpeed",1,"Die")
BNHD F 0 A_JumpIf(CallACS("core_targetexists")==0,"Die")
BNHD F 0 A_JumpIfInTargetInventory("BoneheadToss",1,"Death")
BNHD F 0 Thing_SetTranslation(0,CallACS("core_copytranslation"))
BNHD F 0 A_Warp(AAPTR_TARGET,55,0,0,8,28)
BNHD F 1 A_Explode(BONEHEAD_DMG,20,0,0,20)
loop
XDeath:
Death:
Crash:
BNHD F 0 
stop
Die:
BNHD F 0 A_ChangeFlag("NOINTERACTION",1)
BNHD F 0 A_JumpIf(CallACS("core_targetexists")==0,2)
BNHD F 0 Thing_SetTranslation(0,CallACS("core_copytranslation"))
BNHD F 0 A_SpawnItemEx("BoneheadBoneThrown1",0,0,0,20,0,0,0,1)
TNT1 A 0 A_TakeFromTarget("BoneheadAmmo",999)
TNT1 A 0 A_TakeFromTarget("BoneheadHasShield",999)
TNT1 A 2
stop
}
}

actor BoneheadBone2 : BoneheadBone1
{
States
{
Die:
BNHD F 0 A_ChangeFlag("NOINTERACTION",1)
BNHD F 0 A_JumpIf(CallACS("core_targetexists")==0,2)
BNHD F 0 Thing_SetTranslation(0,CallACS("core_copytranslation"))
BNHD F 0 A_SpawnItemEx("BoneheadBoneThrown2",0,0,0,20,0,0,0,1)
TNT1 A 0 A_TakeFromTarget("BoneheadAmmo",999)
TNT1 A 0 A_TakeFromTarget("BoneheadHasShield",999)
TNT1 A 2
stop
}
}

actor BoneheadBone3 : BoneheadBone1
{
States
{
Die:
BNHD F 0 A_ChangeFlag("NOINTERACTION",1)
BNHD F 0 A_JumpIf(CallACS("core_targetexists")==0,2)
BNHD F 0 Thing_SetTranslation(0,CallACS("core_copytranslation"))
BNHD F 0 A_SpawnItemEx("BoneheadBoneThrown3",0,0,0,20,0,0,0,1)
TNT1 A 0 A_TakeFromTarget("BoneheadAmmo",999)
TNT1 A 0 A_TakeFromTarget("BoneheadHasShield",999)
TNT1 A 2
stop
}
}

actor BoneheadProtect : PowerProtection
{
Powerup.Duration 5
DamageFactor "Bonehead", 0.0
}

actor BoneheadBoneThrown1
{
damagetype "Bonehead"
Obituary "%o was boned by %k's Bone Barrier."

+THRUACTORS
-SOLID
+NOGRAVITY
+THRUSPECIES
+NOEXPLODEFLOOR
+MISSILE
+DONTSPLASH

species "DumbBone"
damage (8)
Radius 20
Height 56
speed 20
scale 2.5
reactiontime 35
States
{
Spawn:
TNT1 A 0
TNT1 A 0 Thing_ChangeTID(0,6000+ACS_ExecuteWithResult(257,0))
TNT1 A 0 A_SpawnItemEx("BoneBlocker1")
Goto Spawn2
Spawn2:
BNHD F 0 A_Explode(BONEHEAD_THROWNDMG,20,0,0,20)
BNHD F 1 A_Countdown
BNHD F 0 A_Explode(BONEHEAD_THROWNDMG,20,0,0,20)
BNHD F 1 A_Countdown
BNHD F 0 A_Explode(BONEHEAD_THROWNDMG,20,0,0,20)
BNHD F 1 A_Countdown
TNT1 A 0 Thing_ChangeTID(0,6300+ACS_ExecuteWithResult(257,0))
Spawn3:
BNHD F 0 A_Explode(BONEHEAD_THROWNDMG,20,0,0,20)
BNHD F 1 A_Countdown
loop
Death:
TNT1 A 0 A_Stop
TNT1 A 0 A_GiveInventory("CutterFlag",1)
BNHD FFFFF 3 A_FadeOut(0.2)
stop
}
}

actor BoneheadBoneThrown2 : BoneheadBoneThrown1
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 Thing_ChangeTID(0,6100+ACS_ExecuteWithResult(257,0))
TNT1 A 0 A_SpawnItemEx("BoneBlocker2")
Goto Spawn2
}
}

actor BoneheadBoneThrown3 : BoneheadBoneThrown1
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 Thing_ChangeTID(0,6200+ACS_ExecuteWithResult(257,0))
TNT1 A 0 A_SpawnItemEx("BoneBlocker3")
Goto Spawn2
}
}

actor BoneBlocker1 : BasicShieldHitbox
{
+INVULNERABLE
species "DumbBone"
painchance 0
Radius 32
Height 56
States
{
Spawn:
TNT1 A 0
TNT1 A 1 ACS_NamedExecuteAlways("CSACS_SetPointer",0,AAPTR_TRACER,6000+ACS_ExecuteWithResult(257,0))
goto Spawn2
Spawn2:
TNT1 A 0 A_JumpIfInventory("CutterFlag",1,"Die",AAPTR_TRACER)
TNT1 A 1 A_Warp(AAPTR_TRACER,0,0,-54,0,24)
TNT1 A 0 A_CheckFlag("NOGRAVITY","Spawn2",AAPTR_TRACER)
stop
Die:
TNT1 A 0
stop
}
}

actor BoneBlocker2 : BoneBlocker1
{
States
{
Spawn:
TNT1 A 0
TNT1 A 1 ACS_NamedExecuteAlways("CSACS_SetPointer",0,AAPTR_TRACER,6100+ACS_ExecuteWithResult(257,0))
goto Spawn2
}
}

actor BoneBlocker3 : BoneBlocker1
{
States
{
Spawn:
TNT1 A 0
TNT1 A 1 ACS_NamedExecuteAlways("CSACS_SetPointer",0,AAPTR_TRACER,6200+ACS_ExecuteWithResult(257,0))
goto Spawn2
}
}

actor BoneAmmoGive : CustomInventory
{
States
{
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("BoneheadHasShield",1,"No")
TNT1 A 1 A_GiveInventory("BoneheadAmmo",2)
stop
No:
TNT1 A 0
stop
}
}

actor BoneAmmoGive2 : CustomInventory
{
States
{
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("BoneheadSpeed",1,"No")
TNT1 A 1 A_GiveInventory("MissedMemoAmmo",1)
stop
No:
TNT1 A 0
stop
}
}

actor BoneheadScriptActivate : CustomInventory
{
States
{
Pickup:
TNT1 A 0
TNT1 A 1 ACS_NamedExecuteAlways("CSACS_BoneheadHealth")
stop
}
}

actor BoneheadBones : CustomInventory
{
States
{
Pickup:
TNT1 A 0
TNT1 A 0 A_PlaySoundEX("actor/boneregen","SoundSlot6")
TNT1 AAAAAAA 0 A_SpawnItemEx("BoneheadReturnFX",random(256,-256),0,random(256,0),0,0,0,random(0, 180),1)
TNT1 AAAAAAA 0 A_SpawnItemEx("BoneheadReturnFX",random(256,-256),0,random(256,0),0,0,0,random(181, 359),1)
stop
}
}